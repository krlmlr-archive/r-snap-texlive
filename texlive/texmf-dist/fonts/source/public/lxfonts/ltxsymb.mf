% txsymb.mf
%
% (c) Copyright 1995, 1996, 1997 J"org Knappen
%
% This file is part of ecfonts version 1.0
%
% Please read the files 00readme.txt, 00inst.txt, 00error.txt, and
% copyrite.txt for further information
%
% You find some documentation in ecdoc.tex (needs LaTeX2e)
%
% EC Version 1.0
% 19-JAN-1997
%
% Driver file for the tc fonts
% Modified and name changed to ltxsymb.mf by Claudio Beccari 2007

version_check(1,0);  % |version_check| was introduced in dc1.3

input exaccess;            % accent accessories
input txpseudo;            % pseudoparameters

mode_setup; font_setup; pseudo_setup;

input txaccent;            % flat accents for capital letters
input txgen;               % genealogical symbols (by DEK)
input txsymbol;            % misc. symbols
if variant_g:              % italics?
 input txitlod;            % old style digits (by GJL)
 input txisuper;           % superscript 1,2,3
 input txifract;           % fractions 1/4, 1/2, 3/4
else:                      % roman
 input txromod;            % old style digits (by DEK)
 input txrsuper;           % superscript 1,2,3
 input txrfract;           % fractions 1/4, 1/2, 3/4
fi;


 
ecchar "Euro currency sign"; % jk
beginchar(oct"277",14u#,1.1cap_height#,0);% cap_height= 197/36pt#
italcorr cap_height#*slant-beak_jut#-.5u#;
adjust_fit(0,0);
pickup fine.nib;% fine=8/36pt#
numeric larg; larg:=slab;% slab=19/36pt# --> larg= 19/36pt#
pos2(larg,90); pos1(larg,0);
top y2r=h; x2=hround 8.5u; rt x1r=hround(w-u); y1=good.y(y2l-1.5u)-eps;
pos3(larg,180); pos4(larg,270); pos5(larg,0);
bot y4r=0; x4=x2; rt x5r=hround(w-u); y5=good.y(y4l+1.6u)+eps;
lft x3r =hround 2.5u; y3=.52h;
filldraw stroke z1e..z2e{left}..z3e{down}..z4e{right}..z5e;% arc
pos6(larg,-90); pos7(larg,-90); pos8(larg,90); pos9(larg,90); 
bot y6r=top y8r+if hefty: 1.333 else: 2 fi vair
       =vround(y3+if hefty: .667 fi vair+eps);
y7l=good.y(y6l)+eps; y9l=good.y(y8l)-eps;
x6=x8=hround u;  x7=x9=hround 10u;
filldraw stroke z6e--z7e; filldraw stroke z8e--z9e; % strokes
penlabels(1,2,3,4,5,6,7,8,9); endchar;



% Ligtable

if not monospace:
 ligtable "-": oct"177" =: oct"177"; % <hyphen><hyphenchar> => <hyphenchar>
fi

% Fontparameter
%
font_coding_scheme:="TeX text companion symbols 1---TS1";
boundarychar:=oct"027";
font_slant slant; 
font_x_height x_height#;
font_cap_height cap_height#;
font_asc_height asc_height#;
font_acc_cap_height (cap_height#+acc_height#);
font_desc_depth desc_depth#;
font_max_height(max(asc_height#,body_height#,
  cap_height#+acc_height#));
font_max_depth(max(desc_depth#,paren_depth#,the_ogonek_depth#));
% font_digit_width % set inside the digits file
font_cap_stem cap_stem#; % No letter capital I in the symbols
font_baselineskip max(1.2*designsize,body_height#+paren_depth#);

if monospace: font_normal_space 9u#; % no stretching or shrinking
 font_quad 18u#;
 font_extra_space 9u#;
else: font_normal_space 6u#+2letter_fit#;
 font_normal_stretch 3u#; font_normal_shrink 2u#;
 font_quad 18u#+4letter_fit#;
 font_extra_space 2u#;
fi

bye.
