.cmchar "The Schwabacher letter a";
.beginchar("a",6.943u#,x_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(1.13stem,45);
penpos3(stem,45); x1r=w; y1l=-apex_o;
z3l=(0.337w,-apex_o);
penpos2(hair,-45); z2r=z3;
penpos4(hair,-45);
z4-z2=whatever*dir45; x4r=x1l-hair;
z5=whatever[z2r,z4r];
z5-z1l=whatever*dir130;
penpos11(0.913stem,29); z11l=(0,0.396h);
penpos9(1.217stem,35);
z9r=(0.658w,h+apex_o);
penpos8(1.13stem,50);
x8r=w; z9r-z8r=whatever*dir144;
penpos7(cap_hair,angle(z8l-z9l));
z7r=z8l; penpos10(cap_hair,angle(z7r-z7l));
z10l=z9l;
z6=(x7,0.459h);
filldraw z1r{z5-z1l}..z6{up}...
z8l---z8r--z9r---z9l...z11l{down}
..z3l--z3r..z11r{up}..z10r{z9r-z9l}
& z10r--z7l--z5--z1l--z1r & cycle;
penstroke z2e--z4e;
.endchar;

.cmchar "The Schwabacher letter b";
.beginchar("b",7.358u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
laimoc_swab; podi_swab(27);
penpos3(1.087stem,27);
z3=whatever[z2.1l,z2.1r]; x3r=w;
penpos4(1.304stem,40);
z4r=(0.711w,x_height+apex_o);
penpos5(hair,-50); 
penpos6(hair,-50); z6l=z4;
z6-z5=whatever*dir40; x5=x1.1;
penstroke z2.3e{z2.2r-z2.2l}..
z3e{up}..z4e;
penstroke z5e--z6e;
.endchar;

.cmchar "The Schwabacher letter c";
.beginchar("c",5.057u#,x_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(0.935stem,53);
penpos2(whatever,34);
z1r=(w,0.874h); y2r=h+apex_o;
z2-z1=whatever*dir148;
z2r-z1r=whatever*dir148;
penpos3(hair,angle(z1l-z2l));
z3l=z2l; penpos4(1.348stem,45);
z4l=(0,0.45h); z5=(0.422w,-apex_o);
penpos7(hair,-57); 
z7r-z5=whatever*dir33; x7r=w; 
z6=(0.627w,0.176h);
penstroke z1e--z2e;
filldraw z7l{z5-z7r}..z6{left}..
z4r{up}..z3r{z2r-z2l} & z3r--z3l &
z3l{z2l-z2r}..z4l{down}..z5 &
z5--z7r--z7l & cycle;
.endchar;

.cmchar "The Schwabacher letter d";
.beginchar("d",7.208u#,1.383x_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(hair,109); penpos2(stem,205);
z1=(0.524w,0.619h);
z2r=(0,0.326h); z3=(0.429w,-apex_o);
z4=(0.639w,0.107h);
penpos5(hair,-67); x5=0.733w;
z5r-z3=whatever*dir23; 
penpos6(0.978stem,39); 
z6r=(w,y2); z7=z1; penpos7(1.043stem,56.5);
penpos8(1.109stem,7);
z8l=(0.094w,0.821h);
penpos9(hair,-90); z9l=(0.461w,h);
filldraw z5r{z5r-z3}..z6r{up}..z7r
..z8r{up}..z9r--z9l &
z9l{left}..z8l{down}..z7l..z6l{down}
..z5l{z3-z5r} & z5l--z5r & cycle;
filldraw z1r{dir199}..z2r{down}..
z3 & z3--z5r--z5l & z5l..z4{left}..
z2l{up}..z1l{dir19} & z1l--z1r &
cycle;
.endchar;

.cmchar "The Schwabacher letter e";
.beginchar("e",5.17u#,x_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(cap_hair,-90); z1r=(0.934w,0.189h);
z3=(0.642w,y1r); z2=(0.343w,-apex_o);
penpos4(1.391stem,42); z4l=(0,0.459h);
z5=(0.175w,0.874h);
penpos6(hair,-57); penpos8(hair,-57);
z6l=(0.693w,h+apex_o);
z8=whatever[z6l,z6r]; x8r=w;
penpos10(stem,angle(z6l-z5)); z10r=z6l;
penpos7(hair,-57); 
z8-z7=whatever*dir33; x7=x4;
penpos9(whatever,angle(z8r-z7r));
z9r=z8r;
z9l-z10l=whatever*(z8r-z10r);
filldraw z1r--z2..z4l{up}...z5---
z6l--z6r{z5-z6l}..z4r{down}..
z3{right}..z1l{z1r-z2}--z1r & cycle;
penstroke z7e--z8e;
penstroke z9e--z10e;
.endchar;

.cmchar "The Schwabacher letter f";
.beginchar("f",6.038u#,1.414x_height#,3.208u#);
.adjust_fit(usual_left#,usual_right#-1.12u#);
boolean isitf; isitf:=true; long_s; .endchar;

.cmchar "The Schwabacher letter g";
.beginchar("g",8.528u#,x_height#,3.623u#);
.adjust_fit(usual_left#-0.35u#,usual_right#);
penpos1(1.217stem,-130); z1r=(0,-2/3d);
penpos2(hair,-90); z2l=z1r;
penpos3(0.8stem,-130); z3r=(0.4w,-d);
penpos3'(hair,-47);
z3'=(0.704w,-0.594d);
penpos4(1.283stem,49); z4=(0.832w,-0.5apex_o);
penpos6(stem,0); z6=(x4-0.5stem,0.898h);
penpos5(whatever,28); x5l=x6l;
x5r=x6r; z9=(0.261w,0);
z5-z9=whatever*dir28;
penpos8(hair,-62); z8r=z5r;
penpos10(stem,0);
z10=(0.235w,y6);
z9-z11=whatever*dir-62; x11=x10l;
z9'-z8l=whatever*(z9-z8r);
z9'=whatever[z11,z9]; z11'=(x10r,y11);
z9''-z11'=whatever*(z9-z11);
z9''=whatever[z9',z8l]; 
penpos13(stem,-90); z13l=z10l;
z12=(x10,h+apex_o); z7=(x6,h+apex_o);
penpos14(whatever,-108); x14l=w;
y14l=y13l; y14r=y13r;
%
%
%
filldraw z2r..z3r{right}..z3'r..z4r{up}
..z5r--z5l..z4l{down}..z3'l..z3l{left}
..z1l{dir140} & z1l--z2l--z2r & 
cycle;
filldraw z9--z8r--z8l--z9''--z11'
--z10r--z12--z10l--z11--z9 & cycle;
filldraw z5l--z6l--z7--z6r--z5r--
z5l & cycle;
penstroke z13e--z14e;
.endchar;

.cmchar "The Schwabacher letter h";
.beginchar("h",7.585u#,cap_height#,3.358u#);
.adjust_fit(usual_left#,usual_right#);
laimoc_swab; z2=(0.279w,-apex_o);
penpos3(hair,-90); z3r=(0.547w,y1.1);
z4=(0.428w,y1.1); z5=(x1.1r,y1.2);
filldraw z1.1'--z2--z3r--z3l
{z2-z3r}..z4{left}..z5{up}--z1.1--
z1.1' & cycle;
%
%
%
penpos6(hair,-40); penpos8(1.217stem,50);
penpos9(1.043stem,24);
penpos10(hair,-45);
z10r=(x5,-d); z9r=(w,0.459x_height);
z7=(0.582w,x_height+apex_o);
x6=x1.1; z7-z6l=whatever*dir50;
z8r=z7;
penpos8'(hair,-40); z8'l=z8;
penstroke z6e--z8'e;
penstroke z10e{dir45}..z9e{up}..
z8e{dir140};
.endchar;

.cmchar "The Schwabacher letter i";
.beginchar("i",4u#,1.387x_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(stem,0); penpos2(1.739stem,0);
penpos3(0.5hair,90); penpos4(0.5hair,-90);
z2l=(0,0.815x_height); x1=x2;
y1=0.252x_height; z3l=z2l; z4r=z2r;
z5=(x1,x_height+apex_o);
penpos1'(stem,0); x1'=x1; y1'=0.7x_height;
filldraw z1r---z1'r...{right}z2r--z4l--z5--
z3r--z2l{right}...z1'l---z1l--z1r & cycle;
%
% teleia
%
penpos6(hair,145); 
z6=(0.375[x2l,x2r],1.090x_height);
z7=(0.875[x2l,x2r],1.387x_height);
z8=(1.125[x2l,x2r],1.324x_height);
filldraw z6l{dir34}..z8{up}..z7{left}
..z6r{dir259}--z6l & cycle;
%
% podi tou i
%
penpos9(hair,90); penpos11(hair,-90);
z9r=(0,foot); z11r=(w,foot);
z12=(0.717w,foot); z10=(0.472w,-apex_o);
filldraw z1l{down}..z9r{left}--z9l
--z10--z11r--z11l{z10-z11r}..z12{left}
..z1r{up} & z1r--z1l & cycle;
.endchar;

.cmchar "The Schwabacher letter j";
.beginchar("j",3.283u#,1.387x_height#,2.943u#);
.adjust_fit(usual_left#,usual_right#);
penpos1(stem,0); penpos2(1.739stem,0);
penpos3(0.5hair,90); penpos4(0.5hair,-90);
z2l=(0,0.815x_height); x1=x2;
y1=0.252x_height; z3l=z2l; z4r=z2r;
z5=(x1,x_height+apex_o);
penpos1'(stem,0); x1'=x1; y1'=0.7x_height;
filldraw z1r---z1'r...{right}z2r--z4l--z5--
z3r--z2l{right}...z1'l---z1l--z1r & cycle;
%
% teleia
%
penpos6(hair,145); 
z6=(0.375[x2l,x2r],1.090x_height);
z7=(0.875[x2l,x2r],1.387x_height);
z8=(1.125[x2l,x2r],1.324x_height);
filldraw z6l{dir34}..z8{up}..z7{left}
..z6r{dir259}--z6l & cycle;
%
% podi tou j
%
penpos9(hair,0); z9l=(0,-d);
penpos10(stem,0); z10=(x1,0);
filldraw z9l{z10r-z9r}...z10l---
z1l--z1r--z10r--z9r--z9l & cycle;
.endchar;

.cmchar "The Schwabacher letter k";
.beginchar("k",6.189u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
laimoc_swab;
penpos10(1.305hair,-90); z10=(x1.2,x_height);
penpos20(hair,-50);
penpos3(1.087stem,40); z3r=z20l;
z3-z10l=whatever*dir40;
y20l=1.423x_height; penpos4(0.957stem,37);
z4r=(w,1.194x_height);
penpos6(0.957stem,-117);
z6l=(w-2/3apex_o,x_height-apex_o);
penpos8(whatever,-90);
y8l=y6l; y8r=y6r; z7=(0,y6r);
z8l-z7=whatever*dir48;
penpos5(hair,-90);z5r=(x1.1r,y6r);
penstroke z10e--z20e;
penstroke z5e{dir40}..z4e{up}..
z3e{dir130};
filldraw z7--z6r--z6l--z8l--z7 &
cycle;
podi_swab(37);
penpos11(hair,angle(z2.3r-z2.3l));
z11-z2.3=whatever*dir37;
x11r=0.921w;
penstroke z2.3e--z11e;
.endchar;

.cmchar "The Schwabacher letter l";
.beginchar("l",4.149u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
laimoc_swab; w:=7.585u;
z2=(0.279w,-apex_o);
penpos3(hair,-90); z3r=(0.547w,y1.1);
z4=(0.428w,y1.1); z5=(x1.1r,y1.2);
filldraw z1.1'--z2--z3r--z3l
{z2-z3r}..z4{left}..z5{up}--z1.1--
z1.1' & cycle;
.endchar;

.cmchar "The Schwabacher letter m";
.beginchar("m",11.925u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
dexi_podi_swab(-apex_o);
penpos3(stem,0); z3=(1/2w,y1.2);
aristero_podi_swab (x3r);
z0=z2.1r-z3r;
penpos4(hair,-90); y4=y2.7;
x4=x1.2r;
z5=z2.6-z0; z6=z2.8-z0;
z8=z2.11-z0; penpos7(1.217stem,33);
z7=z2.9-z0;
z9=whatever[z5,z6]; x9=x3r;
filldraw z4r--z7l--z8--z3l--
z3r--z9--z5--z4l--z4r & cycle;
penpos10(whatever,24);
z10l=(x3l,-apex_o); x10r=x3r;
penstroke z10e--z3e;
.endchar;

.cmchar "The Schwabacher letter n";
.beginchar("n",7.925u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
dexi_podi_swab(-apex_o);
aristero_podi_swab (x1.2r);
.endchar;

.cmchar "The Schwabacher letter o";
.beginchar("o",7.849u#,x_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(hair,0); z1=(0.428w,-apex_o);
penpos4(1.326stem,53); z4l=z1;
penpos2(1.065stem,47);
z2l=(0,0.491h); 
penpos6(hair,angle((w,0)-(0,h)));
z6=whatever[(w,0),(0,h)];
x6=0.202w;
penpos7(hair,angle((w,0)-(0,h)));
z7=whatever[(w,0),(0,h)];
x7=0.798w;
z6'=((0,h)--(w,0)) intersectionpoint 
((x2r,0)--(x2r,h));
z7'=((0,h)--(w,0)) intersectionpoint 
((0,y4r)--(w,y4r));
penpos5(whatever,-133);
z5=whatever[z2l,z2r]; y5r=y6';
y5l=h+2apex_o;
penpos3(whatever,40);
x3l=x7'; x3r=w; y3r=0.563h;
%
%
%
filldraw z1r{dir45}..z3r{up}..
z5l{dir150}--z5r{right}..z3l{down}
..z4r{left}..z2r{up}..z6r..
z5r{right}--z5l & z5l{z5r-z5l}
..z6l..z2l{down}..z1l{dir-45}--z1r
& cycle;
.endchar;

.cmchar "The Schwabacher letter p";
.beginchar("p",8.151u#,x_height#,3.057u#);
.adjust_fit(usual_left#,usual_right#);
dexi_podi_swab(-apex_o);
z2=(0.457[x1.1l,x1.1r],-d);
filldraw z2..z1.1l{up}--z1.1r{down}
..z2 & cycle;
penpos3(1.152stem,36);
penpos4(1.152stem,36);
z3l=(0.574w,0);
z4-z3=whatever*dir157;
x4=x1.1l; 
penstroke z3e--z4e;
penpos6(1.217stem,36); z6r=(w,0.586h);
penpos5(hair,angle(z3r-z4r));
z5r=z3r; penpos7(1.196stem,45);
z7r=(0.722w,h+apex_o);
penpos8(hair,-45);
z8l=z7; penpos9(hair,-45);
z8-z9=whatever*dir45; x9=x2;
penstroke z9e--z8e;
penstroke z5e{z3r-z3l}..z6e{up}..
z7e{dir135};
.endchar;

.cmchar "The Schwabacher letter q";
.beginchar("q",7.774u#,x_height#,3.057u#);
.adjust_fit(usual_left#,usual_right#);
penpos1(stem,0); z1=(0.791w,0.1h);
penpos7(1.174stem,18); z7l=(0,0.523h);
penpos5(1.196stem,30);
x5l=x7r; y5r=h+apex_o;
penpos4(2/3hair,-90); x4=w;
z5r-z4l=whatever*dir155;
penpos3(whatever,30); z3r=z4r;
z3l-z5l=whatever*(z4l-z5r);
penpos11(stem,0); z11=(x1,y4);
z2=(x1,-d); penpos2(eps,0);
penstroke z2e...z1e---z11e;
filldraw z5l--z3l--z4r--z4l &
flex(z4l,0.5[z5r,z4l]-
(apex_o,apex_o),z5r) &
z5r--z5l & cycle;
penpos8(1.152stem,40);
z8l=(0.379w,0); 
penpos6(hair,angle(z3l-z5l));
z6l=z5l; penpos9(hair,-50);
penpos10(hair,-50);
z9r=z8; z10-z9=whatever*dir40;
x10=x1;
penstroke z8e{dir130}..z7e{up}..
z6e{z5r-z5l};
penstroke z9e--z10e;
.endchar;

.cmchar "The Schwabacher letter r";
.beginchar("r",6.038u#,x_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(1.152stem,35);
penpos2(1.152stem,35);
x2l=0; z1l=(1.5stem,-apex_o);
z2-z1=whatever*dir148;
penpos3(hair,-55); penpos4(hair,-55);
z3r=z1; z4r=whatever[z1l,z1r];
x4=0.813w;
dexi_podi_swab(y2l-hair);
penstroke z1e--z2e;
penstroke z3e--z4e;
%
%
%
penpos7(2/3hair,-90);
penpos6(hair,-55);
penpos10(hair,-55);
z6l=(0.838w,h+apex_o);
z6-z10=whatever*dir35; x10=x1.1;
z7l-z6l=whatever*dir-45; x7=w;
z7r-z8=whatever*dir45;
x8=0.825w; z9-z8=whatever*(z6l-z7l);
z9=whatever[z10r,z6r];
penstroke z10e--z6e;
filldraw z6l--z7l--z7r--z8--z9--
z6l & cycle;
.endchar;

.cmchar "The Schwabacher long s";
.beginchar("s",6.038u#,1.414x_height#,3.208u#);
.adjust_fit(usual_left#,usual_right#-2.33u#);
boolean isitf; isitf:=false; long_s; 
.endchar;

.cmchar "The Schwabacher short s";
.beginchar(oct"215",6.34u#,x_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1'(hair,-40); penpos2(hair,-40);
z1'l=(0,0.108h); z2r=(w-0.5apex_o,0.919h);
penpos1(whatever,0); z1l=z1'l;
z1r=whatever[z1'r,z2r];
penstroke z1e--z2e;
penpos5(0.826stem,37); z5r=(w,0.387h);
penpos6(whatever,0); 
z6l=whatever[z1l,z2l]; 
z6r=whatever[z1r,z2r]; y6=0.275h;
z7=whatever[z1r,z2r]; x7=0.488w;
z8=whatever[z1r,z2r]; x8=0.702w;
z3=(0.435w,-apex_o);
penpos4(hair,-55); 
z4r-z3=whatever*dir35; x4=0.774w;
penpos9(0.6stem,90); z9l=z1l;
filldraw z1l---z1r...{dir-28}z3 & 
z3---z4r...z5r{up}..z8 & z8--z7
{right}..z5l{down}..z4l{z3-z4r}
..z6r{z1r-z3}--z6l{z1-z2}..
z9r{left}--z1l & cycle;
%
%
%
penpos10(0.913stem,37);
z10l=(0,0.613h); z12=(0.524w,h+apex_o);
penpos11(hair,-53); 
z12-z11l=whatever*dir37; x11l=x10r+0.5apex_o;
z15=0.488[z1l,z2l];
z14=0.736[z1l,z2l];
z13=(0.827w,y2r);
filldraw z6l{z2l-z1l}..z10l{up}
...z11l---z12 & z12{dir-30}..z13{right}..
z2l{z2-z1} & z2l--z2r--z14 &
z14{z12-z13}..z11r{z11l-z12}..z10r{down}
..z15{z2r-z2l}--z6l & cycle;
.endchar;

.cmchar "The Schwabacher letter t";
.beginchar("t",5.66u#,1.27x_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(1.152stem,35);
penpos2(1.152stem,35);
x2l=hair; z1l=(1.826stem,-apex_o);
z2-z1=whatever*dir148;
penpos3(hair,-55); penpos4(hair,-55);
z3r=z1; z4r=whatever[z1l,z1r];
x4r=w;
penstroke z1e--z2e;
penstroke z3e--z4e;
%
%
%
penpos5(stem,0); z5r=(x1l-apex_o,y2l);
penpos6(stem,0); z6=(x5,0.811x_height);
penpos7(0.826stem,-90);
z7r=(w,y6); z8=(0,y6); z9=(x6r,h);
filldraw z5l--z6l--z8--z9--(x9,y7l)
--z7l--z7r--z6r--z5r--z5l & cycle;
.endchar;

.cmchar "The Schwabacher letter u";
.beginchar("u",7.849u#,x_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(1.283stem,35);
penpos2(1.283stem,35);
z1l=(0.337w,-apex_o);
z2-z1=whatever*dir142; x2=0.168w;
penpos3(hair,-55); penpos4(hair,-55);
z3r=z1; z4-z3=whatever*dir35;
penpos8(stem,0); x8=0.798w=x4;
z8l=whatever[z3r,z4r];
penpos10(stem,0); z10=(x2+0.5stem-apex_o,y2l);
penstroke z1e--z2e;
penstroke z3e--z4e;
z11=(x10l,0.775h); z12=(0.478[x10l,x10r],h+apex_o);
penpos13(hair,-90); x13=0;
z12-z13l=whatever*dir33;
filldraw z13r{z12-z13l}...z11---
z10l--z10r---(x10r,y11)...z12
& z12--z13l--z13r & cycle;
%
%
%
z5=(0.609[x8l,x8r],-apex_o);
penpos9(whatever,24);
y9r=h+apex_o; x9l=x8l; x9r=x8r;
penpos6(eps,-90); 
z6r-z5=whatever*dir43;
x6=w; z7=whatever[z1l,z1r]; x7=x8r;
filldraw z5...z8l---z9l--z9r---
z7...z6l--z6r--z5 & cycle;
.endchar;

.cmchar "The Schwabacher letter v";
.beginchar("v",8.302u#,x_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(1.261stem,37);
penpos2(1.261stem,45);
penpos4(1.261stem,38);
penpos3(1/3hair,-52);
x1l=0; y1r=h+apex_o; z2=(0.295w,0.586h);
z4=(0.145w,0.239h); z3r=z4l;
filldraw z3r--z4r..z2r{up}..z1r--
z1l..z2l{down}..z3l{z4l-z4r}--
z3r & cycle;
%
%
%
penpos5(whatever,48); z5l=(0.473w,-apex_o);
z5r-z4r=whatever*(z5l-z4l);
penpos6(1.174stem,34); z6r=(w,0.631h);
z8=(0.695w,h+apex_o);
penpos9(hair,-55); 
z8-z9l=whatever*dir35; x9=x2;
x7=0.518w; z7=whatever[z2l,z8];
filldraw z9r{z8-z9l}..z7{right}..
z6l{down}...z5r---z5l & z5l{dir39}..
z6r{up}..z8--z9l--z9r & cycle;
penstroke z5e--z4e;
.endchar;

.cmchar "The Schwabacher letter w";
.beginchar("w",12.604u#,x_height#,0);
.adjust_fit(usual_left#,usual_right#);
podi_swab(36);
dexi_podi_swab(foot);
unfilldraw (0,0)--(hair,0)--
(hair,0.5h)--(0,0.5h)--(0,0) &
cycle;
penpos3(hair,-54); penpos4(stem,0);
z3-z2.3=whatever*dir36;
x4=0.539w; z4l-z2.3=whatever*dir36;
x3=x4; penpos15(whatever,28);
x15l=x4l; x15r=x4r; 
penpos8(1.304stem,42);
z8r=(0.796w,h+apex_o);
z15r=whatever[z8l,z8r];
penpos9(hair,-48); 
z8-z9l=whatever*dir42; x9=x4;
penpos13(hair,-48); z13l=z8;
penstroke z2.3e--z3e;
penpos6(1.174stem,37); 
z6r=(0.431w,h+apex_o);
penpos5(hair,-53); z6-z5l=whatever*dir37;
x5=x1.1; penpos14(hair,-53); z14l=z6;
penstroke z5e--z14e;
penstroke z4e---z15e...z6e{dir147};
penpos10(1.087stem,31.5);
z10l=(x8l,-apex_o); penpos10'(whatever,36);
z10'r-z10r=whatever*dir142;
z10'l-z10l=whatever*dir142;
z10'=whatever[z2.3,z3];
penstroke z10e--z10'e;
penpos11(hair,-58.5);
z11r=z10; penpos12(1.304stem,43);
z12r=(w,0.595h); 
penstroke z9e--z13e;
penstroke z11e{z10r-z10l}..z12e{up}
..z8e{dir148};
.endchar;

.cmchar "The Schwabacher letter x";
.beginchar("x",6.038u#,x_height#,3.019u#);
.adjust_fit(usual_left#,usual_right#);
dexi_podi_swab(0.162h);
penpos1(1.043stem,45);
penpos2(1.043stem,45);
x2l=x1.1l; z1l=(0.606w,-apex_o);
z2-z1=whatever*dir153;
penpos3(hair,-45); penpos4(hair,-45);
z3r=z1; z4r=whatever[z1l,z1r];
x4r=w-apex_o;
penstroke z1e--z2e;
penstroke z3e--z4e;
%
%
%
penpos7(2/3hair,-90);
penpos6(hair,-55);
penpos10(hair,-55);
z6l=(0.838w,h+apex_o);
z6-z10=whatever*dir35; x10=x1.1;
z7l-z6l=whatever*dir-45; x7=w;
z7r-z8=whatever*dir45;
x8=0.825w; z9-z8=whatever*(z6l-z7l);
z9=whatever[z10r,z6r];
penstroke z10e--z6e;
filldraw z6l--z7l--z7r--z8--z9--
z6l & cycle;
%
%
%
penpos11(0.783stem,0); 
z11l=z1.1l+(0,apex_o);
penpos12(hair,0); z12r=(0,-d);
penstroke z12e{dir63}..z11e{up};
.endchar;

.cmchar "The Schwabacher letter y";
.beginchar("y",7.585u#,x_height#,3.547u#);
.adjust_fit(usual_left#,usual_right#);
penpos1(1.2hair,0); penpos2(1.2hair,0);
z2=(0.896w,0.649h); 
z2-z1=whatever*dir62; y1=-d;
penstroke z1e--z2e;
penpos3(1.522stem,62);
penpos4(1.522stem,62);
z3=whatever[z1,z2]; y3=0;
z4-z3=whatever*dir107; x4=0.25w;
penpos5(1.174stem,55); y5r=h+apex_o;
x5l=0;
penstroke z3e---z4e...z5e;
%
%
%
penpos9(0.565stem,0); z9r=z2r;
penpos6(whatever,42); z6l=z9l;
x6r=w; penpos7(1.087stem,53);
z7r=(0.7w,h+apex_o);
penpos8(hair,151); z8=whatever[z3,z4];
x8=0.31w; z9'=whatever[z1l,z2l]; 
y9'=y8l;
filldraw z9'{z2-z1}..%z6l{up}..
z7l{left}..z8l{z1-z2}--z8r{z2-z1}
..z7r{right}..z6r{down}..z9r{z1-z2}
--z9' & cycle;
.endchar;

.cmchar "The Schwabacher letter z";
.beginchar("z",5.358u#,x_height#,3.321u#);
.adjust_fit(usual_left#,usual_right#);
penpos1(0.87stem,-139);
penpos2(0.85stem,-139);
penpos3(1.043stem,41);
z1r=(0,-0.682d); z2r=(0.239w,-d);
x3r=w; y3l=0; z4=(0.38w,0.261h);
penpos6(hair,-38);
z5-z6l=whatever*dir45; x5=0.577w;
z6=(0.12w,0.189h);
penpos2'(hair,-45);
z2'=(0.648w,-0.625d);
penstroke z1e{dir-49}..
z2e{right}..z2'e..z3e{up};
filldraw z3r{up}..z5{dir142} &
z5--z6l--z6r{z5-z6l}..z4{right}..
z3l{down} & z3l--z3r & cycle;
%
%
%
penpos7(hair,-38); penpos8(hair,-38);
penpos9(hair,-38); penpos10(hair,-38);
z9r=z5; z7-z9=whatever*dir45;
z8-z10=whatever*dir45;
x10l=0; y8l=h+apex_o; x7r=0.93w;
x8=0.479w;
penstroke z10e--z8e;
penpos11(1.217stem,45); z11l=z9l;
penpos12(1.217stem,45);
z12-z11=whatever*(z8-z7);
z12=whatever[z10r,z8r];
penstroke z9e--z7e;
penstroke z11e--z12e;
z9'=whatever[z6l,z5]; x9'=x8l;
filldraw z6l{z5-z6l}..z9l{z11r-z11l}
--z9r--z6l & cycle;
.endchar;