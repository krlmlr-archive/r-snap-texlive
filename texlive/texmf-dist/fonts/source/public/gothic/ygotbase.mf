def podi(suffix $)=
z$.1=z$l-(2apex_o,0);
penpos$.2(stem,40); z$.2l=z$.1;
penpos$.3(stem,40);
z$.3r-z$.2r=whatever*dir-50;
y$.3l=-apex_o;
penstroke z$.3e--z$.2e
enddef;

def qeri(suffix $)=
z$.1=z$r+(2apex_o,0);
penpos$.2(stem,40); z$.2r=z$.1;
penpos$.3(stem,40);
z$.2r-z$.3r=whatever*dir-50;
y$.3r=y$+foot+apex_o;
penstroke z$.2e--z$.3e
enddef;

def panwqeri(suffix $)=
z$.1=z$r+(2apex_o,0);
penpos$.2(stem,40); z$.2r=z$.1;
penpos$.3(stem,40);
z$.2r-z$.3r=whatever*dir-50;
y$.3r=y$+foot+apex_o;
penstroke z$.2e--z$.3e;
penpos$.4(hair,-50);
z$.4l=z$.3r; z$.5=whatever[z$.3r,z$.3l];
x$.5=x$l-1.233stem; x$.6=x$.5;
z$.6-z$.4r=whatever*(z$.5-z$.3);
filldraw z$.5--z$.4l--z$.4r--z$.6--z$.5
& cycle;
unfilldraw z$.5--(x$.5,y$.3l)
--z$.3l--z$.5 & cycle;
enddef;

def katwpodi(suffix $)= 
z$.1=z$l-(2apex_o,0);
penpos$.2(stem,40); z$.2l=z$.1;
penpos$.3(stem,40);
z$.3r-z$.2r=whatever*dir-50;
y$.3l=-apex_o;
penstroke z$.3e--z$.2e;
penpos$.4(hair,-50);
z$.4r=z$.3l; z$.5=whatever[z$.3r,z$.3l];
x$.5=x$r+1.233stem; x$.6=x$.5;
z$.6-z$.4l=whatever*(z$.5-z$.3);
filldraw z$.5--z$.4r--z$.4l--z$.6--z$.5
& cycle;
unfilldraw z$.5--(x$.5,y$.3l)
--z$.3l--z$.5 & cycle;
enddef;

def Umlaut(suffix $) =
penpos$.1(stem,45); penpos$.2(stem,45);
penpos$.3(stem,45); penpos$.4(stem,45);
penpos$.5(stem,-45); penpos$.6(stem,-45);
y$.1=y$.3; 
z$.5l=z$.1l; z$.5r=z$.2l;
z$.6l=z$.3l; z$.6r=z$.4l;
%
%
%
0.5[y$.1,y$.2]=x_height+2apex_o+2/3stem;
x$.3l-x$.2r=0;
%
%
%
0.5[x$.2r,x$.3l]=x$;
penstroke z$.1e--z$.2e;
penstroke z$.3e--z$.4e;
enddef;

def ygoth_dot (suffix $) =
penpos$.1(stem,45); penpos$.2(stem,45);
penpos$.3(stem,-45);
z$.1l=z$.3l; z$.2l=z$.3r;
z$=.5[z$.1l,z$.2r];
penstroke z$.1e--z$.2e;
enddef;
