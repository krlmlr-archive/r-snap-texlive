.cmchar "The Schwabacher letter A";
.beginchar("A",11.755u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);penpos1(0.714cap_stem,16); 
z1l=(apex_o,0.718h);
z2=(0,0.767h); penpos3(cap_hair,-58);
y3=0.474[h,y2]; x3=0.13w; 
z4=(0.278w,h+apex_o);
penpos5(1.036cap_stem,-158);
x5l=0.574w; y5=0.638h+cap_hair; 
penpos6(0.6cap_hair,-155); x6=x3r;
y6l=0.132h; penpos7(1.286cap_stem,33);
penpos8(1.143cap_stem,33);
penpos9(1.071cap_stem,33);
z7l=z6l; x9r=x5l; y9l=-apex_o;
z8=0.472[z7,z9]+(0.5cap_hair,0.5cap_hair);
penpos10(cap_hair,-57); z10r=z9l;
penpos17(cap_stem,0); x17l=0.657w;
y17=0.724h; penpos11(cap_hair,-57);
z11-z10=whatever*dir33; x11=x17;
z15=whatever[z10,z11]; x15=x17l;
z15-z12=whatever*dir123; y12=-apex_o;
penpos13(cap_hair,-90); z13r=(w,y6l);
z14=(0.567[x17r,w],y13r);
z16=(x17r,0.591[y17,y14]);
penpos18(0.6cap_hair,-90); z18l=(w,h);
z19=(x17l,0.822h);
z7'=(z6{z7r-z7l}..z5{up})
intersectionpoint (z7r+(2u,-2u)--
z7r--z7r+(-2u,2u));
%
%
%
filldraw z1r{z3l-z3r}..z3r{z4-z3l}
..z5r{down}..z6r{z7l-z7r} & z6r--z6l
& z6l{z7r-z7l}..z5l{up}..z4{dir155}
& z4---z3l...z2{down}..z1l &
z1l--z1r & cycle;
penstroke z9e..z8e..z7e;
penstroke z10e--z11e;
filldraw z12--z13r--z13l & z13l{z12-z13r}
..z14{left}...z16---z17r...z18r{right}
& z18r--z18l & z18l{left}..z19{z5r-z19}
& z19--z15--z12 & cycle;
filldraw z7r--z7' & z7'{z6-z7'}..
z6{z7l-z7r} & z6--z6l--z7r & cycle;
.endchar;

.cmchar "The Schwabacher letter B";
.beginchar("B",13.811u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);penpos1(cap_hair,-70);
penpos2(1.667cap_hair,-70);
penpos3(1.071cap_stem,23);
penpos4(2/3cap_hair,90);
penpos5(1.571cap_stem,32);
x1l=0; y1=0.282h; z2r-z1r=whatever*dir12.5;
x2=0.24w; z3=(0.464w,0.672h);
y5r=h+apex_o; x5r=0.295w; z4r=z5l;
filldraw z1r---z2r...z3r{up}..
z5r{dir163} & z5r--z5l--z4l &
z4l{right}..z3l{down}..z2l..z1l
{z1r-z2r} & z1l--z1r & cycle;
%
%
%
penpos6(cap_hair,-46); penpos7(cap_hair,-46);
penpos9(cap_stem,44); penpos10(cap_hair,-46);
z6r=z3; z7-z6=whatever*dir44;
y7l=h+apex_o; z9r=z7l; 
z10=whatever[z7l,z7r]; x10r=0.951w;
z8=(x6,0.552h); penpos8(cap_hair,-90);
z11'-z9l=whatever*(z10-z7);
x11'=w;
z11=(z8{right}..z10{dir44})
intersectionpoint (z11'--z9l);
penstroke z8e{right}..z10e{dir44};
penstroke z6e--z7e;
filldraw z9r--z10r--z11--z9l--z9r &
cycle;
%
%
%
%.endchar; end.
path pipi;
pipi:=z8{right}..z10{dir44};
numeric titi,uuuu;
(titi,uuuu)=pipi
intersectiontimes 
((0.781w,0)--(0.781w,h));
z12= point titi of pipi;
penpos13(0.964cap_stem,39);
z13r=(w,0.362h); penpos14(cap_hair,-57);
z14=(0.822w,0.071h); z14'=(0.623w,-apex_o);
z15=whatever[z1l,z1r]; 
penpos15(cap_hair,-70);
y1r-y15l=cap_hair; 
penpos16(1.857cap_stem,-167.5); 
z16r=z15l; 
filldraw subpath (0,titi) of pipi
 & z12{right}
..{down}z13r..z14r..z14'{left}..
z15r{left} & z15r--z15l--z16l &
z16l{dir-34}..z14l..z13l{up}..
z8r{left} & z8r--z8 & cycle;
.endchar;

.cmchar "The Schwabacher letter C";
.beginchar("C",10.151u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);z1=(w,0.276h); penpos2(cap_stem,68);
z2l=(0.533w,-apex_o); penpos3(0.911cap_stem,45);
z3l=(0,0.442h);
penpos4(cap_hair,-60); 
z4l=(x2l-cap_stem,0.914h);
penpos5(cap_stem,-90); z5l=(x2l,h);
penpos6(1.1cap_stem,-72);
z7=(0.911w,h+apex_o);
z6l=(0.5[x5,x7],h-apex_o);
penpos8(cap_stem,0); z8r=(x2l,0.337h);
penpos10(cap_hair,-56); 
z8r-z10r=whatever*dir34; x10=x8l-0.5cap_stem;
z9=z8l-(0,cap_hair);
filldraw z1..z2r{left}..z3r{up}..
z4r{z5l-z4l} & z4r--z4l &
z4l{z4l-z5l}..z3l{down}..z2l{right}
..z1{up} & cycle;
filldraw z10l{z8r-z10r}..z9{up}--
z4l--z5l..z6l{right}..z7 & 
z7{down}..z6r{left}..z5r{dir170} & 
z5r--z8r--z10r--z10l & cycle;
.endchar;

.cmchar "The Schwabacher letter D";
.beginchar("D",13.736u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(cap_hair,-50); penpos2(1.071cap_stem,-118);
z1=(0.06w,0); z2=(0.302w,0.15h);
z3=(0.585w,-apex_o); 
penpos4(cap_hair,-41);
z4=(0.876w,0.193h); penpos5(0.5cap_stem,38);
z5r=(w,0.472h); penpos6(cap_stem,74);
penpos7(cap_stem,74); penpos8(cap_stem,74);
penpos10(0.5cap_hair,0);
z6l=(0.72w,0.675h); 
z8l-z6l=whatever*dir170;
x8l=cap_hair; x10r=x8; y10=h+apex_o;
z7=0.426[z8,z6]+(0,-apex_o);
z9=whatever[z7l,z8l]; x9=0;
filldraw z1r{dir40}..z2r{right}..z3
& z3..z4r..z5r{up}...
z7r---z8r
{z8r-z7r}..z10r{up} & z10r--z10l & z10l..
z9{down} & z9..z8l{z7r-z8r}---z7l
...z5l{down}..z4l..z2l{left}..z1l{dir220}
& z1l--z1r & cycle;
%
%
%
penpos17(cap_hair,0); z17r=z2;
penpos18(0.536cap_stem,34);
z19=(cap_hair,y5l); y18=0.464[y2l,y19];
x18=x2r-apex_o; z20=(0.203w,0.52h);
penpos21(cap_hair,-60);
z21=whatever[z7,z6]; x21=0.44w;
filldraw z17r{up}..z18r..z20{up}..
z21r{dir30} & z21r--z21l & 
z21l{dir210}..z19 & z19{right}..
z17l{down} & z17l--z17r & cycle;
%
%
%
penpos12(cap_hair,-60); penpos13(cap_hair,-60);
penpos14(cap_hair,-60); penpos11(cap_hair,-60);
penpos15(cap_stem,30); penpos16(cap_stem,30);
z16r=z12r; z15l=z13l; z14=whatever[z6,z7];
z12=(0.563w,0.387h);
z15r-z12=whatever*(z12l-z12r);
x13l=0.287w; 
z14l=whatever[z15l,z15r];
z11r=whatever[z16l,z16r]; x11=x17;
penstroke z11e--z12e;
penstroke z16e--z15e;
penstroke z13e--z14e;
z22=(z17r{up}..z18r..z20{up})
intersectionpoint ((0,y2l+cap_hair)
--(w,y2l+cap_hair));
z23=1/3[z11l,z12l];
z23'=2/3[z13l,z16l];
z24=1/3[z15r,z12r];
z25=whatever[z6l,z7l];
z25=whatever[z13r,z14r];
filldraw z22{right}..z23{z12l-z11l}
..z23'{z13-z16l} & z23'--z16--z11
--z22 & cycle;
filldraw z24{z15r-z12}..z25{z14r-z13r}
& z25--z15--z24 & cycle;
.endchar;

.cmchar "The Schwabacher letter E";
.beginchar("E",12.302u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(cap_hair,-90); z1r=(0,0.227h);
penpos2(cap_stem,0); penpos3(cap_stem,0);
penpos4(cap_hair,-90);
z2r=(0.31w,0.509h); 
z3=(x2,0.761h); y4l=h+apex_o;
x4=0.393w; 
penstroke z1e{dir31}..z2e---z3e...
z4e{dir22};
penpos5(cap_hair,-64); penpos6(cap_hair,-51);
penpos7(cap_stem,0); penpos8(cap_stem,0);
penpos9(1.607cap_stem,18);
penpos10(cap_hair,-90);
z5=(0.049w,0.12h); z6l=(x4,0.337h);
x9=x8=x7; z7l=(0.446w,y2);
y8=y3; z10r=z9r; y10l=h+apex_o;
penstroke z5e{dir31}..z6e{dir36}...z7e---
z8e...z9e;
filldraw z9l--z10l--z10r--z9l & cycle;
%
%
%
penpos11(0.6cap_hair,-90);
z11=whatever[z7r,z8r]; y11r=0.862h;
z12-z11l=whatever*dir30;
penpos14(0.679cap_stem,-90);
y14=0.589h; x14=x11; 
penpos15(whatever,-114);
x15=0.926w; y15r=y14r; y15l=y14l;
penstroke z14e--z15e;
z13=(x15l,y11r);
z17=(x12-apex_o,0.588[y13,y15l]);
z12-z11l=whatever*dir30; y12=h+apex_o;
penpos16(cap_hair,0);
z16=whatever[z14l,z15l]; x16r=x17;
filldraw z11l--z12 & z12..z13{down}
--z16r{dir222} & z16r--z16l &
z16l..z17{up}..z11r{left} &
z11r--z11l & cycle;
%
%
%
z18=(0.417w,0.3h); z19=(0.267w,0.169h);
z20=(0.834w,0.101h); z21=(0.7w,-apex_o);
penpos22(cap_hair,-50);
x22r=w; y22=0.132h;
filldraw z5r{dir31}..z19{right}..
z21{right}..z22r{dir40} & 
z22r--z22l & z22l{dir220}..z20{left}
..z18{left}..z5l{dir211} & z5l--z5r
& cycle;
.endchar;

.cmchar "The Schwabacher letter F";
.beginchar("F",9.925u#,cap_height#,2.943u#);
.adjust_fit(usual_left#,usual_right#);penpos1(2/3cap_hair,-90);
penpos2(1.286cap_stem,180);
penpos2'(1.071cap_stem,-142.5);
penpos3(cap_hair,-90);
penpos4(1.214cap_stem,42.5);
penpos5(1.125cap_stem,40);
penpos6(0.786cap_stem,38);
penpos6'(1.2cap_stem,53);
penpos7(4/3cap_hair,-127);
z1r=(0,3/2cap_hair); z2r=z1l;
y3r=-d-apex_o; x3=0.55w;
x4=0.821w; y4=-y4l; z5=(0.687w,0.368h);
z6=(0.439w,0.807h);
x7l=x5; y7l=h+apex_o;
z8=(w,y7r); z10=z8-(0,cap_stem);
z9=z10-(cap_stem,0);
z2'l=z2l; z6'r=z6r;
filldraw z2l{dir-79}..z3l{right}..
z4l{up}..z5l..z6'l{up}..z7l{right}
..z8 & z8--z10--z9 &
z9{up}..z7r{left}..z6r{down}..
z5r..z4r{down}..z3r{left}..
z2'r..z1r{left} & z1r--z1l--z2l
& cycle;
%
%
%
penpos11(2/3cap_hair,-90);
penpos12(0.786cap_stem,-114);
y11r=y12r=0.506h; x12l=w-apex_o;
x11=0.29w;
filldraw z11r--z11l & z11l{dir40}..
z6{up} & z6--z6r--(x6r,y12l)--
z12l--z12r--z11r & cycle;
.endchar;

.cmchar "The Schwabacher letter G";
.beginchar("G",13.434u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);penpos1(2cap_stem,-142);
z1l=(0.815w,0.742h);
penpos1'(cap_hair,-90); z1'l=z1r;
penpos2(1.179cap_stem,-142);
z2l=(w,0.466h); penpos3(cap_hair,77);
z3=(0.764w,0.067h); 
z4=(0.59w,-apex_o);
penpos5(1.179cap_stem,32);
z5l=(0,0.457h);
penpos6(cap_hair,-56.5);
z7=(x4-0.643cap_stem,h+apex_o);
z7-z6l=whatever*dir33.5;
penpos8(1.286cap_stem,-130);
z8r=z1l+(-apex_o,apex_o);
z6r-z8r=whatever*(z7-z8l);
filldraw z1'r{dir45}..z2r{down}..
z3r{dir207}..z5r{up}..z6r{z7-z6l} &
z6r--z8r--z8l--z7---z6l...
z5l{down}..z4{right}..z3l{dir27}..
z2l{up}..z1l{1/3[z6l,z7]-z1l}
& z1l--z1r--z1'r & cycle;
%
%
%
penpos9(cap_stem,0); penpos10(cap_stem,0);
penpos11(cap_hair,-32);
penpos12(cap_hair,-52);
x9r=x10r=x4; y9=0.3h; y10=0.681h;
z12-z8=whatever*(z7-z8l);
z12l-z10l=whatever*dir27;
z9-z11''=whatever*dir58; y11''=0;
z11=(z2{down}..z3{dir207}..z5{up})
intersectionpoint (z11''--z9);
z11'=whatever[z11,z9]; x11'=x9l;
penstroke z9e--z10e;
filldraw z11r{z9-z11}..z9r{z9r-z11'}
& z9r--z9l & z9l{down}..z11l{z11-z9}
& z11l--z11r & cycle;
filldraw z10r{up}..z12r{z12l-z10l}
& z12r--z12l--z10l--z10r & cycle;
%
%
%
penpos13(cap_hair,angle(z8l-z7));
z13r=z8l; penpos14(1.5cap_hair,-15);
x14r=w-cap_hair; y14l=h+apex_o;
penstroke z13e{z8l-z8r}..z14e{up};
.endchar;

.cmchar "The Schwabacher letter H";
.beginchar("H",11.736u#,cap_height#,2.566u#);
.adjust_fit(usual_left#,usual_right#);penpos1(1.5cap_stem,-16);
z1l=(0.637w,h); penpos2(cap_hair,-106);
z2l=whatever[z1l,z1r]; y2l=h+apex_o;
penpos3(cap_hair,-106); z3l=z1;
penstroke z2e--z3e;
penpos4(1.071cap_stem,18);
penpos5(1.071cap_stem,18);
z4l=(0,0.393h); 
z4r-z5r=whatever*dir120; x5r=0.286w;
filldraw z4l---z4r...z1r{dir59} &
z1r--z1l & z1l{dir260}..{dir208}z4l & cycle;
penstroke z5e--z4e;
%
%
%
penpos6(cap_hair,angle(z5r-z4r));
z6r=z5r; z7=(0.804w,0.503h);
z8=(x1l,0.337h); penpos9(1.071cap_stem,180);
z9r=(x7,0.147h); z10=(0.592w,-d-apex_o);
z11=(0.498w,-0.809d);
z12=(0.386w,-0.397d);
z13=(0.473w,0.043h);
z14=(x12,0.123h);
z15=(x5r,0);
filldraw z6r{z5r-z5l}..z8{right}..
z9r{down}..z11{left}..z12{up}..
z13{up}..z14{left}..z15{down}..
z10{right}..z9l{up}..z7 & z7--z6l
--z6r & cycle;
.endchar;

.cmchar "The Schwabacher letter I";
.beginchar("I",12.943u#,cap_height#,3.019u#);
.adjust_fit(usual_left#,usual_right#);
penpos1(0.786cap_stem,32);
z1l=(apex_o,0.653h);
penpos2(0.821cap_stem,29);
z2l=(0,0.715h); z4=(0.198w,0.85h);
z5=(0.321w,h+apex_o);
penpos3(cap_hair,-55); y3=y4;
z5-z3l=whatever*dir35;
penpos6(1.125cap_stem,-119);
penpos8(cap_hair,-37);
z6=(0.636w,0.822h);
z8=(0.845w,h); penpos9(cap_hair,-37);
penpos9'(cap_amount,-37);
z9'l=z8r; z9l=z9'r;
penpos10(1.304cap_stem,47);
x10r=x8; y10r=y2;
penpos11(1.107cap_stem,26);
z11r=(w,0.215h); z12=(x10l,-0.313d);
z13=(0.545w,-d-apex_o);
z14=(0.19w,-0.45d);
penpos15(cap_hair,-37); x15l=0;
y15=-0.688d; penpos16(2.25cap_stem,-131);
z16r=z15l;
filldraw z1r{dir122}..z2r{up}..
z3r..z4{right}..z6r{right}..
z8r{dir57} & z8r--z8l &
z8l{dir237}..z6l{left}..z5 &
z5---z3l...z2l{down}..z1l{dir302}
& z1l--z1r & cycle;
%
%
%
filldraw z15r{dir49}..z14{right}..
z13 & z13{dir31}..z11r{up}..z10r{up}
..z9r{dir57} & z9r--z9l &
z9l{dir237}..z10l{down}..z11l{down}
..z12{left}..z16l{dir139} &
z16l--z16r--z15r & cycle;
.endchar;

.cmchar "The Schwabacher letter J";
.beginchar("J",12.943u#,cap_height#,3.019u#);
.adjust_fit(usual_left#,usual_right#);
penpos1(0.786cap_stem,32);
z1l=(apex_o,0.653h);
penpos2(0.821cap_stem,29);
z2l=(0,0.715h); z4=(0.198w,0.85h);
z5=(0.321w,h+apex_o);
penpos3(cap_hair,-55); y3=y4;
z5-z3l=whatever*dir35;
penpos6(1.125cap_stem,-119);
penpos8(cap_hair,-37);
z6=(0.636w,0.822h);
z8=(0.845w,h); penpos9(cap_hair,-37);
penpos9'(cap_amount,-37);
z9'l=z8r; z9l=z9'r;
penpos10(1.304cap_stem,47);
x10r=x8; y10r=y2;
penpos11(1.107cap_stem,26);
z11r=(w,0.215h); z12=(x10l,-0.313d);
z13=(0.545w,-d-apex_o);
z14=(0.19w,-0.45d);
penpos15(cap_hair,-37); x15l=0;
y15=-0.688d; penpos16(2.25cap_stem,-131);
z16r=z15l;
filldraw z1r{dir122}..z2r{up}..
z3r..z4{right}..z6r{right}..
z8r{dir57} & z8r--z8l &
z8l{dir237}..z6l{left}..z5 &
z5---z3l...z2l{down}..z1l{dir302}
& z1l--z1r & cycle;
%
%
%
filldraw z15r{dir49}..z14{right}..
z13 & z13{dir31}..z11r{up}..z10r{up}
..z9r{dir57} & z9r--z9l &
z9l{dir237}..z10l{down}..z11l{down}
..z12{left}..z16l{dir139} &
z16l--z16r--z15r & cycle;
.endchar;

.cmchar "The Schwabacher letter K";
.beginchar("K",12.943u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(cap_hair,-90); z1=(1/10w,0);
penpos3(cap_stem,0); z3=(0.419w,0.5h);
z2=(x3l,0.261h); z4=(x3,0.853h);
penpos4(cap_stem,0);
penpos5(1.286cap_stem,-140);
z5r=(0,0.773h); z6=(0.131w,0.69h);
z7=(0.251w,0.828h); penpos8(cap_hair,-50);
x8r=x3r; y8=h;
filldraw z1r{right}...z3r---z4r--
z8r--z4l---z2...z1l{left} & z1l--z1r &
cycle;
filldraw z8r--z8l & z8l{z4l-z8r}..z7{left}
..z5l{dir130} & z5l--z5r & z5r{dir-50}
..z6{right}..z8r{z8r-z4l} & cycle;
%
%
%
penpos9(whatever,-90);
penpos10(cap_hair,-50);
z10=(0.681w,h); z9l=z4r;
z9r-z10r=whatever*(z9l-z10l);
penpos12(cap_hair,-50);
z12=whatever[z10l,z10r];
penpos11(whatever,-90);
z11r=z3r; z11r-z12r=whatever*(z9r-z10r);
z11l-z12l=whatever*(z9r-z10r);
penpos13(1.107cap_stem,angle(z12-z11));
penpos14(1.107cap_stem,angle(z12-z11));
z13r=z12r; z14r=z10r;
penstroke z9e--z10e;
penstroke z11e--z12e;
penstroke z13e--z14e;
%
%
%
penpos16(1.071cap_stem,30);
x16r=w; y16l=-apex_o; 
penpos15(whatever,angle(z12-z11));
z15=whatever[z11r,z12r];
z15r-z16r=whatever*dir124;
z15l-z16l=whatever*dir124;
penstroke z16e--z15e;
%
%
%
z17=(x3,1.5cap_amount); 
z18=(x3r,0.221h); 
penpos19(1.071cap_stem,45);
z19l=(0.613w,-apex_o);
filldraw z1r{right}..z17{right}..
z19l & z19l--z19r--z18 &
z18{dir220}..z1l{left} & z1l--z1r &
cycle;
.endchar;

.cmchar "The Schwabacher letter L";
.beginchar("L",10.377u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(cap_hair,-90);
penpos1'(cap_amount,-90);
penpos2(cap_hair,-90);
x1=x1'=x2=0; y1r=y1'l; y1'r=y2l;
y1'=0.135h; penpos3(whatever,28);
penpos6(whatever,28);
x3r=0.349w; x6l=0.505w;
x3l=x3r-.95cap_stem; x6r=x6l+.95cap_stem;
y3r=0.472h; 
z6=whatever[z3l,z3r]+(0,-u);
penpos4(whatever,18);
penpos7(whatever,0);
x4l=x3l; x4r=x3r; x7l=x6l; x7r=x6r;
y4r=0.779h; z7=whatever[z4l,z4r];
penpos5(cap_hair,-70); 
x5=x7r; y5=0.45[h,y7];
penpos8(cap_hair,-57);
x8=0.753w; y8=h;
penpos9(cap_stem,-148);
penpos10(0.893cap_stem,-137);
penpos11(cap_hair,-53);
z9=(0.287w,0.178h);
x10=x8; y10r=-apex_o;
x11r=w; y11r=y9r;
%
%
%
penstroke z1e{dir23}...z3e---z4e...
z5e{right};
penstroke z1e{dir23}...z6e---z7e...
z8e{right};
penstroke z2e{dir23}..z9e{right}
..z10e{right}..z11e{dir37};
.endchar;

.cmchar "The Schwabacher letter M";
.beginchar("M",17.736u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(1.25cap_stem,47);
penpos2(1.25cap_stem,47);
penpos3(1.107cap_stem,42.5);
penpos4(1.143cap_stem,47);
penpos5(0.5cap_hair,0);
penpos6(cap_hair,-43);
penpos7(cap_hair,-43);
x2l=0; y1l=-apex_o; 
z2-z1=whatever*dir143;
y2l=0.141h; z3r=(0.207w,0.638h);
x4l=0; y4r=h+apex_o; z5l=z4r;
z6r=z1; z7r=whatever[z1l,z1r];
penpos11(cap_stem,0);
x11=0.494w; x9=0.423[x3r,x11l];
x9=x7;
penstroke z6e--z7e;
penstroke z1e--z2e;
filldraw z2r{z2r-z2l}..z3r{up}..
z5r{up} & z5r--z5l--z4l &
z4l{dir-56}..z3l{down}..z2l{z2l-z2r}
& z2l--z2r & cycle;
%
%
%
penpos8(1.214cap_hair,-90);
z8=(x3,y3r); y11=0.709h;
z10=(x11l,h+apex_o);
z9-z8r=whatever*(z10-z8l);
filldraw z8r---z9...z11l{down} &
z11l--z11r & z11r{up}..z10{left}
& z10--z8l--z8r & cycle;
penpos12(whatever,30);
y12l=-apex_o; x12l=x11l; x12r=x11r;
penstroke z12e--z11e;
%
%
%
penpos13(1.25cap_hair,-90);
z13r=z11r; %penpos24(1.036cap_stem,-129);
%penpos25(1.036cap_stem,-129);
penpos20(2/3cap_hair,-52);
penpos18(whatever,11);
penpos19(whatever,31);
x18l=x19l=0.768w;
x18r=x19r=x18l+cap_stem;
y18l=0.187h; y19l=0.54h;
z20r=(w,0.856h); penpos22(cap_hair,-61);
z22r=(w,0.184h); penpos21(cap_hair,-61);
y21=0; x21r=0.749w; 
filldraw z21r--z22r--z22l &
z22l{z21r-z22r}...z18r{up}---
z19r..z20r{dir34} & z20r--z20l &
z20l{dir214}...z19l{down}---z18l..
z21l{dir235} & z21l--z21r & cycle;
%
%
%
penpos24(1.036cap_stem,-128);
penpos25(1.036cap_stem,-128);
z25=(0.789w,0.911h);
z24=z25-(0.714cap_stem,0);
z26-z25r=whatever*dir-34;
x26=w; 
z17=(z18{up}---z19...z20{dir34})
intersectionpoint (z25r--z26);
filldraw z13r---z24r...z25r---
z17 & z17--z20--z20l &
z20l{dir214}..{z25r-z17}z25l...
z24l---z13l & z13l--z13r & cycle;
.endchar;

.cmchar "The Schwabacher letter N";
.beginchar("N",14.868u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(1.107cap_stem,55);
z1l=(0.348w,-apex_o); 
z3=(0.056w,0.184h); 
z2=(0.231w,0.089h);penpos4(cap_hair,0);
z4l=(0.152w,0.445h);
penpos5(1.25cap_stem,49);
x5l=0; y5r=h+apex_o; penpos6(0.5cap_hair,0);
z7=(0.129w,0.945h);
penpos15(cap_hair,-35);
z15-z14=whatever*dir55;
penpos14(cap_hair,-35);
z14r=z1; x15=0.576w; z8'=(x15r,0);
penpos16(cap_stem,-123.5);
x16=0.497w; y16l=h+apex_o;
penpos10(1.071cap_stem,-135);
z10r=(0.797w,0.656h);
filldraw z1l--z1r{z3-z1l}..z4r{up}
& z4r--z4l & z4l{down}..z3 &
flex(z3,z2,z1l) & cycle;
penstroke z14e--z15e;
penstroke z4e{up}..z16e{right}..
z10e{dir-45};
z6l=z5r;
%z8'-z7=whatever*dir-58; y8'=0;
z8=(z7--z8') intersectionpoint 
(z4{up}..z16{right});
filldraw z4l{up}..z5l--z5r--z6r
& z6r...z7---z8 & z8--z4--z4l &
cycle;
%
%
%
penpos11(cap_hair,-45); penpos12(cap_hair,-45);
z11r=z10; z12-z11=whatever*dir45;
x12r=w; z13=(0.891w,0.862h);
filldraw z16--z16l{right}..z13..
z12l{z12-z11} & z12l--z12r--z11r
--z11l & z11l..z16{left} & cycle;
%
%
%
penpos19(2/3cap_hair,0);
penpos20(cap_hair,-90);
z19r=(x11r,-apex_o);
x20=w; z20-z19=whatever*dir40;
z21=(0.694[x19,x20],y20r);
z18=(0.701w,0.301h);
z22=(0.827w,0.485h);
filldraw z19r--z20r--z20l &
z20l..z21{left}..z22{up}..z12r{z12r-z11r}
& z12r--z12l--z10r & 
z10r{z10r-z10l}..z18{down}..z19l &
z19l--z19r & cycle;
.endchar;

.cmchar "The Schwabacher letter O";
.beginchar("O",12.528u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(cap_hair,115);
penpos8(cap_stem,0); x8r=0.536w;
y8=0.451h; penpos9(whatever,25);
x9l=x8l; x9r=x8r; y9r=h+apex_o;
z1r=z9l; penpos2(0.929cap_stem,-154);
z2r=(0,0.5h); penpos3(1.071cap_stem,-128.5);
x3=x8r; y3r=-apex_o;
penpos4(0.946cap_stem,29);
z4r=(w,0.54h); z5=(0.735w,0.837h);
penpos5(1.107cap_stem,44);
penpos6(1.286cap_stem,47);
z6r=(x5,h+apex_o); 
penpos10(cap_hair,-43);
penpos11(cap_hair,-43);
z11l=z6; z11-z10=whatever*dir47;
x10=x8;
z8r-z7'=whatever*dir49;
y7'=0; penpos7(cap_hair,-41);
z7r=(z7'--z8r) intersectionpoint 
(z2{down}..z3{right});
z3''=whatever[z2,z4]; x3''=x8r;
z3'=whatever[z3'',(w,cap_hair)];
penpos3'(cap_hair,angle((w,cap_hair)-z3''));
x3'=0.819w;
penstroke z1e{z9l-z9r}..z2e{down}
..z3e{right}..z3'e..z4e{up}...z5e{z6-z5}...
z6e;
filldraw z7r--z8r--z9r--z9l---z8l
...z7l{z7r-z8r} & z7l--z7r & cycle;
penstroke z10e--z11e;
.endchar;

.cmchar "The Schwabacher letter P";
.beginchar("P",11.849u#,cap_height#,3.2u#);
.adjust_fit(usual_left#,usual_right#);
penpos2(cap_stem,0); x2=0.226w;
y2=0.08h; penpos1(0.1cap_hair,0);
z1=(x2,-d-apex_o);
penpos3(whatever,35);
penpos4(1.25cap_stem,47);
x4l=0; y4r=h+apex_o;
x3l=x2l; x3r=x2r; y3r=0.761h;
penstroke z1e--z2e---z3e{up}...z4e;
%
%
%
penpos5(whatever,-122);
x5r=0; x5l=x2l;
y5r=y2+2/3cap_hair; 
penpos6(cap_stem,-140);
z6r=(0.534w,-apex_o);
penpos8(cap_hair,-50);
z8r=whatever[z6l,z6r]; x8=0.752w;
penpos9(1.107cap_stem,36);
z9r=(w,y3l); z10=(0.536w,h+apex_o);
z11=(0.513w,0.831h);
penpos12(cap_hair,90); x12=x1;
y12=2/3h; z7=(0.634w,0.156h);
z5'=(x2l,0.135h);
filldraw z5r{right}..z6r &
z6r---z6l...z8r..z9r{up}..z10 &
z10--z12r--z12l & z12l{z10-z12r}..
z11{right}..z9l{down}..z8l..z7{left}..
z5l{left} & z5l--z5r & cycle;
.endchar;

.cmchar "The Schwabacher letter Q";
.beginchar("Q",12.528u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#+1.208u#);
penpos1(cap_hair,115);
penpos8(cap_stem,0); x8r=0.536w;
y8=0.451h; penpos9(whatever,25);
x9l=x8l; x9r=x8r; y9r=h+apex_o;
z1r=z9l; penpos2(0.929cap_stem,-154);
z2r=(0,0.5h); penpos3(1.071cap_stem,-128.5);
x3=x8r; y3r=-apex_o;
penpos4(0.946cap_stem,29);
z4r=(w,0.54h); z5=(0.735w,0.837h);
penpos5(1.107cap_stem,44);
penpos6(1.286cap_stem,47);
z6r=(x5,h+apex_o); 
penpos10(cap_hair,-43);
penpos11(cap_hair,-43);
z11l=z6; z11-z10=whatever*dir47;
x10=x8;
z8r-z7'=whatever*dir49;
y7'=0; penpos7(cap_hair,-41);
z7r=(z7'--z8r) intersectionpoint 
(z2{down}..z3{right});
z3''=whatever[z2,z4]; x3''=x8r;
z3'=whatever[z3'',(w,cap_hair)];
penpos3'(cap_hair,angle((w,cap_hair)-z3''));
x3'=0.819w;
penstroke z1e{z9l-z9r}..z2e{down}
..z3e{right}..z3'e..z4e{up}...z5e{z6-z5}...
z6e;
filldraw z7r--z8r--z9r--z9l---z8l
...z7l{z7r-z8r} & z7l--z7r & cycle;
penstroke z10e--z11e;
%
%
%
penpos12(cap_hair,-30);
x12=0.3[x5,x3']; y12=cap_hair;
penpos13(1.179cap_stem,-118);
z13l=(w+1.408u,0.288h);
penstroke z12e{dir60}..z13e{dir-28};
.endchar;

.cmchar "The Schwabacher letter R";
.beginchar("R",12.792u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(1.143cap_stem,33);
penpos2(1.143cap_stem,33);
z1l=(0.342w,-apex_o);
penpos16(cap_hair,-57);
penpos17(cap_hair,-57);
z16r=z1; z17-z16=whatever*dir33;
x17=0.625w; x2=0.242w; z2-z1=whatever*dir133;
penpos3(cap_hair,-47);
z3r=z2l; penpos4(cap_stem,22);
z4=(0.451w,0.583h);
penpos5(1.5cap_stem,48);
x5l=0; y5r=h+apex_o;
penstroke z16e--z17e;
penstroke z1e--z2e;
filldraw z3r---z2r...z4r{up}..z5r{left}
& z5r--z5l & z5l{right}..z4l{down}..
z3l{z2l-z2r} & z3l--z3r & cycle;
z7=(0.687w,h+apex_o);
z7-z7'=whatever*dir45;
x7'=0; z6=(z7'--z7) intersectionpoint 
(z4r{up}..z5r{left});
z6'=z6-(0,cap_hair);
penpos8(cap_stem,-147);
penpos12(cap_hair,90); 
z8=(0.802w,0.804h); x12=x4; y12=0.552h;
penpos8'(1.179cap_stem,-135);
z8'l=z8l;
penpos13(cap_stem,17);
z13=(0.708w,0.506h);
penpos14(1.464cap_stem,25);
x14r=w; y14l=-apex_o; penpos15(2/3cap_hair,-90);
z15r=z14r; z13'-z13r=whatever*(z13l-z14l);
z13'=whatever[z4r,z8];
z11=(0.735w,0.712h);
penpos11'(cap_hair,angle(z13'-z13r));
z11'r=z11; 
filldraw z6--z7..z8'l{down}..z11'l
{dir201} & z11'l---z13r...z15l{right}
& z15l--z15r--z14l---z13l...z12l{dir187}
& z12l--z12r & z12r{dir7}..z11..
z8'r{up}..z6'{dir208} & z6'--z6 &
cycle;
.endchar;

.cmchar "The Schwabacher letter S";
% made in Athens, Greece on july 21, 1990
.beginchar("S",13.887u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(cap_hair,148);
penpos2(cap_hair,180);
penpos3(cap_hair,-90);
penpos4(1.107cap_stem,25);
penpos5(1.125cap_stem,72);
penpos6(1.071cap_stem,84);
penpos7(0.875cap_stem,48);
penpos8(1.036cap_stem,-136);
penpos9(1.161cap_stem,-111);
penpos10(cap_hair,-42); penpos11(cap_hair,-42);
z1=(0.109w,0.799h); z2r=(0,0.552h);
z3r=(0.533w,-apex_o); z4r=(w,0.345h);
z5=(0.685w,0.497h); z6=(0.402w,0.451h);
z7=whatever[z2r,z1l]; x7r=x1l;
z8l=(0.413w,h+apex_o); z9r=(0.832w,0.683h);
y10r=y9l; x10r=w-apex_o;
z10r-z11r=whatever*dir48; y11=y5;
penstroke z1e..z2e{down}..z3e{right}
..z4e{up}..z5e{left}..z6e{left}..
z7e{up}..z8e{right}..z9e{right};
penstroke z11e--z10e;
filldraw z9l{right}..{z10-z11}z10l--z10r--
z9r--z9l & cycle;
.endchar;

.cmchar "The Schwabacher letter T";
.beginchar("T",11.811u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(1.893cap_stem,51); 
y1l=-apex_o; x1r=w-1.3apex_o;
penpos2(cap_hair,-61);
z2l=z1r; %x2r=w; 
penpos3(cap_hair,angle(z2r-z2l));
z2-z3=whatever*dir45; y3r=-apex_o;
penpos4(cap_stem,90);
x4=0.633w; y4l=-apex_o;
penpos5(3/4cap_stem,30); 
z5r=(0.217w,0.5h); 
penpos6(cap_hair,-51);
z6r=(0.396w,h-apex_o-cap_stem);
penstroke z3e--z2e;
penstroke z4e{left}...
z5e{up}..z6e{dir39};
filldraw z4r{right}..z2l{z2-z3} &
z2l--z3r--z4l--z4r & cycle;
%
%
%
penpos10(cap_stem,-90); 
penpos11(whatever,-115);
x10=0.115w; y10l=h-apex_o;
z11l=(w,h+apex_o); penpos10'(cap_stem,-90);
y11r=y10r; y10'=y10; x10'=x3;
penstroke z10e---z10'e...z11e;
penpos13(cap_hair,-34);
x13l=0; y13=0.732h;
z12=whatever[z10r,z11r]; x12=x5;
filldraw z13r{dir56}..z12{right} &
z12--z10l--z13l--z13r & cycle;
%
%
%
penpos9(cap_stem,0); 
penpos8(cap_stem,0);
z9=(0.511w,y10r); z8=(x9,0.381h);
penstroke z8e--z9e;
penpos7(0.5cap_hair,-52);
z8r-z7r=whatever*dir38;
y7r=y4r; z14=(x8l,0.1[y8,y4r]);
filldraw z7r--z8r--z8l---z14...
z7l{z7r-z8r} & z7l--z7r & cycle;
.endchar;

.cmchar "The Schwabacher letter U";
.beginchar("U",11.811u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(cap_stem,0); penpos2(cap_stem,0);
penpos3(2.375cap_stem,0); penpos5(cap_stem,0);
penpos6(cap_stem,0);
x1=w-1.188cap_stem=x2=x3=x4;
y1=0.152h; y2=0.72h; y3=0.848h;
y4=h+apex_o; 
penstroke z1e--z2e;
filldraw z2r{up}..z3r{right} &
z3r--z4--z3l & z3l{right}..z2l{down}
& z2l--z2r & cycle;
penpos9(cap_hair,90); penpos10(whatever,40);
z10l=(x1l,-apex_o); y10r=y1;
x9=w-0.04w; z9r=whatever[z10l,z10r];
filldraw z10l--z9l
& z9l--z9r & z9r{left}..(x1r,0.274h){up}
--z1l--z10l & cycle;
%
%
%
penpos11(1.148cap_stem,39);
penpos12(1.148cap_stem,39);
x5r=0.288w; y5=y1; x6=x5; y6=0.817h;
z11l=(0.393w,-apex_o); 
z12l=z5l; penpos13(cap_hair,-51);
penpos14(cap_hair,-51); 
z14r=whatever[z11l,z11r];
z13r=z11; x14l=x1l;
penstroke z13e--z14e;
penstroke z11e--z12e;
penstroke z5e--z6e;
penpos8(cap_hair,90); z8l=(w-x9,y9l);
filldraw z11l{z12l-z11l}..z8l
& z8l--z8r & z8r{right}..(x5l,0.274h){up}
--z5r--z11l & cycle;
penpos7(whatever,55);
z7l=(0,y6); y7r=h+apex_o;
filldraw z6r{up}..z7r{dir138} &
z7r--z7l & z7l{right}..(x6l,y2) &
(x6l,y2)--z6r & cycle;
.endchar;

.cmchar "The Schwabacher letter V";
.beginchar("V",12.189u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(1.143cap_stem,43);
penpos2(1.5cap_stem,25);
penpos3(1.2cap_stem,16);
penpos4(1.393cap_stem,50);
z1l=(0.526w,-apex_o);
z2=(0.251w,0.186h);
z3=(0.303w,0.494h); x4l=0; y4r=h+apex_o;
%filldraw z1l{dir138}..z2l{left}
%& z2l{dir68}..z3l{up}..z4l{dir150}
%& z4l--z4r & z4r{dir-30}..z3r{down}
%..z2r & z2r{dir-13}..z1r{dir-33} &
%z1r--z1l & cycle;
penstroke z2e..z3e{up}..z4e{dir150};
penstroke z1e{dir143}..z2e{left};
%
%
%
penpos5(1.232cap_stem,46);
z5r=(w,0.61h); z6=(0.557w,0.82h);
z8=(0.681w,h+apex_o);
penpos7(cap_hair,-55);
z8-z7l=whatever*dir37;
x7r=x3; 
filldraw z1l{dir30}..z5r{up}..z8 &
z8--z7l--z7r & z7r{z8-z7l}..z6{right}
..z5l{down}..z1r{dir210} & 
z1r--z1l & cycle;
.endchar;

.cmchar "The Schwabacher letter W";
.beginchar("W",18.189u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(cap_hair,-56);
penpos2(cap_hair,-56);
z2r=(0.245w,-apex_o);
z1r-z2r=whatever*dir34;
y1l=0.216h-cap_hair; 
penstroke z2e--z1e;
penpos3(cap_hair,90); 
y3l=y1l; x3=x2r-1.393cap_stem;
penpos6(cap_stem,0); z6r=(x2r,y3r);
z4=0.592[z2l,z1l]; 
z5-z4=whatever*(z3l-z2r); x5=x6r;
z7=(x6l,0.36h); 
penpos8(cap_stem,0); x8=x6; 
y8=0.704h; 
penpos9(1.179cap_stem,57);
x9l=0; y9r=h+apex_o;
penstroke z6e---z8e...z9e;
filldraw z2r--z3l--z3r..z7{up}
& z7--(x6r,y7)--
z5--z4--z2r & cycle;
%
%
%
penpos11(cap_hair,-90); z12=(0.444w,h+apex_o);
z11r=z8r; z13=(.763[x11,x12],0.802h);
penpos16(cap_stem,0); x16=0.548w;
y16=0.104h; penpos14(cap_stem,0);
z14=(x16,y8); z14'=(x14r,y13);
filldraw z11r{z12-z11l}...z13{right}
...z14l---z16l--z16r---z14'...z12{dir170}
& z12--z11l--z11r & cycle;
penpos18(1.179cap_stem,40);
penpos18'(1.179cap_stem,40);
penpos17(1.179cap_stem,40);
z17l=z16l; z18-z17=whatever*dir-23;
y18l=-apex_o; 
z18'=0.4[z17,z18]+(apex_o,apex_o);
penstroke z18e..z18'e..z17e;
%
%
%
penpos15(cap_hair,-90); z22=(x15-x11+0.444w,h+apex_o);
z15r=z14r; z21=(.763[x15,x22],0.802h);
penpos20(1.071cap_stem,35); x20r=w;
y20=0.552h; penpos19(cap_hair,-60);
z19r=z18l; 
penstroke z19e{dir30}..z20e{up};
filldraw z20l{up}..z21{left}..
z15r{z15l-z22} & z15r--z15l--z22 &
z22..z20r{down} & z20r--z20l & cycle;
.endchar;

.cmchar "The Schwabacher letter X";
.beginchar("X",11.472u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(1.179cap_stem,-130);
penpos2(1.179cap_stem,-122);
penpos3(0.8cap_hair,-17);
penpos4(cap_hair,-22);
penpos5(1.2cap_hair,-23);
penpos6(1.107cap_stem,-120);
penpos7(1.125cap_stem,-124);
z1r=(0,0.065h); z2r=(0.128w,-apex_o);
z3=(0.414w,0.28h); 
z4=(0.49w,0.509h); z5=(0.625w,0.765h);
z6l=(0.878w,h+apex_o);
z7l=(w,0.945h);
penstroke z1e..z2e{right}..z3e..z4e
..z5e..z6e{right}..z7e;
%
%
%
penpos8(cap_hair,90); penpos9(1.393cap_stem,40);
penpos10(0.964cap_stem,82);
penpos11(1.036cap_stem,42);
penpos12(cap_stem,0);
penpos13(1.036cap_stem,43);
penpos14(1.321cap_stem,43);
x9r=w; y9l=-apex_o; z8l=z9r; z10l=z9l;
z11=(0.612w,0.186h); z12=z4;
z13=(0.395w,0.835h); z14r=(0.217w,h+apex_o);
penstroke z10e{left}..z11e..z12e..z13e..
z14e{left};
filldraw z9r--z8r..z10r{left}--z10l
--z9r & cycle;
%
%
%
penpos15(cap_hair,0);
penpos21(0.8cap_stem,-90);
penpos17(cap_hair,0);
z21=z4; y19=y18=y21r; y16=y20=y21l;
y15=y21-0.821cap_stem; y17=y21+0.643cap_stem;
x15=0.118w; x16=0.204w; x17=0.928w;
x18=0.836w; x19=0.263w; x20=3/4w;
filldraw z15r...z19---z18--z17r--
z17l...z20---z16--z15l--z15r & cycle;
.endchar;

.cmchar "The Schwabacher letter Y";
.beginchar("Y",12.302u#,cap_height#,2.34u#);
.adjust_fit(usual_left#,usual_right#);
z1=(0.868w,-d); penpos2(cap_stem,7);
z4=(0.788w,-0.274d);
z2=(0.856w,-0.613d);
penpos5(5/3cap_hair,120);
penpos6(1.2cap_hair,158);
penpos7(1.2cap_hair,-55);
penpos8(1.161cap_stem,22);
z5l=(0.294w,0.171h);
z6r=(0,0.035h); 
z7r=(0.221w,-0.6d);
z8r=(w,0.683h); z11=(0.767w,h+apex_o);
penpos12(cap_stem,0); z12=(0.485w,0.421h);
x9=0.325[x12r,x8l]; y9=0.835h;
penpos10(cap_hair,128.5);
z11-z10r=whatever*dir38.5;
x10l=x12;
penstroke z5e{left}..z6e{down}..z7e{right}
..z8e{up};
filldraw z1{right}..z2r{up}..z5r{left}
& z5r--z5l & z5l{right}..z4{down}..
z2l{down}..z1{right} & cycle;
filldraw z8l{up}..z9{left}..z10l
{z10r-z11} & z10l--z10r--z11 &
z11..z8r{down} & z8r--z8l & cycle;
%
%
%
penpos13(whatever,33); x13r=x12r;
z13=(z7{right}..z8{up})
intersectionpoint ((x12,-d)--z12);
penpos14(cap_hair,90); 
penpos15(1.464cap_stem,31);
y15r=h+apex_o; x15l=0; z14r=z15l;
filldraw z13r---z12r...z15r{left}
& z15r--z15l--z14l & 
z14l{dir20}...z12l---z13l--z13r &
cycle;
.endchar;

.cmchar "The Schwabacher letter Z";
.beginchar("Z",7.736u#,cap_height#,1.132u#);
.adjust_fit(usual_left#,usual_right#);
z1=(apex_o,0.866h); 
penpos2(1.125cap_stem,-135);
penpos3(1.125cap_stem,-135);
penpos4(1.125cap_stem,-135);
penpos5(cap_hair,-45);
z5r=(w,0.89h); 
penpos6(whatever,-90); x6=0.205w;
z5r-z6r=whatever*dir45;
z5l-z6l=whatever*dir45;
z4l=z5l; z3=(0.722w,0.826h);
z2l=(0.312w,h+apex_o);
penstroke z2e{right}..z3e{right}..
z4e;
penstroke z6e--z5e;
filldraw z1..z2l{right}--z2r{left}
..z1 & cycle;
%
%
%
z10=(0,0.128h); z12=(0.327w,y10);
z13=(0.263w,apex_o);
penpos9(cap_hair,-90);
z9r=(0.439w,-d); 
penpos8(1.071cap_stem,36);
z8r=(w,0.317h);
penpos7(cap_stem,68);
z7r=whatever[z6r,z5r]; 
y7l=y6l; penstroke z9e{right}..
z8e{up}..z7e{left};
filldraw z6r{z5r-z6r}..z7l{right}
& z7l--z7r--z6r & cycle;
filldraw z9r{left}..z10{up}..z12{down}
..z13{down}..z9l{right} & z9l--z9r &
cycle;
.endchar;
