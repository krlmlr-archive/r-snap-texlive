 % su-low.mf -Input-File Zeichensatz


%                                                                                                                 a
makewi(7,10ut,ang,an_bot);
beginchar("a",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=y3=y4=mid;
y2=y6=bas;
y5=y7=an_bot;
makex(1,0ut,ang,y1);
makex(2,0ut,ang,y2);
makex(3,  5ut,ang,y3);
makex(4,7.5ut,ang,y4);
makex(5,5.5ut,ang,y5);
makex(6,7ut,ang,y6);
makex(7, 10ut,ang,y7);
%
draw z1{dir mi_gamma}..z2{right}..z3{dir beta};
draw z3{z5-z4}..z4{z4-z5};
draw z4{z5-z4}..z5..z6{right}..{dir gamma}z7;
labels(range 1 thru 7);
endchar;


%                                                                                                                 a-Umlaut
makewi(7,10ut,ang,an_bot);
beginchar(228,wi/hppp,21ut#,0ut);
pickup su_pen;
y1=y3=y4=mid;
y2=y6=bas;
y5=y7=an_bot;
y8=y9=3/7(top-mid)+mid;
makex(1,0ut,ang,y1);
makex(2,0ut,ang,y2);
makex(3,  5ut,ang,y3);
makex(4,7.5ut,ang,y4);
makex(5,5.5ut,ang,y5);
makex(6,7ut,ang,y6);
makex(7, 10ut,ang,y7);
makex(8,2ut,ang,y8);
makex(9,5.5ut,ang,y9);
%
draw z1{dir mi_gamma}..z2{right}..z3{dir beta};
draw z3{z5-z4}..z4{z4-z5};
draw z4{z5-z4}..z5..z6{right}..{dir gamma}z7;
draw z8..z8+(1ut,3ut);draw z9..z9+(1ut,3ut);
labels(range 1 thru 9);
endchar;

%                                                                                                                 b
makewi(10,7.5ut,ang,an_bot);
beginchar("b",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=y9=y11=mid;
y2=Z_anf;
y3=y5=I_anf;
y4=top;
y6=y8=an_mid;
y7=bas;
y10=an_top;

makex(1,  0ut,ang,y1);
makex(2, 2.7ut,ang,y2);
makex(3, 3.5ut,ang,y3);
makex(4, 2ut,ang,y4);
makex(5, -.8ut,ang,y5);
makex(6,-2.5ut,ang,y6);
makex(7, 2ut,ang,y7);
makex(8, 5.6ut,ang,y8);
makex(9, 5ut,ang,y9);
makex(10, 7.5ut,ang,y10);
%
draw z1{dir gamma}..z3{up}..z4{left}..z5{dir mi_beta}..z6{dir mi_beta}..z7{right}..z8{up}..z9;
draw z9..z10{right};
labels(range 1 thru 10);
endchar;

%                                                                                                                 c
makewi(5,8.5ut,ang,an_bot);
beginchar("c",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=y3=y5=mid;
y2=an_top;
y4=bas;
makex(1,0ut,ang,y1);
makex(2,2.0ut,ang,y2);
makex(3,  4ut,ang,y3);
makex(4,-2ut,ang,y4);
makex(5,8.5ut,ang,y5);
%
draw z1..z2{right}..z3{up};
draw z1{z4-z1}..z4;
draw z4..z5;
labels(range 1 thru 5);
endchar;


%                                % c fuer ch-Ligatur                                                                                                      c
makewi(5,8.5ut,ang,an_bot);
beginchar(30,wi/hppp,14ut#,0ut);
pickup su_pen;
y1=y3=y5=mid;
y2=an_top;
y4=bas;
makex(1,0ut,ang,y1);
makex(2,2.0ut,ang,y2);
makex(3,  4ut,ang,y3);
makex(4,-2ut,ang,y4);
makex(5,8.5ut,ang,y5);
%
%draw z1..z2{right}..z3{up};
draw z1{z4-z1}..z4;
draw z4..z5;
labels(range 1 thru 5);
endchar;


%                                                                                                                 d
makewi(5,10ut,ang,an_bot);
beginchar("d",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=y5=mid;
y34=I_anf;
y2=bas;
y3=13/14*mid;
y4=top;
makex(1,0ut,ang,y1);
makex(2,0ut,ang,y2);
makex(3,5ut,ang,y3);
makex(34,.5ut,ang,y34);
makex(4,2.3ut,ang,y4);
makex(5,10ut,ang,y5);
%
draw z1{dir mi_gamma}..z2{right}..z3..z4{left}..z34..{right}z3..z5;
labels(range 1 thru 7,34);
endchar;

%                                                                                                                 e
makewi(5,10.5ut,ang,mid);
beginchar("e",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=y3=y5=mid;
y2=y4=bas;
makex(1,0ut,ang,y1);
makex(2,-2ut,ang,y2);
makex(3,2.7ut,ang,y3);
makex(4,0.5ut,ang,y4);
makex(5,10.5ut,ang,y5);
%
draw z1..z2;
draw z3..z4;
draw z4..z5;
labels(range 1 thru 7);
endchar;

%                                                                                                                 f
makewi(8,4ut,ang,an_mid);
beginchar("f",wi/hppp,21ut#,7ut#);
pickup su_pen;
y1=mid;
y2=Z_anf;
y3=y5=I_anf;
y4=top;
y6=bot;
y7=an_mid+1/7(mid-an_mid);
y8=an_mid;
makex(1,0ut,ang,y1);
makex(2, 2ut,ang,y2);
makex(3,3.3ut,ang,y3);
makex(4,2ut,ang,y4);
makex(5,0ut,ang,y5);
makex(6,-4.5ut,ang,y6);
makex(7,-3.5ut,ang,y7);
makex(8,4ut,ang,y8);
%
draw z1{dir gamma}..z3{up}..z4{left}..z5{z6-z5}..z6;
draw z7..z8{dir delta};
labels(range 1 thru 8);
endchar;

%                                                                                                                 g
makewi(8,14ut,ang,mid);
beginchar("g",wi/hppp,14ut#,7ut#);
pickup su_pen;
y1=y3=y4=y8=mid;
y2=bas;
y5=g_bogen;
y6=bot;
y7=1/2z_bogen;
y23=5/7mid;
makex(1,0ut,ang,y1);
makex(2,.5ut,ang,y2);
makex(3, 5 ut,ang,y3);
makex(4,7.7  ut,ang,y4);
makex(5,  4.3ut,ang,y5);
makex(6, 2.2 ut,ang,y6);
makex(7, 2 ut,ang,y7);
makex(8, 14 ut,ang,y8);
makex(23,4.7ut,ang,y23);
%
draw z1{dir mi_gamma}..z2{right}..z23..z3{up};
draw z3{down}..z4{dir beta};
draw z4{dir mi_beta}..z5{dir mi_beta}..z6{left}..z7{dir gamma}..z8;
labels(range 1 thru 8,23);
endchar;


%                                                                                                                 h
makewi(9,6.5ut,ang,mid);
beginchar("h",wi/hppp,21ut#,7ut#);
pickup su_pen;
y1=y9=mid;
y2=Z_anf;
y3=y5=I_anf;
y4=top;
y6=G_bogen;
y8=bas;
y7=bot;
makex(1,0ut,ang,y1);
makex(2,2 ut,ang,y2);
makex(3,3ut,ang,y3);
makex(4,2ut,ang,y4);
makex(5,-.4ut,ang,y5);
makex(6,-3.7ut,ang,y6);
makex(7,-6ut,ang,y7);
makex(8, -5.7ut,ang,y8);
makex(9, 6.5ut,ang,y9);
%
draw z1{dir gamma}..z3{dir beta}..z4{left}..z5{dir mi_beta}..z6{dir mi_beta}..
          z7{left}..z8{dir gamma}..z9{dir gamma};
          makelabel("T",z9);
labels(range 1 thru 9);
endchar;

%                                                                                                                 i
makewi(3,8ut,ang,mid);
beginchar("i",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=y3=mid;
y2=bas;
y4=4/7(top-mid)+mid;
y5=6/7(top-mid)+mid;
makex(1,0ut,ang,y1);
makex(2,-2.7ut,ang,y2);
makex(3,8ut,ang,y3);
makex(4,1ut,ang,y4);
makex(5,1.5ut,ang,y5);
%
draw z1..z2 ;
draw z2..z3;
draw z4{dir gamma}..z5{dir beta};
labels(range 1 thru 7);
endchar;

%                                                                                                                 j
makewi(5,7ut,ang,mid);
beginchar("j",wi/hppp,21ut#,7ut#);
% makelabel(str[w/hppp],(w/hppp,-10ut));
pickup su_pen;
y1=y5=mid;
y2=g_bogen;
y4=1/2z_bogen;
y3=bot;
y6=4/7(top-mid)+mid;
y7=6/7(top-mid)+mid;
makex(1,0ut,ang,y1);
makex(2,-3.2ut,ang,y2);
makex(3,-5.5ut,ang,y3);
makex(4,-5ut,ang,y4);
makex(5,7ut,ang,y5);
makex(6,.8ut,ang,y6);
makex(7,1.3ut,ang,y7);
%
draw z1{dir mi_beta}..z2{dir mi_beta}..z3{left}..z4{dir gamma}..z5{dir gamma};
draw z6{dir gamma}..z7{dir beta};
labels(range 1 thru 7);
endchar;

%                                                                                                                 k
makewi(7,5ut,ang,an_mid);
beginchar("k",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=mid;
y2=y8=top;
y3=Z_anf;
y4=y6=an_mid+(3/14 an_mid);
y5=bas;
y7=an_mid;
y9=I_anf;
makex(1,0ut,ang,y1);
makex(2,3.7 ut,ang,y2);
makex(3,3ut,ang,y3);
makex(4,1.3ut,ang,y4);
makex(5,0ut,ang,y5);
makex(6,-3ut,ang,y6);
makex(7,5ut,ang,y7);
makex(8,7.2ut,ang,y8);
makex(9,7.8ut,ang,y9);
%
draw z1{dir gamma}..z2{dir beta};
draw z2{dir mi_beta}..z5;
draw z3{dir beta+dir 10}..z8{right}..z9..z4;
draw z6..z7{dir delta};
labels(range 1 thru 9);
endchar;

%                                                                                                                 l
makewi(9,10ut,ang,an_bot);
beginchar("l",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=y9=mid;
y2=Z_anf;
y3=y5=I_anf;
y4=top;
y6=1/2 an_bot+an_bot;
y7=bas;
y8=an_bot-3/14 an_bot;
makex(1,0ut,ang,y1);
makex(2,2ut,ang,y2);
makex(3,3.3ut,ang,y3);
makex(4,1.8ut,ang,y4);
makex(5,0ut,ang,y5);
makex(6,-2.5ut,ang,y6);
makex(7,-1.3ut,ang,y7);
makex(8,1ut,ang,y8);
makex(9,10ut,ang,y9);
%
draw z1{dir gamma}..z3{up}..z4{left}..z5{dir mi_beta}..z6{dir mi_beta}..
                        z7{right}..z8{dir gamma}..z9{dir gamma};
labels(range 1 thru 9);
endchar;

%                                                                                                                 m
makewi(7,24ut,ang,mid);
beginchar("m",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=y3=y5=y7=mid;
y2=y4=y6=bas;
makex(1,0ut,ang,y1);
makex(2,-2ut,ang,y2);
makex(3,8ut,ang,y3);
makex(4,5.7ut,ang,y4);
makex(5,16ut,ang,y5);
makex(6,13.7ut,ang,y6);
makex(7,24ut,ang,y7);
%
draw z1..z2 ; draw z2..z3; draw z3..z4; draw z3..z4; draw z4..z5;
draw z5..z6; draw z6..z7;
labels(range 1 thru 7);
endchar;

%                                                                                                                 n
makewi(5,16.2ut,ang,mid);
beginchar("n",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=y3=y5=mid;
y2=y4=bas;
makex(1,0ut,ang,y1);
makex(2,-2.5ut,ang,y2);
makex(3,8.2ut,ang,y3);
makex(4,6ut,ang,y4);
makex(5,16.2ut,ang,y5);
%
draw  z1..z2;
draw z2..z3;
draw z3..z4;
draw z4..z5;
labels(range 1 thru 5);
endchar;


%                                                                                                                 o
makewi(6,8ut,ang,an_top);
beginchar("o",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=y5=mid;
y6=an_top;
y2=y4=2an_bot;
y3=bas;
makex(1,0ut,ang,y1);
makex(2,-3.8ut,ang,y2);
makex(3,-.5ut,ang,y3);
makex(4,4ut,ang,y4);
makex(5,3.3ut,ang,y5);
makex(6,8ut,ang,y6);
%
draw z1{dir mi_gamma}..z2{dir mi_beta}..z3{right}..z4{dir beta}..z5;
draw z5..z6{right};
labels(range 1 thru 8);
endchar;


%                                                                                                                 o-Umlaut
makewi(6,8ut,ang,an_top);
beginchar(246,wi/hppp,21ut#,0ut);
pickup su_pen;
y1=y5=mid;
y6=an_top;
y2=y4=2an_bot;
y3=bas;
y7=y8=3/7(top-mid)+mid;
makex(1,0ut,ang,y1);
makex(2,-3.8ut,ang,y2);
makex(3,-.5ut,ang,y3);
makex(4,4ut,ang,y4);
makex(5,3.3ut,ang,y5);
makex(6,8ut,ang,y6);
makex(7,1.5ut,ang,y7);
makex(8,4ut,ang,y8);
%
draw z1{dir mi_gamma}..z2{dir mi_beta}..z3{right}..z4{dir beta}..z5;
draw z5..z6{right};
draw z7..z7+(1ut,3ut);draw z8..z8+(1ut,3ut);
labels(range 1 thru 8);
endchar;

%                                                                                                                 p
makewi(9,13ut,ang,mid);
beginchar("p",wi/hppp,14ut#,7ut#);
pickup su_pen;
y1=y4=y9=mid;
y2=mid-2/7(mid);
y5=mid-3/14(mid);
y6=y8=3/2an_bot;
y7=bas;
y3=bot;
makex(1,0ut,ang,y1);
makex(2,-.7ut,ang,y2);
makex(3,-3.5ut,ang,y3);
makex(4,3ut,ang,y4);
makex(5,5.5ut,ang,y5);
makex(6,-.4ut,ang,y6);
makex(7,1.7ut,ang,y7);
makex(8,3.8ut,ang,y8);
makex(9,13ut,ang,y9);
%
draw z1..z3;
draw z2{dir beta}..z4{right}..z5{dir mi_beta}..z6;
draw z6{right}..z7{right}..z8{dir gamma}..z9{dir gamma};
labels(range 1 thru 9);
endchar;

%                                                                                                                 q
makewi(7,16.1ut,ang,mid);
beginchar("q",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=y3=y4=y7=mid;
y2=y6=bas;
y5=bot;
makex(1,0ut,ang,y1);
makex(2,1ut,ang,y2);
makex(3,5.3ut,ang,y3);
makex(4,8.5ut,ang,y4);
makex(5,5.0ut,ang,y5);
makex(6,6.1ut,ang,y6);
makex(7,16.1ut,ang,y7);
%
draw z1{dir mi_gamma}..z2{right}..z3{dir beta};
draw z3..z4{dir gamma};
draw z4..z5;
draw z6..z7;
labels(range 1 thru 7);
endchar;

%                                                                                                                 r
makewi(5,10.3ut,ang,an_top);
beginchar("r",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=y4=mid;
y5=an_top;
y2=y3=bas;
makex(1,0ut,ang,y1);
makex(2,-2.5ut,ang,y2);
makex(3,4.5ut,ang,y3);
makex(4,5.7ut,ang,y4);
makex(5,10.3ut,ang,y5);
%
draw z1..z2{dir mi_gamma};
draw z2{dir gamma}..z3{dir mi_beta};
draw z3{dir mi_alpha}..z4{dir beta};
draw z4..z5{right};
labels(range 1 thru 5);
endchar;

%                                                                                                                 s-am Ende
makewi(4,2ut,ang,an_bot);             % rundes s
beginchar(28,wi/hppp,21ut#,0ut);
pickup su_pen;
y1=an_top;
y2=mid;
y3=an_top-1/14 mid;
y4=y6=an_bot;
y5=bas;
y7=I_anf;
y8=top;
y9=Z_anf;
%
makex(1,-.5ut,ang,y1);
makex(2,2ut,ang,y2);
makex(3,4.1ut,ang,y3);
makex(4,2ut,ang,y4);
makex(5,-.5ut,ang,y5);
makex(6,-2.2ut,ang,y6);
makex(8,3.2ut,ang,y8);
makex(7,0ut,ang,y7);
makex(9,4.5ut,ang,y9);
%
draw z1{dir gamma}..z2{right}..z3{dir mi_beta}..z4..z5{left}..z6{dir beta}..
                                                                                        z7{dir beta}..z8{right}..z9{dir mi_beta};
labels(range 1 thru 9);
endchar;

%                                                                                                                 s
makewi(5,9.2ut,ang,an_bot);
beginchar("s",wi/hppp,21ut#,7ut#);
pickup su_pen;
y1=y5=mid;
y2=top;
y3=bot;
y4=bas;
makex(1,0ut,ang,y1);
makex(2,3.3ut,ang,y2);
makex(3,-1.5ut,ang,y3);
makex(4,0ut,ang,y4);
makex(5,9.2ut,ang,y5);
%
draw z1{dir gamma}..z2{dir beta};
draw z2{dir mi_beta}..z3;
draw z4..z5;
labels(range 1 thru 5);
endchar;


%                                                                                                                 sz
makewi(10,6ut,ang,an_bot);
beginchar(255,wi/hppp,21ut#,7ut#);
pickup su_pen;
y1=mid;
y2=top;
y3=bot;
y4=y6=Z_anf;
y5=15/14I_anf;
y7=mid;
y8=an_mid;
y9=1/2an_bot;
y10=an_bot;
makex(1,0ut,ang,y1);
makex(2,3.3ut,ang,y2);
makex(3,-1.5ut,ang,y3);
makex(4,2.7ut,ang,y4);
makex(5,5.5ut,ang,y5);
makex(6,8ut,ang,y6);
makex(7,4ut,ang,y7);
makex(8,8ut,ang,y8);
makex(9,1.5ut,ang,y9);
makex(10,6ut,ang,y10);
%
draw z1{dir gamma}..z2{dir beta};
draw z2{dir mi_beta}..z3;
draw z4{dir beta}..z5{right}..z6{dir mi_beta}..z7{left};
draw z7{right}..z8{dir mi_beta}..z9;
labels(range 1 thru 10);
endchar;

%                                                                                                                 t
makewi(5,4ut,ang,an_mid);
beginchar("t",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=mid;
y2=top;
y3=bas;
y4=an_mid+1/14an_mid;
y5=an_mid;
makex(1,0ut,ang,y1);
makex(2,4ut,ang,y2);
makex(3,-.7ut,ang,y3);
makex(4,-3ut,ang,y4);
makex(5,4ut,ang,y5);
%
draw z1{dir gamma}..z2{dir beta};
draw z2..z3;
draw z4..z5{dir delta};
labels(range 1 thru 5);
endchar;

%                                                                                                                 u
makewi(5,16.2ut,ang,mid);
beginchar("u",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=y3=y5=mid;
y2=y4=bas;
y6=5/7(top-mid)+mid;
y7=4/7(top-mid)+mid;
makex(1,0ut,ang,y1);
makex(2,-2.5ut,ang,y2);
makex(3,8.2ut,ang,y3);
makex(4,6ut,ang,y4);
makex(5,16.2ut,ang,y5);
makex(6,2.2ut,ang,y6);
makex(7,8.5ut,ang,y7);
%
draw z1..z2;
draw z2..z3;
draw z3..z4;
draw z4..z5;
draw z6..z7{dir gamma};
labels(range 1 thru 7);
endchar;


%                                                                                                                 u-Umlaut
makewi(5,16.2ut,ang,mid);
beginchar(252,wi/hppp,21ut#,0ut);
pickup su_pen;
y1=y3=y5=mid;
y2=y4=bas;
y6=3/7(top-mid)+mid;
y7=3/7(top-mid)+mid;
makex(1,0ut,ang,y1);
makex(2,-2.5ut,ang,y2);
makex(3,8.2ut,ang,y3);
makex(4,6ut,ang,y4);
makex(5,16.2ut,ang,y5);
makex(6,2.5ut,ang,y6);
makex(7,6.5ut,ang,y7);
%
draw z1..z2;
draw z2..z3;
draw z3..z4;
draw z4..z5;
draw z6..z6+(1ut,3ut);draw z7..z7+(1ut,3ut);
labels(range 1 thru 7);
endchar;
%                                                                                                                 v
makewi(6,15.5ut,ang,an_bot);
beginchar("v",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=y4=y6=mid;
y2=y7=bas;
y3=5/6an_top;
y5=an_mid+1/7an_mid;
makex(1,0ut,ang,y1);
makex(2,-2.5ut,ang,y2);
makex(3,2.9ut,ang,y3);
makex(4,6.3ut,ang,y4);
makex(5,8ut,ang,y5);
makex(6,15.5ut,ang,y6);
makex(7,4.3ut,ang,y7);
%
draw z1..z2{dir mi_gamma};
draw z2{dir gamma}..z7;
draw z7..z3{dir beta}..z4{right}..z5{dir mi_beta}..z7;
draw z7..z6;
labels(range 1 thru 7);
endchar;

%                                                                                                                 w
makewi(9,22ut,ang,mid);
beginchar("w",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=y3=y7=y9=mid;
y2=y4=y5=bas;
y6=y8=an_mid+2/7(mid-an_mid);
makex(1,0ut,ang,y1);
makex(2,-2.5ut,ang,y2);
makex(3,8ut,ang,y3);
makex(4,5.5ut,ang,y4);
makex(5,12ut,ang,y5);
makex(6,10.5ut,ang,y6);
makex(7,14.2ut,ang,y7);
makex(8,15.7ut,ang,y8);
makex(9,22ut,ang,y9);
%
draw z1..z2;
draw z2..z3;
draw z3..z4{dir mi_gamma};
draw z4{dir gamma}..z5;
draw z5..z6{dir beta}..z7{right}..z8{dir mi_beta}..z5;
draw z5..z9;
labels(range 1 thru 9);
endchar;

%                                                                                                                 x
makewi(12,15.3ut,ang,mid);
beginchar("x",wi/hppp,14ut#,7ut#);
pickup su_pen;
y1=y7=y12=mid;
y2=y6=y8=an_mid;
y3=y5=an_bot;
y4=bas;
y9=y11=g_bogen;
y10=bot;
makex(1,0ut,ang,y1);
makex(2,0.8ut,ang,y2);
makex(3,2.2ut,ang,y3);
makex(4,4.2ut,ang,y4);
makex(5,6.2ut,ang,y5);
makex(6,7.3ut,ang,y6);
makex(7,4.8ut,ang,y7);
makex(8,0ut,ang,y8);
makex(9,-0.9ut,ang,y9);
makex(10,2ut,ang,y10);
makex(11,4.8ut,ang,y11);
makex(12,15.3ut,ang,y12);
%
draw z1{down}..z2..z3..z4{right}..z5..z6{dir beta}..z7{left}..
                         z8..z9..z10{right}..z11;
draw z5..z12;
labels(range 1 thru 12);
endchar;

%                                                                                                                 y
makewi(10,17ut,ang,mid);
beginchar("y",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=y5=y10=mid;
y2=y3=bas;
y4=y6=an_mid+2/7(mid-an_mid);
y7=G_bogen;
y8=bot;
y9=z_bogen;
makex(1,0ut,ang,y1);
makex(2,-2.6ut,ang,y2);
makex(3,3.5ut,ang,y3);
makex(4,2.8ut,ang,y4);
makex(5,6.2ut,ang,y5);
makex(6,8ut,ang,y6);
makex(7,7ut,ang,y7);
makex(8,5ut,ang,y8);
makex(9,4.3ut,ang,y9);
makex(10,17ut,ang,y10);
%
draw z1..z2{dir mi_gamma};
draw z2{dir gamma}..z3;
draw z3..z4{up}..z5{right}..z7{dir mi_beta}..z8{left}..z9{dir gamma}..z10;
labels(range 1 thru 10);
endchar;

%                                                                                                                 z
makewi(8,13.5ut,ang,mid);
beginchar("z",wi/hppp,14ut#,7ut#);
pickup su_pen;
y1=y8=mid;
y2=an_top-2/5(an_top-an_mid);
y3=an_mid;
y4=bas;
y5=bot;
y6=g_bogen;
y7=z_bogen;
makex(1,0ut,ang,y1);
makex(2,5ut,ang,y2);
makex(3,1ut,ang,y3);
makex(4,5ut,ang,y4);
makex(5,1.3ut,ang,y5);
makex(6,-.6ut,ang,y6);
makex(8,13.5ut,ang,y8);
makex(7,1ut,ang,y7);
%
draw z1{right}..z2{dir mi_beta}..z3{left};
draw z3{right}..z4{dir mi_beta}..z5{left}..z7{dir gamma}..z8;
labels(range 1 thru 8);
endchar;

%                                                                                                                 tz
makewi(12,18ut,ang,mid);
beginchar(27,wi/hppp,21ut#,7ut#);
pickup su_pen;
y1=y5=y12=mid;
y2=top;
y3=bas;
y4=an_mid;
y6=an_mid+3/14(mid-an_mid);
y7=an_mid-3/10(an_mid-an_bot);
y8=an_bot;
y9=y11=G_bogen;
y10=bot;
makex(1,0ut,ang,y1);
makex(2,3.3ut,ang,y2);
makex(3,-.4ut,ang,y3);
makex(4,0ut,ang,y4);
makex(5,7.5ut,ang,y5);
makex(6,10ut,ang,y6);
makex(7,6.5ut,ang,y7);
makex(8,9.6ut,ang,y8);
makex(9,9ut,ang,y9);
makex(10,6ut,ang,y10);
makex(11,4.5ut,ang,y11);
makex(12,18ut,ang,y12);
%
draw z1{dir gamma}..z2{dir beta};
draw z2..z3;
draw z4{dir gamma}..z5{right}..z6{dir mi_beta}..z7{left};
draw z7{right}..z8..z9{dir mi_beta}..z10{left}..z11{dir gamma}..z12;
labels(range 1 thru 12);
endchar;
