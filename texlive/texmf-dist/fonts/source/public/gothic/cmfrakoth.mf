.cmchar "The Fraktur comma";
.beginchar(",",5u#,x_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(0.23570*(x_height+2apex_o),45);
penpos2(0.23570*(x_height+2apex_o),45);
penpos3(0.23570*(x_height+2apex_o),45);
penpos4(0.23570*(x_height+2apex_o),45);
penpos5(0.23570*(x_height+2apex_o),45);
penpos6(0.23570*(x_height+2apex_o),45);
x1l=x3l=x5l=0.5w;
z2r=z3l; z4r=z5l;
z2-z1=whatever*dir135;
z4-z3=whatever*dir135;
z6-z5=whatever*dir135;
y1l=-apex_o; y6r=x_height+apex_o;
%penstroke z1e--z2e;
penpos10(0.11785*(x_height+2apex_o),-30); 
z10l=(x2l-apex_o,-1.3y2r);
filldraw z10l--z2l+(2hair,0){up}..{down}
z1r--z10r--z10l & cycle;
last_char:=currentpicture;
.endchar;


.cmchar "Apostrophe";
.beginchar("'",3.5u#,asc_height#,0);
currentpicture:=last_char
    shifted (0,1.2x_height);
.endchar;

.cmchar "Opening Gaensefuesschen";
.beginchar(open_gaens,9u#,x_height#,0);
currentpicture:=last_char;
addto currentpicture also last_char shifted (4u,0);
.endchar;

.cmchar "Closing Gaensefuesschen";
.beginchar(oct"042",9u#,asc_height#,0);
currentpicture:=(last_char rotatedaround((3u,0),180))
    shifted (0,1.2x_height);
addto currentpicture also (last_char rotatedaround((3u,0),180))
    shifted (4u,1.2x_height);
.endchar;

.cmchar "Reverse apostrophe";
.beginchar("`",5u#,asc_height#,0);
currentpicture:=(last_char rotatedaround((3u,0),180))
    shifted (0,1.2x_height);
.endchar;

.cmchar "The Fraktur colon";
.beginchar(":",5u#,x_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(0.23570*(x_height+2apex_o),45);
penpos2(0.23570*(x_height+2apex_o),45);
penpos3(0.23570*(x_height+2apex_o),45);
penpos4(0.23570*(x_height+2apex_o),45);
penpos5(0.23570*(x_height+2apex_o),45);
penpos6(0.23570*(x_height+2apex_o),45);
x1l=x3l=x5l=0.5w;
z2r=z3l; z4r=z5l;
z2-z1=whatever*dir135;
z4-z3=whatever*dir135;
z6-z5=whatever*dir135;
y1l=-apex_o; y6r=x_height+apex_o;
penstroke z1e--z2e;
penstroke z5e--z6e;
.endchar;

.cmchar "The Fraktur semicolon";
.beginchar(";",5u#,x_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(0.23570*(x_height+2apex_o),45);
penpos2(0.23570*(x_height+2apex_o),45);
penpos3(0.23570*(x_height+2apex_o),45);
penpos4(0.23570*(x_height+2apex_o),45);
penpos5(0.23570*(x_height+2apex_o),45);
penpos6(0.23570*(x_height+2apex_o),45);
x1l=x3l=x5l=0.5w;
z2r=z3l; z4r=z5l;
z2-z1=whatever*dir135;
z4-z3=whatever*dir135;
z6-z5=whatever*dir135;
y1l=-apex_o; y6r=x_height+apex_o;
penstroke z1e--z2e;
penstroke z5e--z6e;
penpos10(hair,-30); z10l=(x2l,-y2r);
filldraw z10l{z1r-z10r}..z2l{z2-z1}
& z2l--z1r--z10r--z10l & cycle;
.endchar;

.cmchar "The Fraktur point";
.beginchar(".",5u#,x_height#,0);
.adjust_fit(usual_left#,usual_right#);
pointpoint;
.endchar;


.cmchar "Hachek (check) accent";
if (dc_layout>0): xchar_code:=oct"007";
        else: xchar_code:=oct"024";
        fi;
.beginchar(xchar_code,9u#,.75[x_height#,min(asc_height#,2x_height#)],0);
z100=(0.5w,x_height); hacek(100);
.endchar;
 
.cmchar "Breve accent";
if (dc_layout>0): xchar_code:=oct"010";
        else: xchar_code:=oct"025";
        fi;
.beginchar(xchar_code,9u#,min(asc_height#,2x_height#),0);
z100=(0.5w,x_height); breve(100);
.endchar;
 
.cmchar "Macron (bar) accent";
if (dc_layout>0): xchar_code:=oct"011";
        else: xchar_code:=oct"026";
        fi;
numeric macron_breadth#; macron_breadth#=.2[vair#,stem#];
.beginchar(xchar_code,6u#,.4[x_height#,asc_height#]+macron_breadth#,0);
italcorr h#*slant-.75u#;
.adjust_fit(0,0);
numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
pickup if serifs: crisp.nib else: fine.nib fi;
pos1(2/3stem,70); pos2(2/3stem,70);
top y1r=top y2r=h+o; lft x1r=w-rt x2l=hround 1.25u;
filldraw stroke z1e--z2e;  % bar
penlabels(1,2); .endchar;
 
.cmchar "Cedilla accent";
if (dc_layout>0): xchar_code:=oct"013";
        else: xchar_code:=oct"030";
        fi;
.beginchar(xchar_code,9u#,0,.875desc_depth#);
z100=(0.5w,0); cedilla(100);
.endchar;
 
.cmchar "Ogonek accent";
if (dc_layout>0): xchar_code:=oct"014";
beginchar(xchar_code,9u#,0,.875desc_depth#);
z100=(0.5w,0); ogonek(100);
endchar;
fi;

.cmchar "L-bar accent";
if unknown l_width#:
 l_width#:=3lower_u#; fi  % nominal width of `l'
ligtable oct"040": "l" kern -l_width#,
 "L" kern -l_width#;
.beginchar(oct"040",l_width#,x_height#,0);
z100=(0,0.5cap_height); ellbar(100);
.endchar;
 
if (dc_layout>0): xchar_code:=oct"012";
        else: xchar_code:=oct"137";
        fi;
iff ligs>0: .cmchar "Dot accent";
.beginchar(xchar_code,5u#,2x_height#,0);
z100=(0.5w,x_height);
pointaccent(100);
.endchar;
 
if (dc_layout>0): xchar_code:=oct"005";
        else: xchar_code:=oct"175";
        fi;
iff ligs>0: .cmchar "Long Hungarian umlaut accent";
.beginchar(xchar_code,9u#,min(asc_height#,2x_height#),0);
z100=(0.5w,x_height); hungarum(100);
.endchar;
 
if (dc_layout>0): xchar_code:=oct"004";
        else: xchar_code:=oct"177";
        fi;
iff ligs>0: .cmchar "umlaut accent";
.beginchar(xchar_code,9u#,min(asc_height#,2x_height#),0);
z100=(0.5w, x_height);
umlaut(100);
.endchar;
 
if (dc_layout>0): xchar_code:=oct"000";
        else: xchar_code:=oct"022";
        fi;
iff ligs>0: .cmchar "grave accent";
.beginchar(xchar_code,9u#,min(asc_height#,2x_height#),0);
z100=(0.5w, x_height);
grave(100);
.endchar;
 
if (dc_layout>0): xchar_code:=oct"001";
        else: xchar_code:=oct"023";
        fi;
iff ligs>0: .cmchar "acute accent";
.beginchar(xchar_code,9u#,min(asc_height#,2x_height#),0);
z100=(0.5w, x_height);
acute(100);
.endchar;
 
if (dc_layout>0): xchar_code:=oct"002";
        else: xchar_code:=oct"136";
        fi;
iff ligs>0: .cmchar "hat accent";
.beginchar(xchar_code,9u#,min(asc_height#,2x_height#),0);
z100=(0.5w, x_height);
hat(100);
.endchar;
 
.cmchar "Tilde (squiggle) accent";
if (dc_layout>0): xchar_code:=oct"003";
        else: xchar_code:=oct"176";
        fi;
.beginchar(xchar_code,9u#,min(asc_height#,10/7x_height#+.5dot_size#),0);
z100=(0.5w,x_height); tilde(100);
.endchar;



.cmchar "En dash";
.beginchar(en_dash,9u#,x_height#,0);
italcorr .61803x_height#*slant+.5u#;
.adjust_fit(0,0);
pickup rule.nib;
 pos1(vair,90); pos2(vair,90);
top y1r=top y2r=vround(.61803h+.5vair); lft x1=-eps; rt x2=w+eps;
filldraw stroke z1e--z2e;  % bar
penlabels(1,2); .endchar;

.cmchar "Em dash";
.beginchar(em_dash,18u#,x_height#,0);
italcorr .61803x_height#*slant+.5u#;
.adjust_fit(letter_fit#,letter_fit#);
pickup rule.nib; pos1(vair,90); pos2(vair,90);
top y1r=top y2r=vround(.61803h+.5vair); lft x1=-eps; rt x2=w+eps;
filldraw stroke z1e--z2e;  % bar
penlabels(1,2); .endchar;

.cmchar "Hash mark (number sign)";
if monospace: compute_spread(.6x_height#,.7x_height#);
else: compute_spread(.45x_height#,.55x_height#); fi
.beginchar("#",15u#,asc_height#,asc_depth#);
italcorr (math_axis#+.5(spread#+rule_thickness#))*slant-.5u#;
.adjust_fit(0,0);
pickup rule.nib; lft x1=hround u-eps; x3=x1; x2=x4=w-x1;
y1=y2; y3=y4; y1-y3=spread; .5[y1,y3]=math_axis;
draw z1--z2;  % upper bar
draw z3--z4;  % lower bar
lft x6=hround 3u; rt x7=hround(w-3u); x5-x6=x7-x8;
x8=good.x if monospace: .6 else: .5 fi\\w;
top y5=top y7=h+eps; bot y6=bot y8=-d-eps;
y15=y1; z15=whatever[z5,z6]; y36=y3; z36=whatever[z5,z6];
y27=y2; z27=whatever[z7,z8]; y48=y4; z48=whatever[z7,z8];
draw z5--if x5>x6+1:(good.x(x15+.5),y1)--(good.x(x15-.5),y1)
 --(good.x(x36+.5),y3)--(good.x(x36-.5),y3)--fi\\z6;  % left diagonal
draw z7--if x7>x8+1:(good.x(x27+.5),y2)--(good.x(x27-.5),y2)
 --(good.x(x48+.5),y4)--(good.x(x48-.5),y4)--fi\\z8;  % right diagonal
labels(1,2,3,4,5,6,7,8,15,27,36,48); .endchar;

.cmchar "Per cent sign";
.beginchar("%",15u#,
  body_height#,body_height#-asc_height#);
italcorr if hefty: .4asc_height#*slant-.5u# else: h#*slant-u# fi;
.adjust_fit(0,0); pickup fine.nib;
numeric left_curve,right_curve;
left_curve=hround 5/6[fudged.hair,fudged.stem];
right_curve=max(fine.breadth,hround(fudged.hair if hefty:-2stem_corr fi));
pos1(vair,90); pos2(left_curve,180); pos3(vair,270); pos4(right_curve,360);
top y1r=h; lft x2r=hround u; rt x4r=hround(.5w-1.5u);
bot y3r=floor(if monospace: .7 else: .5 fi\\ asc_height);
x1=x3=.5[x2,x4]; y2=y4=.5[y1,y3];
filldraw stroke pulled_super_arc.e(1,2)(superpull)
 & pulled_super_arc.e(2,3)(superpull);  % left half of upper bowl
filldraw stroke super_arc.e(3,4) & super_arc.e(4,1); % right half of upper bowl
pos5(vair,90); pos6(left_curve,180); pos7(vair,270); pos8(right_curve,360);
bot y7r=-d; rt x8r=hround(w-u); lft x6r=hround(.5w+1.5u);
top y5r=vround(if monospace: .3 else: .5 fi\\ asc_height);
x5=x7=.5[x6,x8]; y6=y8=.5[y5,y7];
filldraw stroke pulled_super_arc.e(5,6)(superpull)
 & pulled_super_arc.e(6,7)(superpull);  % left half of lower bowl
filldraw stroke super_arc.e(7,8) & super_arc.e(8,5); % right half of lower bowl
pickup rule.nib; top y9=h; bot y10=-d;
if hefty: x9=good.x(x5-eps); x10=good.x(x1+eps);
 draw z9--z10;  % diagonal
else: rt x9=hround(w-2.5u); lft x10=hround 2.5u; draw z9--z10;  % diagonal
 pickup fine.nib; pos9(rule_thickness,angle(z9-z10)+90);
 pos11(vair,angle(z1r-z4r)-90); pos12(vair,angle(z9-z10)+90);
 path p; p=super_arc.r(1,4); z11r=point 2/3 of p; z12r=z9r;
 filldraw stroke z11e{direction 2/3 of p}...{z9-z10}z12e; fi  % link
penlabels(1,2,3,4,5,6,7,8,9,10,11,12); .endchar;

.cmchar "Left parenthesis";
.beginchar("(",7u# if monospace: -u# fi,body_height#,paren_depth#);
italcorr body_height#*slant-.5u#;
.adjust_fit(0,0); pickup fine.nib;
pos1(vair,0); pos2(.75[hair,stem],0); pos3(vair,0);
rt x1r=rt x3r=hround(w-u); lft x2l=hround(x1-4u if monospace: +4/3u fi);
top y1=h; y2=.5[y1,y3]=math_axis;
filldraw stroke z1e{3(x2e-x1e),y2-y1}...z2e
 ...{3(x3e-x2e),y3-y2}z3e;  % arc
penlabels(1,2,3); .endchar;

.cmchar "Right parenthesis";
.beginchar(")",7u# if monospace: -u# fi,body_height#,paren_depth#);
italcorr math_axis#*slant-.5u#;
.adjust_fit(0,0); pickup fine.nib;
pos1(vair,0); pos2(.75[hair,stem],0); pos3(vair,0);
lft x1l=lft x3l=hround u; rt x2r=hround(x1+4u if monospace: -4/3u fi);
top y1=h; y2=.5[y1,y3]=math_axis;
filldraw stroke z1e{3(x2e-x1e),y2-y1}...z2e
 ...{3(x3e-x2e),y3-y2}z3e;  % arc
penlabels(1,2,3); .endchar;

.cmchar "Asterisk";
.beginchar("*",9u#,
  if low_asterisk:math_axis#+.5x_height# else: body_height# fi,0);
italcorr h#*slant-.75u#;
.adjust_fit(0,0);
numeric ast_flare; ast_flare=hround .7[thin_join,stem];
x0=.5w; y0=h-.5x_height;
for d=-150 step 60 until 150: z[d]=z0+.5dir d xscaled 7.5u yscaled x_height;
 numeric theta; theta=angle(z[d]-z0);
 fill z0+.5(0,-thin_join)rotated theta
  ---z[d]+.5(-ast_flare,-ast_flare)rotated theta
  ..z[d]..z[d]+.5(-ast_flare,ast_flare)rotated theta
  ---z0+.5(0,thin_join)rotated theta--cycle; endfor  % diagonal at angle |d|
labels(0,[-150],[-90],[-30],30,90,150); .endchar;

.cmchar "Plus sign";
beginarithchar("+"); pickup rule.nib;
x1=x2=good.x .5w; top y1=h+eps; .5[y1,y2]=math_axis;
lft x3=hround u-eps; x4=w-x3; y3=y4=math_axis;
draw z1--z2;  % stem
draw z3--z4;  % crossbar
labels(1,2,3,4); .endchar;

.cmchar "Virgule (slash)";
.beginchar("/",9u#,body_height#,paren_depth#);
italcorr body_height#*slant-.5u#;
.adjust_fit(0,0); pickup rule.nib;
rt x1=hround(w-u)+eps; top y1=h+eps;
lft x2=hround u-eps; bot y2=-d-eps;
draw z1--z2;  % diagonal
penlabels(1,2); .endchar;

.cmchar "Backslash";
.beginchar("\",9u#,body_height#,paren_depth#);
italcorr body_height#*slant-.5u#;
.adjust_fit(0,0); pickup rule.nib;
rt x1=hround(w-u)+eps; top y1=h+eps;
lft x2=hround u-eps; bot y2=-d-eps;
draw (z1--z2) reflectedabout ((4.5u,0),(4.5u,u));  % diagonal
penlabels(1,2); .endchar;

.cmchar "Equals sign";
compute_spread(.45x_height#,.55x_height#);
.beginchar("=",14u#,v_center(spread#+rule_thickness#));
italcorr h#*slant-.5u#;
.adjust_fit(0,0); pickup rule.nib;
lft x1=hround u-eps; x3=x1; x2=x4=w-x1;
y1=y2; y3=y4; y1-y3=spread; .5[y1,y3]=math_axis;
draw z1--z2;  % upper bar
draw z3--z4;  % lower bar
labels(1,2,3,4); .endchar;

.cmchar "Left bracket";
numeric wd#; wd#=max(5u#,6u#);
.beginchar("[",wd#,body_height#,paren_depth#);
italcorr body_height#*slant;
.adjust_fit(0,0);
numeric top_thickness,side_thickness;
if hefty: top_thickness=vair;
 side_thickness=max(crisp.breadth,stem-2stem_corr);
else: top_thickness=side_thickness=rule_thickness; fi;
pickup crisp.nib; pos1(side_thickness,0); pos2(side_thickness,0);
top y1=h; bot y2=-d; lft x1l=lft x2l=hround(2.5u-.5side_thickness)-1-eps;
filldraw stroke z1e--z2e;  % stem
pos3(top_thickness,90); pos4(top_thickness,90);
pos5(top_thickness,90); pos6(top_thickness,90);
x3=x5=x1l; rt x4=rt x6=ceiling(w-.4u)+eps; y3r=y4r=y1; y5l=y6l=y2;
filldraw stroke z3e--z4e;  % upper bar
filldraw stroke z5e--z6e;  % lower bar
penlabels(1,2,3,4,5,6); .endchar;

.cmchar "Right bracket";
numeric wd#; wd#=max(5u#,6u#);
.beginchar("]",wd#,body_height#,paren_depth#);
italcorr body_height#*slant-2u#+.5if hefty:stem# else:rule_thickness# fi;
.adjust_fit(0,0);
numeric top_thickness,side_thickness;
if hefty: top_thickness=vair;
 side_thickness=max(crisp.breadth,stem-2stem_corr);
else: top_thickness=side_thickness=rule_thickness; fi;
pickup crisp.nib; pos1(side_thickness,0); pos2(side_thickness,0);
top y1=h; bot y2=-d; rt x1r=rt x2r=hround(w-2.5u+.5side_thickness)+1+eps;
filldraw stroke z1e--z2e;  % stem
pos3(top_thickness,90); pos4(top_thickness,90);
pos5(top_thickness,90); pos6(top_thickness,90);
x3=x5=x1r; lft x4=lft x6=floor .4u-eps; y3r=y4r=y1; y5l=y6l=y2;
filldraw stroke z3e--z4e;  % upper bar
filldraw stroke z5e--z6e;  % lower bar
penlabels(1,2,3,4,5,6); .endchar;


cmchar "Exclamation point";
beginchar("!",5u#,asc_height#,0);
italcorr asc_height#*slant-2u#-.5width_adj#+.5dot_size#;
adjust_fit(0,0);
pointpoint;
pickup tiny.nib; pos103(dot_size,0); pos104(dot_size,90);
lft x103l=hround(.5w-.5dot_size); bot y104l=0; z103=z104;  % dot
numeric bot_width;
bot_width=if hefty:max(hround .8dot_size,fine.breadth) else: hair fi;
pickup fine.nib; pos101(dot_size,0); pos102(bot_width,0);
x101=x102=x103; bot y102=.25[top y104r,x_height]+1;
if square_dots: top y101=h;
 filldraw stroke z101e--z102e;  % stem
else: top z100=(x101,h+o); y101+.5dot_size=h+o;
 filldraw z101r...z100...z101l---z102l--z102r---cycle; fi  % stem and bulb
penlabels(0,1,2,3,4); endchar;

cmchar "Question mark";
beginchar("?",8.5u#,asc_height#,0);
italcorr .8asc_height#*slant;
adjust_fit(0,0);
pointpoint;
pickup tiny.nib; pos107(dot_size,0); pos108(dot_size,90);
lft x107l=hround(.5w-.25u-.5dot_size); bot y108l=0; z107=z108;   % dot
numeric bot_width;
bot_width=if hefty:max(hround .8dot_size,fine.breadth) else: hair fi;
pickup fine.nib; pos102(vair,90); pos103(curve,0);
pos104(vair,-90); pos105(bot_width,0); pos106(bot_width,0);
x102=x104=x105=x106=x107; rt x103r=hround(w-u); bot y106=1+.25[top y108r,x_height];
top y102r=h+oo; y103=.75[y106,y102]; y104=.5[y106,y102]; y105=.1[y106,y102];
{{interim superness:=more_super;
filldraw stroke pulled_super_arc.e(102,103)(superpull)
 & subpath (0,1) of super_arc.e(103,104) .. z105e---z106e\\}};  % arc and stem
if serifs: pos101(hair,180); pos100(flare,180);
 lft x101r=hround u; y101=y103; bulb(102,101,100);  % bulb
else: pos101(Vround 5/7[vair,flare],110);
 lft x101r=hround u; top y101r=vround .9[y106,top y102r];
 filldraw stroke term.e(102,101,left,101,104); fi  % terminal
penlabels(0,1,2,3,4,5,6,7,8); endchar;


cmchar "Ampersand";
beginchar("&",14u#,asc_height#,0);
italcorr x_height#*slant-serif_fit# - if serifs: .4u# else: 1.5u# fi;
adjust_fit(0,serif_fit#);
pickup tiny.nib; pos2(slab,-90); x2=4u; bot y2r=-o;
if not hefty:(x,y2l)=whatever[z2r,(w-5u,x_height)]; x2l:=x; fi
if serifs: pos0(fudged.hair,0);
 rt x0r+jut=hround(w-.9u); top y0=x_height;
 pos1(fudged.hair,0); z1=whatever[z0,(.6[x0,x2],0)];
 y1=max(y0-1.5bracket-.2x_height,2/3x_height);
 filldraw stroke z0e---z1e...{left}z2e;  % short diagonal
else: pickup fine.nib; pos1(.25[slab,flare],-15); rt x1r=hround(w-2u);
 y1r=good.y .75[bar_height,x_height]; x1l:=good.x x1l; y1l:=good.y y1l;
 top z2'l=(x2l,tiny.top y2l); bot z2'r=(x2r,tiny.bot y2r);
 filldraw stroke term.e(2',1,right,1,4); fi  % short diagonal and terminal
pickup tiny.nib; numeric slope,theta,reduced_hair;
slope=(h-2vair-slab)/10.5u; theta=angle(-slope,1);
reduced_hair=max(tiny.breadth,hround(fudged.hair if hefty:-2stem_corr fi));
lft x3r=hround .75u; x5=.5[x3r,x6l]; lft x6r=hround .5(w-u);
x3l-x3r=curve-tiny; pos6(reduced_hair,180);
pos5(vair,theta); y5=.5h;
ellipse_set(2l,3l,4l,5l); ellipse_set(2r,3r,4r,5r);
pos7(vair,270); top y7l=h+o; x7=.45[x6r,x8r];
pos8(fudged.stem,30); x8l=good.x(x8l+3.5u-x8); y8r=y6;
ellipse_set(7l,6l,5',5l);
filldraw stroke z2e{left}...z3e{up}...z4e---z5e...{up}z6e
 ...z7e{left}...z8e{down};  % bowls
pos10(slab,90); x10=w-3.5u; bot y10l=-o;
pos9(fudged.stem,angle(z8-z10)-90);
z9=.5[z8,z10]+(1.75u,0) rotated(angle(z8-z10)+90);
filldraw stroke z8e{down}...z9e{z10-z8}...{right}z10e;  % long diagonal
if serifs: pickup crisp.nib; pos10'(slab,90); z10'=z10;
 pos11(fudged.hair,180); rt x11l=hround(w-u); y11=.5bar_height;
 filldraw stroke z10'e{right}...{up}z11e;  % terminal
 numeric inner_jut; if rt x6l+.5u<lft x0l-1.5jut: inner_jut=1.5jut;
 else: rt x6l+.5u=lft x0l-inner_jut; fi
 dish_serif(0,1,a,.6,inner_jut,b,.5,jut)(dark);  % serif
else: pickup fine.nib; pos10'(slab,90); z10'=z10;
 pos11(Vround .5[slab,flare],90);
 rt x11=hround(r-letter_fit-u); bot y11l=vround .07bar_height-o;
 filldraw stroke term.e(10',11,right,1,4); fi  % terminal
penlabels(0,1,2,3,4,5,6,7,8,9,10,11); endchar;


cmchar "Left curly brace";
beginchar(oct"173",9u#,body_height#,paren_depth#);
italcorr body_height#*slant+.5vair#-u#;
adjust_fit(0,0); pickup fine.nib;
forsuffixes $=1,1',4,4',7,7': pos$(vair,0); endfor
forsuffixes $=2,3,5,6: pos$(stem,0); endfor
x2=x3=x5=x6; x1=x1'=x7=x7'=w-x4=w-x4';
lft x4l=hround(1.5u-.5vair); lft x2l=hround(.5w-.5stem);
top y1=h; bot y7=-d; .5[y4,y4']=.5[y1,y7]=.5[y2,y6]=.5[y3,y5];
y1-y2=y3-y4=(y1-y4)/4; y1-y1'=y4-y4'=y7'-y7=vair-fine;
filldraw z1l{left}...z2l---z3l...{left}z4l
 --z4'l{right}...z5l---z6l...{right}z7l
 --z7r--z7'r{left}...z6r---z5r...{left}.5[z4r,z4'r]{right}
 ...z3r---z2r...{right}z1'r--z1r--cycle;  % stroke
penlabels(1,2,3,4,5,6,7); endchar;


cmchar "Right curly brace";
beginchar(oct"175",9u#,body_height#,paren_depth#);
italcorr math_axis#*slant+.5vair#-u#;
adjust_fit(0,0); pickup fine.nib;
forsuffixes $=1,1',4,4',7,7': pos$(vair,0); endfor
forsuffixes $=2,3,5,6: pos$(stem,0); endfor
x2=x3=x5=x6; x1=x1'=x7=x7'=w-x4=w-x4';
rt x4r=hround(w-1.5u+.5vair); rt x2r=hround(.5w+.5stem);
top y1=h; bot y7=-d; .5[y4,y4']=.5[y1,y7]=.5[y2,y6]=.5[y3,y5];
y1-y2=y3-y4=(y1-y4)/4; y1-y1'=y4-y4'=y7'-y7=vair-fine;
filldraw z1r{right}...z2r---z3r...{right}z4r
 --z4'r{left}...z5r---z6r...{left}z7r
 --z7l--z7'l{right}...z6l---z5l...{right}.5[z4l,z4'l]{left}
 ...z3l---z2l...{left}z1'l--z1l--cycle;  % stroke
penlabels(1,2,3,4,5,6,7); endchar;


cmchar "Vertical line";
beginchar(oct"174",5u#,body_height#,paren_depth#);
italcorr body_height#*slant+.5rule_thickness#-2u#;
adjust_fit(0,0); pickup rule.nib;
x1=x2=good.x .5w; top y1=h+eps; bot y2=-d-eps;
draw z1--z2;  % stem
labels(1,2); endchar;



cmchar "Sterling sign";
beginchar(oct"277",12u#,asc_height#,0);
adjust_fit(0,.75asc_height#*slant-.5u#); pickup fine.nib;
pos0(flare,0); pos1(hair,0); pos2(vair,90); pos3(stem,180);
pos4(stem,180); pos4'(stem,0); pos5(vair,-90); z4'=z4;
x2=2/3w-.5u; rt x3l=rt x4l=hround(.5w-u+.5stem); x5=2.5u;
y1=y3=.75h; top y2=h+oo; y4=.25h; bot y5r=-oo;
rt x1r=hround(w-1.5u); bulb(2,1,0);  % bulb
filldraw stroke pulled_arc.e(2,3)..z4e;  % stem
numeric light_stem; light_stem=2/3[vair,vstem];
pos6(.5[hair,light_stem],-180); pos7(light_stem,-300);
pos8(light_stem,-300); pos9(hair,-180);
lft x6r=hround u; x7=3u; x8=w-3.5u; rt x9r=hround(w-u);
y6=.4[y5,y7]; top y7r=vround .2h; bot y8l=-oo; y9=good.y .2h;
filldraw stroke pulled_arc.e(4',5)...z6e{up}...z7e{right}
 ..{right}z8e...{up}z9e;  % loop and arm
pos10(bar,90); pos11(bar,90); x10=3u; x11=w-4.5u;
top y10r=top y11r=vround(.5h+.5bar); filldraw stroke z10e--z11e;  % bar
penlabels(1,2,3,4,5,6,7,8,9,10,11); endchar;


