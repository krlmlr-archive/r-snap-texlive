def n_foot(suffix $) =
w':=7.806u; penpos$.1(hair,90); penpos$.2(hair,0);
penpos$.3(hair,-90); 
y$.1=y$.3=0.5foot; y$.2=-apex_o;
x$.1=x$l-1/20w'; x$.3=x$r+1/20w';
x$.2=x$;
filldraw z$.2r{dir75}..z$.3r{dir15}--
z$.3l{dir165}..z$r{up}--z$l{down}..
z$.1r{dir195}--z$.1l{dir-15}..z$.2l{dir-65}
--z$.2r & cycle; 
enddef;

def e_umlaut(suffix $) =
penpos$.1(2u,45); z$.1=z$;
pickup pensquare yscaled 0.5hair xscaled 0.3[hair,stem] rotated 30;
z$.2=z$.1r; z$.3=z$.1l; rt x$.4=rt x$.2; top y$.4=y$;
draw z$.3--z$.2..z$.3..z$.4;
enddef;

def pointpoint =
penpos1(0.23570*(x_height+2apex_o),45);
penpos2(0.23570*(x_height+2apex_o),45);
penpos3(0.23570*(x_height+2apex_o),45);
penpos4(0.23570*(x_height+2apex_o),45);
penpos5(0.23570*(x_height+2apex_o),45);
penpos6(0.23570*(x_height+2apex_o),45);
x1l=x3l=x5l=0.5w;
z2r=z3l; z4r=z5l;
z2-z1=whatever*dir135;
z4-z3=whatever*dir135;
z6-z5=whatever*dir135;
y1l=-apex_o; y6r=x_height+apex_o;
penstroke z1e--z2e;
enddef;




def old_e_umlaut(suffix $) =
numeric ww;
ww:=2u; penpos$.1(hair,147);
penpos$.2(1.5hair,90); penpos$.3(2.5hair,0);
penpos$.4(1.2hair,-90);
penpos$.5(hair,-147);
penpos$.6(hair,-57); penpos$.7(hair,-57);
z$.1=z$+(0.4ww,-0.2ww);
z$.2l=z$-(0.111ww,0.556ww);
z$.3l=z$-(0.5ww,apex_o);
z$.4l=(x$,y$+0.556ww);
z$.6-z$=whatever*dir33; x$.6r=x$+0.5ww;
z$.5l=z$.6l; z$.7=whatever[z$,z$.6];
x$.7=x$.3;
penstroke z$.1e..z$.2e{left}..z$.3e{up}
..z$.4e{right}..z$.5e;
penstroke z$.7e--z$.6e
enddef;

def umlaut (suffix $) =
y0=0; 0.5[x0,x0']=x$-2u;
z0'=z0+(2.5u,0);
penpos60(hair,145); 
z60=(x0+stem,1.090x_height);
z61=(x0+1.522stem,1.387x_height);
z62=(x0+1.956stem,1.324x_height);
z60'=(x0'+stem,1.090x_height);
z61'=(x0'+1.522stem,1.387x_height);
z62'=(x0'+1.956stem,1.324x_height);
filldraw z60l{dir34}..z62{up}..z61{left}
..z60r{dir259}--z60l & cycle;
penpos60'(hair,145); 
filldraw z60'l{dir34}..z62'{up}..z61'{left}
..z60'r{dir259}--z60'l & cycle
enddef;
