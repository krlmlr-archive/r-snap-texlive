.cmchar "The Fraktur letter A";
.beginchar("A",9.522u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
% made in Athens, Greece on Friday July 27, 1990
penpos1(cap_hair,-51);
penpos2(cap_hair,-51);
z2-z1=whatever*dir39;
x2r=w; z1r=(0.722w,-apex_o); 
penpos4'(cap_hair,39); z4'l=z1l;
z4'r=z4; 
penpos5(cap_stem,0); 
z5=(x1r,0.223h);
penpos13(cap_hair,-62); z13r=z5r;
z13l-z14=whatever*dir26; 
z13r-z14'=whatever*dir26;
y14=0; y14'=-apex_o;
z6=whatever[z14',z13r]; x6=x5l;
filldraw z14--z13l--z13r{down}..
z2l{z2-z1}--z2r--z1r--z1l--z4--
z6--z14'--z14 & cycle;
filldraw z5l--z6--z5r--z5l & cycle;
z1.1=whatever[z6,z4]; y1.1=y4+cap_hair;
filldraw z1l{z2-z1}..z1.1{z6-z4}--
z2--z1r--z1l & cycle;
%
%
%
penpos7(cap_stem,0); z7=(x5,0.862h);
penpos8(cap_hair,-57);
penpos9(cap_hair,-57);
z8l=z7l; z9-z8=whatever*dir33;
y9=h; z10=(0.937w,h+apex_o);
z11=(0.99w,0.981h); z12=(0.953w,0.955h);
z9'=whatever[z8r,z9r];
x9'=0.4[x7r,x9r];
penstroke z5e--z7e;
filldraw z7r{up}..z9'{z9-z8}--
z9r..z12{right}..z11{up}..z10{left}
...z9l---z8l--z7r & cycle;
%
%
%
penpos15(cap_hair,-90); z15=(0,0.213h);
z15r-z16=whatever*dir137;
x16=0.936x14; z17=whatever[z14,z13l];
x17=0.392w; 
penpos18(cap_hair,-43);
z18-z17=whatever*dir133;
y18r=y15l; penpos19(0.815cap_stem,0);
x19=0.5w; y19=0.521h;
z14''=(x14'-cap_hair,0);
filldraw z15l{right}..z18l..z19l{up}
--z19r{down}..z18r{dir-162}--
z17--z14''{dir72}...z16---z15r--
z15l & cycle;
%
%
%
penpos20'(1.037cap_stem,29.5);
z20'=(0.405w,0.846h);
penpos20(0.889cap_stem,50);
z20=(x18r,0.904h);
penpos21(1.25cap_hair,180);
z21r=(0,3/4h);
penpos22(1.148cap_stem,-162);
z22l=(x20,0.678h);
penpos23(cap_hair,180);
z23=(0.284w,0.535h);
penpos24(0.685cap_stem,66.6);
z24l=(x22,0.367h);
z25=(0.027w,0.426h); z26=(0.079w,0.473h);
penstroke z19e{up}..z20'e{dir135}..
z20e{left}..z21e{down}..z22e{dir-45}
..z23e{down}..z24e{left};
filldraw z24l{left}..z25{up}..z26{right}
..z24r{right}--z24l & cycle;
.endchar;

.cmchar "The Fraktur letter B";
.beginchar("B",12.415u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
z1=(0.045w,0.096h); z2=(0.081w,0.141h);
z3=(0,0.165h); z4=(x1,0.213h);
penpos5(cap_stem,-142);
z5=(0.179w,0.301h);
penpos6(1.259cap_stem,-147);
z6r=(0.538w,-apex_o);
penpos7(cap_hair,-65);
z7=(0.775w,0.103h);
penpos8(0.907cap_stem,30);
z8r=(w,0.4h); 
penpos9(1.741cap_stem,27);
z9l=(x6r,0.6h); 
filldraw z1{right}..z2{up}..z4 &
z4..z5r{right}..z6r{right} & 
z6r---z7r...z8r{up}..z9r{left}--
z9l{right}..z8l{down}..z7l..z6l{left}
..z5l{left}..z3{down}..z1{right} &
cycle;
%
%
%
penpos10(1.222cap_stem,-153);
z10r=0.883[z9l,z9r];
penpos11(2/3cap_hair,-63);
z11r=whatever[z9,z10]; x11=x10l+cap_hair;
penpos13(1.056cap_stem,-125);
z13l=(0.691w,h); 
z12=(z13l{right}..z10l{dir-63})
intersectionpoint ((x10l-1.5hair,0)--
(x10l-1.5hair,h));
penpos14(cap_hair,0);
penpos16(0.889cap_stem,17); z16r=z9l;
z14=(x16,0.686h);
filldraw z14l{dir78}..z13l{right}..z12..
z11l{z10-z9}--z11r--z10r{dir117}..
z13r{left}..{dir258}z14r--z14l & cycle;
%
%
%
penpos15'(cap_hair,-90);
z15'l=z5l;
penpos15(0.889cap_stem,0); 
z14-z15=whatever*dir85; y15=y5l+2cap_hair;
penpos17(cap_stem,76);
z17r=(0.256w,h+apex_o);
penpos18(cap_hair,180);
z18=(0.055w,0.745h);
penpos19(1.185cap_stem,-145);
z19=(0.148w,0.644h);
penpos20(cap_hair,197);
z20=(0.246w,0.548h);
penpos21(cap_hair,118);
z21=whatever[(0,0),z17]; x21=0.108w;
penstroke z15'e{right}
..z15e..z16e{up}..z17e{left}
..z18e{down}..z19e{dir-45}..z20e{z20l-z19l}..
z21e{dir202};
.endchar;

.cmchar "The Fraktur letter C";
.beginchar("C",10.406u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
z1=(0.157w,0.08h); z2=(0.211w,0.138h);
z3=(0.084w,0.141h);
z5=(0.113w,0.209h); z4=0.4[z3,z5];
penpos6(cap_hair,180);
z6r=(0,0.117h); penpos7(cap_hair,-118);
z7r=(0.189w,-apex_o); 
penpos8(cap_hair,-48);
z8=(0.469w,0.136h);
penpos9(1.074cap_stem,36);
z9=(0.623w,0.367h); z10=(0.5w,0.721h);
penpos10(0.889cap_stem,30);
z11'=(0.736w,0.931h);
z13=(0.803w,0.729h);
z14=(w,0.777h); penpos11(cap_hair,
angle(z13-z11'));
z11l=z11'; z12-z11l=whatever*dir7;
x12=0.912w; z16'-z12=whatever*(z13-z11);
z16'=whatever[z13,z14];
penpos15(cap_hair,angle(z13-z11));
z15r=z14; z15l-z16=whatever*(z14-z13);
z16=whatever[z12,z16'];
penstroke z6e{down}..z7e{right}..
z8e..z9e{up}..z10e{up}..z11e{z12-z11l};
filldraw z11l--z12--z16--z15l--z15r
--z13--z11l & cycle;
filldraw z6l{up}..z4{right} &
z4..z3{down}..z1{right}..z2{up}..
z5{left}..z6r{down}--z6l & cycle;
%
%
%
penpos17(1.037cap_stem,90);
z17l=(0.738w,-apex_o);
penpos18(1.093cap_stem,38);
z18=(0.17w,0.58h); 
penpos19(1.5cap_hair,-57);
z19l=(0.45w,h+apex_o);
penpos20(1.2cap_hair,-197);
z20=(0.692w,0.824h);
penpos21(cap_hair,-219);
z21=(3/4[x9l,x9r],y10);
penstroke z17e{left}..z18e{up}..
z19e..z20e{down}..z21e;
penpos22(cap_hair,-44);
z22r-z17l=whatever*dir46;
x22r=w;
filldraw z17r{right}..z22l{z22r-z17l}
--z22r--z17l--z17r & cycle;
.endchar;

.cmchar "The Fraktur letter D";
.beginchar("D",12.168u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
z1=(0.073w,-apex_o);
penpos2(0.6cap_stem,0);
z2=(x1,0.04h); penpos4(1.204cap_stem,-148);
z4=(0.229w,0.190h); 
penpos5(1.185cap_stem,-144);
z5r=(0.545w,-apex_o);
penpos6(cap_hair,-65); x6=0.787w;
z6r-z5r=whatever*dir25;
penpos7(0.815cap_stem,38);
z7r=(w,0.418h); penpos8(0.926cap_stem,67.5);
z8r=(0.41w,h+apex_o);
penpos9(0.537cap_stem,-173);
z9r=(0,0.685h); penpos10(2/3cap_hair,-90);
z10=(0.242w,0.53h);
penstroke z2e{up}..z4e{right}..z5e{right};
penstroke z6e{dir25}..z7e{up}..z8e{left}
..z9e{down}..z10e{right};
filldraw z2l{down}..z1..z2r{up}--z2l
& cycle;
filldraw z5l{right}..z6l{dir25}--
z6r--z5r--z5l & cycle;
%
%
%
z11=(0.171w,0.389h); z12=(0.197w,0.427h);
penpos13(cap_hair,-90);
z13=(x4l,0.3h); penpos14(cap_stem,0);
z14=(0.462w,0.5[y11,y12]);
penpos15(1.074cap_stem,34);
z15=(0.353w,0.655h); z16=(0.488w,0.78h);
penpos16(cap_hair,-90);
penstroke z13e{right}..z14e{up}..
z15e{up}..z16e{right};
filldraw z11{up}..z12{right}..z13l{right}
--z13r{left}..z11{up} & cycle;
.endchar;

.cmchar "The Fraktur letter E";
.beginchar("E",8.508u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos1(1.074cap_stem,62.5);
z1l=(0.623w,-apex_o);
penpos2(0.796cap_stem,16);
z2l=(0,0.485h); penpos3(4/3cap_hair,-90);
z3l=(0.485w,h+apex_o);
penpos6(cap_stem,-151.5);
penpos5(cap_stem,-151.5);
x6l=w; y5l=h; z6-z5=whatever*dir-37;
x5l=0.808w;
penstroke z5e--z6e;
penpos4(cap_hair,-151.5);
z4r=z5r; penstroke z3e{right}..
z4e{z6-z5};
penpos16(cap_hair,-90);
z16=(w,0.149h);
filldraw z1l--z16r--z16l..z1r{left}
--z1l & cycle;
penstroke z1e{left}..z2e{up}..z3e{right};
%
%
%
penpos7(cap_hair,-164); z7=whatever[z2l,z2r];
x7=0.31w;
penpos8(0.444cap_stem,-51.5);
z8=(0.492w,0.393h);
penpos9(0.796cap_stem,6.5);
z9=(x1l,y2-eps);
penpos10(0.963cap_stem,34);
z10=(0.441w,0.767h);
penpos11(cap_hair,-37); z11l=z4r;
penstroke z7e{dir-74}..z8e{right}..
z9e{up}..z10e{up}..z11e{z5l-z5r};
%
%
%
penpos12(cap_hair,-55.5);
z12l=(z9{up}..z10{up})
intersectionpoint (z9l--(x9l,h));
z13-z12l=whatever*dir31.5;
x13=0.854w; penpos14(whatever,0);
z15=(x13,0.53h); z14r=(w,0.639h);
z14l-z12r=whatever*(z13-z12l);
filldraw z12l--z13--z14r--z15--
z14l--z12r--z12l & cycle;
.endchar;

.cmchar "The Fraktur letter F";
.beginchar("F",8.213u#,cap_height#,3.010u#);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos1(2/3cap_hair,68);
z1=(0.34w,0.535h); penpos2(cap_hair,0);
z2l=(0.036w,0.739h); 
penpos3(4/3cap_stem,-150);
z3l=(0.614w,h+apex_o);
penpos4(cap_stem,-128); z4l-z3l=whatever*dir-37;
x4l=w; penpos5(cap_hair,-38);
x5l=w; z5r=whatever[z4r,z4l];
penstroke z1e{dir158}..z2e{up}..
z3e{right};
z5'=whatever[z3l,z4l]; z5'-z5l=whatever*(z4-z5r);
filldraw z3l--z5'--z5l--z5r--z4r
{z3l-z4l}...z3r{left}--z3l & cycle;
%
%
%
penpos6(cap_hair,-23); penpos7(cap_hair,-90);
penpos8(0.778cap_stem,-219);
penpos9(0.741cap_stem,-203);
penpos10(cap_hair,-90);
penpos11(cap_stem,17);
penpos12(1.111cap_stem,30);
penpos13(eps,-63);
z6l=(0,0.351h); z7=(x2r+cap_hair,0.44h);
x8l=x1; y8=0.339h; z9r=(x2l,0);
z10r=(0.462w,-d); z11l=(0.709w,0);
z12=(0.542w,0.652h);
z13=(0.789w,0.821h);
penstroke z6e{dir67}..z7e{right}..z8e{down}
..z9e{down}..z10e{right}..z11e{up}..
z12e{up}..z13e{dir27};
%
%
%
penpos14(cap_hair,-47);
penpos15(cap_hair,-47);
z14=(z11{up}..z12{up}) intersectionpoint 
((0,0.476h)--(w,0.476h));
z15-z14=whatever*dir43;
x15l=x4r; penpos16(0.833cap_stem,-137);
z16l=z15l; penpos17(0.833cap_stem,-137);
x17l=w; z17-z16=whatever*dir-47;
penstroke z14e--z15e;
penstroke z16e--z17e;
.endchar;

.cmchar "The Fraktur letter G";
.beginchar("G",10.638u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos1(cap_hair,0);
penpos2(0.5cap_stem,90);
penpos3(cap_stem,180);
penpos4(cap_hair,-90);
penpos5(0.926cap_stem,14);
penpos7(cap_hair,-51);
penpos9(cap_hair,-51);
z1=(0.595w,0.739h); z2r=(0.385w,h+apex_o);
z3r=(0,0.517h); z4r=(0.537w,-apex_o);
z5r=(w,0.392h); z6=(0.686w,0.628h);
z7=(x4,0.566h); z9-z7=whatever*dir39;
x9=0.963w; z8=whatever[z7r,z9r];
x8=0.817w;
penstroke z1e{up}..z2e{left}..z3e{down}
..z4e{right}..z5e{up};
filldraw z7r{z9-z7}..z6{right}..
z5l{down}--z5r{up}..z8--z9r--z9l--
z7l--z7r & cycle;
%
%
%
penpos11(1.148cap_stem,39);
penpos12(1.148cap_stem,39);
z11=whatever[z7l,z9l]; x11=0.856w;
penpos10(cap_hair,-51);
z10=whatever[z9l,z9r]; y10l=h+apex_o;
z12-z11=whatever*dir129;
z10l-z12=whatever*dir39;
penpos13(cap_hair,-51); z13l=z12l;
penstroke z11e--z12e;
penstroke z13e--z10e;
%
%
%
penpos14(cap_hair,180); penpos15(0.5cap_stem,-45);
penpos16(0.796cap_stem,21.5);
penpos17(0.926cap_stem,43);
penpos18(cap_hair,-37);
penpos19(cap_hair,-37);
z14=(0.3w,0.58h); z15=(0.441w,0.385h);
z16=(0.5[x7,x1],0.507h);
z17=(0.385w,0.776h); z18=(0.442w,0.873h);
z19-z18=whatever*dir53; y19=1.032h;
penstroke z14e{down}..z15e{right}..
z16e{up}..z17e{up}...z18e---z19e;
.endchar;

.cmchar "The Fraktur letter H";
.beginchar("H",10.275u#,cap_height#,1.996u#);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos1(cap_hair,111);
penpos2(cap_hair,90);
penpos3(cap_hair,0);
penpos8(cap_hair,-45);
penpos7(cap_hair,-45);
penpos9(0.889cap_stem,0);
penpos10(1.222cap_stem,48.5);
penpos11(cap_hair,132);
z1=(0.57w,-0.679d);
z2l=(0.452w,-d); z3=(0.354w,-0.23d);
z4=(0.503w,0.082h); z5=(0.621w,0.217h);
z6=(0.71w,0.12h); z8l=(0.592w,-apex_o);
z7r-z8r=whatever*dir45;
x7r=0.809w; z9r=(w,0.429h);
z10=(0.624w,3/4h); %z11=whatever[z1l,z1r];
z11=(0.28w,0.543h);
penstroke z1e..z2e{left}..z3e{up};
filldraw z3r{up}..z4{right}..
z8l{z8r-z7r}--z8r---z7r...z9r{up}--
z9l{down}..z7l{z8r-z7r}..z6{left}..
z5{dir123} & z5{dir211}..z3l{down}--
z3r & cycle;
penstroke z9e{up}..z10e{left}..
z11e{dir222};
%
%
%
penpos12(1.259cap_stem,-131);
penpos13(1.2cap_hair,-41);
penpos14(1.074cap_stem,25);
penpos15(cap_hair,-41);
penpos18(cap_hair,-36);
z12r=(0,0.141h); y13=0.2h; x13l=x11;
z14=z11; z15=(x11,0.845h);
z16=(0.478w,h); z17=(0.516w,0.837h);
z18r-z17=whatever*dir54; 
y18r=h+apex_o;
penstroke z12e{dir-41}..z13e{dir49}
..z14e..z15e{dir49};
filldraw z15r{dir49}..z17--z18r--
z18l{z17-z18r}..z16{left}..z15l
{dir229}--z15r & cycle;
.endchar;

.cmchar "The Fraktur letter I";
.beginchar("I",8.541u#,cap_height#,2.497u#);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos1(cap_hair,28);
penpos2(cap_hair,-17.5);
penpos3(0.889cap_stem,-116);
penpos4(cap_stem,-126);
penpos5(cap_hair,-44);
penpos6(1.056cap_stem,53);
penpos7(1.056cap_stem,21);
penpos8(cap_hair,-90);
penpos9(cap_hair,180);
z1=(0.115w,-0.886d); z2r=(x1,-0.519d);
z3=(0.234w,-0.1d); z4r=(0.517w,-d);
z5=(0.795w,-0.329d); z6r=(0.966w,0.196h);
z7=(0.636w,0.56h); z8=(0.808w,0.683h);
z9l=(x6r,0.554h); 
penstroke z1e..z2e{up}..z3e{right}
..z4e{right}..z5e..z6e{up}..z7e{up}
..z8e{right}..z9e{down};
%
%
%
penpos10(1.037cap_stem,50);
penpos11(cap_hair,-45);
penpos12(0.889cap_stem,43);
penpos13(1.185cap_stem,-146);
penpos14(0.741cap_stem,-119);
penpos15(cap_hair,0);
penpos16(cap_hair,28);
z10l=z11r=(0.1w,0.515h);
z12l=(0,0.69h); z13l=(0.506w,h+apex_o);
z14r=z8l+(0,0.035h);
z15r=(w,0.875h); z16=(x15l,0.924h);
filldraw z11l{z12r-z11l}..z12l{up}--
z12r{down}..z10r--z10l--z11l & cycle;
penstroke z12e{up}..z13e{right}..
z14e{right}..z15e{up}..z16e{dir118};
.endchar;

.cmchar "The Fraktur letter J";
.beginchar("J",8.541u#,cap_height#,2.497u#);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos1(cap_hair,28);
penpos2(cap_hair,-17.5);
penpos3(0.889cap_stem,-116);
penpos4(cap_stem,-126);
penpos5(cap_hair,-44);
penpos6(1.056cap_stem,53);
penpos7(1.056cap_stem,21);
penpos8(cap_hair,-90);
penpos9(cap_hair,180);
z1=(0.115w,-0.886d); z2r=(x1,-0.519d);
z3=(0.234w,-0.1d); z4r=(0.517w,-d);
z5=(0.795w,-0.329d); z6r=(0.966w,0.196h);
z7=(0.636w,0.56h); z8=(0.808w,0.683h);
z9l=(x6r,0.554h); 
penstroke z1e..z2e{up}..z3e{right}
..z4e{right}..z5e..z6e{up}..z7e{up}
..z8e{right}..z9e{down};
%
%
%
penpos10(1.037cap_stem,50);
penpos11(cap_hair,-45);
penpos12(0.889cap_stem,43);
penpos13(1.185cap_stem,-146);
penpos14(0.741cap_stem,-119);
penpos15(cap_hair,0);
penpos16(cap_hair,28);
z10l=z11r=(0.1w,0.515h);
z12l=(0,0.69h); z13l=(0.506w,h+apex_o);
z14r=z8l+(0,0.035h);
z15r=(w,0.875h); z16=(x15l,0.924h);
filldraw z11l{z12r-z11l}..z12l{up}--
z12r{down}..z10r--z10l--z11l & cycle;
penstroke z12e{up}..z13e{right}..
z14e{right}..z15e{up}..z16e{dir118};
.endchar;

.cmchar "The Fraktur letter K";
.beginchar("K",9.478u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
cap_stem:=0.95cap_stem;
%cap_hair:=0.95cap_hair;
z1=(0,0.182h); z2=(0.177w,0.207h);
z3=(0.033w,-apex_o); 
penpos4(cap_hair,-90);
z4=(0.282w,0.045h); penpos5(cap_hair,0);
z5=(0.4w,0.136h); z7=(0.434w,0.227h);
z7'=(0.328w,0.556h);
z8=(0.197w,0.732h); z6=(0.052w,0.566h);
penpos9(1.25cap_hair,-90);
z9l=(0.561w,h+apex_o);
penpos9'(cap_hair,180); 
z9'l=(w-apex_o,0.707h);
penpos10(cap_hair,140); z10=(0.813w,0.492h);
filldraw z1--z3{dir30}..z4r{right}
..z5r..z7{up}..z7'..z8{up}..z9r{right}
--z9l{left}..z6{down} & z6{dir-52}
..z5l..z4l{left}..z2{up}--z1 & cycle;
penstroke z9e{right}..z9'e{down}..
z10e;
%
%
%
penpos12(cap_hair,-78);
penpos11(cap_hair,-78);
z12l-z7'=whatever*dir12;
z7'-z11l=whatever*dir12;
x12=x10; x11=0.243w;
penstroke z11e--z12e;
penpos13(cap_hair,12); z13r=z7';
penpos14(0.463cap_stem,-116);
z14=(0.492w,0.8h);
penpos16(1.074cap_stem,-168);
z16=whatever[z11l,z12l]; x16l=x12-cap_hair;
z12-z15=whatever*dir-78;
penpos15(cap_hair,-78);
y15=0.823h; z17-z15r=whatever*(z12-z11);
z16l-z17=whatever*(z12-z15);
filldraw z13r{dir102}..z14r{right}
..z16r{dir-78}--z16l--z17--z15r--
z15l{z13-z12}..z14l{left}..z13l{dir-78}
--z13r & cycle;
%
%
%
penpos18(1.053cap_stem,-168);
z18=0.5[z12r,z11r];
penpos18'(1.053cap_stem,-168);
z19=(0.767w,-apex_o); penpos21(cap_hair,-90);
z21r-z19=whatever*dir45; x21=w;
z20=(0.869w,0.149h); 
z18'-z18=whatever*dir-70; x18'l=x15;
filldraw z18r---z18'r...z19--z21r--
z21l{z19-z21r}..z20{left}...z18'l
---z18l--z18r & cycle;
.endchar;

.cmchar "The Fraktur letter L";
.beginchar("L",6.131u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
cap_stem:=1.022cap_stem;
penpos1(cap_hair,0); penpos2(cap_hair,-60);
penpos3(1.056cap_stem,30);
penpos4(cap_stem,23); penpos5(0.481cap_stem,-67);
penpos6(cap_hair,-149); 
penpos7(whatever,-140);
penpos8(cap_hair,-147);
penpos9(cap_hair,-90); penpos10(cap_hair,0);
z1l=(0,0.082h); z2=(0.15w,0.207h);
z3=(0.453w,0.37h); z4l=(0,0.731h);
z5l=(0.539w,h+apex_o); 
z6=(0.829w,0.886h); z7l=(0.933w,0.739h);
x7r=x3r;
z8=(0.658w,0.53h); z9=(x6,0.476h);
z10=(x7l,0.543h);
penstroke z1e{up}..z2e..z3e{up}..
z4e{up}..z5e{right}..z6e..z7e{down}
..z8e..z9e{right}..z10e{up};
%
%
%
z11=(0.109w,0.111h); z13=(0.539w,-apex_o);
z12=(0.731w,y11); penpos14(cap_hair,-21);
z14r=(w,0.223h);
z15'-z11=whatever*(z12-z13); x15'=w;
z15=(z1{up}..z2..z3{up}) intersectionpoint 
((0.238w,0)--(0.238w,h));
filldraw z1r{up}..z11{right}..z13{dir-25}
& z13{dir25}..z14r{dir69}--z14l{dir249}
..z12{left}..z15{left}& z15..z2..
z1{down}--z1r & cycle;
.endchar;

.cmchar "The Fraktur letter M";
.beginchar("M",13.432u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos1(cap_hair,-40); penpos2(cap_hair,-40);
z1=(0.067w,0.035h); z2-z1=whatever*dir50;
x2=0.176w; z3=(1/5w,-apex_o);
penpos4(cap_hair,-41.5);
z4r-z3=whatever*dir48.5;
z4=whatever[z2l,z2r];
penpos5(1.3cap_stem,48);
% ant'i 1.111 pou j'aprepe n'anai
z5=(0.4w,0.394h); penpos6(0.778cap_stem,44);
z6=(0.148w,0.915h); penpos7(cap_hair,180);
z7r=(0,0.782h); penpos8(1.111cap_stem,-147);
z8l=(x6,0.649h); penpos9(2.2cap_hair,-171.5);
penpos10(cap_hair,90); z9r=(x2l,0.46h);
z10=(0.122w,0.378h); penpos11(cap_hair,58);
z11=(0.047w,0.418h);
z1'=whatever[z1r,z2r]; x1'=x1r+0.014117w;
filldraw z1l--z2l--z4r--z3--z1'--z1r
--z1l & cycle;
penstroke z4e{z4r-z3}..z5e{up}..
z6e{left}..z7e{down}..z8e..z9e{down}
..z10e{left}..z11e;
%
%
%
penpos12(cap_hair,-40); penpos13(cap_hair,-40);
z12=(0.329w,0.024h); z13-z12=whatever*dir50;
x13=x5r; z13'=(0.466w,-apex_o);
penpos14(cap_hair,-40); z14r-z13'=whatever*dir48;
z14=whatever[z13l,z13r];
penpos15(1.5cap_stem,-50);
penpos16(0.741cap_stem,48);
penpos17(cap_hair,180);
z15=(0.647w,0.548h);
x16=0.424w; y16r=h+apex_o;
z17=(0.306w,0.843h);
penstroke z12e--z13e;
z12'=whatever[z12r,z13r]; x12'=x12r+0.014117w;
filldraw z12'--z13'--z14r--z13l--
z12' & cycle;
penstroke z14e{z14r-z13'}..z15e{up}
..z16e{left}..z17e{dir251};
%
%
%
penpos18(cap_hair,-46); penpos19(cap_hair,-46);
penpos20(1.13cap_stem,37);
penpos21(0.852cap_stem,-15);
penpos22(cap_hair,-72);
z18r=(0.814w,-apex_o);
z19-z18=whatever*dir44;
x19r=w; z20=(x18-eps,0.34h);
z21=(x18r+eps,0.556h);
z22=(0.906w,0.609h);
z20'=0.7[z18l,z19l];
penstroke z18e--z19e;
filldraw z18l{up}..z20l{up}..z21l--
z22l--z22r{z21l-z22l}..z21r..
z20r{down}..z20'{dir-35}--z18l
& cycle;
%
%
%
penpos23(1.148cap_stem,53);
penpos24(0.944cap_stem,12);
penpos27(2/3cap_hair,90);
z23r=(w,0.670h); z24=(0.871w,0.84h);
z25=(0.715w,0.947h); z26=(x25,0.87h);
z27=(0.621w,0.824h);
filldraw z23r{dir124}..z24r%{dir124}
...z25---z27r--z27l{z25-z27r}..
z26{right}..z24l%{dir-56}
..z23l{dir-56}
--z23r & cycle;
.endchar;

.cmchar "The Fraktur letter N";
.beginchar("N",11.662u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos1(cap_hair,-45); penpos3(cap_hair,-45);
penpos4(cap_hair,-90);
penpos5(0.944cap_stem,0);
penpos6(0.811cap_stem,64);
penpos7(cap_hair,180);
penpos8(1.094cap_stem,-132);
penpos9(cap_hair,180); penpos10(cap_hair,90);
penpos11(cap_hair,21);
z1=(0.123w,0); z3-z1=whatever*dir52;
x3=0.239w; 0.5[z1,z3]-z2=whatever*dir-45;
x2=x1l-eps; z4r=(0.325w,-apex_o);
z5=(0.55w,0.371h); z6=(x3,0.932h);
z7r=(0,0.751h); z8=(0.197w,0.553h);
z9=(x4,0.428h); z10=(x3,0.317h);
z11=(0.125w,0.423h);
penpos2(1/3cap_hair,52);
filldraw z1l{dir71}..z2l--z2r
..{dir31}z3l--z3r{z1-z3}..z4l{right}
--z4r{left}..z1r{z1-z3}--z1l{z3-z1}
& cycle;
penstroke z4e{right}..z5e{up}..
z6e{left}..z7e{down}..z8e{dir-22}..
z9e{down}..z10e{left}..z11e;
%
%
%
penpos12(2/3cap_hair,0); penpos13(0.778cap_stem,-126);
penpos14(0.944cap_stem,-162);
penpos15(0.944cap_stem,-90);
penpos16(cap_hair,0);
penpos17(cap_hair,-32);
penpos19(0.981cap_stem,0);
penpos20(0.793cap_stem,0);
penpos21(cap_hair,-90);
z12=(0.516w,0.745h); y13l=h+apex_o;
z14=(0.886w,0.751h);
z15=(w,0.669h);
z19=(0.769w,y5); x13=x19l;
z16=(x19r,-apex_o); 
z17r-z16r=whatever*dir58;
x17r=0.977w; z20=(0.798w,0.537h);
z21r=z15r;
filldraw z16r--z17r--z17l{z16r-z17r}
..z19r{up}..z20r..z21r{dir18}--
z21l{dir198}..z20l..z19l{down}..
z16l--z16r & cycle;
penstroke z12e{up}..z13e{right}..
z14e{dir-71}..z15e{right};
.endchar;

.cmchar "The Fraktur letter O";
.beginchar("O",12.01u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos1(cap_hair,41); penpos2(cap_hair,0);
penpos3(cap_hair,-59); 
penpos5(1.037cap_stem,24);
penpos6(1.074cap_stem,38);
penpos7(cap_hair,-47); penpos8(2/3cap_hair,0);
penpos4(1.315cap_stem,-146);
penpos10(1.111cap_stem,70);
penpos11(0.704cap_stem,22);
penpos14(cap_hair,-46);
z1r=(0.084w,0); z2l=(0,0.09h);
z3=(0.234w,0.298h); z4=(0.184w,0.197h);
z5=(0.332w,0.412h); z6=(0.179w,0.71h);
z7=(0.263w,0.843h); z8=(0.389w,h+apex_o);
z9=(x8,0.787h); z10=(0.595w,0.864h);
z11r=(w,0.484h); z12=(0.637w,0.144h);
z13=(2x10-x12,-apex_o);
z14=(0.858w,0.227h);
penstroke z1e..z2e{up}..z3e..z5e{up}
..z6e{up}..z7e{dir43};
penstroke z1e..z2e{up}..z4e{dir-19};
filldraw z4r{dir-19}..{z13-(x4r,0.5y4r)}z13 &
z13{dir27}..z14r..z11r{up}..z10r{left}..
z8r{dir100}--z8l{down}..z7l{dir223}--
z7r..z9..z10l{right}..z11l{down}..
z14l..z12{left}..z4l{dir161}--z4r &
cycle;
.endchar;

.cmchar "The Fraktur letter P";
.beginchar("P",9.482u#,cap_height#,2.651u#);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos1(cap_hair,0); penpos2(cap_stem,0);
penpos3(cap_stem,0); 
penpos4(0.926cap_stem,58);
penpos5(cap_hair,180);
penpos8(1.5cap_hair,-145);
penpos9(cap_hair,114);
z1=(0.427w,-d); z2=(0.453w,0.09h);
z3=(x2,0.7h); z4r=(0.227w,h+apex_o);
z5r=(0,0.778h); z6=(0.173w,0.67h);
z7=(0.083w,0.564h); z8=(0.275w,0.524h);
z9r=(x7,0.4h);
penstroke z1e...z2e---z3e..z4e{left}
..z5e{down};
penstroke z8e{down}..z9e;
filldraw z5r{down}..z7 & z7{right}..
z8r{down}--z8l{up}..z6..z5l{up}--
z5r & cycle;
%
%
%
penpos18(cap_hair,0); penpos19(cap_hair,-80);
penpos16(1.037cap_stem,23);
penpos15(cap_stem,35);
penpos14(cap_hair,-56);
z18r=z3r; z19=(0.74w,0.931h);
z16=(0.793w,0.782h);
z15r=(w,0.42h); z14=(0.687w,0.059h);
penstroke z14e{dir33}..z15e{up}..
z16e{up};
penstroke z18e{up}..z19e{dir22.5};
z17'=(0.917w,h-apex_o);
filldraw z16l{up}..z19l & z19l{dir22.5}
..z17'{right} & z17'..z16r{down}--
z16l & cycle;
%
%
%
penpos10(cap_stem,64); penpos11(cap_stem,71);
penpos12(cap_hair,-36);
penpos13(cap_hair,-23);
z10=(0.713w,0.027h); z11=(0.387w,0.16h);
z12=(x6,0.059h); z13l=(x8,0.266h);
filldraw z10r..z11r...z13r---z13l
& z13l..z12l{dir234}--z12r{dir54}...
z11l---z10l--z10r & cycle;
.endchar;

.cmchar "The Fraktur letter Q";
.beginchar("Q",13.559u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
w':=12.01u;
penpos1(cap_hair,41); penpos2(cap_hair,0);
penpos3(cap_hair,-59); 
penpos5(1.037cap_stem,24);
penpos6(1.074cap_stem,38);
penpos7(cap_hair,-47); penpos8(2/3cap_hair,0);
penpos4(1.315cap_stem,-146);
penpos10(1.111cap_stem,70);
penpos11(0.704cap_stem,22);
penpos14(cap_hair,-46);
penpos16(1.074cap_stem,-127);
penpos17(cap_hair,-45);
z1r=(0.084w',0); z2l=(0,0.09h);
z3=(0.234w',0.298h); z4=(0.184w',0.197h);
z5=(0.332w',0.412h); z6=(0.179w',0.71h);
z7=(0.263w',0.843h); z8=(0.389w',h+apex_o);
z9=(x8,0.787h); z10=(0.595w',0.864h);
z11r=(0.925w,0.503h); z12=(0.637w',0.144h);
z13=(2x10-x12,-apex_o);
z14=(0.884w',0.227h);
penpos15(1.3cap_stem,angle(z13-z14));
z15l=z14; z16r=(0.825w,-apex_o);
z17r=(w,0.144h);
penstroke z1e..z2e{up}..z3e..z5e{up}
..z6e{up}..z7e{dir43};
penstroke z1e..z2e{up}..z4e{dir-19};
filldraw z4r{dir-19}..{z13-(x4r,0.5y4r)}z13 &
z13{dir27}..z14r..z11r{up}..z10r{left}..
z8r{dir100}--z8l{down}..z7l{dir223}--
z7r..z9..z10l{right}..z11l{down}..
z14l..z12{left}..z4l{dir161}--z4r &
cycle;
penstroke z15e..z16e{dir-10} & z16e..z17e
{dir45};
.endchar;

.cmchar "The Fraktur letter R";
.beginchar("R",10.493u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#+0.3u#);
penpos1(cap_hair,-49); penpos2(cap_hair,-49);
penpos4(cap_hair,-90);
z1l=(0,0.056h); z2=(0.253w,0.234h);
z3=(apex_o,0.202h); z7=(0.036w,0.128h);
z4r=(0.3w,-apex_o);
z5=(0.181w,0.128h); z6=(0.084w,0.051h);
filldraw z1l..z7..z3 & z3{dir-10}..
z2l--z2r..z5..z4l{right}--z4r{left}
..z6..z1r--z1l & cycle;
penpos8(0.963cap_stem,19);
penpos9(cap_stem,60); penpos10(cap_hair,180);
penpos13(0.574cap_stem,-153);
penpos14(cap_hair,90); penpos15(cap_hair,0);
z8=(0.53w,0.375h); z9r=(0.247w,h+apex_o);
z10r=(0,0.771h); z11=(0,0.614h);
z12=(0.169w,0.641h); z13=(0.247w,0.481h);
z14=(0.117w,0.348h); z15l=(0,0.468h);
penstroke z4e{right}..z8e{up}..z9e
{left}..z10e{down};
penstroke z13e{down}..z14e{left}..
z15e{up};
filldraw z10r---z11..z13r{down}--
z13l{up}..z12..z10l{up}--z10r &
cycle;
%
%
%
penpos16(cap_hair,-90); penpos17(cap_hair,-90);
penpos21(cap_hair,0); z16=(x8,0.585h);
z17-z16=whatever*dir30;
x17=0.934w; z21=(0.488w,3/4h);
z20=(0.578w,0.936h); z22=(0.651w,0.91h);
z19=(0.768w,h+apex_o); 
z23=whatever[z16l,z17l]; x23=x19;
z18=(0.895w,0.867h);
penstroke z16e--z17e;
filldraw z21r{up}..z22{right}..
z23{dir-76}--z17l{left}..z18..
z19 & z19{z22-z19}..z20..z21l{down}
--z21r & cycle;
%
%
%
penpos25(0.926cap_stem,-153);
penpos26(1.074cap_stem,-174);
penpos27(cap_hair,-32);
z24=whatever[z16r,z17r]; x24=x23+0.18cap_stem;
z25=(3/4w,0.612h); z26=(0.825w,0.205h);
penpos26'(0.926cap_stem,-153); z26'r=z26r;
y27r=0.189h; x27l=w; z28=(0.849w,-apex_o);
z27'-z27l=whatever*(z28-z27r);
x27'=w-cap_hair;
filldraw z16r{z17-z16}...z25r
---z26'r...z28 & z28--z27r--z27l &
z27l---z27'...z26'l---
z25l..z24--z16r & cycle;
.endchar;

.cmchar "The Fraktur letter S";
.beginchar("S",12.813u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos0(0.032067w,-27); penpos0'(0.032067w,24);
z0'r=z0l=(0.715w,0.745h);
penpos1(1.5cap_hair,-156);
penpos2(cap_hair,114);
penpos7(cap_hair,114);
penpos8(1.2cap_stem,-142);
penpos9(cap_stem,-107);
penpos10(0.926cap_stem,-123);
penpos11(cap_hair,0);
penpos16(0.722cap_stem,40);
penpos17(cap_hair,-58);
penpos18(cap_hair,-118);
z1l=(w,0.952h); x2=0.93w;
z3=(0.8w,0.859h); z5-z3=whatever*dir153;
y5=h+apex_o; z4=z0r; z2l-z4=whatever*dir24;
z6=whatever[z0r,z0l]; x6=0.462w;
z7=(0.291w,0.931h); z8r=(0,0.505h);
z9r=(0.562w,-apex_o);
z10r=(0.76w,0.077h); z11=(0.933w,0.364h);
z13=z0'l; z15-z13=whatever*dir204;
x15=x14=0.407w; z12=(x9l,0.512h);
z14-z12=whatever*(z15-z13);
z16=(0.258w,0.649h);
z17=(0.324w,0.782h); z18=(0.508w,0.739h);
penstroke z7e{dir204}..z8e{down}..
z9e{right}..z11e{up};
penstroke z16e{up}..z17e{dir24}..
z18e{dir-27};
filldraw z1r{down}..z2r{z4-z2l}...
z3---z5 & z5..z7r{dir204}--z7l
{dir24}...z6---z4---z2l...z1l{up}
& z1l--z1r & cycle;
filldraw z11r{up}..z13{z6-z4} &
z13---z15...z16r{up}--z16l{down}
..z14{dir-27} & z14---z12...
z11l{down}--z11r & cycle;
.endchar;

.cmchar "The Fraktur letter T";
.beginchar("T",10.386u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
z1=(0.041w,0.053h); z2=(0,0.104h);
z3=(0.078w,y2); penpos4(1.037cap_stem,-133);
z4=(0.165w,0.253h); z5=(0.592w,-apex_o);
z6=(0.731w,0.123h); penpos7(cap_hair,0);
z7r=(w,0.303h); penpos8(0.5cap_hair,-90);
z8=(0.29w,0.271h); penpos9(1.185cap_stem,10);
penpos10(1.148cap_stem,19);
z9=(x5,0.415h); z10=(0.381w,0.691h);
penpos11(0.5cap_hair,-90); z11=(0.677w,0.83h);
penpos12(cap_hair,-112);
penpos13(cap_hair,180);
penpos14(1.111cap_stem,31);
penpos14'(1.037cap_stem,57);
z14'=(0.562w,0.939h);
penpos15(1.185cap_stem,72);
penpos16(cap_hair,-37.5);
penpos17(cap_hair,-48);
z12=(0.236w,0.6h); z13=(0.072w,0.761h);
z14r=(0.436w,h+apex_o);
z15=(0.858w,0.755h); z16=z15l;
penpos15'(1.5cap_stem,72); z15'l=z15l;
z17l=whatever[z16,z15r]; x17=0.956w;
filldraw z1{right}..z3{up}..z4r{right}
..z5{dir-29} & z5{dir22}..z7r{up}--
z7l{down}..z6{left}..z4l{left}..
z2{down}..z1{right} & cycle;
penstroke z8e{right}..z9e{up}..z10e
{up}..z11e{right};
penstroke z15e{dir142.5}..z14'e..z14e{left}..z13e{down}
..z12e{dir-22};
penstroke z16e{z15r-z15l}..z17e
{dir42};
.endchar;

.cmchar "The Fraktur letter U";
.beginchar("U",10.608u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos1(cap_stem,0); penpos2(whatever,39);
penpos3(1.176cap_stem,48);
penpos4(cap_hair,-42); penpos5(cap_hair,-42);
penpos10(cap_hair,-47);
penpos11(cap_hair,-47);
penpos13(cap_stem,0);
penpos14(cap_stem,0);
penpos15(cap_hair,-90);
penpos19(cap_hair,-41);
z1=(0.258w,0.162h); z2r=(x1r,0.853h);
x2l=x1l;
z4l=(0,0.822h); z5-z4=whatever*dir48;
y5l=h+apex_o; z3=whatever[z4r,z5r]; y3r=y5r-apex_o;
z7=(x1l,0.26h); z8=(0.111w,y1);
z9=(x1r,-apex_o); z8'=(0.278w,0.082h);
z11r-z9=whatever*dir43; x11=0.737w;
z12-z11l=whatever*(z9-z11r);
x12=0.472w; z6-z12=whatever*(z8-z9);
x6=x1r; 
penstroke z1e---z2e...z3e{dir138};
penstroke z4e--z5e;
filldraw z9--z11r--z11l--z12--z6--
z7{down}..z8{dir231} &
flex(z8,z8',z9) & cycle;
%
%
%
z13=(x11,y7); z14=(x13,y2r-cap_hair);
z15=(0.573w,0.898h); z16=(0.737w,h+apex_o);
z17=(0.886w,0.915h); z18=(x17,y1);
z20=(0.786w,-apex_o);
z21=(x13l,y1); x19r=w;
z19r-z20=whatever*dir49;
penstroke z13e--z14e;
filldraw z15r{z16-z15l}..z14l{down}
--z14r{up}..z17{right} & z17
..z16 & z16..z15l--z15r & cycle;
filldraw z20--z19r--z19l{z20-z19r}
..z18{left}..z13r{up}--z13l--z21--
z20 & cycle;
.endchar;

.cmchar "The Fraktur letter V";
.beginchar("V",10.303u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos1(cap_hair,41); penpos2(cap_hair,0);
penpos3(0.926cap_stem,-117);
penpos6(cap_hair,-63);
penpos7(0.704cap_stem,30);
penpos8(cap_stem,44);penpos9(cap_hair,-65);
penpos10(cap_hair,-65);
penpos11(0.852cap_stem,-138);
penpos12(2/3cap_hair,-55);
penpos13(cap_hair,-48); penpos14(0.907cap_stem,-12);
penpos15(1.037cap_stem,35);
penpos16(0.889cap_stem,46);
penpos17(cap_hair,155);
penpos17'(0.489cap_stem,-123);
penpos18(0.889cap_stem,-118);
penpos20(0.537cap_stem,-147);
penpos21(cap_hair,90);
penpos22(cap_hair,44);
z1=(0.115w,0.046h); z2=(0.072w,0.117h);
z3=(0.209w,0.228h); z4=(0.613w,-apex_o);
z5=(0.755w,0.103h); z5'=(x4,0.172h);
z6=(0.853w,0.114h);
z7r=(w,0.293h); z8=(0.742w,0.543h);
z9=(0.822w,0.655h);
z10r=(w,3/4h); z10'=(x7,0.788h);
z11l=(0.75w,h+apex_o);
z15=(0.414w,0.875h); z13=(0.288w,0.242h);
z14=(0.534w,0.527h); z12=z15+(0.1w,-0.1h);
z16r=(0.252w,h+apex_o); 
z17=(0.044w,0.859h); 
z17'r=(0,0.742h); z19=(0.043w,0.628h);
z18=(0.096w,0.674h);
z20=(0.267w,0.549h); z21l=(0.173w,0.435h);
z22=(x19,0.516h);
penstroke z1e..z2e{up}..z3e{right};
filldraw z3r{right}..z4{dir-44} &
z4---z6r...z7r{up}--z7l{down}..
z6l..z5{left}..z5'..z3l{left}--z3r &
cycle;
penstroke z7e{up}..z8e{up}..z9e..
z10e;
penstroke z13e{dir42}..z14e{up}..
z15e..z16e{left}..z17e..z17'e{down};
filldraw z17'r{down}..z19{z3-z19} & z19---
z18r...z20r{down}--z20l{up}..z18l..
z17'l{up}--z17'r & cycle;
penstroke z20e{down}..z21e{left}..
z22e;
filldraw z12r{up}..z11r{right}..
1/10[z9,z10]{right} & 1/10[z9,z10]--z10--z10l & 
z10l{left}..z10'..z11l{left}..z12l{down}
--z12r & cycle;
.endchar;

.cmchar "The Fraktur letter W";
.beginchar("W",13.812u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos1(cap_hair,44); penpos2(cap_hair,22);
penpos3(1.074cap_stem,-133);
z1=(0.095w,0); z2=(0.055w,0.09h);
z3=(0.16w,0.213h); z4=(0.366w,-apex_o);
z5=(0.435w,0.118h); penpos6(cap_hair,-56);
penpos7(4/3cap_stem,45);
z6=(0.508w,0.126h); z7=(0.645w,0.41h);
penpos9(1.222cap_stem,52);
penpos10(cap_hair,0);
z9=(0.532w,0.733h); z10=(0.604w,h+2apex_o);
penstroke z1e..z2e..z3e{right};
filldraw z3r{right}..z4{dir-30} &
z4---z6r...z7r{dir72} & z7r{dir120}..
z9r{up}..z10r--z10l..z9l{down}..
z7l{down}..z6l{z4-z6r}...z3l{left}
--z3r & cycle;
%
%
%
penpos11(cap_hair,-37); penpos12(0.778cap_stem,-140);
penpos13(1.056cap_stem,-133);
z11=(0.561w,0.865h); z12l=(0.735w,h+apex_o);
z13l=(0.982w,0.756h);
penstroke z11e{dir53}..z12e{right}..
z13e{dir-60};
%
%
%
penpos14(cap_hair,-60); z14r=z13l+(apex_o,apex_o);
penpos15(cap_stem,43); penpos16(0.741cap_stem,37.5);
penpos17(cap_hair,-64); penpos18(1.222cap_stem,26);
penpos17'(cap_hair,-64);
z15=(0.808w,0.537h); z16r=(w,0.303h);
z18l=(0.716w,-apex_o); z17r=z18; z17'r=z18r;
z18.1-z18l=whatever*dir146;
z18.2-z18r=whatever*dir146; x18.2=x18.1=0;
z18'=(z6{z6r-z4}..z7{up}) intersectionpoint  
(z18l--z18.1);
z18''=(z6{z6r-z4}..z7{up}) intersectionpoint  
(z18r--z18.2);
filldraw z18l--z18'--z18''--z18r--z18l & cycle;
penpos16'(whatever,54); z16'l=z16l; x16'r=w;
penstroke z17e---z17'e...z16'e{up}..z15e{up}..
z14e{dir36};
%
%
%
penpos19(cap_hair,-58); penpos22'(1.111cap_stem,44);
penpos22(1.037cap_stem,41.5);
penpos23(1.2cap_hair,-139);
penpos24(cap_stem,-90); penpos25(0.556cap_stem,-140);
penpos26(cap_hair,90); penpos27(cap_hair,37);
z19=(0.224w,0.213h); x20=x5; z20-z19r=whatever*dir32;
z21=(0.339w,0.483h); z22'=(0.326w,0.84h);
z22r=(0.2w,h+apex_o); z23r=(0,0.767h);
z24=(0.114w,0.654h); z25=(0.197w,0.551h);
z26=(0.119w,0.437h); z27=(0.025w,0.5h);
filldraw z19r--z20 & z20{up}..z22'r
{dir129.5}--z22'l{dir-50.5}..z21{down}
..z19l{z19r-z20}--z19r & cycle;
penstroke z22'e{dir129.5}..z22e{left}
..z23e{down}..z24e{right}..z25e{down}
..z26e{left}..z27e{dir127};
%
%
%
penpos28(cap_hair,-32); penpos29(cap_hair,-90);
penpos30(cap_hair,-141.5);
z28=(0.357w,0.848h); z30=z11;
z29=(0.5[x28,x30],0.958h);
penstroke z28e..z29e..z30e;
.endchar;

.cmchar "The Fraktur letter X";
.beginchar("X",9.418u#,cap_height#,0);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos0(cap_stem,0); z0=(0.5w,0.537h);
z1=z0l; penpos5(cap_stem,-90); z5l=z0r;
penpos2(cap_hair,0); z2r=(x0r,0.697h); 
penpos3(cap_hair,0); z3l=(x0l,0.253h);
penpos6(cap_hair,-40); penpos7(cap_hair,-40);
z6r=(0.758w,-apex_o); z7-z6=whatever*dir50;
x7r=w; z7'=whatever[z6l,z7l];
z6'=whatever[z6,z7]; y6'=apex_o;
x7'=0.893w;
penstroke z6e--z7e;
filldraw z6'{left}...z3l---z1--z5l
---z5r...z7'---z7l--z7--z6' & cycle;
penpos12(cap_hair,-45); penpos13(cap_hair,-45);
z13l=(0.289w,h+apex_o); z13-z12=whatever*dir45;
x12l=apex_o; z13'=whatever[z12,z13];
y13'=h-2apex_o; 
penstroke z12e--z13e;
filldraw z12r{z13-z12}..z1{down}--
z5l---z2r...z13'{dir148}--z12--z12r & cycle;
%
%
%
penpos15(0.852cap_stem,-116);
penpos16(cap_hair,-21);
penpos14(cap_hair,-21);
z16=(0.852w,0.723h); z14-z16=whatever*dir69;
y14=y15l+apex_o; z15=(0.77w,0.91h);
penstroke z2e{up}..z15e{right};
filldraw z15r{right}..z16l{z16-z14}
--z16r--z14r--z14l{z1-z14l}
..z15l{left}--z15r & cycle;
%
%
%
penpos9(cap_hair,-35); penpos10(cap_hair,-35);
z9l=(0,0.069h); z10-z9=whatever*dir55;
y10=0.229h; z8=(0.221w,-apex_o);
z11=(0.297w,0.1h);
filldraw z9r{dir20}..z8{right}..
z3r{up}--z3l{down}..z11{left}..
z10r{z10-z9}--z10l--z9l--z9r & 
cycle;
%
%
%
penpos20(cap_hair,0); penpos21(cap_hair,0);
penpos22(cap_hair,0); penpos23(cap_hair,0);
z21-z20=whatever*dir74;
z23-z22=whatever*dir74;
y22=y5r; y21=y5l; x22=0.772w;
x21=0.235w; y23=y21+0.072h;
y20=y22-0.072h;
z20'=whatever[z20,z21]; z23'=whatever[z22,z23];
y20'=y22; y23'=y21;
penstroke z20e--z21e;
penstroke z22e--z23e;
filldraw z20'--z22--z23'--z21--z20' &
cycle;
z20''=whatever[z20',z22]; x20''=x20'+2cap_hair;
z23''=whatever[z21,z23']; x23''=x23'-2cap_hair;
filldraw z20r{z21-z20}..z20''{right}
--z21--z20--z20r & cycle;
filldraw z23''{right}..z23l{z23-z22}
--z23--z22--z23'' & cycle;
.endchar;

.cmchar "The Fraktur letter Y";
.beginchar("Y",10.556u#,cap_height#,2.181u#);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
z1=(0,0); z2=(0.079w,0.089h);
z3=(2x2,0.025h); penpos4(0.611cap_stem,-35);
penpos5(1.074cap_stem,13);
penpos7'(1.111cap_stem,39);
z4r=(0.338w,-d); z5r=(w,0.513h);
z7'r=(0.778w,h); z6'=(0.656w,0.864h);
penpos7(cap_hair,-51); penpos8(cap_hair,-51);
z8=whatever[z7'l,z7'r]; y8r=h;
z7=whatever[z7'l,z7'r]; y7=0.787h;
filldraw z1{up}..z2{right}..z3{down}
..z4l{right}--z4r{left}..z1{up} &
cycle;
penstroke z4e{right}..z5e{up}..z7'e
{dir129};
z6=(z5l{up}..z7'l{dir129}) intersectionpoint 
((0,0.8h)--(w,0.8h));
filldraw z5l{up}..z6..z6'..z7r{z7-z8}--
z7--z7'{dir-51}..z5{down}--z5l &
cycle;
penstroke z7e--z8e;
%
%
%
penpos11(cap_hair,-90); penpos9(cap_hair,-42);
penpos10(cap_hair,-42); penpos22(1.148cap_stem,48);
z9r=(0.359w,-apex_o); z10-z9=whatever*dir48;
x10=0.719w; z22=whatever[z9,z10]; x22=0.479w;
z11=(0.126w,0.215h); penpos12(2/3cap_hair,-42);
z12-z22r=whatever*dir-42;
x12=0.371w; penpos13(whatever,19);
x13l=0.5w-0.5cap_stem; x13r=0.5w+0.5cap_stem;
y13=0.585h; 
penstroke z9e--z10e;
filldraw z11r{right}..z22l{dir-42}--
z22r{dir138}--z12r{dir23}..z13r{up}
--z13l{down}..z12l..z11l{left}--
z11r & cycle;
%
%
%
penpos14(1.037cap_stem,32); penpos15(0.889cap_stem,42);
penpos16(cap_hair,150); penpos17(0.389cap_stem,-134);
penpos18(0.759cap_stem,-90);
penpos19(cap_hair,180); 
penpos20(cap_hair,90);
penpos21(1.5cap_hair,45);
z14=(x13l,0.904h); z15r=(0.281w,h+apex_o);
z16=(0.066w,0.883h); z17=(0.051w,0.822h);
z18=(0.159w,0.713h); z19=(0.275w,0.606h);
z20r=(0.189w,0.532h); z21=(0.087w,0.561h);
penstroke z13e{up}..z14e{dir136}..
z15e{left}..z16e..z17e{down}..z18e
{right}..z19e{down}..z20e{left}..
z21e{dir135};
.endchar;

.cmchar "The Fraktur letter Z";
.beginchar("Z",8.407u#,cap_height#,2.528u#);
.adjust_fit(0.05alpha*w#+usual_left#,0.05alpha*w#+usual_right#);
penpos1(0.704cap_stem,0); z1l=(0.038w,0.213h);
penpos2(0.815cap_stem,-147);
penpos3(1.148cap_stem,-152.5);
penpos4(cap_hair,90);
penpos1'(cap_hair,-66);
z2=(0.395w,0.4h); z3l=(w-x1l,0.096h);
z4l=(0.586w,-0.763d); z1'=(0.237w,0.372h);
penstroke z1e{up}..z1'e..z2e..
z3e{down}..z4e{left};
z5=(0.368w,-0.525d); z6=(0.305w,-0.575d);
z7=(0.338w,-0.15d); z8=(0.523w,0.077h);
z9=(0.406w,0.061h); penpos10(cap_hair,90);
z10r=(0.248w,0.077h);
z8'=(x8,-0.288d);
penpos11(cap_hair,180); z11=(0.068w,-0.3d);
penpos12(1.5cap_hair,-90); z12r=(0.218w,-d);
filldraw z4l{left}..z5{left}..z6 &
z6..z7{up}..z10l{left}--z10r{right}
..z9{right}..z8 & z8---z8'...z4r{right}
--z4l & cycle;
penstroke z10e{left}..z11e{down}
..z12e{right};
%
%
%
penpos14(cap_hair,-90); penpos15(0.778cap_stem,25);
penpos16(cap_stem,90); penpos17(0.5cap_stem,159);
z15r=(w,0.7h); z16r=(0.5w,h+apex_o);
z17r=(0,x15r); z14=(0.5w,0.415h);
penstroke z15e{up}..z16e{left}..
z17e{down}..z14e{right}..z15e{up};
%
%
%
z0=(x16,y17r); z18-z0=whatever*dir201;
z13-z0=whatever*dir221;
x18=x13=0; 
unfilldraw z18--z0--z13--z18 &
cycle;
.endchar;