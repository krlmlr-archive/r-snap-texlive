.cmchar "The gothic numeral 1";
.beginchar("1",4.591u#,1.242x_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(cap_stem,0); penpos2(cap_stem,0);
x1=x2=0.68w; y1=0.211h; y2=0.827h;
y6=y1; x6=x1r+0.235cap_stem; 
penpos5(hair,-45); y5=0; x5=0.08w;
penpos4(cap_hair,-26); y4=h; x4r=x1r;
penpos3(hair,-72); x3=0; y3=0.704h;
penpos7(cap_stem,0); x7=x1; y7=0.289h;
z8=(z3{dir18}..{dir64}z4) intersectionpoint 
(z7l--(x7l,h));
penstroke z3e{dir18}..{dir64}z4e;
filldraw z1l--z8..z4--z4r--z1r--z1l &
cycle;
filldraw z5l{z1-z5}..z7l{up} & z7l--z7r &
z7r{down}..z6 & z6{left}..{z5-z1}z5r &
z5r--z5l & cycle;
.endchar;

.cmchar "The gothic numeral 2";
.beginchar("2",7.591u#,1.242x_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(hair,25); z1=(0.171w,0.514h);
z1'=(0.293w,0.88h); z2=(0.624w,h+apex_o);
penpos4(cap_stem,180); z4r=(0.673w,0.676h);
penpos5(cap_hair,-32); penpos6(hair,-15);
z7=(0.38w,0.077h); z3=(0.424w,0.82h);
z8=(0.624w,0.225h); z6=(0,0);
z5=(0.229w,0.225h);
penpos9(1.15cap_stem,-128);
x9l=w; y9r=-apex_o;
filldraw z6l{dir75}..z5l..z4r{up}..z3{left}
..z1r{dir-65} & z1r--z1l &
z1l{dir115}...z1'---z2 & z2{dir-40}..
z4l{down}..z5r..z6r{dir-105} & z6r--z6l &
cycle;
filldraw z6l{dir75}..z8{right}..z9l &
z9l--z9r & z9r..z7{left}..z6r{dir-105} &
z6r--z6l & cycle;
.endchar;

.cmchar "The gothic numeral 3";
.beginchar("3",7.517u#,1.242x_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(hair,0); z1=(0.177w,0.729h);
z1'=(0.281w,0.9h); z2=(0.645w,h+apex_o);
penpos3(1.15cap_stem,-164);
z3=(0.837w,0.676h); penpos4(cap_hair,-90);
z4=(0.232w,0.521h); z5=(w-1.13cap_stem,0.232h);
z6=(w,0.303h); penpos7(cap_hair,90);
z7l=(0.448w,-apex_o); z8=(0,0.134h);
penpos10(hair,-45); x10l=x1;
z10l-z8=whatever*dir57;
filldraw z1l{up}...z1'---z2 &
z2..{dir-74}z3l & z3l..{left}z4r &
z4r--z4l & z4l{right}..z3r &
z3r{dir106}..z1r{down} & z1r--z1l & cycle;
filldraw z4l{right}..z6{down}..z7l..z8
& z8--z10l--z10r & z10r{dir-95}..z7r..
z5{up}..z4r{left} & z4r--z4l & cycle;
.endchar;

.cmchar "The gothic numeral 4";
.beginchar("4",8.813u#,1.242x_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(hair,-47); z2=(0,0.345h);
z1r-z2=whatever*dir47; y1r=h;
z2'-z1l=whatever*(z2-z1r); x2'=x2;
penpos4(stem,-90); x4=.5[x1r,w]; y4r=y2;
z3=whatever[z2,z1r]; y3=y4l;
penpos7(cap_stem,0); x7r=x1r; 
y7=0.197h; z6=whatever[z2,z1r];
x6=x7l; penpos5(hair,-30);
x5r=w; z5r-z4r=whatever*dir55;
penpos8(cap_hair,-90); 
z8=(0.273w,0); z9=(x8+1/3cap_stem,y8r);
penpos10(cap_hair,-90); z10=(0.782w,0.077h);
filldraw z2'--z1l--z1r---z7r...z10l &
z10l--z10r & z10r{left}...z9---z8r &
z8r--z8l{right}..z7l{up}--z6--z2--z2' &
cycle;
filldraw z2--z3---z4l...z5l{z5-z4r} &
z5l--z5r--z4r--z2 & cycle;
.endchar;

.cmchar "The gothic numeral 5";
.beginchar("5",7.332u#,1.242x_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(hair,-68); penpos2(1.17cap_stem,62);
z1=(w,h); z1r=z2r; 
penpos5(1.5cap_hair,-10); z5l=(0.354w,h+apex_o);
penpos6(hair,-34);
z6=(0.0707w,0.458h); 
z2'-z5l=whatever*(z5r-z5l);
z2'=whatever[z2l,z2r];
z4'-z2l=whatever*(z5-z2');
z4'=whatever[z6r,z5r];
penpos7(cap_stem,-90); z7=(0.439w,0.637h);
z9=(w,0.394h); z8=(0.783w,0.285h);
penpos10(1.5cap_hair,90); 
z10l=(x7,-apex_o); penpos11(1.06cap_stem,40);
z11l=(-apex_o,0.183h); penpos12(hair,-50);
z12l=z11r;
z3=2/3[z5,z2'];
penstroke z6e--z5e; filldraw z4'--z5r---
z3...z1l & z1l--z1r--z2l--z4' & cycle;
filldraw z6r{dir56}..z7r..z8{down}..z10r
{left}..z12r{up} & z12r--z11r--z11l &
z11l..z10l{right}..z9{up}..z7l..z6l{dir-124}
& z6l--z6r & cycle;
.endchar;

.cmchar "The gothic numeral 6";
.beginchar("6",7.332u#,1.242x_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(hair,90); z1=(0.889w,h);
penpos2(1.059cap_stem,-153);
z2r=(0,0.465h); penpos3(1.1cap_stem,-133);
z3r=(0.535w,-apex_o); penpos4(cap_hair,-43);
z4r=z3; penpos5(1.059cap_stem,35);
z5r=(w,0.387h);
z6=(0.394w,0.549h); z7=(0.586w,0.746h);
penpos8(cap_hair,-50); 
z7-z8l=whatever*dir40; x8=x2;
penstroke z1e{left}..z2e..z3e;
filldraw z4r---z3l...z5r{up}..z7 &
z7--z8l--z8r{z7-z8l}..z6{right}..z5l{down}..
z4l{z3r-z3l} & z4l--z4r & cycle;
.endchar;

.cmchar "The gothic numeral 7";
.beginchar("7",7.591u#,1.242x_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos4(1.765cap_stem,23); x4=0.39w;
y4l=-hair; penpos5(hair,-45); z5r=(w,h);
penpos1(cap_hair,-20); penpos2(cap_hair,-20);
z1l=(0,0.729h); z2-z1=whatever*dir70;
y2r=h; penpos3(1.078cap_stem,70);
z3=whatever[z1,z2]; y3r=h;
z6=(z3l--(w,y3l)) intersectionpoint 
(z4..z5{dir45});
z3'=0.284[z3l,z6]; 
filldraw z1r{z2-z1}..z3'{right} & 
z3'--z2r--z2l--z1l--z1r & cycle;
filldraw z3l--z6..z5r--z3r--z3l & cycle;
filldraw z4r{z3'-z4r}..z5r{dir45} &
z5r--z5l & z5l{dir225}..z4l & z4l--z4r &
cycle;
.endchar;

.cmchar "The gothic numeral 8";
.beginchar("8",7.332u#,1.242x_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(1.176cap_stem,-142); 
penpos2(0.824cap_stem,-147);
penpos3(cap_hair,117);
penpos4(0.824cap_stem,-151);
penpos5(1.412cap_stem,-126);
z1l=(0.465w,h+apex_o); z2l=(w-hair,0.796h);
z3=(0.5w,0.531h); z4r=(0,0.248h);
z5r=(2/3w,-apex_o); 
penstroke z1e{dir-10}..
z2e{down}..z3e{dir203}..
z4e{down}..z5e{right};
penpos6(cap_hair,-36); z6r=z5;
penpos7(0.824cap_stem,41); x7r=w; y7l=y4;
z8=z3; penpos8(cap_stem,75);
penpos9(0.853cap_stem,45); 
x9l=hair; y9=y2r; penpos10(cap_hair,-52);
z10l=z1r; z11=(z1r..z2r{down})
intersectionpoint ((x1l,0)--(x1l,h));
penstroke z6e{z5l-z5r}..z7e{up}..z8e{dir165}
..z9e{up}..z10e{z1l-z1r};
filldraw z10r{z1l-z1r}..z11..z2r{down} &
z2r--z2 & z2{up}..z1 & z1--z10l--z10r &
cycle; .endchar;

.cmchar "The gothic numeral 9";
.beginchar("9",7.332u#,1.242x_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(hair,90); z1=(0.889w,h);
penpos2(1.059cap_stem,-153);
z2r=(0,0.465h); penpos3(1.1cap_stem,-133);
z3r=(0.535w,-apex_o); penpos4(cap_hair,-43);
z4r=z3; penpos5(1.059cap_stem,35);
z5r=(w,0.387h);
z6=(0.394w,0.549h); z7=(0.586w,0.746h);
penpos8(cap_hair,-50); 
z7-z8l=whatever*dir40; x8=x2;
penstroke z1e{left}..z2e..z3e;
filldraw z4r---z3l...z5r{up}..z7 &
z7--z8l--z8r{z7-z8l}..z6{right}..z5l{down}..
z4l{z3r-z3l} & z4l--z4r & cycle;
transform metatopish;
metatopish=identity 
reflectedabout ((0,0.5h),(w,0.5h));
picture krufo;
krufo:= currentpicture transformed
metatopish;
picture currentpicture;
currentpicture=krufo;
transform metatopish;
metatopish=identity 
reflectedabout ((0.5w,0),(0.5w,h));
picture krufo;
krufo:= currentpicture transformed
metatopish;
picture currentpicture;
currentpicture=krufo;
.endchar;

.cmchar "The gothic numeral 0";
.beginchar("0",7.332u#,1.242x_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(1.059cap_stem,-143);
penpos2(cap_stem,-140);
penpos3(1.05cap_stem,-136);
penpos4(1.1cap_stem,-142);
penpos5(cap_hair,128);
penpos6(cap_hair,127);
z1l=(0.535w,h+apex_o); z4r=(w-x1l,-apex_o);
z2l=(w,0.528h); x3r=0; y3=y2l+apex_o;
z5l=z4; z6r=z1;
penstroke z1e{dir-25}..z2e{down}..
z5e{z4r-z4l};
penstroke z6e{z1r-z1l}..z3e{down}..
z4e{dir-31};
.endchar;
