% su-spec.mf -Input-File Zeichensatz

makewi(8,13ut,ang,top);
beginchar("0",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=y7=y8=top;
y2=y6=mid;
y3=y5=an_bot;
y4=bas;
makex(1,7ut,ang,y1);
makex(2,0ut,ang,y2);
makex(3,1ut,ang,y3);
makex(4,4ut,ang,y4);
makex(5,6.5ut,ang,y5);
makex(6,9ut,ang,y6);
makex(7,8ut,ang,y7);
makex(8,12ut,ang,y8);
%
draw z1{dir gamma-dir 10}..z2..z3..z4{right}..z5..z6..z7;
draw z7..z8{dir gamma};
labels(range 1 thru 8);
endchar;


makewi(2,8ut,ang,mid);
beginchar("1",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=mid;
y3=bas;
y2=top;
makex(1,0ut,ang,y1);
makex(2,7ut,ang,y2);
makex(3,3ut,ang,y3);
%
draw z1..z2;
draw z2..z3;
labels(range 1 thru 3);
endchar;


makewi(2,10ut,ang,mid);
beginchar("2",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=Z_anf;
y2=top;
y3=mid;
y4=an_mid;
y5=y6=bas;
y7=an_bot;
makex(1,0ut,ang,y1);
makex(2,4ut,ang,y2);
makex(3,7ut,ang,y3);
makex(4,3.3ut,ang,y4);
makex(5,-2ut,ang,y5);
makex(6,4.5ut,ang,y6);
makex(7,7.3ut,ang,y7);
%
draw z1..z2{right}..z3..z4..z5{dir mi_beta};
draw z5{dir gamma}..z6{right}..z7;
labels(range 1 thru 7);
endchar;


makewi(2,9ut,ang,mid);
beginchar("3",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=an_bot;
y2=bas;
y3=an_mid;
y4=an_top;
y5=y7=Z_anf;
y6=top;
makex(1,-2ut,ang,y1);
makex(2,0ut,ang,y2);
makex(3,6ut,ang,y3);
makex(4,3ut,ang,y4);
makex(5,8ut,ang,y5);
makex(6,5ut,ang,y6);
makex(7,0ut,ang,y7);
%
draw z7..z6{right}..z5..z4{left};
draw z4{right}..z3..z2{left}..z1;
labels(range 1 thru 7);
endchar;


makewi(2,10ut,ang,mid);
beginchar("4",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=top;
y2=an_mid;
y3=1/2(mid-an_top)+an_top;
y4=mid;
y5=bas;
makex(1,7ut,ang,y1);
makex(2,0ut,ang,y2);
makex(3,12ut,ang,y3);
makex(4,8.3ut,ang,y4);
makex(5,3.5ut,ang,y5);
%
draw z1..z2;
draw z2{right}..z3;
draw z4..{dir mi_beta}z5;
labels(range 1 thru 7);
endchar;


makewi(2,11ut,ang,mid);
beginchar("5",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=y6=top;
y2=y3=an_top;
y4=bas;
y5=an_bot;
makex(1,5ut,ang,y1);
makex(2,1ut,ang,y2);
makex(3,8ut,ang,y3);
makex(4,2ut,ang,y4);
makex(5,-1ut,ang,y5);
makex(6,10ut,ang,y6);
%
draw z1..z2;
draw z2{dir gamma}..z3..z4{left}..z5;
draw z1..z6{dir gamma};
labels(range 1 thru 7);
endchar;


makewi(1,12ut,ang,mid);
beginchar("6",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=I_anf;
y2=top;
y3=O_bogen;
y4=bas;
y5=an_mid;
y6=an_top;
makex(1,11ut,ang,y1);
makex(2,8.5ut,ang,y2);
makex(3,0ut,ang,y3);
makex(4,3ut,ang,y4);
makex(5,8ut,ang,y5);
makex(6,6ut,ang,y6);
%
draw z1..z2{left}..z3{dir mi_beta}..z4{right}..z5..
                                        z6{left}..z3{dir mi_beta};
labels(range 1 thru 6);
endchar;


makewi(2,10ut,ang,mid);
beginchar("7",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=I_anf;
y2=top;
y3=bas;
y4=an_top=y5;
makex(1,0ut,ang,y1);
makex(2,9ut,ang,y2);
makex(3,3.5ut,ang,y3);
makex(4,2ut,ang,y4);
makex(5,8ut,ang,y5);
%
draw z1{dir beta}..z2{dir beta};
draw z2..z3;
draw z4..z5{dir beta-dir 90};
labels(range 1 thru 7);
endchar;


makewi(9,13ut,ang,mid);
beginchar("8",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=y8=y9=top;
y2=y7=mid;
y3=an_top;
y4=y6=an_mid;
y5=bas;
makex(1,7ut,ang,y1);
makex(2,3.4ut,ang,y2);
makex(3,5ut,ang,y3);
makex(4,7.5ut,ang,y4);
makex(5,4ut,ang,y5);
makex(6,0ut,ang,y6);
makex(7,9ut,ang,y7);
makex(8,9ut,ang,y8);
makex(9,12ut,ang,y9);
%
draw z1{left}..z2..z3..z4..z5{left}..z6..z3..z7..z8;
draw z8..z9{dir beta};
labels(range 1 thru 9);
endchar;


makewi(2,10ut,ang,mid);
beginchar("9",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=y3=mid;
y4=top;
y2=an_top;
y5=y7=an_bot;
y6=bas;
makex(1,7.5ut,ang,y1);
makex(2,4ut,ang,y2);
makex(3,1.2ut,ang,y3);
makex(4,5.2ut,ang,y4);
makex(5,4.5ut,ang,y5);
makex(6,1.3ut,ang,y6);
makex(7,-1.5ut,ang,y7);
%
draw z1{dir mi_beta}..z2{left}..z3..z4{right}..z1{dir mi_beta}..
                                                   z5..z6{left}..z7;
labels(range 1 thru 7);
endchar;


makewi(2,4ut,ang,mid);
beginchar(131,wi/hppp,14ut#,0ut);     % uebergang an_top{gamma} -> z
pickup su_pen;
y1=an_top;
y2=mid;
makex(1,0ut,ang,y1);
makex(2,4ut,ang,y2);
%
draw z1{dir gamma}..z2{right};
labels(range 1 thru 2);
endchar;


makewi(2,4ut,ang,mid);
beginchar(132,wi/hppp,14ut#,0ut);   % uebergang an_top{right} -> z
pickup su_pen;
y1=an_top;
y2=mid;
makex(1,0ut,ang,y1);
makex(2,4ut,ang,y2);
%
draw z1{right}..z2{right};
labels(range 1 thru 2);
endchar;


makewi(3,12ut,ang,mid);
beginchar(133,wi/hppp,14ut#,0ut);   % uberggang blank -> z
pickup su_pen;
y1=bas;
y2=an_top;
y3=mid;
makex(1,0ut,ang,y1);
makex(2,8.5ut,ang,y2);
makex(3,12ut,ang,y3);
%
draw z1..z2..z3{right};
labels(range 1 thru 3);
endchar;


makewi(3,7ut,ang,mid);
beginchar(135,wi/hppp,14ut#,0ut);   % uberggang an_mid -> z
pickup su_pen;
y1=an_mid;
y2=an_top;
y3=mid;
makex(1,0ut,ang,y1);
makex(2,3.5ut,ang,y2);
makex(3,7ut,ang,y3);
%
draw z1..z2..z3{right};
labels(range 1 thru 3);
endchar;


makewi(2,9ut,ang,mid);
beginchar(128,wi/hppp,14ut#,0ut);   %uebergang an_bot -> mid
pickup su_pen;
y1=an_bot;
y2=mid;
makex(1,0ut,ang,y1);
makex(2,9ut,ang,y2);
%
draw z1..z2;
labels(range 1 thru 7);
endchar;

makewi(2,4ut,ang,mid);
beginchar(129,wi/hppp,14ut#,0ut);   % uebergang an_top -> mid
pickup su_pen;
y1=an_top;
y2=mid;
makex(1,0ut,ang,y1);
makex(2,4ut,ang,y2);
%
draw z1{right}..{dir gamma}z2;
labels(range 1 thru 7);
endchar;

makewi(2,7ut,ang,mid);
beginchar(130,wi/hppp,14ut#,0ut);  % uebergang an_mid -> mid
pickup su_pen;
y1=an_mid;
y2=mid;
makex(1,0ut,ang,y1);
makex(2,7ut,ang,y2);
%
draw z1{dir delta}..{dir gamma}z2;
labels(range 1 thru 7);
endchar;

makewi(4,9ut,ang,mid);
beginchar("+",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=an_top;
y2=an_bot;
y3=y4=an_mid;
makex(1,5.3ut,ang,y1);
makex(2,3ut,ang,y2);
makex(3,0ut,ang,y3);
makex(4,8.8ut,ang,y4);
%
draw z1..z2;
draw z3..z4;
labels(range 1 thru 4);
endchar;


makewi(4,9ut,ang,mid);
beginchar("*",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=an_top;
y2=an_bot;
y3=y4=an_mid;
y5=y6=6/7y1;
y7=y8=9/7y2;
makex(1,5.3ut,ang,y1);
makex(2,4ut,ang,y2);
makex(3,0ut,ang,y3);
makex(4,8.8ut,ang,y4);
makex(5,2ut,ang,y5);
makex(6,8ut,ang,y6);
makex(7,1ut,ang,y7);
makex(8,6.8ut,ang,y8);
%
draw z1..z2;
draw z3..z4;
draw z5..z8;
draw z6..z7;
labels(range 1 thru 7);
endchar;


makewi(2,8ut,ang,mid);
beginchar("-",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=y2=an_mid;
makex(1,0ut,ang,y1);
makex(2,8ut,ang,y2);
%
draw z1..z2;
labels(range 1 thru 7);
endchar;


makewi(2,12.5ut,ang,mid);
beginchar(134,wi/hppp,14ut#,0ut);   % --
pickup su_pen;
y1=y2=an_mid;
makex(1,0ut,ang,y1);
makex(2,12.5ut,ang,y2);
%
draw z1..z2;
labels(range 1 thru 7);
endchar;


makewi(2,9ut,ang,mid);
beginchar(";",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=an_mid;
y2=an_bot;
y3=1/7bot;
makex(1,2.5ut,ang,y1);
makex(2,2.5ut,ang,y2);
makex(3,1.5ut,ang,y3);
%
draw z2{dir mi_beta+dir 10}..z3{dir mi_beta};
pickup su_pen_small;
drawdot z1;
labels(range 1 thru 3);
endchar;


makewi(2,9ut,ang,mid);
beginchar(",",wi/hppp,14ut#,0ut);
pickup su_pen;
y2=an_bot;
y3=1/7bot;
makex(2,2.5ut,ang,y2);
makex(3,1.5ut,ang,y3);
%
draw z2{dir mi_beta+dir 10}..z3{dir mi_beta};
pickup su_pen_small;
drawdot z2;
labels(range 2 thru 3);
endchar;


makewi(2,3ut,ang,mid);
beginchar(".",wi/hppp,14ut#,0ut);
y2=bas;
makex(2,2ut,ang,y2);
%
pickup su_pen_small;
drawdot z2;
labels(range 1 thru 7);
endchar;


makewi(2,9ut,ang,top);
beginchar("!",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=Z_anf;
y2=an_mid;
y3=bas;
makex(1,4ut,ang,y1);
makex(2,1ut,ang,y2);
makex(3,0.5ut,ang,y3);
%
draw z1..z2;
pickup su_pen_small;
drawdot z3;
labels(range 1 thru 3);
endchar;


makewi(2,4ut,ang,mid);
beginchar(":",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=6/7an_top;;
y2=an_bot;
makex(1,1.5ut,ang,y1);
makex(2,.3ut,ang,y2);
%
pickup su_pen_small;
drawdot z1; drawdot z2;
labels(range 1 thru 2);
endchar;


makewi(2,8ut,ang,top);
beginchar("?",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=Z_anf;
y2=top;
y3=mid;
y4=an_mid;
y5=1/2an_bot;
makex(1,1ut,ang,y1);
makex(2,5.5ut,ang,y2);
makex(3,7ut,ang,y3);
makex(4,3.3ut,ang,y4);
makex(5,3ut,ang,y5);
%
draw z1..z2{right}..z3..z4{dir mi_beta};
pickup su_pen_small;
drawdot z5;
labels(range 1 thru 7);
endchar;


makewi(2,7ut,ang,mid);
beginchar("=",wi/hppp,14ut#,0ut);
pickup su_pen;
y1=4/3an_mid=y2;
y3=y4=2/3an_mid;
makex(1,0ut,ang,y1);
makex(2,6.5ut,ang,y2);
makex(3,0ut,ang,y3);
makex(4,6.5ut,ang,y4);
%
draw z1..z2{dir 5};
draw z3..z4{dir 5};
labels(range 1 thru 7);
endchar;


makewi(2,6ut,ang,mid);
beginchar("(",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=top;
y2=bas;
makex(1,5ut,ang,y1);
makex(2,1ut,ang,y2);
%
draw z1{dir mi_gamma}..z2;
labels(range 1 thru 2);
endchar;


makewi(2,6ut,ang,mid);
beginchar(")",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=top;
y2=bas;
makex(1,5ut,ang,y1);
makex(2,1ut,ang,y2);
%
draw z1..z2{dir mi_gamma};
labels(range 1 thru 2);
endchar;


makewi(2,6ut,ang,mid);
beginchar("/",wi/hppp,21ut#,0ut);
pickup su_pen;
y1=top;
y2=bas;
makex(1,5ut,ang,y1);
makex(2,1ut,ang,y2);
%
draw z1..z2;
labels(range 1 thru 2);
endchar;


makewi(2,6ut,ang,mid);
beginchar(16,wi/hppp,21ut#,0ut);     % einleitende Anfuehrungszeichen
pickup su_pen;
y1=an_bot;
y2=an_bot;
y3=y4=bas-1.5ut;
makex(1,1ut,ang,y1);
makex(2,3ut,ang,y2);
makex(3,1ut,ang,y3);
makex(4,3ut,ang,y4);
%
draw z1{dir mi_gamma+dir 20}..z3{dir mi_gamma};
draw z2{dir mi_gamma+dir 20}..z4{dir mi_gamma};
labels(range 1 thru 4);
endchar;

makewi(2,6ut,ang,mid);
beginchar(17,wi/hppp,21ut#,0ut);   %schliessendes Anfuehrungszeichen
pickup su_pen;
y1=y2=top;
y3=top-1/2(top-mid)=y4;
makex(1,1ut,ang,y1);
makex(2,3ut,ang,y2);
makex(3,1ut,ang,y3);
makex(4,3ut,ang,y4);
%
draw z1{dir mi_gamma+dir 20}..z3{dir mi_gamma};
draw z2{dir mi_gamma+dir 20}..z4{dir mi_gamma};
labels(range 1 thru 4);
endchar;

