.cmchar "The gothic letter A";
.beginchar("A",10.65u#,1.065cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
x1=x2=.75w; y1=0.157h; y2=0.762h;
penpos1(cap_stem,0);
penpos2(cap_stem,0);
penpos3(stem,-90);
x3=x1l; y3=0.4h; x4=0.15w;
y4=y1-cap_hair; penpos4(cap_hair,-30);
x5=x1; z5-z4=whatever*dir60;
penpos5(cap_hair,-30);
penpos6(cap_hair,60);
penpos7(cap_curve,-120);
z7l=z5l; y6r=y3r; 
z6=whatever[z5l,z4l];
penstroke z6e{dir150}...{dir-30}z7e;
penstroke z4e--z5e;
penstroke z1e--z2e;
y3.1=y3r; y3.2=y3l;
z3.1=whatever[z4r,z5r];
z3.2=whatever[z4r,z5r];
filldraw z3r--z3.1 & z3.1--z3.2
& z3.2--z3l & z3l--z3r & cycle;
penpos8(cap_stem,60);
penpos9(cap_stem,60);
y8l=0; x8l=0.45w; 
z9-z8=whatever*dir150;
z9=whatever[z4r,z5r];
penstroke z8e...z9e;
y1.1=0; x1.1=0.8w; 
penpos1.2(cap_hair,-42);
z1.2r-z1.1=whatever*dir48;
x1.2r=w=x1.3; 
z1.3-z1.2l=whatever*(z1.2r-z1.1);
x1.4=x1r; x1.5=x1l;
z1.5-z8l=whatever*(z1.2r-z1.1);
y1.4=y1.3;
filldraw z1l--z1.1 & z1.1--z1.2r
& z1.2r--z1.3 & z1.3--z1.2l...
z1.4{up} & z1.4--z1r & z1r--z1l
& cycle;
x1.6=x1l; 
z1.6-z8r=whatever*(z1.5-z8l);
filldraw z8l--z1.5 & z1.5--z1.6
& z1.6--z8r & z8r--z8l & cycle;
x5.1=x1.1; 
z5.1=whatever[z5r,z5l];
x5.3=x5.2=w; y5.2=0.5[y5.1,h];
y5.3=y5.2-0.5cap_hair;
x2.1=x2l; z2.1=whatever[z4r,z5r];
filldraw z5r--z5.1 & 
z5.1{z5.1-(0,y3l)}...{right}z5.2
& z5.2--z5.3 & z5.3{left}...
{down}z2r & z2r--z2l & 
z2l--z2.1 & z2.1--z5r & cycle;
x4.1=x4; y4.1=0;
filldraw z4l{z4l-z5l}...
z4.1{right}...z4r{dir120} &
z4r--z4l & cycle;
x10=x3.2; 
y10=y2;
x5.15=x5.1; y5.15=0.75[y2,y5.1];
filldraw z10{z5.1-z10}...
z2.1{right} & z2.1--z5.15 &
z5.15{left}...z10{dir225} &
cycle;
transform katebasma;
katebasma=identity 
shifted (0,-apex_o);
%transform megalwma;
%megalwma=identity scaled 1.065;
picture krufo;
%krufo= currentpicture transformed
%megalwma;
krufo:= currentpicture transformed
katebasma;
picture currentpicture;
currentpicture=krufo;
.endchar;


.cmchar "The gothic letter B";
.beginchar("B",10.72u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
x1=x2=0.39w; y2=0.85h; 
y1=y7=y18=1/6h;
penpos1(cap_stem,0);
penpos2(cap_stem,0);
penstroke z1e--z2e;
penpos3(cap_hair,0);
penpos2'(cap_stem,180); z2'=z2;
x3l=0; y3=y2; 
x3.5=0.5[x3,x2]; 
penpos3.5(0.5[cap_hair,cap_stem],
-90); y3.5l=h+apex_o;
penstroke z3e{dir60}...z3.5e...
z2'e{down};
penpos4(1.2cap_hair,-90);
penpos9(1.2cap_hair,-90);
x4=x3r; 
y4r=0.56h;
y9l=y12.2-2cap_hair=0.56h;
z4.1-z4l=whatever*(z2r-z4r);
y4.1=y2;
filldraw z4r--z2r--z4.1--z4l--z4r &
cycle;
penpos5(1.2cap_hair,-90); z5l=z2r;
penpos6(whatever,angle(z2r-z4r)-90);
z6l=whatever[z4r,z5l];
z6r-z5r=whatever*(z5l-z4r);
y6l=h+apex_o; 
filldraw z5l--z6l--z6r--z5r--z5l
& cycle;
x7=0.54w; penpos7(0.9cap_hair,0);
z8.1=whatever[z4r,z6l];
x8.1=x7l; x8.2=x7r;
z8.2=whatever[z4r,z6l];
filldraw z7r--z8.2--z8.1--z7l--z7r
& cycle;
penpos11(1.118cap_stem,
angle(z6l-z4r));
z11=whatever[z4r,z6l]; x11r=x6l;
x9l=x7l; 
z10.1-z6=whatever*(z6r-z6l);
z10.1-z9l=whatever*(z6l-z4r);
z10.4=whatever[z9l,z10.1];
z10.4-z11l=whatever*(z10.1-z6);
filldraw z10.4--z11l--z11r--z10.1
--z10.4 & cycle;
z10.2-z9r=whatever*(z10.1-z9l);
z10.2=whatever[z6,z10.1];
z12.1=whatever[z9r,z10.2];
z12.1=whatever[z11l,z10.4];
filldraw z9l--z10.1--z10.2--z9r
--z9l & cycle;
x12.2=w; 
z12.4=(x7l,y9l);
%z12.4=whatever[z9l,z10.1];
%x12.4=x7r;
x12.2-x12.3=1.2cap_stem; 
z12.3-z12.4=whatever*(z12.2-z12.1);
filldraw z12.4--z12.1--z12.2--z12.3
--z12.4 & cycle;
x1.1=x1l; y1.1=0.247h;
x13.2=x1l; y13.2=0.56h;
z13.1-z13.2=
whatever*dir(angle(z6l-z4r)+100);
z13.1=whatever[z4r,z5l];
filldraw z4l{dir-30}...z1.1{down}
& z1.1--z13.2--z13.1--z4l &
cycle;
unfill z4l{dir-30}...z1.1{down}
& z1.1--(x4l,y1.1)--z4l &
cycle;
z15=(0,-apex_o); z17=(0.73w,-apex_o);
x18=w; z19=(x12.3,y18);
filldraw z15{dir80}...{right}(x1,y1.1)
...(w,-apex_o){dir-50} &
(w,-apex_o)--z17 & z17{dir135}...
z15{dir225} & cycle;
x18.2=x18.1=x12.3+cap_stem;
y18.2=7/8[y18,y12.2];
y18.1=1/8[y18,y12.2];
filldraw z12.3--(x12.3,y12.2)--z12.2 &
z12.2{left}...z18.2{down}--
z18.1{down}...z18{right} & 
z18--(w,-apex_o)--(0.8w,-apex_o)
--z12.3 & cycle;
unfilldraw z17{dir60}...z18{right}
& z18--(w,-apex_o)--z17 &
cycle;
unfilldraw z17{dir60}...z18{right}
& z18--(w,-apex_o)--z17 &
cycle;
.endchar;

.cmchar "The gothic letter C";
.beginchar("C",11.39u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#-0.5u#);
penpos1(0.765cap_stem,0);
z1l=(0,0.5h); 
penpos2(cap_stem,37.5);
z2l=(2/3w,-apex_o);
penpos3(cap_hair,-40);
penpos4(cap_hair,0); 
z4r-z2r=whatever*dir153;
x4=0.5w; %x3r=x4r; 
penpos2'(cap_stem,37.5);
x2'r=0.36w-0.5cap_stem;
z2'-z2=whatever*(z4r-z2r);
z4'=(x4,h); penpos4'(cap_hair,0);
%penstroke z4e--z4'e;
penpos2.1(cap_hair,angle(z2'-z2));
z2.1=z2'l; 
z5=whatever[z2'l,z2'r];
z6=whatever[z2'l,z2'r];
x5=x4r; x6=0.86w;
penpos5(whatever,-90);
z5l-z2.1l=whatever*(z5-z2.1);
penpos6(whatever,-27.5);
z6l-z2.1l=whatever*(z6-z2.1);
z7.1=whatever[z5l,z6l];
x7.1=x2'r; x7.2=x7.1+1.2cap_stem;
z7.2=whatever[z5l,z6l];
z14'=(x2l,h);
z3l=z14';
penpos8.1(1.2cap_hair,-40);
z8.1=(x7.1,0.8h);
penstroke z2e..z2'e..z1e{up}...
z8.1e---z3e{dir24};
z8.2=(z1{up}..z3{dir34})
intersectionpoint (z7.2--(x7.2,h));
penpos12(1.2cap_stem,angle(z8.2-z8.1));
 z12r=z14'; 
%
%
%
filldraw z7.1--z7.2--z8.2--z8.1--
z7.1 & cycle;
x7.3=x7.2+slab; y7.3=0.5[y7.1,y7.2];
filldraw z7.1--
z7.3 & z7.3..{z8.2-z7.2}z7.2 
& z7.2--z7.1 & cycle;
%
%
%
x9=w-slab; penpos9(cap_hair,-90);
z9r=whatever[z2l,z2r];
z9.1=whatever[z2.1,z2l];
z9.1-z9l=whatever*(z2l-z9r);
filldraw z9.1--z2l--z9r--z9l--
z9.1 & cycle;
z9.2=whatever[z9.1,z9l];
x9.2=0.5[x2,x9];
z9.4=whatever[z9.1,z9];
z9.4=whatever[z4r,z2r];
filldraw z7.1{z2r-z4r}..
z9l{z9-z9.2} & z9l--z2l--z7.1
& cycle;
%
%
%
penpos11'(cap_stem,-27.5);
z11'l=whatever[z5l,z6l];
y11'r=y1+0.8slab; 
z10=whatever[z6l,z6r];
z10-z11'r=whatever*(z6r-z5r);
z10.1=whatever[z6,z10];
z10.2=whatever[z11'r,z10];
x10.1=x10.2=w;
penpos13(0.8cap_hair,angle(z6-z5));
z13=whatever[z6r,z5r];
z13r=z6r; 
%
%
%
z13.1-z3l=whatever*(z3r-z3l);
x13.1=x9-cap_hair;
z13.2-z13.1=whatever*(z12r-z12l);
z13.2-z12l=whatever*(z13.1-z3);
filldraw z12l--z13.2--z13.1--z12r
--z12l & cycle;
z13.3=0.5[z13.2,z13.1];
z13.4=1/3[z6r,z5r];
z14=whatever[z12r,z13.1];
y14=h+apex_o;
penpos15(cap_hair,-90);
x15=x9; z15r=whatever[z13.2,z13.1];
z15.1=whatever[z12r,z13.1];
z15.1-z15l=whatever*(z13.2-z13.1);
filldraw z13.1--z15r--z15l--
z15.1--z13.1 & cycle;
%
%
%
x20=x2l=x20'; y20=y2; 
z20'=whatever[z12,z13.3];
penpos20(cap_hair,0);
penpos20'(cap_hair,0);
penstroke z20e--z20'e;
z30=whatever[z13.2,z13.1];
z30-z8.2=whatever*(z13.1-z14);
z31=(x7.1,y7.2); 
filldraw z31{down}..z2' & z2'--
z7.1--z31 & cycle;
%
%
%
z30'-z13.2=whatever*(z13.1-z14');
z15r-z30'=whatever*dir(angle(z12l-z13.2)+90);
filldraw z13.2--z30'--z15r--z13.2
& cycle;
.endchar;


.cmchar "The gothic letter D";
.beginchar("D",12.055u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(cap_stem,0);
penpos2(cap_stem,0);
penpos3(cap_stem,90);
penpos4(cap_stem,90);
z1r=(w,-apex_o); z2r=(w,h-1.5cap_stem);
z3r=(w-1.5cap_stem,h); z4r=(0.19w,h);
penpos5(0.5cap_hair,0);
z5l=(0,h+2apex_o);
penstroke z1e--z2e{up}...z3e{left}
--z4e{left}...z5e{up};
penpos6(0.8cap_hair,-45);
x6l=0; y6r=-apex_o;
z6.4=(0.83w,-apex_o); 
z6.3=(w,0.8cap_stem-apex_o);
x6.2=0.5w; y6.2=0.28h;
x6.1=0.433w; 
z6.1-z6l=whatever*dir30;
filldraw z6r{dir30}...z6.4{dir-45}
& z6.4--z6.3 & z6.3{dir140}...
z6.2{left}...z6.1---z6l &
z6l--z6r & cycle;
unfilldraw z6.4{dir60}...
(w,cap_stem){right} & 
(w,cap_stem)--(w,-apex_o)--
z6.4 & cycle;
unfilldraw z6.4{dir60}...
(w,cap_stem){right} & 
(w,cap_stem)--(w,-apex_o)--
z6.4 & cycle;
x7=x8=0.5w; x9=x10=0.6w;
y8=y10=y3l; y7=y9=0.225h;
penpos7(0.9cap_hair,0);
penpos8(0.9cap_hair,0);
penpos9(0.9cap_hair,0);
penpos10(0.9cap_hair,0);
penstroke z7e--z8e;
penstroke z9e--z10e;
x11=x12=0.5[x9r,x1l];
y11=0.59h; y12=0.44h;
penpos11(0.9cap_hair,-90);
penpos12(0.9cap_hair,-90);
x11.1=x12.1=x9r; x11.2=x12.2=x1l;
penpos11.1(0.9cap_hair,-90);
penpos11.2(0.9cap_hair,-90);
penpos12.1(0.9cap_hair,-90);
penpos12.2(0.9cap_hair,-90);
y11.1r=y11l=y11.2r;
y12.1l=y12r=y12.2l;
penstroke z11.1e...{right}z11e...
z11.2e;
penstroke z12.1e...{right}z12e...
z12.2e;
penpos6'(cap_hair,-45);
z6'r=z6l; penpos13(0.9cap_hair,-45);
z13-z6'l=whatever*(z6.1-z6l);
x13=1/3cap_stem; penpos14(cap_stem,0);
penpos15(cap_stem,0); 
penpos16(0.9cap_hair,-45);
y14=y12; y15=y11;
x14=x15=1/3w; z16r=z8l;
x13.1=x14l; 
z13.1-z13r=whatever*(z6.1-z6l);
filldraw z13r{dir45}--z13.1..
z14r{up}--z15r{up}..z16r{dir45}
& z16r--z16l & z16l{dir225}..
z15l{down}--z14l{down}..z13l{dir225}
& z13l--z13r & cycle;
x17=0.6cap_stem; y17=y12l;
x17.1=0.5[x17,x14l]; y17.1=1/3[y11,y12];
filldraw z13l{dir45}..z14{up}..
z17.1{left}..z17{down} & z17{up}..
(x14l-2apex_o,0.5[y13.1,y15])
{down}..
z13l{dir225} & cycle;
z20=(z6.3{dir140}...
z6.2{left}...z6.1---z6l) 
intersectionpoint (z1l--z2l);
penpos20(.5cap_stem,0);
%filldraw z20r..z20l..cycle;

.endchar;

.cmchar "The gothic letter E";
.beginchar("E",11.39u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(0.765cap_stem,0);
z1l=(0,0.5h); 
penpos2(cap_stem,37.5);
z2l=(2/3w,-apex_o);
penpos3(cap_hair,-66);
penpos4(cap_hair,0); 
z4r-z2r=whatever*dir153;
x4=0.6w; x3r=x4r; y3r=0.9h;
penpos2'(cap_stem,37.5);
x2'r=0.36w-0.5cap_stem;
z2'-z2=whatever*(z4r-z2r);
penstroke z2e--z2'e..z1e{up}..
z3e{dir34};
z4'=(x4,h); penpos4'(cap_hair,0);
penstroke z4e--z4'e;
penpos2.1(cap_hair,angle(z2'-z2));
z2.1=z2'l; 
z5=whatever[z2'l,z2'r];
z6=whatever[z2'l,z2'r];
x5=x4r; x6=0.86w;
penpos5(whatever,-90);
z5l-z2.1l=whatever*(z5-z2.1);
penpos6(whatever,-27.5);
z6l-z2.1l=whatever*(z6-z2.1);
filldraw z5r--z6r--z6l--z5l--z5r
& cycle;
z7.1=whatever[z5l,z6l];
x7.1=x2'r; x7.2=x7.1+1.2cap_stem;
z7.2=whatever[z5l,z6l];
x8.1=x7.1; x8.2=x7.2;
z3-z8.1=whatever*dir24;
z8.2=whatever[z8.1,z3];
filldraw z7.1--z7.2--z8.2--z8.1--
z7.1 & cycle;
x7.3=x7.2; y7.3=0.5[y7.1,y7.2];
filldraw z7.1{z7.2-z7.1}..{right}
z7.3 & z7.3--z7.2--z7.1 & cycle;
x9=w-slab; penpos9(cap_hair,-90);
z9r=whatever[z2l,z2r];
z9.1=whatever[z2.1,z2l];
z9.1-z9l=whatever*(z2l-z9r);
filldraw z9.1--z2l--z9r--z9l--
z9.1 & cycle;
z9.2=whatever[z9.1,z9l];
x9.2=0.5[x2,x9];
z9.4=whatever[z9.1,z9];
z9.4=whatever[z4r,z2r];
filldraw z4r{z2r-z4r}..
z9.2{z9-z9.2} & z9.2--z9.4--z4r
& cycle;
penpos11'(cap_stem,-27.5);
z11'l=whatever[z5l,z6l];
y11'r=y1+0.8slab; 
z10=whatever[z6l,z6r];
z10-z11'r=whatever*(z6r-z5r);
filldraw z11'l--z11'r--z10
--z6r--z11'l & cycle;
z10.1=whatever[z6,z10];
z10.2=whatever[z11'r,z10];
x10.1=x10.2=w;
unfilldraw z10.1--z10--z10.2--
z10.1 & cycle;
penpos12(whatever,angle(z8.2-z8.1));
z12l=z3l; y12r=h;
penpos13(0.8cap_hair,angle(z6-z5));
z13=whatever[z6r,z5r];
z13r=z6r; z13.2=whatever[z3r,z13l];
y13.2=y6r;
z13.1-z13.2=whatever*(z12r-z12l);
z13.1-z12r=whatever*(z13.2-z3r);
filldraw z3r--z13.2--z13.1--z12r
--z12l--z3r & cycle;
z13.3=0.5[z13.2,z13.1];
z13.4=1/3[z6r,z5r];
filldraw z13.4--z6r & z6r{up}..
{z13.1-z13.3}z13.3 & 
z13.3--z13.2 & z13.2{down}..
{z5r-z6r}z13.4 & cycle;
z14=whatever[z12r,z13.1];
y14=h+apex_o;
filldraw z12{z12r-z12l}..{dir60}
z14 & z14--z12r--z12 & cycle;
penpos15(cap_hair,-90);
x15=w; z15r=whatever[z13.2,z13.1];
z15.1=whatever[z12r,z13.1];
z15.1-z15l=whatever*(z13.2-z13.1);
if x15>x13.1: filldraw z13.1--z15r--z15l--
z15.1--z13.1 & cycle; fi


.endchar;


.cmchar "The gothic letter F";
.beginchar("F",13.6u#,cap_height#,2.5u#);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(0.5cap_hair,0);
y1=-d+apex_o; x1l=0;
z2=(0.085w,0.5y1);
penpos6(0.8cap_stem,0);
penpos7(0.5cap_stem,0);
penpos8(0.8cap_stem,0);
x7=0.555w; y7=2/3u;
z6r=z7l; z8l=z7r;
x3=0.5x6l; y3=0;
x4=0.75[x3,x6l]; y4=0.2y1;
y5=-d-apex_o; x5=3/4x6l;
x10=0.5[x6l,x6]; y10=-3/5d;
penpos10(0.9cap_hair,-90);
penpos9(0.8cap_stem,0);
penpos6'(0.8cap_stem,0);
penpos9'(0.8cap_stem,0);
x9=x9'=x8; x6'=x6;
y6'=y9'=0.82h; y9=0.2h;
filldraw z1r{dir60}..z2{right}..
z5{right}..z9r{up}--z9'r &
z9'r--z9'l--z8l & z8l{down}..
z10r{z10r-z8r} & z10r--z10l &
z10l{z8l-z10l}..z6r{up}--z6'r
& z6'r--z6'l--z6l & z6l{down}..
z4{left}..z3{left}..z1l{dir270} &
z1l--z1r & cycle;
%
% mpara
%
penpos11(0.9cap_stem,-90);
penpos12(0.9cap_stem,-90);
y11=y12=0.538h; x11=x6l; x12=x8r;
penpos13(0.8cap_hair,-60);
y13=y11r-0.45cap_stem;
x13=0.127w; z11.1=whatever[z11l,z12l];
z12.1=whatever[z11l,z12l];
x11-x11.1=x12.1-x12=0.18w;
z12.2=whatever[z11r,z12r];
x12.2=0.8[x12,x12.1];
filldraw z13r{dir45}..z11r{right}
--z11l--z11.1--
z13l--z13r & cycle;
filldraw z12r--z12.2 
& z12.2--z12.1--z12l--z12r & cycle;
%
%  panw meroc
%
numeric jhta; jhta:=32;
penpos14(cap_stem,90-jhta); y14r=h+apex_o;
x14r=x9r+cap_hair;
penpos15(cap_stem,90-jhta); x15r=w-apex_o;
z15r-z14r=whatever*dir(180-jhta);
penpos17(0.8cap_hair,-jhta);
z17r=whatever[z15l,z15r];
x17r=w; z16=whatever[z14l,z15l];
z9'r-z16=whatever*(z15l-z15r);
z18=whatever[z14r,z15r];
x18=0.75[x14r,x15r];
filldraw z6'l{right}..z14r{z14r-z14l}
& z14r--z18{z15r-z14r}..
{z15r-z15l}z17l & z17l--z17r--z15l
--z16--z9'r--z6'l & cycle;
%
%
%
z19=z6'l+(-0.4cap_hair,0.4cap_hair);
penpos19(0.1cap_hair,45);
z19.1=z6'l+(0,-cap_hair);
z19.2=z6'l+(cap_hair,0);
filldraw z19.1{up}..{z19-z6'l}z19l
& z19l--z19r & z19r{z6'l-z19}..
z19.2{right} & z19.2--z19.1 &
cycle;
%
%
%
penpos20(0.5cap_stem,0);
penpos21(0.5cap_stem,0);
x20=x21=x7; y20=y9'-cap_hair;
y21=y20-0.5cap_hair;
filldraw z21l{up}..z20{right}..
z21r{down} & z21r--z9'l--z6'r--
z21l & cycle;
.endchar;


.cmchar "The gothic letter G";
.beginchar("G",12.6u#,cap_height#,0); %prin htan 11.6u#
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(0.8cap_hair,111);
penpos2(0.9cap_stem,-150);
penpos3(0.8cap_hair,-57.5);
penpos4(cap_stem,0);
penpos5(1.765cap_stem,48);
z1=(0.43w,0.87h); z2=(0.115w,0.318h);
z3=(0.742w,0.141h);
z4r=(w,0.427h);
z6=(0.517w,-apex_o);
y5=0.6h; x5l=x6+cap_hair;
filldraw z1r..z2r..z6 & z6..z3r
..z4r{up}..z5r & z5r--z5l &
z5l{z5r-z5l}..z4l..z3l..z2l..z1l
& z1l--z1r & cycle;
%
%
%
penpos6'(cap_hair,0);
penpos7(cap_hair,0);
x6'l=x7l=x6; y7=y1r;
y6'=y6+0.5cap_stem;
penstroke z6'e--z7e;
penpos8(cap_hair,-42);
penpos9(cap_hair,-42);
z8r=z5l; z9r=whatever[z5l,z5r];
x9r=w-slab;
penstroke z8e--z9e;
penpos11(cap_hair,48); z11l=z5r;
penpos10(whatever,48);
z10l=z1r; y10r=h+apex_o;
z11.1=whatever[z11,z9r];
z11.1-z10r=whatever*(z11r-z1r);
z11.2=0.5[z10r,z11.1];
filldraw z1r--z11r--z9r--z9l &
z9l{z8l-z9l}..z11.2{z10r-z11.1}
--z10r & z10r--z1r & cycle;
%
%
%
x14=0.3445w=x14';
penpos14(1.05cap_stem,0);
penpos14'(1.05cap_stem,0);
penpos12(cap_hair,-60);
penpos13(cap_hair,-60);
z12l=z2; z13r-z12r=whatever*(z2l-z2r);
x13r=x14r+apex_o;
penstroke z12e--z13e;
z14.1=whatever[z12l,z13l];
x14.1=x14l; y14=y13r;
y14'=h;
z15=(z1r..z2r..z6) 
intersectionpoint (z14l--z14'l);
x15.05=x15; y15.05=0.5[y15,y1];
z15'=whatever[z1,z15.05];
x15'=0.5[x15,x1];
x13.1=x14r; y13.1=y13l+cap_hair;
filldraw z2l{z2l-z2r}..z14l{up}
& z14l--z15--z15'--z1r--z13.1
& z13.1{down}..z13r & z13r--z12r
--z2l & cycle;
%
% grammoulec
%
z20=(z5l{z5r-z5l}..z4l..z3l..
z2l..z1l) intersectionpoint
(z6'r--(x6'r,1/3h));
y16=2/3[y20,y8r];
y18=1/3[y20,y8r];
x16=x18=x20;
penpos16(cap_hair,-90);
penpos17(cap_hair,-90);
penpos18(cap_hair,-90);
penpos19(cap_hair,-90);
x17=x19=0.5[x4l,x4];
z17-z16=whatever*(z13-z12);
z19-z18=whatever*(z13-z12);
penstroke z18e--z19e;
penstroke z16e--z17e;
%
%
%
z21=whatever[z6'l,z7l];
z21=whatever[z11r,z1r];
filldraw z1l{dir21}...z21{z11r-z21}
& z21--z10l--z1l & cycle;
.endchar;


.cmchar "The gothic letter H";
.beginchar("H",11.444u#,cap_height#,2.61u#);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(0.45cap_stem,0);
penpos2(0.9cap_stem,0);
penpos3(0.9cap_stem,0);
x1=0.367w; z2r=z1l; z3l=z1r;
y1=0.323h; 
penpos4(0.9cap_stem,0);
penpos5(0.9cap_stem,0);
x4=x3; x5=x2; y5=0.706h; 
y4=0.8h;
penstroke z2e--z5e;
penstroke z3e--z4e;
penpos6(0.6cap_hair,-90);
penpos7(cap_hair,-90);
x6=5/6[x4l,x4r]; x7=0.84w;
y6l=y7l=h;
penstroke z5e{up}..z6e{right};
penstroke z4e{up}..z7e{right};
%
%
%
penpos8(cap_hair,-90);
z10=(x2r,0.426h); z9-z10=whatever*dir45;
x9=x7; z8l=whatever[z10,z9];
x8=x4r; 
penpos10'(1.12cap_stem,angle(z9-z8l));
z10'r=z9; penpos11(cap_stem,0);
x11r=w; y11=y10;
penpos12(cap_hair,-45);
y12l=0; x12l=0.607w;
penstroke z12e{dir45}..z11e{up}..z10'e;
z10''-z8r=whatever*(z10'-z8l);
x10''=x10';
filldraw z8l--z10'--z10''--z8r--
z8l & cycle;
%
% dexi stroggulo
%
penpos12'(0.9cap_stem,0);
x12'=x12l; y12'=-d-apex_o+0.45cap_stem;
filldraw z12l{dir225}..z12'l{down}
..z12'r{up}..z12r{z12l-z12r}
& z12r--z12l & cycle;
%
% aristero stroggulo
%
penpos13(0.9cap_stem,90);
y13=-apex_o-0.45cap_stem;
x13=0.5cap_hair+0.45cap_stem;
x13'=x13-apex_o; y13'=-apex_o;
x14=0; y14=2/3y13;
x15=x13; x16=x2l; 
penpos20(0.6cap_hair,-90); 
y20r=0.1636h; x20=2/3[x2l,x2r];
y16=0.5[y20r,y2]; y15=0.5[y14,y16];
x17=x3r; y17=2/3[y20r,y3];
z18=(x3l,y20r); z19=(x12',y18);
penpos21(cap_hair,-90);
y21r=y19+apex_o; x21=x9-0.7cap_stem;
z23=(x20,0.5y19);
z22'=whatever[z3l,z18];
z22'-z21r=whatever*(z10-z9);
z22=(z13'..z23{right}..z12'r)
intersectionpoint (z21r--z22');
%
filldraw z13'..z23{right}..z22
& z22--z21r--z21l & z21l{z22-z21r}
..z19{left}--z18 & z18--z17--z3r
--z3l & z3l{down}..z20r{dir215}
& z20r--z20l & z20l{z1-z20l}..
z2r{up} & z2r--z2l--z16 &
z16{down}..z15..z14{down}..
z13l{right}..z13r{left}--z13'
& cycle;
%
% pterugia
%
penpos24(0.5cap_hair,0);
penpos25(0.75cap_stem,-90);
penpos26(0.62cap_stem,-90);
x24l=0; y25l=h+apex_o; 
y26=0.5[y24,y25l];
y24=0.85h; x25=1/8w; x26=0.271w;
penstroke z24e{up}..z25e{right}..
z26e..z6e{right};
x27=0; y27=0.547h; y28=2/3[y27,y5];
x28=0.5x5l; x30=x29=x5l; y29=5/6[y27,y28];
y32=y2; 
x30-x32=min(cap_stem,x30-0.5cap_hair);
x31=0.5[x32,x30]; y31=y10; 
y30=3/3[y32,y31];
x33=0.43[x32,x30]; y33=1/5[y32,y31];
filldraw z16{up}..z33..z32 &
z32{up}..z31..z30 & 
z30{up}..z27{left} &
z27{up}..z28..z29{dir-30} & z29--z16
& cycle;
%
% grammoulec
%
penpos34(0.8cap_hair,-90);
penpos35(0.8cap_hair,-90);
penpos36(0.8cap_hair,-90);
penpos37(0.8cap_hair,-90);
penpos38(0.8cap_hair,-90);
penpos39(0.8cap_hair,-90);
x34=x35=x17; x36=x37=w-cap_stem;
x38=x39=1/2[x34,x36];
y34l=y36r=y27;
y38l=y34r; y39r=y35l;
y35r=y37l=y32;
penstroke z34e..z38e..z36e;
penstroke z35e..z39e..z37e;

.endchar;


.cmchar "The gothic letter I";
.beginchar("I",10.555u#,cap_height#,2.167u#);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos2(1/3cap_stem,-58);
y2r=-d-apex_o; x2r=0.321w;
x3=0.442w; y3=-apex_o;
penpos4(1/3cap_stem,-53); y4l=0;
x4l=0.637w; x5=0.579w;
y5=0.144h; penpos5(0.7cap_stem,0);
y5.1=y5.4=0.386h; y5.2=y5.3=0.45h;
penpos6(1/3cap_stem,-51);
penpos7(1/3cap_stem,-30);
y6r=y7r=0.83h; x7r=x5.4=0.86w;
x5.3=x5.4-0.9cap_stem;
x5.1=x3; x5.2=x5.1+0.9cap_stem;
x6r=0.758w;
penpos8(0.5cap_hair,-45);
z8=(w,h);
filldraw z2r..z5r..z5.2..z6r..z8r
& z8r--z8l & z8l..z6l..z5.1..z5l..
z3..z2l & z2l--z2r & cycle;
filldraw z2r..z4r..z5.4..z7r..
z8r & z8r--z8l & z8l..z7l..z5.3
..z4l..z2l & z2l--z2r & cycle;
%
% panw meroc
%
penpos9(cap_hair,0); x9l=0;
y9=y6r; penpos10(0.88cap_stem,-90);
y10l=h+apex_o; x10=0.276w;
penpos11(cap_stem,-90);
y11r=y9; x11=x5.2;
penstroke z9e{up}..z10e..z11e..
z8e;
%
% mpalla
%
penpos12(cap_stem,90); x12=0.134w;
y12r=y9-apex_o; 
filldraw z12r..z12l..cycle;
filldraw z12l{left}..z9l{up} &
z9l--z9r & z9r{down}..z12{right}
& z12--z12l & cycle;
%
% katw meroc
%
penpos1(cap_hair,0); z1=(0,-d);
z13=(0.279w,0.031h);
filldraw z1r..z2r{z2r-z2l} &
z2r--z3 & z3..z13{left}..z1l{down}
& z1l--z1r & cycle;
%
% pterugia
%
x14=0.5[x5.1,x5.2]; y14=y15=y5.2;
x15=x17=0.2w; y17=1/4h;
x18=1/2[x17,x5l]; y18=1/4[y17,y5.1];
filldraw z15{dir77}..z14--z5l
& z5l{up}..z18..z17 &
z17{dir76}..z5.1{right} &
z5.1..z15{dir198} & cycle;
.endchar;


.cmchar "The gothic letter J, which is the same as I";
.beginchar("J",10.555u#,cap_height#,2.167u#);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos2(1/3cap_stem,-58);
y2r=-d-apex_o; x2r=0.321w;
x3=0.442w; y3=-apex_o;
penpos4(1/3cap_stem,-53); y4l=0;
x4l=0.637w; x5=0.579w;
y5=0.144h; penpos5(0.7cap_stem,0);
y5.1=y5.4=0.386h; y5.2=y5.3=0.45h;
penpos6(1/3cap_stem,-51);
penpos7(1/3cap_stem,-30);
y6r=y7r=0.83h; x7r=x5.4=0.86w;
x5.3=x5.4-0.9cap_stem;
x5.1=x3; x5.2=x5.1+0.9cap_stem;
x6r=0.758w;
penpos8(0.5cap_hair,-45);
z8=(w,h);
filldraw z2r..z5r..z5.2..z6r..z8r
& z8r--z8l & z8l..z6l..z5.1..z5l..
z3..z2l & z2l--z2r & cycle;
filldraw z2r..z4r..z5.4..z7r..
z8r & z8r--z8l & z8l..z7l..z5.3
..z4l..z2l & z2l--z2r & cycle;
%
% panw meroc
%
penpos9(cap_hair,0); x9l=0;
y9=y6r; penpos10(0.88cap_stem,-90);
y10l=h+apex_o; x10=0.276w;
penpos11(cap_stem,-90);
y11r=y9; x11=x5.2;
penstroke z9e{up}..z10e..z11e..
z8e;
%
% mpalla
%
penpos12(cap_stem,90); x12=0.134w;
y12r=y9-apex_o; 
filldraw z12r..z12l..cycle;
filldraw z12l{left}..z9l{up} &
z9l--z9r & z9r{down}..z12{right}
& z12--z12l & cycle;
%
% katw meroc
%
penpos1(cap_hair,0); z1=(0,-d);
z13=(0.279w,0.031h);
filldraw z1r..z2r{z2r-z2l} &
z2r--z3 & z3..z13{left}..z1l{down}
& z1l--z1r & cycle;
%
% pterugia
%
x14=0.5[x5.1,x5.2]; y14=y15=y5.2;
x15=x17=0.2w; y17=1/4h;
x18=1/2[x17,x5l]; y18=1/4[y17,y5.1];
filldraw z15{dir77}..z14--z5l
& z5l{up}..z18..z17 &
z17{dir76}..z5.1{right} &
z5.1..z15{dir198} & cycle;
.endchar;

.cmchar "The gothic letter K";
.beginchar("K",13.111u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
z1=(0,0); x2=1/3w; x3=0.37w;
x4=0.40w; x5=0.52w; x6=0.614w;
y2=0.3h; y3=0.506h; y4=0.7h;
y5=0.886h;
penpos1(cap_hair,-63);
penpos2(cap_stem,-23);
penpos3(cap_stem,-10);
penpos4(0.765cap_stem,-21);
penpos5(1/3cap_stem,-41);
penpos6(cap_hair,-58);
penpos1'(0.05cap_hair,-63);
z1'l=z1l;
y6l=h+apex_o;
penstroke z1'e..z2e..z3e..z4e..
z5e..z6e{dir32};
penpos7(cap_hair,-33);
penpos8(0.88cap_stem,-115);
x7=slab; x8=0.26w; x8.1=0.345w;
x8.2=0.466w; y7=0.785h; y8=0.919h;
y8.1=0.796h; y8.2=0.92h;
filldraw z7r..z8r..z8.1..z6l{dir32} &
 z6l{dir212}..z8.2..z8l..z7l
& z7l--z7r & cycle;
penpos10(cap_hair,-23);
penpos10'(1.5cap_hair,-23);
penpos11(cap_hair,-10);
penpos11'(1.5cap_hair,-10);
penpos12(cap_hair,-21);
penpos12'(1.5cap_hair,-21);
penpos13(cap_hair,-41);
penpos13'(cap_hair,-41);
penpos14(cap_hair,-58);
penpos9(cap_hair,-65);
z2r=z10'l; z10'r=z10l;
z3r=z11'l; z11'r=z11l;
z4r=z12'l; z12'r=z12l;
z5r=z13'l; z13'r=z13l;
x14=0.684w; x9=0.3w; y14l=h;
y9r=0.06h;
penstroke z9e..z10e..z11e..z12e
..z13e..z14e;
%
%
%
x15=x10r; y15=0.1w;
penpos15(0.8cap_stem,-90);
penpos16(1.176cap_stem,-126.5);
y16r=-apex_o; x16l=x14;
filldraw z1r..z9r..z15r..z16r
& z16r--z16l & z16l..z15l{left}
..z1l
& z1l--z1r & cycle;
%
% panw podi
%
penpos17(cap_hair,-60);
penpos18(cap_hair,-60);
z18r=z12r; z17l=z11r;
x20=0.773w; 
penpos20(cap_hair,-60);
penpos19(cap_hair,-60);
z20-z18=whatever*dir30;
z19-z17=whatever*(z20-z18);
z19=whatever[z20l,z20r];
z17''=whatever[z17r,z19r];
x17''=0;
z17'=(z9..z10..z11..z12..z13..z14)
intersectionpoint (z17''--z19r);
filldraw z17'--z11--z11r--z17r--
z17' & cycle;
penstroke z18e--z20e;
penstroke z17e--z19e;
penpos21(cap_stem,angle(z19-z17));
penpos22(cap_stem,angle(z19-z17));
z21=2/3[z17,z19];
z22-z21=whatever*(z20l-z20r);
z22=whatever[z18,z20];
penstroke z21e--z22e;
%
% katw podi
%
penpos22'(1.09cap_stem,angle(z19-z17));
penpos23(1.09cap_stem,angle(z19-z17));
penpos24(1.09cap_stem,angle(z19-z17));
z22'l=z17r; x23=x14; y23=y17r;
x24=0.82w; y24=y16l;
penstroke z22'e{z22'r-z22'l}..
z23e{z24-z23}--z24e;
z25=(0.86w,-apex_o);
penpos26(cap_hair,-90);
z26r-z25=whatever*(z16l-z16r);
x26=w;
filldraw z24r{z24-z23}..
z26l{z26r-z25} & z26l--z26r &
z26r--z25 & z25{left}..z24l
{z23-z24} & z24l--z24r & cycle;
penpos27(cap_hair,angle(z16l-z16r)-90);
z27r=z16;
z29=whatever[z23,z24];
z29-z16=whatever*(z16l-z16r);
z28=whatever[z23,z24];
z28-z27l=whatever*(z29-z16);
filldraw z27r--z29--z28--z27l--
z27r & cycle;
%
% pterugia
%
z2''=z2; penpos2''(0.7cap_hair,
angle(z2r-z2l));
x27.1=x8r; y27.1=y10;
x28.1=0.093w+cap_hair; y28.1=0.377h;
x29.1=0.127w+cap_hair; y29.1=0.605h;
filldraw z1l{z10-z1}..z27.1{up}..
z28.1{left} & z28.1{dir60}..z2''r
& z2''r--z2''l &
 z2''l..z29.1{left} & z29.1{dir60}..
z3 & z3--z2..z1r{z1-z10} &
 z1r--z1l & cycle;


.endchar;


.cmchar "The gothic letter L";
.beginchar("L",11.27u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
z1=(0,0); x2=0.43w; x3=0.46w;
x4=0.495w; x5=0.66w; x6=0.74w;
y2=0.31h; y3=0.515h; y4=0.71h;
y5=0.933h;
penpos1(cap_hair,-67.5);
penpos2(cap_stem,-10);
penpos3(1.06cap_stem,-7.5);
penpos4(0.823cap_stem,-9);
penpos5(1/3cap_stem,-36);
penpos6(cap_hair,-55);
penpos1'(0.05cap_hair,-67.5);
z1'l=z1l;
y6l=h+apex_o;
penstroke z1'e{dir22.5}..z2e..z3e..z4e..
z5e..z6e{dir35};
penpos7(cap_hair,-30);
penpos8(0.85cap_stem,-100);
x7=1/8w; x8=0.35w; x8.1=x3;
x8.2=0.625w; y7=0.77h; y8l=h+apex_o;
y8.1=0.86h; y8.2=0.955h;
filldraw z7r..z8r..z8.1..z6l{dir35} &
 z6l{dir215}..z8.2..z8l..z7l{dir240}
& z7l--z7r & cycle;
penpos10(0.56cap_stem,-14);
penpos10'(1.4cap_hair,-14);
penpos11(0.91cap_stem,-7.5);
penpos11'(1.5cap_hair,-7.5);
penpos12(0.82cap_stem,-9);
penpos12'(1.3cap_hair,-9);
penpos13(0.56cap_stem,-36);
penpos13'(0.7cap_hair,-36);
penpos14(0.75cap_hair,-90);
penpos9(cap_hair,-90);
z2r=z10'l; z10'r=z10l;
z3r=z11'l; z11'r=z11l;
z4r=z12'l; z12'r=z12l;
z5r=z13'l; z13'r=z13l;
x14=w-slab; x9=0.325w; y14l=h-slab;
y9r=0.085h;
penstroke z9e..z10e..z11e..z12e
..z13e..z14e{right};
%
%
%
x15=x5r; y15=0.134w;
penpos15(0.82cap_stem,-90);
penpos16(1.06cap_stem,-126.5);
y16r=-apex_o; x16l=x14;
penpos30(cap_hair,angle(z16l-z16r)-90);
z30r=whatever[z16l,z16r];
x30r=w+apex_o;
filldraw z1r..z9r..z15r..z16r
& z16r--z30r--z30l 
& z30l{z16r-z16l}..z15l{left}
..z1l
& z1l--z1r & cycle;
%
%
% pterugia
%
z2''=z2; penpos2''(0.7cap_hair,
angle(z2r-z2l));
z27.1=(z1l{dir22.5}..z2l..z3l..
z4l..z5l..z6.)
intersectionpoint (z9r--(x9r,h));
x28.1=x7r+cap_hair; y28.1=0.4h;
x29.1=x7r+0.5cap_hair; y29.1=0.58h;
filldraw z27.1{up}..
z28.1{left} & z28.1{dir60}..z2''r
& z2''r--z2''l &
 z2''l..z29.1{left} & z29.1{dir60}..
z3 & z3--z2--z27.1 & cycle;
%
%
%

.endchar;

.cmchar "The gothic letter M";
.beginchar("M",16.39u#,cap_height#,2u#);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric theta; theta=-45;
penpos1(cap_stem,0);
penpos2(cap_stem,0);
penpos3(cap_stem,0);
penpos4(cap_stem,0);
penpos4''(cap_stem,0);
z4''=(x4,y4+cap_hair);
penpos5(2/3cap_stem,0);
penpos7(2/3cap_stem,0);
x1=x3=0.525w;
x2=x4=0.86w; 
x5=x7=1/4w;
y1=y2=y5=0.2h;
y3=0.7h; y4=0.7h; y7=0.623h;
penstroke z5e--z7e;
penstroke z1e--z3e;
penstroke z2e--z4e;
penpos6(cap_hair,0);
penpos6'(1.5cap_hair,0);
penpos8(cap_hair,0);
penpos8'(1.5cap_hair,0);
z6'l=z5r; z6l=z6'r;
z8'l=z7r; z8l=z8'r;
penstroke z6e--z8e;
penpos11(cap_hair,-90);
z11=z3r; penpos1'(1.7cap_stem,0);
x1'=x1; y1'=0.5y1;
z14=(0.534w,-apex_o);
penpos13(cap_hair,0);
x13=0.5[x1r,x2l]; z13'=z13;
y13=y1; penpos13'(cap_hair,90-theta);
penpos15(cap_hair,90-theta);
z15l=whatever[z14,z13'l];
x15r=0.5[x13,x2l];
z15'=whatever[z1'l,z14];
z15'=whatever[z13'r,z15r];
filldraw z1l{down}..z1'l{left}
& z1'l--z14--z1'r &
z1'r{left}..z1r{up} &
z1r--z1l & cycle;
filldraw z15'--z14--z15l--
z15r--z15' & cycle;
z13.1=whatever[z13l,z15'];
x13.1=x1r;
z13.2=0.5[z13.1,z13l];
filldraw z1r{down}..
z13.2{z15r-z15'} & z13.2--
z14--z1'--z1r & cycle;
y12=h+apex_o; 
z12-z11l=whatever*(z15r-z14);
x11.1=x13r; 
z11.1-z11=whatever*(z12-z11l);
z11.2=whatever[z11,z11.1];
 x11.2=x13l;
filldraw z13r--z11.1--z11.2
--z13l--z13r & cycle;
z11.3=0.5[z11.1,z11.2];
z16-z12=whatever*(z4l-z11.3);
z16-z4''r=whatever*(z12-z11l);
penpos18(cap_hair,theta);
z18r=whatever[z4''r,z16];
x18r=w-apex_o;
penpos18'(cap_hair,theta);
z18'r=z4''r; 
penstroke z18'e--z18e;
z11.4-z11r=whatever*(z11.3-z11);
z11.4-z4''l=whatever*(z12-z16);
filldraw z11r--z11.4--z4''l--
z4''r--z16--z12--z11l--z11r &
cycle;
z9=(0.271w,0.8h+cap_hair); 
%z17-z9=whatever*(z12-z11l);
y17=h+apex_o;x180=x11;
y180=y11+cap_stem;
z180-z17=whatever*(z16-z12);
penpos19(cap_hair,-90);
penpos19'(cap_hair,-60);
x19=x19'=x17; 
y19=y19'=0.9h+cap_hair;
z3'=z3l+(0,-3cap_hair);
%x20=0.5[x6r,x1l]; 
%y20=3/4[y8,y19];
z20=(z8r{up}..z19'r)
intersectionpoint 
((0,y9-cap_hair)--
(w,y9-cap_hair));
filldraw z8r{up}..z20
..z19'r & z19'r--z19'l &
z19'l..z8l{down} & 
z8l--z8r & cycle;
filldraw z7r{up}..z19r &
z19r--z19l & z19l--z17---z9...z7l{down}
& z7l--z7r & cycle;
%
%
%
filldraw z3l{up}
..z9{z9-z17}
& z9--z17--z180--z3r--z3l
& cycle;
z21=(x2l,y1');
z22-z21=whatever*(z14-z1'l);
y22=y14;
penpos23(cap_hair,-45);
z23r-z22=whatever*dir45;
x23r=w;
filldraw z2l--z21--z22--
z23r--z23l & z23l{z22-z23r}..
{up}z2r & z2r--z2l & cycle;
y24=y26=0.462h-0.4cap_stem;
y25=y27=y24+0.8cap_stem;
x25=x1l-0.15cap_stem;
x27=x13r+0.6cap_stem;
x24=x1l-0.6cap_stem;
x26=x13r+0.15cap_stem;
filldraw z24--z25--z27--z26--
z24 & cycle;
z10=(0,-d); penpos10(cap_hair,0);
penpos30(cap_stem,-90);
x30=x5l;
y30r=2/3y10;
penpos31(0.5cap_stem,-90);
x31=0.5[x10,x5]; y31r=0;
penstroke z10e{up}..z31e{right}..
z5e{up};
penstroke z10e{up}..z30e{right}
..z6e{up};
y33=y24; z31'=z31+(2cap_hair,0);
z34=(z9{z9-z17}..z31'{down})
intersectionpoint 
((0,y25)--z25);
y32=y33-0.5cap_hair;
x32=2cap_hair; x33=0.5[x32,x34];
%
%
%
filldraw z5l{up}..z33..z32
& z32{up}..z34{right} &
z34{dir80}..z9{z17-z9} & z9--z5l
& cycle;
filldraw z4''l--z4''r--z4r--z4l--z4''l
& cycle;
.endchar;

.cmchar "The gothic letter N";
.beginchar("N",12.78u#,cap_height#,2u#);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(cap_hair,0); z1l=(0,-d);
penpos2(cap_stem,0); x2=0.323w;
y2=0.43h; y3=0.834h; 
x5=0.572w; x3=x2l; 
y5=h+apex_o; z4=whatever[z3,z5];
x4=x2r; y6=y3; x6=0.7w;
penpos6(cap_stem,47);
z7=(0.753w,0.533h); 
%z8=(0.893w,0.646h);
z8=(0.893w,0.6h);
z8'=(0.57w,y3);
penpos9(2/3cap_stem,-22);
x9=0.77w; y9=y2;
penpos10(cap_hair,-35);
z10=(0.557w,-0.556d);
penpos11'(1.5cap_amount,0);
penpos11(cap_hair,0);
z12.1=whatever[z3,z5];
z12.2=whatever[z3,z5];
z12.3=whatever[z3,z5];
z11'l=(x2r,0.2h);
z11l=z11'r;
x12.1=x4; x12.2=x11'r;
x12.3=x11r; z3'=(x2,y3);
filldraw z2l--z3--z4--z2r--z2l &
cycle; filldraw
z5{dir-31}..z8{down}..z10r{dir234} &
z10r--z10l & z10l{dir54}..z7{up}
..z8'..z3' & z3'--z3--z5  & cycle;
filldraw z11l--z12.2--z12.3--
z11r--z11l & cycle;
z14=(0.275w,-0.5cap_amount);
penpos14(1.08cap_stem,-90);
penstroke z1e{up}..z14e{right}..z11e{up};
penpos15(2/3cap_stem,0);
z15r=z11'l;
penstroke z1e{up}..z15e{up}..
z2e{up};
penpos13(1.32cap_stem,-121);
z13r=(x8-0.5cap_amount,-apex_o);
penstroke z10e{dir54}..z13e{dir-30};
penpos16(cap_hair,-31);
z16r=whatever[z13r,z13l];
x16r=w;
penpos16'(cap_hair,-31);
z16'r=z13r;
penstroke z16'e--z16e;
penpos20(cap_hair,-90);
penpos21(cap_hair,-90);
penpos22(cap_hair,-90);
penpos23(cap_hair,-90);
penpos24(cap_hair,-90);
penpos25(cap_hair,-90);
z23=(x11r,y2); x20=x23;
x22=x25=x7;
x21=x24=0.5[x23,x25];
y21=y24+cap_stem; y21l=y20r;
y24r=y23l; y25l=y24r;
y22r=y20l;
penstroke z20e..z21e..z22e;
penstroke z23e..z24e..z25e;
z26=(0.083w,0.623h);
filldraw (x2l,0.48h){up}..z26 &
z26{dir40}..(x2r,0.48h){down} & 
(x2r,0.48h)--(x2l,0.48h) &
cycle;
z27=(x26,0.47h);
filldraw z15l{up}..z27{left} & z27{dir40}
..z2r & z2r--z15l & cycle;
z28=(x26+0.5cap_amount,0.265h);
filldraw z15l{up}..z28{dir210} &
z28{dir60}..(x15r,y28) & (x15r,y28)--
z15l & cycle;
.endchar;

.cmchar "The gothic letter O";
.beginchar("O",12.86u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(cap_stem,-90); 
x1=0.6w; y1r=-apex_o;
penpos2(0.8cap_stem,180);
y2=0.467w; x2r=0;
penpos7(0.8cap_stem,0); y7=0.53h;
x7r=w; penpos5(cap_hair,0);
z5r=z1; 
x4.1=x1-cap_hair; y4.1=h;
z4.2=(z7r{up}..z4.1{dir158}) 
intersectionpoint 
((x1,0)--(x1,h));
z3.1=(0.0756w,0.628h);
z3.1-z3.2=whatever*(z4.1-z4.2);
z3.2-z4.2=whatever*(z3.1-z4.1);
z3.3=whatever[z3.2,z4.2];
x3.3=0.216w;
penstroke z2e{down}..z1e{dir-16};
filldraw z2l{up}...z3.3{z4.2-z3.3}
---z4.2 & z4.2--z4.1 & 
z4.1---z3.1...z2r{down} &
z2r--z2l & cycle;
filldraw z5r--z4.2--z4.1--z5l--
z5r & cycle;
%
%
%
penpos1'(cap_hair,-90);
z1'r=z1;
penpos2'(0.4cap_stem,180);
z2'r=z2;
penpos6(1.1cap_stem,63);
z6r=(x4.1-cap_hair,h+apex_o);
penpos8(cap_hair,angle(z4.2-z3.2)-90);
z8=(0.78w,0.161h);
filldraw z1l{dir-16}...
z8l{z4.2-z3.2}..
z7l{up}..z6l{left} & z6l--z6r &
z6r..z7r{down}...
z8r{z3.2-z4.2}..{dir-117}z1r &
z1r--z1l & cycle;
z10=(z2l{down}..z1l{dir-16})
intersectionpoint ((0.3w,0)--
(0.3w,h));
z11-z10=whatever*(z4.1-z3.1);
x11=x10+cap_stem;
z12=whatever[z3.2,z4.2];
z13=whatever[z3.2,z4.2];
x12=x10; x13=x11;
filldraw z10--z11--z13--z12--z10
& cycle;
z15'=whatever[z10,z11];
x15'=0;
z15=(z2r{down}..z1r{dir-16})
intersectionpoint 
(z15'--z11);
penpos15''(0.1cap_hair,-90);
z15''r=z15;
z14=(x10,0.5[y10,y2]);
filldraw z15''l{z11-z10}..z14{up}
& z14--z10 & z10--z15''r & 
z15''r--z15''l & cycle;
z16=whatever[z4.1,z3.1];
z17=whatever[z4.1,z3.1];
y17=y6l; x16=x11;
filldraw z6l{left}--z17 &
z17---z16...z6r{z6r-z6l} &
z6r--z6l & cycle;
z11.1=whatever[z10,z11];
x11.1=x11+2/3cap_hair;
z11.2=(x11,y2);
filldraw z11.2{down}..z11.1 &
z11.1--z11--z11.2 & cycle;
%
% 2 grammoulec
%
penpos20(cap_hair,-90);
penpos21(cap_hair,-90);
penpos22(cap_hair,-90);
penpos23(cap_hair,-90);
z22r=(x1,y2); z20r=(x1,0.29h);
z23'-z22r=whatever*(z3.2-z4.2);
z21'-z20r=whatever*(z3.2-z4.2);
x21'=x23'=0;
z23''=whatever[z23',z22r];
z21''=whatever[z21',z20r];
x21''=x23''=w;
z23r=(z8{z4.2-z3.2}..z7{up}..
z6) intersectionpoint 
(z23'--z23'');
z21r=(z8{z4.2-z3.2}..z7{up}..
z6) intersectionpoint 
(z21'--z21'');
penstroke z22e--z23e;
penstroke z20e--z21e;
.endchar;

.cmchar "The gothic letter P";
.beginchar("P",12.33u#,cap_height#,2.72u#);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos4(cap_stem,0);
z4r=(0.48w,-d);
penpos4'(0.5cap_stem,-90);
z4'r=z4l;
penpos6(4/3cap_amount,0);
penpos5(cap_hair,0);
z6l=z4r; z5l=z6r; 
penpos4''(0.5cap_stem,-90);
z4''r=z5r;
penpos3(cap_stem,0);
penpos7(cap_hair,0);
x3=x4; x7=x5;
y3=0.8h; y7=h;
penstroke z4e--z3e;
penstroke z5e--z7e;
penstroke z4'e--z4''e;
penpos17(cap_hair,90); x17=x5r;
y17=y3;
y18=y17; x18=x17+1.5cap_amount;
y16=h+apex_o; x16=0.765w;
penpos19(1.06cap_stem,43);
y19=y18+1/4cap_amount;
x19=0.8w;
penpos20(cap_stem,0);
x20r=w; y20=0.423h;
x15=0.9685w; y15=0.193h;
z13=(0.8w,-apex_o);
z14=0.5[z13,z15]+(0,3/4cap_amount);
penpos12(0.85cap_stem,-90);
x12=x4; y12=0.175h;
z11=(0.23w,0.242h);
z10=(x11-0.3cap_amount,y11-0.8cap_stem);
penpos9(cap_hair,0);
z9=(0.108w,0);
penpos19.5(1.09cap_stem,22);
x19.5=0.63[x19,x20];
y19.5=0.433[y19,y20];
filldraw z17r--z16 & z16..
z19.5r..z20r..z15 & z15..z14..z13 &
z13..z20l{dir94}..z19l{dir125}..z18..z17l &
z17l--z17r & cycle;
z14'=(z9l{dir82}..z11..
z12l..z14) intersectionpoint 
(z13..z20l{dir94}..
z19l{dir123}..z18..z17l);
z14.5=0.5[z14',z14];
filldraw z9l{dir82}..z11..z12l..z14.5
& z14.5--z13 & z13..z12r..z10..
z9r{dir262} & z9r--z9l & cycle;
unfilldraw (x6l,0)--(x6l,y20)--
(x6r,y20)--(x6r,0)--(x6l,0) &
cycle;
penpos1(cap_hair,0);
z1l=(0,y3);
z1'=(0.5[x1r,x3l],y1+3cap_amount);
z2=0.5[z3r,(x3r,h+apex_o)];
z2'=(0.266w,h);
filldraw z1r..z1'..z3l & z3l--
z3r--z2 & z2..z2'..z1l &
z1l--z1r & cycle;
penpos21(1.32cap_stem,50);
penpos23(0.5cap_hair,-40);
z21l=(0.063w,0.5w); z23l=z21l;
z21.2=(x4r,y20); z21.1=(x4l,y11);
filldraw z21.2--z21r--z21l--z23r &
z23r{dir30}..z21.1{down} &
z21.1--z21.2 & cycle;
%
%
%
x25=x28=x5r;
y25=0.59h; y28=0.386h;
penpos25(cap_hair,-90);
penpos26(cap_hair,-90);
penpos27(cap_hair,-90);
penpos28(cap_hair,-90);
penpos29(cap_hair,-90);
penpos30(cap_hair,-90);
x30=x20l+apex_o;
y30l=y28r; 
y25l=y27r; x27=x30;
x26=x29=0.5[x25,x27];
y26l=y25r; y29r=y28l;
penstroke z25e..z26e..z27e;
penstroke z28e..z29e..z30e;
.endchar;


.cmchar "The gothic letter Q";
.beginchar("Q",12.86u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(cap_stem,-90); 
x1=0.6w; y1r=-apex_o;
penpos2(0.8cap_stem,180);
y2=0.467w; x2r=0;
penpos7(0.8cap_stem,0); y7=0.53h;
x7r=w; penpos5(cap_hair,0);
z5r=z1; 
x4.1=x1-cap_hair; y4.1=h;
z4.2=(z7r{up}..z4.1{dir158}) 
intersectionpoint 
((x1,0)--(x1,h));
z3.1=(0.0756w,0.628h);
z3.1-z3.2=whatever*(z4.1-z4.2);
z3.2-z4.2=whatever*(z3.1-z4.1);
z3.3=whatever[z3.2,z4.2];
x3.3=0.216w;
penstroke z2e{down}..z1e{dir-16};
filldraw z2l{up}...z3.3{z4.2-z3.3}
---z4.2 & z4.2--z4.1 & 
z4.1---z3.1...z2r{down} &
z2r--z2l & cycle;
filldraw z5r--z4.2--z4.1--z5l--
z5r & cycle;
%
%
%
penpos1'(cap_hair,-90);
z1'r=z1;
penpos2'(0.4cap_stem,180);
z2'r=z2;
penpos6(1.1cap_stem,63);
z6r=(x4.1-cap_hair,h+apex_o);
penpos8(cap_hair,angle(z4.2-z3.2)-90);
z8=(0.78w,0.161h);
filldraw z1l{dir-16}...
z8l{z4.2-z3.2}..
z7l{up}..z6l{left} & z6l--z6r &
z6r..z7r{down}...
z8r{z3.2-z4.2}..{dir-117}z1r &
z1r--z1l & cycle;
z10=(z2l{down}..z1l{dir-16})
intersectionpoint ((0.3w,0)--
(0.3w,h));
z11-z10=whatever*(z4.1-z3.1);
x11=x10+cap_stem;
z12=whatever[z3.2,z4.2];
z13=whatever[z3.2,z4.2];
x12=x10; x13=x11;
filldraw z10--z11--z13--z12--z10
& cycle;
z15'=whatever[z10,z11];
x15'=0;
z15=(z2r{down}..z1r{dir-16})
intersectionpoint 
(z15'--z11);
penpos15''(0.1cap_hair,-90);
z15''r=z15;
z14=(x10,0.5[y10,y2]);
filldraw z15''l{z11-z10}..z14{up}
& z14--z10 & z10--z15''r & 
z15''r--z15''l & cycle;
z16=whatever[z4.1,z3.1];
z17=whatever[z4.1,z3.1];
y17=y6l; x16=x11;
filldraw z6l{left}--z17 &
z17---z16...z6r{z6r-z6l} &
z6r--z6l & cycle;
z11.1=whatever[z10,z11];
x11.1=x11+2/3cap_hair;
z11.2=(x11,y2);
filldraw z11.2{down}..z11.1 &
z11.1--z11--z11.2 & cycle;
%
% 2 grammoulec
%
penpos20(cap_hair,-90);
penpos21(cap_hair,-90);
penpos22(cap_hair,-90);
penpos23(cap_hair,-90);
z22r=(x1,y2); z20r=(x1,0.29h);
z23'-z22r=whatever*(z3.2-z4.2);
z21'-z20r=whatever*(z3.2-z4.2);
x21'=x23'=0;
z23''=whatever[z23',z22r];
z21''=whatever[z21',z20r];
x21''=x23''=w;
z23r=(z8{z4.2-z3.2}..z7{up}..
z6) intersectionpoint 
(z23'--z23'');
z21r=(z8{z4.2-z3.2}..z7{up}..
z6) intersectionpoint 
(z21'--z21'');
penstroke z22e--z23e;
penstroke z20e--z21e;
%
% Q
%
z33=(x7l-cap_hair,-apex_o);
z34-z33=whatever*dir45;
x34=w; 
penpos33'(cap_stem,angle(z34-z33));
z33'l=z33;
z33-z33''=whatever*dir-50;
x33''=0;
z33'r-z33.1''=whatever*(z33-z33'');
x33.1''=0;
z30=(z1..z8..z7{up})
intersectionpoint (z33''--z33);
z31=(z1..z8..z7{up})
intersectionpoint (z33.1''--z33'r);
filldraw z30--z31--z33'r--z33--
z30 & cycle;
penpos35(cap_hair,-90);
z35r=z34;
filldraw z31{z33'r-z31}..z35l{
z34-z33} & z35l--z35r & z35r--
z33 & z33--z31 & cycle;
z100=(z1r{dir63}..z33{z33-z30})
intersectionpoint 
(0.5[z1r,z33]--(0.5[x1r,x33],h));
filldraw z1..
%z100+(0,0.5cap_hair)..
z33{z33-z30} & z33--z30--z1
 & cycle;
.endchar;

.cmchar "The gothic letter R";
.beginchar("R",10.72u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#+2.05u#);
x1=x2=0.39w; y2=0.85h; 
y1=y7=y18=1/6h;
penpos1(cap_stem,0);
penpos2(cap_stem,0);
penstroke z1e--z2e;
penpos3(cap_hair,0);
penpos2'(cap_stem,180); z2'=z2;
x3l=0; y3=y2; 
x3.5=0.5[x3,x2]; 
penpos3.5(0.5[cap_hair,cap_stem],
-90); y3.5l=h+apex_o;
penstroke z3e{dir60}...z3.5e...
z2'e{down};
penpos4(1.2cap_hair,-90);
penpos9(1.2cap_hair,-90);
x4=x3r; 
y4r=0.56h;
y9l=y12.2-2cap_hair=0.5h;
z4.1-z4l=whatever*(z2r-z4r);
y4.1=y2;
filldraw z4r--z2r--z4.1--z4l--z4r &
cycle;
penpos5(1.2cap_hair,-90); z5l=z2r;
penpos6(whatever,angle(z2r-z4r)-90);
z6l=whatever[z4r,z5l];
z6r-z5r=whatever*(z5l-z4r);
y6l=h+apex_o; 
filldraw z5l--z6l--z6r--z5r--z5l
& cycle;
x7=0.54w; penpos7(0.9cap_hair,0);
z8.1=whatever[z4r,z6l];
x8.1=x7l; x8.2=x7r;
z8.2=whatever[z4r,z6l];
filldraw z7r--z8.2--z8.1--z7l--z7r
& cycle;
penpos11(1.118cap_stem,
angle(z6l-z4r));
z11=whatever[z4r,z6l]; x11r=x6l;
x9l=x7l; 
z10.1-z6=whatever*(z6r-z6l);
z10.1-z9l=whatever*(z6l-z4r+(0,1/3cap_stem));
z10.4=whatever[z9l,z10.1];
z10.4-z11l=whatever*(z10.1-z6);
filldraw z10.4--z11l--z11r--z10.1
--z10.4 & cycle;
z10.2-z9r=whatever*(z10.1-z9l);
z10.2=whatever[z6,z10.1];
z12.1=whatever[z9r,z10.2];
z12.1=whatever[z11l,z10.4];
filldraw z9l--z10.1--z10.2--z9r
--z9l & cycle;
x12.2=w; 
z12.4=(x7l,y9l);
x12.2-x12.3=1.2cap_stem; 
z12.3-z12.4=whatever*(z12.2-z12.1);
x1.1=x1l; y1.1=0.247h;
x13.2=x1l; y13.2=0.56h;
z13.1-z13.2=
whatever*dir(angle(z6l-z4r)+100);
z13.1=whatever[z4r,z5l];
filldraw z4l{dir-30}...z1.1{down}
& z1.1--z13.2--z13.1--z4l &
cycle;
unfill z4l{dir-30}...z1.1{down}
& z1.1--(x4l,y1.1)--z4l &
cycle;
z15=(0,-apex_o); z17=(0.73w,-apex_o);
x18=w; z19=(x12.3,y18);
%
%
%
penpos40(1.09cap_stem,-114);
z40r=(x7r+0.5cap_stem,-apex_o);
filldraw z15{dir80}%...{right}(x1,y1.1)
...z40l{dir-24} &
z40l--z40r & z40r{dir156}...
z15{dir225} & cycle;
penpos41(cap_hair,-24);
penpos41'(0.5cap_stem,-114);
z41'r=z40l; z41r=z41'l;
penpos40'(cap_hair,-24);
z40'r=z40;
penstroke z40'e--z41e;
x18.2=x18.1=x12.3+cap_stem;
y18.2=7/8[y18,y12.2];
y18.1=1/8[y18,y12.2];
z101=0.5[z9r,z10.2];
z104=(1.09w,1.1176cap_stem);
penpos105(cap_hair,-37);
z105r=(1.192w,0.193h);
z106=(w,-apex_o);
z107=whatever[z106,z40l];
x107=x106-0.44cap_stem;
penpos103(0.9cap_stem,-157);
penpos102(cap_stem,-138);
z102l=z101+(0.6cap_amount,-0.7cap_amount);
z103l=(w+1.1cap_hair,0.18h);
z100=whatever[z9r,z101];
x100=x7r;
filldraw z101..z102l..z103l..
z104..z105l{dir52} &
z105l--z105r & z105r--z106 &
z106---z107...%z103r---
z102r{dir(angle(z102l-z102r)+90)} 
...z100 & z100--
z100+(0,0.3cap_amount)--z101 & cycle;
.endchar;

.cmchar "The gothic letter S";
.beginchar("S",12.28u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(1.32cap_stem,44);
penpos2(cap_stem,70);
penpos2'(0.82cap_stem,90);
penpos3(cap_hair,-16);
penpos4(0.5cap_stem,180);
penpos5(cap_hair,-90);
penpos6(1.3cap_hair,-24);
penpos7(1.148cap_stem,50);
penpos8(0.975cap_stem,90);
penpos9(1.1cap_stem,90);
penpos10(0.56cap_stem,0);
penpos11(cap_hair,-39);
penpos12(cap_hair,-46);
penpos13(0.65cap_stem,0);
penpos14(cap_stem,90);
penpos15(1.07cap_stem,90);
penpos16(0.56cap_stem,0);
penpos3'(cap_hair,-16);
penpos20(cap_stem,-106);
penpos19(1.2cap_hair,-50);
x1=0.718w; x2=0.55w; x3=0.367w;
x2'=0.252w; x4r=0;
x5=0.164w; x6=0.725w;
x7=0.734w; x8=x1l; x9=x2';
x10=x4l; x11r=x5; z12r=z1;
x13r=w; x14=x1r-1/4cap_amount;
x15=0.302w; x16=x10r; x17=x10;
 x18=x15; x20=0.595w;
z3'-z3=whatever*dir74;
y3'l=h+apex_o; 
y1l=-apex_o; y2=0.114h;
y3=0.143h; y2'=0.157h;
y4=0.027h; y5=-0.067h; y6=0.211h;
y7=0.374h; y8r=0.462h;
y9=0.365h; y10=0.52h;
y11=0.659h; y13=y10; y14=0.652h;
y15=0.628h; y16=0.789h;
y17=0.839h; 
z19l=0.285[z17,z18];
y18=h+apex_o; y20=y19;
penpos5'(cap_hair,-66);
z5'=(0.5w,0);
penstroke z3e--z3'e;
penstroke z1e{dir141}..z2'e..z4e..
z5e{right}..z5'e..z6e{dir66}..z7e..z8e{left}..z9e..z10e..
z11e{dir51};
filldraw z1--z1r{z1r-z1l}...z13r{up}..
z14r{left}..z15r..z16r..%z19r..
z20r{z20l-z18} & z20r--z20l--z18
& z18---z17...z16l{down}..
z15l{right}..z14l{right}..
z13l..z12l{z1l-z1r} & 
z12l--z1 & cycle;
.endchar;

.cmchar "The gothic letter T";
.beginchar("T",12.82u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(cap_hair,121);
penpos3(cap_stem,211);
penpos5(cap_hair,-45);
x3l=0.288w; x1=0.455w; x5r=w-0.6cap_amount;
y1=0.9h; y3l=0.324h; y5=0.277h;
z4=(0.613w,-apex_o);
filldraw z1r..z3r..
{dir-15}z4 & z4{dir25}..z5r &
z5r--z5l & z5l..z3l..z1l &
z1l--z1r & cycle;
penpos6(cap_hair,0);
penpos7(cap_stem,-90);
penpos8(0.853cap_stem,-90);
penpos9(cap_hair,-35);
x6l=0; x9=w; x7=0.347w; 
x8=0.779w;
y6=0.7h; y7l=h+apex_o;
y8l=0.915h; y9=h-cap_amount;
penstroke z6e{up}..z7e..z8e..z9e;
%
%
%
penpos10(cap_hair,0);
penpos10'(cap_hair,0);
x10l=x10'l=0.64w;
z10'=whatever[z7,z8];
y10=0.5cap_stem;
penstroke z10e--z10'e;
%
%
%
z20=(z6r{up}..z7r..z8r..z9r)
intersectionpoint 
(z5l..z3l..z1l);
penpos11(cap_stem,0);
penpos12(cap_stem,0);
y11=0.44h;
x11l=x12l=x20;
z12=whatever[z7,z8];
z21=(z1..z3..z5)
intersectionpoint 
((0.3w,0)--(0.3w,y11));
penpos21(1.2cap_hair,-50);
penstroke z21e{dir40}..z11e{up}
--z12e;
.endchar;

.cmchar "The gothic letter U";
.beginchar("U",13.72u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(cap_hair,90);
penpos4(cap_hair,-90);
z2=(0.053w,0.404h);
z2'=(x2+cap_stem,0.462h);
z3=(0.409w,-apex_o);
z3'=(x3,0.123h);
z1=(0.555w,0.888h);
z4=(0.735w,0.215h);
filldraw z1r{left}..z2..z3{dir-18}
& z3--z4r--z4l & 
z4l{dir209}..z3'..z2'..z1l{right}
& z1l--z1r & cycle;
%
%
%
penpos5(cap_hair,0);
penpos6(cap_hair,0);
z5r=whatever[z3,z4r];
x5r=x1; z6=(x5,h+apex_o);
penstroke z5e--z6e;
%
%
%
z7=(z1{left}..0.5[z2,z2']..
0.5[z3,z3']..z4)
intersectionpoint 
((0.255w,0)--(0.255w,y2));
penpos7(cap_hair,-50);
penpos8(cap_stem,0);
y8=0.561h; x8r=x5l-cap_amount;
penpos9(cap_hair,-43);
y9=0.928h; x9=x5;
penstroke z7e{dir41}..z8e{up}
..z9e{dir48};
%
%
%
x10=0; y10=0.673h;
penpos10(cap_hair,0);
penpos11(cap_stem,-90);
x11=0.19w; y11l=h+apex_o;
penpos12(0.588cap_stem,-90);
z12=(0.457w,0.906h);
penstroke z10e{up}..z11e{right}..
z12e..z6e{up};
%
%
%
penpos20(cap_stem,0);
penpos21(cap_stem,0);
z20l=z4l; 
z21=(x20,0.841h);
penstroke z20e--z21e;
%
%
%
z25=(x20l,0.405y20);
z26=(x25+0.824cap_stem,-apex_o);
penpos27(cap_hair,-45);
z27r-z26=whatever*dir47;
x27r=w;
filldraw z20l--z25--z26--z27r--
z27l & z27l{z26-z27}..{up}z20r
& z20r--z20l & cycle;
%
%
%
penpos24(cap_hair,145);
z24=(0.291[x5r,x20l],0.789h);
z23=(x20l,h+apex_o);
z22=z21r+(0.6cap_hair,cap_hair);
filldraw z21l{up}..z24l{z24r-z23}
& z24l--z24r--z23--z22 &
z22..{down}z21r & z21r--z21l
& cycle;
%
%
%
z250=(x1,0.574h);
z270=(x1,0.413h);
z260=(x20l,y250);
z280=(x20l,y270);
penpos250(cap_hair,-90);
penpos260(cap_hair,-90);
penpos270(cap_hair,-90);
penpos280(cap_hair,-90);
penpos290(cap_hair,-90);
penpos300(cap_hair,-90);
x290=x300=0.5[x250,x260];
y290l=y250r; y300r=y270l;
penstroke z250e..z290e..z260e;
penstroke z270e..z300e..z280e;
.endchar;

.cmchar "The gothic letter V";
.beginchar("V",15.44u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(cap_hair,0);
penpos2(0.765cap_stem,-90);
penpos4(cap_hair,-100);
x1=0; y1=y3=0.83h; y2l=h+apex_o;
x2=0.094w; x3=0.205w;
h-y3'=0.4cap_stem; x3'=0.288w;
y4l=h; x4=0.549w;
filldraw z1r{up}..z2r..z3..
z4r{dir-13} & z4r--z4l &
z4l{dir167}..z3'..z2l..z1l{down}
& z1l--z1r & cycle;
%
%
%
penpos5(cap_hair,-90);
penpos6(1.118cap_stem,20);
penpos7(0.882cap_stem,0);
y5r=0; x5=0.197w; y6=0.516h;
x6=1/3w; z7=(0.3w,0.744h);
penstroke z5e{right}..z6e..z7e
..z4e{dir-13};
%
%
%
penpos8(cap_hair,-90); x8=x6;
y8=0; z9=(0.78w,0.657h);
z9'=(x9+1.5cap_hair,y9);
z10=(0.586w,h-1.06cap_stem);
z11=(0.545[x10,x9],h);
z13=(w,y10-0.5cap_amount);
z12=(0.5[x9',w],h-0.6cap_amount);
z10'=(0.345[x10,x9],y10+0.7cap_amount);
filldraw z5r..z8r..z9'{up}..
z11{left}..z10{down} &
z10{dir27}..z10'{right}..z9{down}..z8l..z5l
& z5l--z5r & cycle;
filldraw z9{up}..z12{right}..z13{down}
& z13{dir146}..z9'{down} & z9'--z9 &
cycle;
%
%
%
penpos14(cap_hair,0);
penpos15(cap_hair,18);
penpos16(cap_hair,0);
z14l=(0.46w,0.408h);
z15l=(0.432w,0.628h);
z16l=(0.387w,0.87h);
penstroke z5e{right}..z14e{up}..
z15e..z16e{up}..z4e{dir-13};
%
%
%
x17=0.152[x14r,x9];
x18=0.405[x14r,x9];
y17=0.691h; y18=0.556h;
filldraw z15{z15r-z15l}..z17{right}
..z18{dir241} & z18{dir152}..
z14r{down} & z14r--z14..z15 &
cycle;
.endchar;

.cmchar "The gothic letter W";
.beginchar("W",14.556u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(1.2cap_hair,-42.5);
penpos2(cap_stem,0);
penpos3(cap_hair,180);
y1l=0; x3=0; y3=y2=0.7h;
x2r=0.349w; x1l=0.05w;
z2'=(0.143w,0.897h);
penpos2'(cap_stem,60);
penstroke z1e{dir47.5}..z2e{up}..z2'e..
z3e{down};
%
%
%
penpos4(cap_hair,-34);
penpos5(1.06cap_stem,0);
penpos6(1.4cap_stem,35);
z4r=(0.42w,0); z6r=(0.6w,h+apex_o);
z5r=(0.672w,0.632h);
penstroke z4e{dir56}..z5e{up}..z6e{dir125};
%
%
%
penpos7(cap_hair,-44);
penpos8(1.06cap_stem,0);
penpos9(1.4cap_stem,35);
y7r=0; x8r=w; y9r=h+apex_o;
x7r=0.626w; y8=0.655h; x9r=w-0.4cap_stem;
penstroke z7e{dir46}..z8e{up}..
z9e{dir125};
%
%
%
z10'=(x2r,0.161h);
z10=(z1l{dir47.5}..z10'..z7r)
intersectionpoint 
(z4{dir56}..z5{up});
filldraw z1r{dir47.5}..z4r{z4r-z4l}
& z4r--z10 & z10..z10'..
z1l{dir227.5} & z1l--z1r &
cycle;
%
%
%
z11'=(x5r,0.175h);
z11.1=(0.935w,0);
z11=(z7{dir46}..z8{up})
intersectionpoint (z4l{dir56}..
z11'..z11.1);
filldraw z4r{dir56}..z7r{z7r-z7l}
& z7r--z11 & z11..z11'..
z4l{dir236} & z4l--z4r & cycle;
%
%
%
penpos6.1(cap_hair,-55);
penpos6.2(cap_hair,-55);
penpos9.1(cap_hair,-55);
penpos9.2(cap_hair,-55);
z6.1l=z6; z9.1l=z9;
z6.2l=whatever[z6l,z6r];
z9.2l=whatever[z9l,z9r];
x6.2=x2; x9.2=x5;
penstroke z6.2e--z6.1e;
penstroke z9.2e--z9.1e;
%
%
%
penpos100(cap_hair,-90);
penpos110(cap_hair,-90);
penpos120(cap_hair,-90);
penpos130(cap_hair,-90);
penpos140(cap_hair,-90);
penpos150(cap_hair,-90);
penpos160(cap_hair,-90);
penpos170(cap_hair,-90);
penpos180(cap_hair,-90);
penpos190(cap_hair,-90);
penpos200(cap_hair,-90);
x100=x2; y100=0.677h; z120=z5;
y110l=y120-cap_amount;
x110=0.47[x100,x120];
y130=y110; y140=y120; x140=x8;
x130=0.47[x120,x140];
penstroke z100e..z110e..z120e;
penstroke z120e..z130e..z140e;
%
z150=(x2l,0.309h); 
y170l=y180r=y150;
x170=x180=x6.1r;
z200=((0,y180r)--(w,y180r))
intersectionpoint 
(z7{dir46}..z8{up});
y190=y160=y180l+cap_hair;
x160=0.536[x150,x170];
x190=0.532[x180,x200];
penstroke z150e..z160e..z170e;
penstroke z170e..z190e..z200e;
%
%
%
z21=(x3l,0.404h);
z20=(x1r,y21+0.5cap_amount);
z22=(0.095w,0.552h);
filldraw z1l{dir47.5}..z20{left}
..z21 & z21{up}..z22{right}..
z1r{dir227.5} & z1r--z1l & cycle;
.endchar;

.cmchar "The gothic letter X";
.beginchar("X",8.556u#,cap_height#,1.11u#);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
z1=(0.442w,0.202h);
penpos1(cap_stem,0);
penpos2(cap_stem,0);
x2=x1; y2=0.843h;
penstroke z1e--z2e;
z3=(0,-d); penpos3(0.5cap_hair,-45);
z4=(x1l,0.108h);
z5=(0.61w,-apex_o);
penpos6(cap_hair,-45);
z6r-z5=whatever*(z4-z3r);
x6r=w-cap_amount;
z5'-z6l=whatever*(z5-z6r);
z5'=whatever[z4,z5];
z7-z1r=whatever*(z5-z4);
z7=whatever[z5',z6l];
filldraw z3r--z4--z5--z6r--
z6l--z7--z1r--z1l--z3l--z3r
& cycle;
%
%
%
penpos8(cap_hair,-34);
x8=cap_amount; y8=0.74h;
z9=(x2l-0.5cap_hair,h+apex_o);
z9'=whatever[z9,z2r];
z9'-z8r=whatever*(z9-z8l);
penpos9.5(1.1cap_stem,angle(z9'-z8r));
z9.5r=z9';
z10=z9.5l;
z10'-z10=whatever*(z2r-z9);
x10'=x2l;
filldraw z8l--z9--z2r--z10'--
z10--z8r--z8l & cycle;
%
%
%
penpos11(cap_hair,-49);
penpos11'(cap_hair,-49);
penpos12(1.176cap_stem,-145);
penpos13(1.176cap_stem,-145);
x11=x2; y11=0.717h;
z11'l=z2r; 
z12.5=whatever[z11l,z11'l];
y12.5=h+apex_o;
z13l=(w,y2);
z12l=0.5[z12.5,z13l];
z12.3=0.3[z12.5,z13l];
z12.1=whatever[z11l,z12.5];
y12.1=y12.3;
filldraw z11l--z11'l---z12.1...
z12.3---z12l--z13l & z13l--z13r
& z13r---z12r...z11'r---z11r &
z11r--z11l & cycle;
%
%
%
z4.1=whatever[z3r,z4];
z4.2=whatever[z4,z5];
y4.1=y4.2=0.5y4;
filldraw z4.1{z4-z3r}..
z4.2{z5-z4} & z4.2--z4--z4.1 &
cycle;
%
%
%
penpos14(1.4cap_hair,-90);
penpos15(1.4cap_hair,-90);
penpos16(1.4cap_hair,-90);
penpos17(1.4cap_hair,-90);
penpos14'(1.4cap_amount,-90);
x14=x16=x8; x15=x17=x13;
x14'=0;
y14'=0.475h;
y14r=y15r=y14'l;
y16l=y17l=y14'r;
penstroke z14e--z15e;
penstroke z16e--z17e;
.endchar;

.cmchar "The gothic letter Y";
.beginchar("Y",9.167u#,cap_height#,4/3u#);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(cap_stem,0);
penpos4(cap_hair,0);
penpos5(cap_hair,90);
penpos6(cap_hair,130);
penpos7(cap_hair,90);
penpos8(cap_hair,0);
penpos9(cap_hair,-90);
x1=0.285w; y1=0.8h;%762
z2=(x1l,0.535y1);
z4l=(0,0.045h);
z3=(0.5x2,0.629[y2,y4]);
y5l=-d; x5=0.218w;
z6l=(0.491w,0);
z7=(0.679w,0.1h);
z8=(0.788w,0.5y4);
z9r=(0.63w,-d);
z9'=(x9,y9r+2cap_hair);
z11=(w,0.283h);
z12=z11+(0,1.2cap_hair);
z10=(x2,0.12h);
z12-z13=whatever*(z11-z10);
x13=x1r;
z12'=whatever[z12,z13];
x12'=x11-cap_hair;
filldraw z9'..z9r..z9'..cycle;
filldraw z9r{right}..z8r..z7r..
z6r..z5r..z4r{up}...z10---z11 &
z11--z12'--z13--z1r--z1l---
z2...z3...z4l{down}..z5l..z6l..
z7l..z8l..z9l{left} &
z9l--z9r & cycle;
%
%
%
z17=(0.788w,h+apex_o);
z14=(0,0.9h);%avant:848
y15=h+apex_o;
z15-z14=whatever*(z17-z1r);
z16=whatever[z1r,z17];
x16=x1r+2cap_amount;
z18-z17=whatever*(z16-z15);
x18=w; penpos18'(cap_stem,0);
x18'r=w-cap_amount;
y18'=y18; 
z19=whatever[z12,z13];
z19''=whatever[z12,z13];
x19''=x18'r;
x19=x18'l;
filldraw z19--z18'l--z18'r--
z19''--z19 & cycle;
penpos15'(cap_hair,angle(z15-z14));
z15'r=z15;
z15.1-z15'l=whatever*(z16-z15);
x15.1=x13.2=x16+cap_hair;
z15.2=whatever[z15,z16];
x15.2=x15.1;
filldraw z15.1--z15'l--z15'r--
z15.2--z15.1 & cycle;
filldraw z14--z15--z16--z1r--
z1l & z1l{up}..{left}z14 &
cycle;
x13.1=x16; z13.1=whatever[z13,z12];
z13.2=whatever[z13,z12];
z16'=whatever[z16,z17]; x16'=x13.2;
filldraw z13.1--z16--z16'--z13.2
--z13.1 & cycle;
%
%
%
penpos20(1.1cap_stem,angle(z17-z16));
z20r=z17;
z21-z20l=whatever*(z18-z17);
x21=x19;
penpos17'(cap_hair,angle(z18-z17));
z17'l=z17;
z16''-z17'r=whatever*(z16-z17);
x16''=x16;
filldraw z16--z17--z17'r--z16''
--z16 & cycle;
filldraw z20r--z18 & z18..
{down}(x19'',y18-0.5cap_stem) &
(x19'',y18-0.5cap_stem)--z18'r--
z18'l--z21--z20l--z20r & cycle;
z11.5=whatever[z10,z11];
x11.5=x19;
filldraw z11..{up}
(x19'',y11+0.5cap_stem)
& (x19'',y11+0.5cap_stem)--
z19''--z19--z11.5--
z11 & cycle;
%
%
%
x31=x33=x19; x30=x32=x13.2;
x35=x37=x2; x34=x36=-apex_o;
penpos30(cap_hair,-90);
penpos31(cap_hair,-90);
penpos32(cap_hair,-90);
penpos33(cap_hair,-90);
penpos34(0.5cap_hair,-90);
penpos36(0.5cap_hair,-90);
penpos35(0.647cap_stem,-90);
penpos37(0.647cap_stem,-90);
y31=0.3[y18,y19];
y33=0.6[y18,y19];
z31-z30=whatever*dir20;
z33-z32=whatever*dir20;
y34=y35=0.5[y30,y31];
y36=y37=0.5[y32,y33];
penstroke z30e--z31e;
penstroke z32e--z33e;
penstroke z34e--z35e;
penstroke z36e--z37e;
.endchar;

.cmchar "The gothic letter Z";
.beginchar("Z",10.667u#,cap_height#,0);
.adjust_fit(cap_serif_fit#,cap_serif_fit#);
penpos1(1.414cap_hair,-90);
penpos2(1.414cap_hair,-90);
penpos3(1.414cap_hair,-90);
penpos4(1.414cap_hair,-90);
penpos5(1.414cap_hair,-90);
penpos6(1.414cap_hair,-90);
x1=x2=x5=cap_amount;
x3=x4=x6=w;
y2r=-apex_o;
y5r=y2l; y1r=y5l;
y3l=h+apex_o; y6l=y3r; y4l=y6r;
penstroke z1e--z3e;
penstroke z2e--z4e;
%
%
%
z7=(0.281w,0.076h);
z8=(0.505w,0.22h);
z9=(0.708w,-apex_o);
z10=(0.839w,0.103h);
penpos11(cap_hair,0);
x11r=w; y11=0.224h;
penpos12(0.91cap_stem,0);
z12r=z11r;
filldraw z2r{z4-z2}..z7..z9..
z11r{up}
& z11r--z11l & z11l{down}..z10
..z8..z2l{z2-z4} & z2l--z2r &
cycle;
filldraw z12r..z12l..z12r..cycle;
%
%
%
y17=y14=0.673h;
penpos14(cap_hair,180);
penpos15(0.88cap_stem,0);
penpos16(cap_hair,90);
penpos17(cap_hair,0);
x14=0.297w;
x17l=0; z15r=z14l;
filldraw z15r..z15l..z15r..cycle;
x16=0.148w; y16=0.565h;
z18=(0.255w,0.843h);
z19=(0.349w,h+apex_o);
z20=(0.656w,0.758h);
z21=(0.781w,0.892h);
filldraw z14r{down}..z16r..z17r
..z18..z20..z3r{z3-z1} & z3r--z3l
& z3l{z1-z3}..z21..z19..z17l..
z16l..z14l{up} & z14l--z14r &
cycle;
.endchar;
