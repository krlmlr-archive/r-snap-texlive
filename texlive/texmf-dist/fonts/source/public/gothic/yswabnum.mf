.cmchar "The Schwabacher numeral 0";
.beginchar("0",8.377u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(1.107cap_stem,47);
z1l=(0.432w,-apex_o);
penpos5(0.946cap_stem,33);
z5l=(0,0.475h); z7=(0.365w,0.85h);
penpos2(whatever,52.5); z2r=(0.55w,h+apex_o);
penpos7(cap_hair,-47);
z2l=whatever[z7l,z7r];
penpos4(cap_hair,-37.5); z4l=z2;
penstroke z1e{dir137}..z5e{up}..
z7e..z4e{dir52.5};
%
%
%
penpos3(cap_hair,-64);
penpos6(0.893cap_stem,28);
z1r=2/3[z3l,z3r]; z6r=(w,0.529h);
filldraw z1l{z1r-z1l}..
%z3r{dir26}..
z6r{up}..
z2r{dir133}--z2l{dir-24}..z6l{down}
..z3l{dir206}--(x1,y3l)--z1l
& cycle;
.endchar;

.cmchar "The Schwabacher numeral 1";
.beginchar("1",5.509u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(cap_hair,-58); x1l=0;
y1r=-apex_o; penpos2(cap_hair,60);
z2=(0.932w,0.171h);
penpos3(0.93cap_stem,0);
penpos4(0.93cap_stem,0);
x3=x4=0.589w; y3=0.354h; y4=0.823h;
penpos5(cap_hair,-90); 
penpos6(cap_hair,-90);
z6l=(w,h+apex_o); z5l=(0.301w,0.951h);
filldraw z1r{dir32}..z2l{dir-30} &
z2l--z2r{dir150}...z3r---z4r...
z6r--z6l--z5l--z5r...z4l---
z3l...z1l{dir212}--z1r & cycle;
.endchar;

.cmchar "The Schwabacher numeral 2";
.beginchar("2",8.415u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(cap_hair,-45); z2=(0.359w,h-apex_o);
x1l=0; z2-z1l=whatever*dir48;
z3=whatever[z1l,z2]; y3=h+apex_o;
penpos5(1.154cap_stem,27);
z5=(0.789w,0.607h);
z4=(0.323w,0.768h);
filldraw z5r{up}..z2{dir149}--
z1l--z1r{z2-z1l}..z4..z5l{down}
& z5l--z5r & cycle;
z3'=(z5r{up}..z2{dir149})
intersectionpoint ((0,0.963h)--
(w,0.963h));
filldraw z3'{dir149}..z3{up} &
z3--z1l--z3' & cycle;
%
%
%
penpos6(1.3cap_hair,-53);
penpos7(hair,0); z7l=(0,-apex_o);
z6=(0.363w,0.314h);
penstroke z7e{up}..z6e..z5e{up};
penpos8(cap_stem,-124);
penpos9(1.179cap_stem,-123);
z8=(0.395w,0.14h); z9r=(0.731w,-apex_o);
penstroke z7e{up}..z8e{right}..
z9e;
penpos10(cap_hair,-33);
penpos11(cap_hair,-33);
z10r=z9; z11-z10=whatever*dir57;
x11r=w;
penstroke z10e--z11e;
.endchar;

.cmchar "The Schwabacher numeral 3";
.beginchar("3",6.302u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos5(cap_hair,-72); z5=(0.132w,0.454h);
z4=(0.749w,0.753h);
penpos4(0.857cap_stem,34);
penpos2(0.964cap_stem,90);
x2=0.377w; y2r=h+apex_o;
penpos1(cap_hair,-41);
z1l=(0,0.78h);
penstroke z5e{dir18}..z4e{up}..
z2e{left};
filldraw z1l--z2r--z2l{left}..
z1r{z1l-z2r}--z1l & cycle;
%
%
%
penpos11(1.071cap_stem,56);
penpos10(cap_stem,32);
penpos9(cap_hair,-64);
penpos8(0.571cap_stem,-135);
y11l=y5l; x11r=x4l; x10r=w;
y10=0.332h; z9=(0.515w,0.049h);
z8r=(0.228w,-apex_o);
penstroke z8e{right}..z9e..
z10e{up}..z11e{left};
filldraw z5{dir18}..z11r{z11r-z11l}
--z11l{left}..z5r--z5 & cycle;
z6=(apex_o,y8l); z7=(0.198w,0.146h);
filldraw z8r{left}..z6{up}..
z7{right}..z8l{right}--z8r &
cycle;
.endchar;

.cmchar "The Schwabacher numeral 4";
.beginchar("4",8.377u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(3/4cap_stem,90);
penpos2(whatever,56);
x1=w; x2l=0; y1=0.46h; y2l=y1l;
y2r=y1r; z3'=whatever[z2l,z2r];
y3'=h; penpos4(0.93cap_stem,0);
z4l=z3'; penpos12(cap_hair,-34);
penpos13(cap_hair,-34);
z12l=z2; z13l=z3';
penpos5(whatever,25); x5l=x4l;
x5r=x4r; y5=0.268h; 
penstroke z12e--z13e; 
penstroke z5e--z4e;
penstroke z1e--z2e;
%
%
%
penpos10(cap_hair,-90);
penpos11(cap_hair,-90);
y10=0; x10=0.36w; z11=(0.964w,0.162h);
filldraw z10r{right}..z11r{right}
--z11l{left}..z5r{up}--z5l{down}
..z10l{left}--z10r & cycle;
penpos6(0.5cap_hair,90);
penpos8(0.5cap_hair,0);
z6r=(w-cap_hair,h); z8r=(w,0.61h);
z7=(x4r,0.841h);
filldraw z7{up}..z6l{right}--
z6r--z4r--z7 & cycle;
filldraw z1r--z8r--z8l{down}..
{left}(x4r,y1r)--z1l--z1r & cycle;
.endchar;

.cmchar "The Schwabacher numeral 5";
.beginchar("5",5.623u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(cap_hair,-65);
penpos2(0.93cap_stem,33);
penpos3(0.964cap_stem,57.5);
z1=(0.134w,0); z2=(0.779w,0.369h);
z3=(0.477w,0.567h);
z4=(0,0.579h); z5=(0.174w,0.759h);
z6=(0.221w,h);
penpos8(2/3cap_hair,0);
z8r=(w,h+apex_o); 
penpos9(1.161cap_stem,-108);
z9l=z8r;
filldraw z1r..z2r{up}..
z3r..z5{up}..z9r{right}--z9l--
z8l & flex(z8l,(0.5[x6,x8],h-apex_o),
z6) & z6..z4{down} & z4..z3l..
z2l{down}..z1l--z1r & cycle;
.endchar;

.cmchar "The Schwabacher numeral 6";
.beginchar("6",7.472u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(0.464cap_stem,-43);
z1r=(0.187w,-apex_o);
z2=(0.737w,0.433h); penpos2(cap_hair,-32);
penpos3(0.964cap_stem,44);
z3r=(w,0.747h);
z4=(0.631w,h+apex_o);
z5=(0.5w,0.838h); penpos6(0.93cap_stem,-150);
z6r=(0,0.579h);
z7=(0.343w,0.341h); z8=(x5,0.482h);
penpos9(cap_hair,-48);
z9=(2/3w,0.512h); penpos10(cap_hair,132);
y10r=y5; x10l=x7;
penstroke z1e..z2e..z3e{up};
filldraw z3r{up}..z4{dir156}---z10r...
z6r{down}..z7--z9r--z9l{z7-z9r}..
z8{left}..z6l{up}..z5{right}..z3l
{down}--z3r & cycle;
%
%
%
transform metatopish;
metatopish=identity 
reflectedabout ((0,0.5h),(w,0.5h));
picture krufo;
krufo:= currentpicture transformed
metatopish;
picture currentpicture;
currentpicture=krufo;
transform metatopish;
metatopish=identity 
reflectedabout ((0.5w,0),(0.5w,h));
picture krufo;
krufo:= currentpicture transformed
metatopish;
picture currentpicture;
currentpicture=krufo;
.endchar;

.cmchar "The Schwabacher numeral 7";
.beginchar("7",8.302u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos2(1.068cap_stem,61);
z2r=(w,0.95h);
penpos3(2/3cap_hair,0); 
penpos4(2/3cap_hair,0);
z3l=(0,0.686h); z4-z3=whatever*dir73;
y4=h; penpos1(whatever,73);
y1l=y2l; y1r=y2r; z1=whatever[z3l,z4l];
penpos1'(whatever,73);
y1'r=y1r; y1'l=y1l; x1'=x1+0.182w;
penpos6(cap_hair,0); 
penpos5(0.939cap_stem,0);
z6r=z2l; z5=(0.382w,0);
filldraw z2r---z1'r...z4r{up}--z4l
--z3l--z3r{z4r-z3r}...z1'l---z2l
--z2r & cycle;
penstroke z5e{up}..z6e{z2r-z2l};
.endchar;

.cmchar "The Schwabacher numeral 8";
.beginchar("8",8.226u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(0.5cap_hair,0);
z1=(0.45w,-apex_o); penpos2(cap_hair,-60);
z2=(0.761w,0.157h); penpos3(0.857cap_stem,37);
z3r=(w,0.383h); penpos4(0.93cap_stem,72);
z4=(0.463w,0.546h); penpos5(0.714cap_stem,31);
z5=(0.138w,0.735h); 
penpos6(cap_hair,-56);
z7=(0.523w,h+apex_o);
z7-z6l=whatever*dir34; x6=0.266w;
penpos8(3/4cap_stem,65); 
z8=(0.596w,0.895h); penpos9(0.875cap_stem,33);
y9l=y5l; x9=x3l+apex_o;
penpos10(cap_hair,-56); x10=x8;
y10l=y4; penpos11(cap_hair,-48);
y11l=y4; x11l=x6r; penpos12(0.8cap_stem,30);
z12l=(0,y3l); penpos13(0.893cap_stem,48);
x13=x6l; y13=y2l;
%
%
%
filldraw z1r{dir60}..z2r..z3r{up}..
z4r{dir162}..z5r{up}..z6r..z8l..
z9l{down}..z10l{dir214}--z10r
{dir34}..z9r{up}..z8r..z7 &
z7---z6l...z5l{down}..z4l{dir-18}
..z3l{down}..z2l..z13r..%{dir161}..
z12r{up}..z11r{dir34}--z11l{dir214}
..z12l{down}..z13l..z1l{dir-60}--
z1r & cycle;
.endchar;

.cmchar "The Schwabacher numeral 9";
.beginchar("9",7.472u#,cap_height#,0);
.adjust_fit(usual_left#,usual_right#);
penpos1(0.464cap_stem,-43);
z1r=(0.187w,-apex_o);
z2=(0.737w,0.433h); penpos2(cap_hair,-32);
penpos3(0.964cap_stem,44);
z3r=(w,0.747h);
z4=(0.631w,h+apex_o);
z5=(0.5w,0.838h); penpos6(0.93cap_stem,-150);
z6r=(0,0.579h);
z7=(0.343w,0.341h); z8=(x5,0.482h);
penpos9(cap_hair,-48);
z9=(2/3w,0.512h); penpos10(cap_hair,132);
y10r=y5; x10l=x7;
penstroke z1e..z2e..z3e{up};
filldraw z3r{up}..z4{dir156}---z10r...
z6r{down}..z7--z9r--z9l{z7-z9r}..
z8{left}..z6l{up}..z5{right}..z3l
{down}--z3r & cycle;
.endchar;