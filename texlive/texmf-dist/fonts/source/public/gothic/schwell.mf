% SUET Zeichensatz in schraeggestellter Form
%
%
%                         ---------------------
%
% Wuensche, Fehler und Anregungen bitte an:
%
% B. Ludewig, Uni Siegen, HRZ, Hoelderlinstr. 3, 5900 Siegen.
%
%                         Ludewig@hrz.uni-siegen.dbp.de
%                                                 oder
%                                 GZ006@DSIHRZ51.BITNET
%
%  plain

font_identifier "SCHWELL"; font_size 14pt#;
font_coding_scheme:="SUETTERLIN Text";

mode_setup;
boolean serifs,monospace;
%
xpt#:=pt#;
monospace:=false;
serifs:=true;
%
ut#:=0.5;
ut=ut#*hppp;
%                             % <-----hier kann gedreht werden
numeric top,mid,base,bot,an_top,an_mid,an_bot,ang;
numeric Z_anf,I_anf,o_bogen,L_o,U_bogen,g_bogen,G_bogen,z_bogen;
numeric alpha,beta,gamma,delta,mi_alpha,mi_beta,mi_gamma,mi_delta;
ang:=20;
alpha=-45-ang;mi_alpha=-45+180-ang;
beta=85-ang;mi_beta=85+180-ang;
gamma=50-ang;mi_gamma=50+180-ang;
delta=30-ang;mi_delta=30+180-ang;
%
%
top:=21ut; mid:=8ut; bas:=0ut; bot:=-14ut;
%
an_top=7ut; an_mid=4ut; an_bot=1ut;
%
Z_anf=17ut; I_anf=18ut; O_bogen=5ut; L_o=4.5ut; U_bogen=3ut;
g_bogen=-12ut; G_bogen=-11ut;z_bogen=-10ut;
%
pen su_pen;
su_pen := pencircle xscaled .151ut yscaled 2.5ut rotated -45;

pen su_pen_small;
%su_pen_small := pencircle scaled 1.51ut;
su_pen_small := pencircle scaled 2.01ut;

pen su_pen_big;
su_pen_big := pencircle scaled 4.51ut;

def makex(suffix i)(expr xnull,ang,yy)=
   if yy=0: wi:=x.i:=xnull;
   else:   wi:=x.i:=((sind ang*yy)/cosd ang)+xnull; fi
enddef;
def makewi(suffix i)(expr xnull,ang,yy)=
   if yy=0: wi:=x.i:=xnull;
   else: if yy=mid:  wi:=x.i:=((sind ang*yy)/cosd ang)+xnull-1.2*hppp;
   else: if yy=an_top: wi:=x.i:=((sind ang*yy)/cosd ang)+xnull-1.0*hppp;
   else: if yy=an_mid: wi:=x.i:=((sind ang*yy)/cosd ang)+xnull-0.5*hppp;
   else: if yy=an_bot: wi:=x.i:=((sind ang*yy)/cosd ang)+xnull-0.1*hppp;
   fi fi fi fi fi
enddef;

def makebox (text r) =
  for y=-7ut step 1ut until 21ut:       r((0,y),(28ut,y)); endfor;
  for x=-2ut step 1ut until 28ut:       r((x,-7ut),(x,21ut)); endfor;
  for y=bas+.5,bas-.5,an_bot+.5,an_mid+.5,an_top+.5,mid-.5,mid+.5,
          Z_anf,Z_anf+.5,I_anf,I_anf+.5,O_bogen,O_bogen+.5,L_o,L_o+.5,
            U_bogen,U_bogen+.5,g_bogen,g_bogen+.5,G_bogen,G_bogen+.5,
              z_bogen,z_bogen+.5:    r((0,y),(28ut,y)); endfor;
  z90=(32ut,mid);makelabel("mid",z90);makelabel("top",(32ut,top));
  makelabel("an_mid",(32ut,an_mid));makelabel("an_bot",(32ut,an_bot));
  makelabel("an_top",(32ut,an_top));makelabel("bot",(32ut,bot));
  makelabel("Z_anf",(32ut,Z_anf));  makelabel("I_anf",(32ut,I_anf));
  makelabel("O_bo",(32ut,O_bogen));  makelabel("L_o",(32ut,L_o));
  makelabel("U_bo",(32ut,U_bogen));  makelabel("g_bo",(32ut,g_bogen));
  makelabel("G_bo",(32ut,G_bogen));  makelabel("z_bo",(32ut,z_bogen));
  makelabel("0",(0,24ut)); makelabel("5",(5ut,24ut)); makelabel("10",(10ut,24ut));
  makelabel("15",(15ut,24ut)); makelabel("20",(20ut,24ut));
  makelabel("25",(25ut,24ut));
  makelabel(str[w],(w,-10ut));
enddef;

%
input su-low;
input su-spec;
input su-upp;
%
font_quad 33pt#;
font_normal_space 6.6pt#;
font_normal_stretch 3pt#;
font_normal_shrink 1pt#;
%
input su-lig;
end.
