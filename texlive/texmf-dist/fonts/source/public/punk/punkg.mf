% punk greek uppercase

beginpunkchar(oct"000",11,1,2); % $\Gamma$
z1=pp(2u,0); z2=pp(2u,h); z3=pp(w-1.5u,h);
pd z1; pd z3; draw z1--z2--z3;  % stem and arm
endchar;

beginpunkchar(oct"001",15,1,2); % $\Delta$
z1=pp(u,0); z2=pp(.5w,h); z3=pp(w-u,0);
pd z1; draw z1--z2..tension 5..z3..tension 5..z1; % triangle
endchar;

beginpunkchar(oct"002",15,.5,2); % $\Theta$
z1=pp(.5w,h); z2=pp(u,.6h); z3=pp(.5w,0); z4=pp(w-u,.6h);
pd z1; draw z1..tension.8..z2..z3..z4..tension.8..z1; % bowl
z5=pp(x2+2u,.4h); z6=pp(x4-2u,.4h); pd z5; pd z6; draw z5--z6; % bar
endchar;

beginpunkchar(oct"003",12,1,2); % $\Lambda$
z1=pp(u,0); z2=pp(.5w,h); z3=pp(w-u,0);
pd z1; pd z3; draw z1--z2--z3;  % diagonals
endchar;

beginpunkchar(oct"004",12,1,1); % $\Xi$
z1=pp(u,h); z2=pp(w-u,h); pd z1; pd z2; draw z1--z2;  % upper arm
z3=pp(2u,.55h); z4=pp(w-2u,.55h); pd z3; pd z4; draw z3--z4;  % bar
z5=pp(u,0); z6=pp(w-u,0); pd z5; pd z6; draw z5--z6;  % lower arm
endchar;

beginpunkchar(oct"005",13,1,.5); % $\Pi$
z1=pp(1.5u,0); z2=pp(1.5u,h); z3=pp(w-1.5u,h); z4=pp(w-1.5u,0);
pd z1; pd z4; draw z1--z2--z3--z4;  % stems and bar
endchar;

beginpunkchar(oct"006",13,1,1); % $\Sigma$
z1=pp(w-u,h); z2=pp(u,h); z3=pp(.5w-u,.5h); z4=pp(u,0); z5=pp(w-u,0);
pd z1; pd z5; draw z1--z2{.5[z4,z5]-z2}..z3--z4--z5; % arms and diagonals
endchar;

beginpunkchar(oct"007",15,1,.5); % $\Upsilon$
z1=pp(u,.8h); z2=pp(.3w,h); z3=pp(.5w,.5h); z4=pp(.5w,0);
pd z1; pd z4; draw z1..z2..tension2..z3---z4; % left arc and stem
z5=pp(w-u,.8h); z6=pp(.7w,h);
pd z5; draw z5..z6..tension2..{z4-z3}z3; % right arc
endchar;

beginpunkchar(oct"010",13,1,2); % $\Phi$
z1=pp(.5w,h); z2=pp(.5w,0); pd z1; pd z2; draw z1--z2; % stem
z3=pp(.5w,2/3h); z4=pp(u,.5h); z5=pp(.5w,1/4h); z6=pp(w-u,.5h);
pd z3; draw z3..z4..z5..z6..z3; % bowl
endchar;

beginpunkchar(oct"011",14,1,1); % $\Psi$
z1=pp(.5w,h); z2=pp(.5w,0); pd z1; pd z2; draw z1--z2; % stem
z3=pp(u,.8h); z4=pp(.5w,.2h); z5=pp(w-u,.8h);
pd z3; pd z5; draw z3{.4[z1,z2]-z3}..z4{right}..{z5-.4[z1,z2]}z5; % stroke
endchar;

beginpunkchar(oct"012",13,1,2); % $\Omega$
z1=pp(u,0); z2=pp(1/3w,0); z3=pp(u,2/3h); z4=pp(.5w,h);
z5=pp(w-u,2/3h); z6=pp(2/3w,0); z7=pp(w-u,0);
pd z1; pd z7; draw z1--z2{up}..z3..z4..z5..{down}z6--z7; % bowl and arms
endchar;
