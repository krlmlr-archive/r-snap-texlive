% Punk accents

beginpunkchar(oct"022",9,1,1); % \`{}
z1=pp(2.5u,h); z2=pp(.6w,.8h); pd z1; pd z2; draw z1--z2; % diagonal
endchar;

beginpunkchar(oct"023",9,1,1); % \'{}
z1=pp(w-2.5u,h); z2=pp(.4w,.8h); pd z1; pd z2; draw z1--z2; % diagonal
endchar;

beginpunkchar(oct"136",13,1,1); % \^{}
z1=pp(2.5u,.8h); z2=pp(.5w,h); z3=(w-2.5u,.8h);
pd z1; pd z3; draw z1--z2--z3; % diagonals
endchar;

beginpunkchar(oct"024",13,1,1); % \v{}
z1=pp(2.5u,.9h); z2=pp(.5w,.7h); z3=pp(w-2.5u,.9h);
pd z1; pd z3; draw z1--z2--z3; % diagonals
endchar;

beginpunkchar(oct"025",11,1,1); % \u{}
z1=pp(2u,h); z2=pp(.5w,.75h); z3=pp(w-2u,h);
pd z1; pd z3; draw flex(z1,z2,z3); % stroke
endchar;

beginpunkchar(oct"026",12,1,1); % \={}
z1=pp(u,.8h); z2=pp(w-u,.8h); pd z1; pd z2; draw z1--z2; % bar
endchar;

beginpunkchar(oct"137",5,1,1); % \.{}
pd pp(.5w,.9h); % dot
endchar;

beginpunkchar(oct"177",13,1,1); % \"{}
pd pp(1/5w,.9h); pd pp(4/5w,.9h); % dots
endchar;

beginpunkchar(oct"176",13,1,1); % \~{}
z1=pp(u,.75h); z2=pp(w-u,.9h); pd z1; pd z2; draw z1{up}..{up}z2; % stroke
endchar;

beginpunkchar(oct"175",13,1,1); % \H{}
z1=pp(4u,h); z2=pp(2.5u,.7h); z3=pp(w-2u,h); z4=pp(w-3.5u,.7h);
pd z1; pd z3; draw z1--z2--z4--z3; % diagonals (linked)
endchar;

beginpunkchar(oct"027",13,0,0); % Scandinavian loop, for \AA\ and \aa
z0=(.5w,.66h); % point $z2$ of lowercase A
z1=(.5w,.9h); draw z0{z0-(1.5u,0)}..z1..{(w-1.5u,0)-z0}z0; % loop
endchar;

beginpunkchar(oct"030",13,.5,.5); % Cedilla, for \c c
z1=(.6w,0); z2=pp(.6w,-.1h); z3=pp(2.5u,-.1h);
pd z3; draw z1--z2--z3; % stroke
endchar;

beginpunkchar(oct"040",11,.5,.5); % Polish cross, for \L\ and \l
z1=pp(0,.25h); z2=pp(4u,.4h); pd z1; pd z2; draw z1--z2; % diagonal
endchar;
ligtable oct"040": "l" kern -charwd, "L" kern -charwd;
