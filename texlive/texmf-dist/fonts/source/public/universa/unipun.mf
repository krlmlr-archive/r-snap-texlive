%%
%% This is file `unipun.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% uni.dtx  (with options: `pun')
%% 
%% Copyright (C) 1998 Christian Holm.
%% 
%% The universal font and package is distributed in the hope that
%% it will be useful, but WITHOUT ANY WARRANTY; without even the
%% implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
%% PURPOSE.  See the GNU General Public License for more details.
%% 
%% See the file uni.dtx for further comments and documentation.
%% 


beginchar("!",P_T#,l_h#,0) ; "The exclamation mark (!)" ;
  pickup UniPensqu ;
  top lft z1 = (0,h) ; bot lft z2 = (0,2P_T) ; draw z1--z2 ;
  pickup UniPencir ;
  bot lft z3 = (0,0) ; drawdot z3 ;
  penlabels(1,2,3) ;
endchar;

beginchar("(",x_w#/2,l_h#,p_d#) ; "The left paranthesis (()" ;
  z1 = (w,h)      ; z2 = (p_t,(h-d)/2) ; z3 = (w,-d)    ;
  z4 = (w-p_t,-d) ; z5 = (0,(h-d)/2)   ; z6 = (w-p_t,h) ;
  fill z1..z2..z3--z4..z5..z6--cycle ;
  penlabels(range 1 thru 6) ;
endchar;

beginchar(")",x_w#/2,l_h#,p_d#) ; "The right paranthesis ())" ;
  z1 = (p_t,h) ; z2 = (w,(h-d)/2)     ; z3 = (p_t,-d)    ;
  z4 = (0,-d)  ; z5 = (w-p_t,(h-d)/2) ; z6 = (0,h)       ;
  fill z1..z2..z3--z4..z5..z6--cycle ;
  penlabels(range 1 thru 6) ;
endchar;

beginchar("#",x_w#,x_h#,0) ; "The hash mark (#)" ;
  unilne(p_t,(0,0),(w/2,h),1,1,10) ;
  unilne(p_t,(w/2,0),(w,h),1,1,20) ;
  pickup unipensqu ;
  top lft z1 = (0,(h-c_d)/2) ; top rt z2 = (w,(h-c_d)/2) ;
  bot lft z3 = (0,(h+c_d)/2) ; bot rt z4 = (w,(h+c_d)/2) ;
  draw z1--z2 ; draw z3--z4 ;
  penlabels(1,2,3,4) ;
endchar;

beginchar("%",x_w#,l_h#,0) ; "The percent sign (%)" ;
  temp := 3c_d/2 ;
  unicir(temp,p_t,(temp,h-temp),10) ;
  unicir(temp,p_t,(w-temp,temp),20) ;
  unilne(p_t,(0,0),(w,h),1,1,30) ;
endchar;

beginchar("'",2P_T#,l_h#,0) ; "The apostrophe (')" ;
  uniend(w/2,(w/2,h-w/2),0,180,10) ;
  z1 = (0,h-w/2) ; z2 = (w,h-w/2) ; z3 = (0,h-3w/2) ;
  fill z1--z2--z3--cycle ; penlabels(1,2,3) ;
endchar;

beginchar("*",x_w#,l_h#,0) ; "The ast (*)" ;
  path rect ;
  temp := h-(w*sind(60)-p_t*cosd(60))/2 ;
  pickup unipensqu ;
  lft z1 = (0,temp) ; rt z2 = (w,temp) ; draw z1--z2 ;
  rect = (bot lft z1)--(top lft z1)--(top rt z2)--(bot rt z2)--cycle ;
  fill rect rotatedabout((w/2,temp),60) ;
  fill rect rotatedabout((w/2,temp),120) ;
  penlabels(1,2) ;
endchar;

beginchar("+",x_w#,x_h#,0) ; "The plus sign (+)" ;
  pickup unipensqu ;
  lft z1 = (0,w/2) ; rt  z2 = (w,w/2) ; draw z1--z2 ;
  bot z3 = (w/2,0) ; top z4 = (w/2,w) ; draw z3--z4 ;
endchar;

beginchar(",",2P_T#,2P_T#,2P_T#) ; "The punctuation ," ;
  uniend(w/2,(w/2,0),0,180,10) ;
  z1 = (0,0) ; z2 = (w,0) ; z3 = (0,-d) ;
  fill z1--z2--z3--cycle  ; penlabels(1,2,3) ;
endchar;

beginchar("-",4x_w#/5,x_h#,0) ; "The hyphen (-)" ;
  pickup unipensqu ;
  lft z1 = (0,h/2) ; rt z2 = (w,h/2) ; draw z1--z2 ;
  penlabels(1,2);
endchar;

beginchar(".",2P_T#,2P_T#,0) ; "The punctuation ." ;
  uniend(w/2,(w/2,h/2),0,360,10) ;
endchar;

beginchar("/",x_w#,l_h#,0) ; "The slash (/)" ;
  unilne(p_t,(0,0),(w,h),1,1,10) ;
endchar;

beginchar(":",2P_T#,x_h#,0) ; "The punctuation :" ;
  uniend(w/2,(w/2,h-w/2),0,360,10) ;
  uniend(w/2,(w/2,w/2),0,360,20) ;
endchar;

beginchar(";",2P_T#,x_h#,2P_T#) ; "The punctuation ;" ;
  uniend(w/2,(w/2,h-w/2),0,360,10) ;
  uniend(w/2,(w/2,0),0,180,20) ;
  z2 = (0,0) ; z3 = (w,0) ; z4 = (0,-d) ; fill z2--z3--z4--cycle ;
  penlabels(1,2,3,4) ;
endchar;

beginchar("=",x_w#,x_h#,0) ; "The equal sign (=)" ;
  pickup unipensqu ;
  top lft z1 = (0,(h-p_t)/2) ; top rt z2 = (w,(h-p_t)/2) ;
  bot lft z3 = (0,(h+p_t)/2) ; bot rt z4 = (w,(h+p_t)/2) ;
  draw z1--z2 ; draw z3--z4 ;
  penlabels(1,2,3,4) ;
endchar;

beginchar("@",l_h#,l_h#,0) ; "The at sign (@)" ;
  uniarc(w/2,p_t,(w/2,h/2),0,240,10) ;
  unicir(w/3,p_t,(2w/3,w/3),20) ;
  pickup unipensqu ;
  top rt z1 = (w,w/2) ; bot rt z2 = (w,0) ; draw z1--z2 ;
  penlabels(1,2) ;
endchar;

beginchar("[",3p_t#,l_h#,p_d#) ; "The left bracket ([)" ;
  uniend(p_t,(p_t,h-p_t),90,180,10) ;
  uniend(p_t,(p_t,-d+p_t),180,270,20) ;
  pickup unipensqu ;
  top lft z1 = (p_t,h)   ; top rt  z2 = (w,h)      ; draw z1--z2 ;
  top lft z3 = (0,h-p_t) ; bot lft z4 = (0,-d+p_t) ; draw z3--z4 ;
  bot lft z5 = (p_t,-d)  ; bot rt  z6 = (w,-d)     ; draw z5--z6 ;
  penlabels(range 1 thru 6) ;
endchar;

beginchar("]",3p_t#,l_h#,p_d#) ; "The right braket (])" ;
  uniend(p_t,(w-p_t,h-p_t),0,90,10) ;
  uniend(p_t,(w-p_t,-d+p_t),270,360,20) ;
  pickup unipensqu ;
  top lft z1 = (0,h)     ; top rt  z2 = (w-p_t,h)  ; draw z1--z2 ;
  top rt  z3 = (w,h-p_t) ; bot rt  z4 = (w,-d+p_t) ; draw z3--z4 ;
  bot lft z5 = (0,-d)    ; bot rt  z6 = (w-p_t,-d) ; draw z5--z6 ;
  penlabels(range 1 thru 6) ;
endchar;

beginchar("`",2P_T#,l_h#,0) ; "The reversed apostroph (`)" ;
  uniend(w/2,(w/2,h-w),180,360,10) ;
  z1 = (0,h) ; z2 = (0,h-w) ; z3 = (w,h-w) ; fill z1--z2--z3--cycle ;
  penlabels(1,2,3) ;
endchar;

beginchar(hex"7B",m_w#,x_h#,0) ; "The en-dash (--)" ;
  pickup unipensqu ;
  lft z1 = (0,h/2) ; rt z2 = (w,h/2) ; draw z1--z2 ;
  penlabels(1,2) ;
endchar;

beginchar(hex"7C",M_w#,x_h#,0) ; "The em-dash (---)" ;
  pickup unipensqu ;
  lft z1 = (0,h/2) ; rt z2 = (w,h/2) ; draw z1--z2 ;
  penlabels(1,2) ;
endchar;

\endinput
%%
%% End of file `unipun.mf'.
