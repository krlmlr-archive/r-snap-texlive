%%
%% This is file `unispe.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% uni.dtx  (with options: `spe')
%% 
%% Copyright (C) 1998 Christian Holm.
%% 
%% The universal font and package is distributed in the hope that
%% it will be useful, but WITHOUT ANY WARRANTY; without even the
%% implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
%% PURPOSE.  See the GNU General Public License for more details.
%% 
%% See the file uni.dtx for further comments and documentation.
%% 


beginchar(hex"10",p_t#,x_h#,0) ; "The dotless letter i" ;
  pickup unipensqu ;
  bot lft z1 = (0,0) ; top lft z2 = (0,h) ; draw z1--z2;
  penlabels(1,2);
endchar;

beginchar(hex"11",p_t#,x_h#,p_d#) ; "The dotless letter j" ;
  pickup unipensqu ;
  bot lft z1 = (0,-d) ;  top lft z2 = (0,h) ; draw z1--z2 ;
  penlabels(1,2);
endchar;

beginchar("$",(l_h#-c_d#+p_t#)/2,l_h#,c_d#) ; "The dollar sign ($)" ;
  uniarc(w/2,p_t,(w/2,w/2),180,450,10) ;
  uniarc(w/2,p_t,(w/2,h-c_d-w/2),0,270,20) ;
  pickup unipensqu ;
  top z1 = (w/2,h) ; bot z2 = (w/2,-d) ; draw z1--z2 ;
  penlabels(1,2) ;
endchar;

beginchar("&",(l_h#+p_t#)/2+c_d#,l_h#,c_d#) ; "The ampersand (&)" ;
  rad := (w-c_d)/2 ;
  unicir(rad,p_t,(rad,h-rad),10) ;
  uniarc(rad,p_t,(rad,rad),0,270,20) ;
  pickup unipensqu ;
  lft bot z1 = (rad,0)     ; rt bot z2 = (w,0)      ; draw z1--z2 ;
  rt  top z3 = (w-c_d,rad) ; rt bot z4 = (w-c_d,-d) ; draw z3--z4 ;
  penlabels(1,2,3,4) ;
endchar;

beginchar("?",l_h#-5c_d#/2,l_h#,0) ; "The question mark (?)" ;
  temp := angle((P_T,-w/2)) - 0.01 ;
  uniarc(w/2,P_T,(w/2,h-w/2),temp,180,10) ;
  uniend(P_T,((w+P_T)/2,h-w),90,180,20) ;
  uniend(P_T,((w+P_T)/2,h-w+P_T),270,360+temp,30) ;
  pickup UniPensqu ;
  top z1 = (w/2,h-w) ; bot z2 = (w/2,2P_T) ; draw z1--z2 ;
  pickup UniPencir ;
  bot z3 = (w/2,0) ; drawdot z3 ;
  penlabels(1,2,3) ;
endchar;

beginchar(spanish_query,l_h#-5c_d#/2,x_h#,p_d#) ; "The Spanish query" ;
  temp := angle((-P_T,w/2))-0.01 ;
  uniarc(w/2,P_T,(w/2,-d+w/2),temp,360,10) ;
  uniend(P_T,((w-P_T)/2,-d+w),270,360,20) ;
  uniend(P_T,((w-P_T)/2,-d+w-P_T),90,temp,30) ;
  pickup UniPensqu ;
  bot z1 = (w/2,-d+w) ; top z2 = (w/2,h-2P_T) ; draw z1--z2 ;
  pickup UniPencir ;
  top z3 = (w/2,h) ; drawdot z3 ;
  penlabels(1,2,3) ;
endchar;

beginchar(spanish_shriek,P_T#,x_h#,p_d#) ; "The Spanish shriek" ;
  pickup UniPencir ; top z1 = (w/2,h) ; drawdot z1 ;
  pickup UniPensqu ; top z2 = (w/2,h-2P_T) ; bot z3 = (w/2,-d) ;
  draw z2--z3 ; penlabels(1,2,3) ;
endchar;

\endinput
%%
%% End of file `unispe.mf'.
