%%
%% This is file `unilow.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% uni.dtx  (with options: `low')
%% 
%% Copyright (C) 1998 Christian Holm.
%% 
%% The universal font and package is distributed in the hope that
%% it will be useful, but WITHOUT ANY WARRANTY; without even the
%% implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
%% PURPOSE.  See the GNU General Public License for more details.
%% 
%% See the file uni.dtx for further comments and documentation.
%% 


if not caps :

  beginchar("a",x_w#,x_h#,0); "The letter a";
    uniarc(w/2,p_t,(w/2,h/2),0,290,10);
    pickup unipensqu ;
    z1 = ((cosd(290)*(w-p_t)+w)/2,(sind(290)*(w-p_t)+w)/2);
    z2 = (w-p_t/2,2h/5) ; penpos1(p_t,-70); penpos2(p_t,-45);
    fill z1l{dir(20)}..z2l{dir(45)}--
      z2r{dir(225)}..z1r{dir(200)}--cycle;
    top rt z3 = (w,.5h) ; bot rt z4 = (w,0) ; draw z3{down}--z4 ;
    penlabels(1,2,3,4);
    a_pic := currentpicture ;
  endchar;

  beginchar("b",x_w#,l_h#,0); "The letter b";
    unicir(w/2,p_t,(w/2,w/2),10);
    pickup unipensqu ;
    lft bot z1 = (0,w/2) ; lft top z2 = (0,h) ; draw z1{up}--z2 ;
    penlabels(1,2);
  endchar;

  beginchar("c",x_w#*(1+cosd(30))/2,x_h#,0); "The letter c";
    uniarc(h/2,p_t,(h/2,h/2),30,330,10);
  endchar;

  beginchar("d",x_w#,l_h#,0); "The letter d";
    unicir(w/2,p_t,(w/2,w/2),10);
    pickup unipensqu ;
    rt bot z1 = (w,w/2) ; rt top z2 = (w,h) ; draw z1{up}--z2 ;
    penlabels(1,2);
  endchar;

  beginchar("e",x_w#,x_h#,0); "The letter e";
    uniarc(w/2,p_t,(w/2,h/2),0,330,10);
    temp := p_t/2 ;
    pickup pensquare xscaled p_t yscaled temp slanted slantratio ;
    top rt z1 = (w,h/2) ; drawdot z1 ;
    pickup unipensqu ;
    z2 = (p_t,h/2) ; z3 = (w-p_t,h/2) ; draw z2--z3 ;
    penlabels(1,2,3);
    e_pic := currentpicture ;
  endchar;

  beginchar("f",4p_t#,l_h#,0); "The letter f";
    uniarc(3/4w,p_t,(3/4w,h-3/4w),90,180,10);
    pickup unipensqu ;
    top rt z1 = (w,h) ; drawdot z1 ;
    lft top z2 = (0,h-3/4w) ; bot lft z3 = (0,0)   ; draw z2--z3 ;
    top lft z4 = (0,x_h)    ; top rt  z5 = (w,x_h) ; draw z4--z5 ;
    penlabels(1,2,3,4,5);
  endchar;

  beginchar("g",x_w#,x_h#,p_d#); "The letter g" ;
    uniarc(w/2,p_t,(w/2,h/2),30,360,10) ;
    pickup unipensqu ;
    top rt z1 = (w,h/2) ; bot rt z2 = (w,-d) ; draw z1--z2;
    penlabels(1,2);
  endchar;

  beginchar("h",x_w#,l_h#,0); "The letter h" ;
    uniarc(w/2,p_t,(w/2,w/2),0,180,10) ;
    pickup unipensqu ;
    top lft z1 = (0,h)       ; bot lft z2 = (0,0) ; draw z1--z2 ;
    top rt  z3 = (w,x_h-w/2) ; bot rt  z4 = (w,0) ; draw z3--z4 ;
    penlabels(1,2,3,4) ;
  endchar;

  beginchar("i",p_t#,l_h#,0) ; "The letter i" ;
    pickup unipensqu ;
    top lft z1 = (0,x_h) ; bot lft z2 = (0,0) ; draw z1--z2 ;
    pickup unipencir ;
    top lft z3 = (0,h) ; drawdot z3 ;
    penlabels(1,2,3) ;
  endchar;

  beginchar("j",p_t#,l_h#,p_d#); "The letter j" ;
    pickup unipensqu ;
    top lft z1 = (0,x_h) ; bot lft z2 = (0,-d) ; draw z1--z2 ;
    pickup unipencir ;
    top lft z3 = (0,h) ; drawdot z3 ;
    penlabels(1,2,3) ;
  endchar;

  beginchar("k",(x_h#+3p_t#)/2,x_h#,0) ; "The letter k" ;
    uniarc((h+p_t)/2,p_t,(p_t,h),270,360,10) ;
    uniarc((h+p_t)/2,p_t,(p_t,0),0,90,20) ;
    pickup unipensqu ;
    top lft z1 = (0,h) ; bot lft z2 = (0,0) ; draw z1--z2 ;
    penlabels(1,2);
  endchar;

  beginchar("l",p_t#,l_h#,0) ; "The letter l" ;
    pickup unipensqu ;
    top lft z1 = (0,h) ; bot lft z2 = (0,0) ; draw z1--z2;
    penlabels(1,2);
  endchar;

  beginchar("m",m_w#,x_h#,0) ; "The letter m" ;
    temp := .25*(w+p_t);
    uniarc(temp,p_t,(temp,h-temp),0,180,10) ;
    uniarc(temp,p_t,(w-temp,h-temp),0,180,20) ;
    pickup unipensqu ;
    top lft z1 = (0,h-temp)   ; bot lft z2 = (0,0)   ; draw z1--z2 ;
    top     z3 = (w/2,h-temp) ; bot     z4 = (w/2,0) ; draw z3--z4 ;
    top rt z5 = (w,h-temp)    ; bot rt  z6 = (w,0)   ; draw z5--z6 ;
    penlabels(1,2,3,4,5,6);
  endchar;

  beginchar("n",x_w#,x_h#,0); "The letter n" ;
    uniarc(w/2,p_t,(w/2,h-w/2),0,180,10) ;
    pickup unipensqu ;
    top lft z1 = (0,h-w/2) ; bot lft z2 = (0,0) ; draw z1--z2 ;
    top rt  z3 = (w,h-w/2) ; bot rt  z4 = (w,0) ; draw z3--z4 ;
    penlabels(1,2,3,4) ;
  endchar;

  beginchar("o",x_w#,x_h#,0) ; "The letter o";
    unicir(w/2,p_t,(w/2,h/2),10);
  endchar;

  beginchar("p",x_w#,x_h#,p_d#) ; "The letter p" ;
    unicir(w/2,p_t,(w/2,w/2),10);
    pickup unipensqu ;
    lft top z1 = (0,h/2) ; lft bot z2 = (0,-d) ; draw z1--z2 ;
    penlabels(1,2);
  endchar;

  beginchar("q",x_w#,x_h#,p_d#) ; "The letter q" ;
    unicir(w/2,p_t,(w/2,w/2),10);
    pickup unipensqu ;
    rt top z1 = (w,h/2) ; rt bot z2 = (w,-d) ; draw z1--z2 ;
    penlabels(1,2);
  endchar;

  beginchar("r",3p_t#,x_h#,0) ; "The letter r" ;
    uniarc(w,p_t,(w,h-w),90,180,10) ;
    pickup unipensqu ;
    top lft z1 = (0,h-w) ; bot lft z2 = (0,0) ; draw z1--z2;
    penlabels(1,2);
  endchar;

  beginchar("s",x_w#,x_h#,0) ; "The letter s" ;
    temp := (h+p_t)/4 ;
    uniarc(temp,p_t,(temp,temp),180,270,10) ;
    uniarc(temp,p_t,(w-temp,temp),270,450,20) ;
    uniarc(temp,p_t,(temp,h-temp),90,270,30) ;
    uniarc(temp,p_t,(w-temp,h-temp),0,90,40) ;
    pickup unipensqu ;
    bot lft z1 = (temp,0)   ; bot rt z2 = (w-temp,0)   ; draw z1--z2 ;
    lft     z3 = (temp,h/2) ; rt     z4 = (w-temp,h/2) ; draw z3--z4 ;
    top lft z5 = (temp,h)   ; top rt z6 = (w-temp,h)   ; draw z5--z6 ;
    penlabels(1,2,3,4,5,6) ;
  endchar;

  beginchar("t",p_t#+2c_d#,l_h#,0); "The letter t" ;
    pickup unipensqu ;
    top     z1 =  (w/2,h) ; bot    z2 = (w/2,0) ; draw z1--z2 ;
    top lft z3 = (0,x_h)  ; top rt z4 = (w,x_h) ; draw z3--z4 ;
    penlabels(1,2,3,4);
  endchar;

  beginchar("u",x_w#,x_h#,0); "The letter u" ;
    uniarc(w/2,p_t,(w/2,w/2),180,360,10) ;
    pickup unipensqu ;
    top lft z1 = (0,h) ; bot lft z2 = (0,w/2) ; draw z1--z2 ;
    top rt  z3 = (w,h) ; bot rt  z4 = (w,w/2) ; draw z3--z4 ;
    penlabels(1,2,3,4) ;
  endchar;

  beginchar("v",x_w#,x_h#,0); "The letter v" ;
    unilne(p_t,(0,h),(w/2,p_t),1,1,10);
    unilne(p_t,(w/2,p_t),(w,h),1,1,20);
    uniarc(p_t,p_t,(w/2,p_t),180,360,30);
  endchar;

  beginchar("w",m_w#,x_h#,0) ; "The letter w" ;
    temp := .25*(w+p_t);
    uniarc(temp,p_t,(temp,temp),180,360,10) ;
    uniarc(temp,p_t,(w-temp,temp),180,360,20) ;
    pickup unipensqu ;
    top lft z1 = (0,h)   ; bot lft z2 = (0,temp)   ; draw z1--z2 ;
    top     z3 = (w/2,h) ; bot     z4 = (w/2,temp) ; draw z3--z4 ;
    top rt  z5 = (w,h)   ; bot rt  z6 = (w,temp)   ; draw z5--z6 ;
    penlabels(1,2,3,4,5,6);
  endchar;

  beginchar("x",x_w#,x_h#,0) ; "The letter x" ;
    uniarc(h/2,p_t,(p_t/2,h/2),270,90,10);
    uniarc(h/2,p_t,(w-p_t/2,h/2),90,270,20);
    temp := (w-h+p_t)/2 ;
    pickup pensquare xscaled temp yscaled p_t slanted slantratio ;
    lft top z1 = (0,h) ; drawdot z1 ;
    lft bot z2 = (0,0) ; drawdot z2 ;
    rt  top z3 = (w,h) ; drawdot z3 ;
    rt  bot z4 = (w,0) ; drawdot z4 ;
    penlabels(1,2,3,4);
  endchar;

  beginchar("y",x_w#,x_h#,p_d#); "The letter y" ;
    uniarc(w/2,p_t,(w/2,w/2),180,360,10) ;
    pickup unipensqu ;
    top lft z1 = (0,h) ; bot lft z2 = (0,w/2); draw z1--z2 ;
    top  rt z3 = (w,h) ; bot rt  z4 = (w,-d) ; draw z3--z4 ;
    penlabels(1,2,3,4) ;
  endchar;

  beginchar("z",x_w#,x_h#,0) ; "The letter z" ;
    pickup unipensqu ;
    top lft z1 = (0,h)   ; top rt z2 = (w-p_t,h) ; draw z1--z2 ;
    bot lft z3 = (p_t,0) ; bot rt z4 = (w,0)     ; draw z3--z4 ;
    phi := angle((w,h-p_t)-(0,p_t)) ; % Uuuuupps !?!!!!!!
    z5 = (p_t,p_t)     ; z6 = z5 + p_t*(-sind(phi),cosd(phi)) ;
    z7 = (w-p_t,h-p_t) ; z8 = z7 + p_t*(sind(phi),-cosd(phi)) ;
    uniend(p_t,z5,90+phi,270,10);
    uniend(p_t,z7,-90+phi,90,20);
    fill z5--z6--z7--z8--cycle ;
    penlabels(range 1 thru 8);
  endchar;

else :
  do_caps(ASCII"a" - ASCII"A") ;
  input uniupp ;
  end_do_caps ;
fi ;

\endinput
%%
%% End of file `unilow.mf'.
