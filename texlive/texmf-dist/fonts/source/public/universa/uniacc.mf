%%
%% This is file `uniacc.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% uni.dtx  (with options: `acc')
%% 
%% Copyright (C) 1998 Christian Holm.
%% 
%% The universal font and package is distributed in the hope that
%% it will be useful, but WITHOUT ANY WARRANTY; without even the
%% implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
%% PURPOSE.  See the GNU General Public License for more details.
%% 
%% See the file uni.dtx for further comments and documentation.
%% 


beginchar(hex"12",x_w#,l_h#,0) ; "The accent `" ;
  uniend(P_T/2,(w/2,a_h+P_T/2),90,270,10) ;
  z1 = (w/2,a_h+P_T) ; z2 = (w/2,a_h) ; z3 = (w/2+P_T,a_h) ;
  fill z1--z2--z3--cycle ; penlabels(1,2,3) ;
endchar ;

beginchar(hex"13",x_w#,l_h#,0) ; "The accent '" ;
  uniend(P_T/2,(w/2,a_h+P_T/2),-90,90,10) ;
  z1 = (w/2,a_h+P_T) ; z2 = (w/2,a_h) ; z3 = (w/2-P_T,a_h) ;
  fill z1--z2--z3--cycle ; penlabels(1,2,3) ;
endchar ;

beginchar(hex"14",x_w#,l_h#,0) ; "The accent hachek" ;
  z1 = (w/2,a_h+p_t) ; uniend(p_t,z1,225,315,10) ;
  z2 = z1 + p_t*(-1,-1)/sqrt(2) ; z3 = z1 + p_t*(-1,1) ;
  z4 = z3 + (-p_t*sqrt(2),0) ; path rombe ;
  rombe := z1--z2--z4--z3--cycle ; fill rombe ;
  fill rombe reflectedabout ((w/2,0),(w/2,h)) ;
  penlabels(1,2,3,4) ;
endchar ;

beginchar(hex"15",x_w#,l_h#,0) ; "The accent breve" ;
  rad := 3p_t/2 ; uniarc(rad,p_t,(w/2,a_h+rad),180,360,10) ;
endchar ;

beginchar(hex"16",x_w#,a_h#+p_t#,0) ; "The accent macron (bar)" ;
  pickup unipensqu ;
  bot lft z1 = (w/2-2p_t,a_h) ; bot rt z2 = (w/2+2p_t,a_h) ;
  draw z1--z2 ; penlabels(1,2) ;
endchar ;

beginchar(hex"17",A_w#+4/3*(l_h#-x_h#)*slantratio,l_h#,0) ;
  "The accent circle" ;
  rad := P_T ; unicir(rad,p_t,(w/2,h-rad),10) ;
endchar ;

beginchar(hex"18",x_w#,x_h#,2c_d#) ; "The accent cidelia" ;
  uniarc(d/2,2P_T/3,(w/2+d/4,-d/2),90,270,10) ;
endchar ;

phi := 15 ; tmp# := 3c_d# ; define_pixels(tmp) ;
beginchar(hex"20",tmp#*cosd(phi)+p_t#*sind(phi),l_h#,0) ;
  "The accent bar over l" ;
  z1 = (0,(h+p_t*cosd(phi)-tmp*sind(phi))/2) ;
  z2 = (p_t*sind(phi),(h-p_t*cosd(phi)-tmp*sind(phi))/2) ;
  z3 = (w,(h-p_t*cosd(phi)+tmp*sind(phi))/2) ;
  z4 = (w-p_t*sind(phi),(h+p_t*cosd(phi)+tmp*sind(phi))/2) ;
  fill z1--z2--z3--z4--cycle ;
  penlabels(1,2,3,4) ;
endchar ;

beginchar(hex"5E",x_w#,l_h#,0) ; "The accent hat" ;
  z1 = (w/2,a_h+p_t) ; uniend(p_t,z1,45,136,10) ;
  z2 = z1 + p_t*(-1,1)/sqrt(2) ; z3 = z1 + p_t*(-1,-1) ;
  z4 = z3 + (-p_t*sqrt(2),0) ; path rombe ;
  rombe := z1--z2--z4--z3--cycle ; fill rombe ;
  fill rombe reflectedabout ((w/2,0),(w/2,h)) ;
  penlabels(1,2,3,4) ;
endchar ;

beginchar(hex"5F",x_w#,l_h#,0) ; "The accent dot" ;
  pickup UniPencir ; bot z1 = (w/2,a_h) ; drawdot z1 ; penlabels(1) ;
endchar ;

beginchar(hex"7D",x_w#,l_h#,0) ; "The accent ''" ;
  rad := P_T/2 ;
  uniend(rad,(w/2-rad,a_h+rad),-90,90,10) ;
  uniend(rad,(w/2+P_T,a_h+rad),-90,90,20) ;
  z1 = (w/2-rad-P_T,a_h) ; z2 = (w/2-rad,a_h) ;
  z3 = (w/2-rad,a_h+P_T) ; fill z1--z2--z3--cycle ;
  z4 = (w/2,a_h)         ; z5 = (w/2+P_T,a_h) ;
  z6 = (w/2+P_T,a_h+P_T) ; fill z4--z5--z6--cycle ;
  penlabels(range 1 thru 6) ;
endchar ;

beginchar(hex"7E",x_w#,l_h#,0) ; "The accent ~" ;
  rad := (w*sqrt(2)+p_t)/4 ; temp := (2w-p_t*sqrt(2))/8 ;
  uniarc(rad,p_t,(w/2+temp,a_h+rad),225,315,10) ;
  uniarc(rad,p_t,(w/2-temp,a_h+rad-2temp),45,135,20) ;
endchar ;

beginchar(hex"7F",x_w#,l_h#,0) ; "The accent umlout (double dot)" ;
  pickup UniPencir ;
  bot z1 = (w/2-P_T,a_h) ; bot z2 = (w/2+P_T,a_h) ;
  drawdot z1 ; drawdot z2 ; penlabels(1,2) ;
endchar ;

\endinput
%%
%% End of file `uniacc.mf'.
