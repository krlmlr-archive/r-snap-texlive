%%
%% This is file `uniupp.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% uni.dtx  (with options: `upp')
%% 
%% Copyright (C) 1998 Christian Holm.
%% 
%% The universal font and package is distributed in the hope that
%% it will be useful, but WITHOUT ANY WARRANTY; without even the
%% implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
%% PURPOSE.  See the GNU General Public License for more details.
%% 
%% See the file uni.dtx for further comments and documentation.
%% 


if not strict :

  beginchar("A",A_w#,l_h#,0); "The letter A";
    uniarc(w/2,P_t,(w/2,h-w/2),0,180,10);
    pickup Unipensqu ;
    top lft z1 = (0,h-w/2) ; bot lft z2 = (0,0)   ; draw z1--z2 ;
    top rt  z3 = (w,h-w/2) ; bot rt  z4 = (w,0)   ; draw z3--z4 ;
    lft z5     = (0,h/2)   ; rt      z6 = (w,h/2) ; draw z5--z6 ;
    penlabels(1,2,3,4,5,6);
  endchar;

  beginchar("B",A_w#,l_h#,0) ; "The letter B" ;
    rad := 1/4(h+P_t) ;
    uniarc(rad,P_t,(w-rad,h-rad),-90,90,10) ;
    uniarc(rad,P_t,(w-rad,rad),-90,90,20) ;
    uniend(P_t,(P_t,h-P_t),90,180,30) ;
    uniend(P_t,(P_t,P_t),180,270,40) ;
    pickup Unipensqu ;
    top lft z1 = (0,h-P_t) ; bot lft z2 = (0,P_t)      ; draw z1--z2 ;
    top lft z3 = (P_t,h)   ; top rt  z4 = (w-rad,h)    ; draw z3--z4 ;
    lft     z5 = (P_t,h/2) ; rt      z6 = (w-rad,1/2h) ; draw z5--z6 ;
    bot lft z7 = (P_t,0)   ; bot rt  z8 = (w-rad,0)    ; draw z7--z8 ;
    penlabels(range 1 thru 8);
  endchar;

  beginchar("C",l_h#*(1+cosd(30))/2,l_h#,0); "The letter C";
    uniarc(h/2,P_t,(h/2,h/2),30,330,10);
  endchar;

  beginchar("D",A_w#,l_h#,0); "The letter D";
    uniarc(1/2h,P_t,(w-1/2h,1/2h),-90,90,10);
    uniend(P_t,(P_t,h-P_t),90,180,20);
    uniend(P_t,(P_t,P_t),180,270,30);
    pickup Unipensqu ;
    top lft z1 = (0,h-P_t) ; bot lft z2 = (0,P_t)    ; draw z1--z2 ;
    top lft z3 = (P_t,h)   ; top rt  z4 = (w-1/2h,h) ; draw z3--z4 ;
    bot lft z5 = (P_t,0)   ; bot rt  z6 = (w-1/2h,0) ; draw z5--z6 ;
    penlabels(1,2) ;
  endchar;

  beginchar("E",A_w#,l_h#,0); "The letter E" ;
    uniend(P_t,(P_t,h-P_t),90,180,10);
    uniend(P_t,(P_t,P_t),180,270,20);
    pickup Unipensqu ;
    lft top z1 = (P_t,h)   ; rt  top z2 = (w,h)   ; draw z1--z2 ;
    lft     z3 = (P_t,h/2) ; rt      z4 = (w,h/2) ; draw z3--z4 ;
    lft bot z5 = (P_t,0)   ; rt  bot z6 = (w,0)   ; draw z5--z6 ;
    lft top z7 = (0,h-P_t) ; lft bot z8 = (0,P_t) ; draw z7--z8 ;
    penlabels(range 1 thru 8) ;
  endchar;

  beginchar("F",A_w#,l_h#,0); "The letter F" ;
    uniend(P_t,(P_t,h-P_t),90,180,10);
    pickup Unipensqu ;
    lft top z1 = (P_t,h)   ; rt  top z2 = (w,h)   ; draw z1--z2 ;
    lft     z3 = (P_t,h/2) ; rt      z4 = (w,h/2) ; draw z3--z4 ;
    lft top z5 = (0,h-P_t) ; lft bot z6 = (0,0)   ; draw z5--z6 ;
    penlabels(range 1 thru 8);
  endchar;

  beginchar("G",l_h#,l_h#,0); "The letter G" ;
    uniarc(w/2,P_t,(w/2,h/2),30,360,10) ;
    pickup Unipensqu ;
    top rt z1 = (w,h/2) ; bot rt z2 = (w,0) ;
    draw z1--z2;
    penlabels(1,2);
  endchar;

  beginchar("H",A_w#,l_h#,0); "The letter H" ;
    pickup Unipensqu ;
    top lft z1 = (0,h) ; bot lft z2 = (0,0) ; draw z1--z2 ;
    top rt  z3 = (w,h) ; bot rt  z4 = (w,0) ; draw z3--z4 ;
    lft z5 = (0,h/2) ; rt z6 = (w,h/2) ; draw z5--z6 ;
    penlabels(1,2,3,4,5,6);
  endchar;

  beginchar("I",P_t#,l_h#,0); "The letter I" ;
    pickup Unipensqu ;
    top lft z1 = (0,h) ; bot lft z2 = (0,0) ; draw z1--z2 ;
    penlabels(1,2);
  endchar;

  beginchar("J",P_t#+2C_d#,l_h#,0); "The letter J";
    uniend(P_t,(w-P_t,P_t),270,360,10);
    pickup Unipensqu ;
    lft bot z1 = (0,0)   ; rt bot z2 = (w-P_t,0) ; draw z1--z2 ;
    rt  bot z3 = (w,P_t) ; rt top z4 = (w,h)     ; draw z3--z4 ;
    penlabels(1,2,3,4);
  endchar;

  beginchar("K",(l_h#+3P_t#)/2,l_h#,0) ; "The letter K" ;
    uniarc((h+P_t)/2,P_t,(P_t,h),270,360,10) ;
    uniarc((h+P_t)/2,P_t,(P_t,0),0,90,20) ;
    pickup Unipensqu ;
    top lft z1 = (0,h) ; bot lft z2 = (0,0) ; draw z1--z2 ;
    penlabels(1,2);
  endchar;

  beginchar("L",P_t#+2C_d#,l_h#,0) ; "The letter L" ;
    uniend(P_t,(P_t,P_t),180,270,10) ;
    pickup Unipensqu ;
    top lft z1 = (0,h)   ; bot lft z2 = (0,P_t) ; draw z1--z2 ;
    bot lft z3 = (P_t,0) ; bot rt  z4 = (w,0)   ; draw z3--z4 ;
    penlabels(1,2,3,4) ;
  endchar;

  beginchar("M",M_w#,l_h#,0) ; "The letter M" ;
    temp := .25*(w+P_t);
    uniarc(temp,P_t,(temp,h-temp),0,180,10) ;
    uniarc(temp,P_t,(w-temp,h-temp),0,180,20) ;
    pickup Unipensqu ;
    top lft z1 = (0,h-temp) ; bot lft z2 = (0,0) ;  draw z1--z2 ;
    top z3 = (w/2,h-temp)   ; bot z4 = (w/2,0)   ;  draw z3--z4 ;
    top rt z5 = (w,h-temp)  ; bot rt z6 = (w,0)  ;  draw z5--z6 ;
    penlabels(1,2,3,4,5,6);
  endchar;

  beginchar("N",A_w#,l_h#,0); "The letter N" ;
    uniarc(w/2,P_t,(w/2,h-w/2),0,180,10) ;
    pickup Unipensqu ;
    top lft z1 = (0,h-w/2) ; bot lft z2 = (0,0);  draw z1--z2 ;
    top rt z3 = (w,h-w/2)  ; bot rt z4 = (w,0) ;  draw z3--z4 ;
    penlabels(1,2,3,4) ;
  endchar;

  beginchar("O",l_h#,l_h#,0) ; "The letter O";
    unicir(w/2,P_t,(w/2,h/2),10);
  endchar;

  beginchar("P",A_w#,l_h#,0) ; "The letter P";
    unicir(w/2,P_t,(w/2,h-w/2),10)
    pickup Unipensqu ;
    lft top z1 = (0,h-w/2) ; lft bot z2 = (0,0) ; draw z1--z2 ;
    penlabels(1,2);
  endchar;

  beginchar("Q",l_h#,l_h#,p_d#) ; "The letter Q";
    unicir(w/2,P_t,(w/2,w/2),10);
    pickup Unipensqu ;
    top z1 = (w/2,3P_t) ; bot z2 = (w/2,-d) ; draw z1--z2 ;
    penlabels(1,2);
  endchar;

  beginchar("R",A_w#,l_h#,0) ; "The letter R";
    unicir(w/2,P_t,(w/2,h-w/2),10) ;
    rad := h-w+P_t ;
    uniarc(rad,P_t,(w-rad,0),0,90,20) ;
    pickup Unipensqu ;
    lft top z1 = (0,h-w/2)  ; bot lft z2 = (0,0) ; draw z1--z2 ;
    temp := w/2-rad ;
    if temp>0 :
      pickup pensquare xscaled temp yscaled P_t ;
      lft top z3 = (1/2w,rad) ; drawdot z3 ;
    fi;
    penlabels(1,2,3) ;
  endchar;

  beginchar("S",A_w#,l_h#,0) ; "The letter S" ;
    temp := (h+P_t)/4 ;
    uniarc(temp,P_t,(temp,temp),180,270,10) ;
    uniarc(temp,P_t,(w-temp,temp),270,450,20) ;
    uniarc(temp,P_t,(temp,h-temp),90,270,30) ;
    uniarc(temp,P_t,(w-temp,h-temp),0,90,40) ;
    pickup Unipensqu ;
    bot lft z1 = (temp,0)   ; bot rt z2 = (w-temp,0)   ; draw z1--z2 ;
    lft     z3 = (temp,h/2) ; rt     z4 = (w-temp,h/2) ; draw z3--z4 ;
    top lft z5 = (temp,h)   ; top rt z6 = (w-temp,h)   ; draw z5--z6 ;
    penlabels(1,2,3,4,5,6) ;
  endchar;

  beginchar("T",A_w#,l_h#,0); "The letter T" ;
    pickup Unipensqu ;
    top     z1 = (w/2,h) ; bot    z2 = (w/2,0) ; draw z1--z2 ;
    top lft z3 = (0,h)   ; top rt z4 = (w,h)   ; draw z3--z4 ;
    penlabels(1,2,3,4);
  endchar;

  beginchar("U",A_w#,l_h#,0); "The letter U" ;
    uniarc(w/2,P_t,(w/2,w/2),180,360,10) ;
    pickup Unipensqu ;
    top lft z1 = (0,h) ; bot lft z2 = (0,w/2) ; draw z1--z2 ;
    top rt  z3 = (w,h) ; bot  rt z4 = (w,w/2) ; draw z3--z4 ;
    penlabels(1,2,3,4) ;
  endchar;

  beginchar("V",A_w#,l_h#,0); "The letter V" ;
    unilne(P_t,(0,h),(w/2,P_t),1,1,10);
    unilne(P_t,(w/2,P_t),(w,h),1,1,20);
    uniarc(P_t,P_t,(w/2,P_t),180,360,30);
  endchar;

  beginchar("W",M_w#,l_h#,0) ; "The letter W" ;
    temp := .25*(w+P_t);
    uniarc(temp,P_t,(temp,temp),180,360,10) ;
    uniarc(temp,P_t,(w-temp,temp),180,360,20) ;
    pickup Unipensqu ;
    top lft z1 = (0,h)   ; bot lft z2 = (0,temp)   ; draw z1--z2 ;
    top     z3 = (w/2,h) ; bot     z4 = (w/2,temp) ; draw z3--z4 ;
    top rt  z5 = (w,h)   ; bot rt  z6 = (w,temp)   ; draw z5--z6 ;
    penlabels(1,2,3,4,5,6);
  endchar;

  beginchar("X",l_h#,l_h#,0) ; "The letter X" ;
    uniarc(h/2,P_t,(P_t/2,h/2),270,90,10);
    uniarc(h/2,P_t,(w-P_t/2,h/2),90,270,20);
    temp := (h-w+P_t)/2 ;
    pickup pensquare xscaled temp yscaled P_t slanted slantratio ;
    lft top z1 = (0,h) ; draw z1 ;
    lft bot z2 = (0,0) ; draw z2 ;
    rt  top z3 = (w,h) ; draw z3 ;
    rt  bot z4 = (w,0) ; draw z4 ;
    penlabels(1,2,3,4);
  endchar;

  beginchar("Y",A_w#,l_h#,0); "The letter Y" ;
    uniarc(w/2,P_t,(w/2,h-P_t),180,360,10) ;
    pickup Unipensqu ;
    top lft z1 = (0,h) ; drawdot z1 ;
    top rt  z2 = (w,h) ; drawdot z2 ;
    top     z3 = (w/2,h-w/2) ; bot z4 = (w/2,0) ; draw z3--z4 ;
    penlabels(1,2,3,4) ;
  endchar;

  beginchar("Z",A_w#,l_h#,0) ; "The letter Z" ;
    pickup Unipensqu ;
    top lft z1 = (0,h)   ; top rt z2 = (w-P_t,h) ; draw z1--z2 ;
    bot lft z3 = (P_t,0) ; bot rt z4 = (w,0)     ; draw z3--z4 ;
    phi := angle((w,h-P_t)-(0,P_t)) ;
    z5 = (P_t,P_t)     ; z6 = z5 + P_t*(-sind(phi),cosd(phi)) ;
    z7 = (w-P_t,h-P_t) ; z8 = z7 + P_t*(sind(phi),-cosd(phi)) ;
    uniend(P_t,z5,90+phi,270,10);
    uniend(P_t,z7,-90+phi,90,20);
    fill z5--z6--z7--z8--cycle ;
    penlabels(range 1 thru 8);
  endchar;

else :
  code_offset := ASCII"A" - ASCII"a" ;
  input unilow ;
  code_offset := 0 ;
fi ;

\endinput
%%
%% End of file `uniupp.mf'.
