%%
%% This is file `sarab10.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% sarabian.dtx  (with options: `up')
%% ------------------------------------------------------------------
%%   Author: Peter Wilson (Herries Press) herries dot press at earthlink dot net
%%   Copyright 2005 Peter R. Wilson
%% 
%%   This work may be distributed and/or modified under the
%%   conditions of the Latex Project Public License, either
%%   version 1.3 of this license or (at your option) any
%%   later version.
%%   The latest version of the license is in
%%     http://www.latex-project.org/lppl.txt
%%   and version 1.3 or later is part of all distributions of
%%   LaTeX version 2003/06/01 or later.
%% 
%%   This work has the LPPL maintenance status "author-maintained".
%% 
%%   This work consists of the files listed in the README file.
%% ------------------------------------------------------------------
%%% SARAB10.MF  South Arabian font 10 point design size.

%%% This font was designed by Alan M Stanier      ( alan@essex.ac.uk ),
%%% at Essex University Computer Service, Colchester, Essex CO4 3EA. England.
%%%
%%% If you make significant additions or improvements to the font,
%%% please email me an updated version
%%%
%%% This font was used for several languages in Southern Arabia in the
%%% second millenium BC
%%%

mode_setup;
font_identifier:="sarabian";
font_size:= 10pt#;           % nominally, a 10pt font
font_normal_space:=7pt#;     % width of a blank space
font_normal_shrink:=.9pt#;   % width correction for blank space
font_x_height:=4.5pt#;       % height of one ex
font_quad:=10pt#;            % an em

numeric pmg; pmg:=0.8;            % a scale factor

xpensize#:=pmg*0.8pt;
ypensize#:=pmg*0.2pt;
height#:=pmg*10pt#;
width#:=pmg*7pt#;
narrow#:=pmg*5.3pt#;

mg#:= pmg*0.3pt#;
define_pixels(mg);

numeric cscale;                   % scale factor for original circles
cscale# := mg#;
define_pixels(cscale);
let cmchar=\;


define_pixels(height,width,narrow);
def getpen = pickup pencircle xscaled xpensize# yscaled ypensize# rotated 30 enddef;



cmchar "letter h";
beginchar("h",width#,height#,0);
 getpen;
  draw (0,mg*30){down} .. (mg*7.5,mg*15){right} .. (mg*15,mg*30){up};
  draw (mg*7.5,mg*15) -- (mg*7.5,0);
endchar;


cmchar "letter l";
beginchar("l",width#,height#,0);
 getpen;
 draw (0,mg*15) -- (mg*15,mg*30) -- (mg*15,0);
endchar;


cmchar "letter h sub dot (coded as H)";
beginchar("H",width#,height#,0);
 getpen;
 draw (0,mg*30){down} .. (mg*7.5,mg*15){right} .. (mg*15,mg*30){up};
 draw (mg*7.5,mg*30) -- (mg*7.5,0);
endchar;


cmchar "letter m";
beginchar("m",width#,height#,0);
 getpen;
 draw (mg*15,mg*30) -- (mg*15,0) -- (0,mg*7.5) -- (mg*15,mg*15) --
(0,mg*22.5) -- (mg*15,mg*30);
endchar;


cmchar "letter q";
beginchar("q",narrow#,height#,0);
 getpen;
 draw fullcircle scaled (10cscale) shifted(mg*5,mg*15);
 draw (mg*5,mg*20) -- (mg*5,mg*30);
 draw (mg*5,mg*10) -- (mg*5,0);
endchar;


cmchar "letter w";
beginchar("w",width#,height#,0);
 getpen;
 draw fullcircle scaled (15cscale) shifted(mg*7.5,mg*15);
 draw (mg*7.5,mg*7.5) -- (mg*7.5,mg*22.5);
endchar;


cmchar "letter s sup v (coded as S)";
beginchar("S",width#,height#,0);
 getpen;
 draw (0,0) -- (0,mg*15) -- (mg*15,mg*15) -- (mg*15,0);
 draw (mg*7.5,mg*15) -- (mg*7.5,mg*30);
endchar;


cmchar "letter r";
beginchar("r",width#,height#,0);
 getpen;
 draw (0,mg*30){right} .. (mg*15,mg*15){down} .. (0,0){left};
endchar;


cmchar "letter b";
beginchar("b",width#,height#,0);
 getpen;
 draw (0,0) -- (0,mg*30) -- (mg*15,mg*30) -- (mg*15,0);
endchar;


cmchar "letter t";
beginchar("t",width#,height#,0);
 getpen;
 draw (0,0) -- (mg*15,mg*30);
 draw (0,mg*30) -- (mg*15,0);
endchar;


cmchar "letter s";
beginchar("s",width#,height#,0);
 getpen;
 draw (0,0) -- (mg*15,mg*15) -- (0,mg*30);
 draw (mg*15,0) -- (0,mg*15) -- (mg*15,mg*30);
endchar;


cmchar "letter k";
beginchar("k",width#,height#,0);
 getpen;
 draw (0,0) -- (0,mg*15) -- (mg*15,mg*15) -- (mg*15,0);
 draw (0,mg*15) -- (mg*7.5,mg*30);
endchar;


cmchar "letter n";
beginchar("n",width#,height#,0);
 getpen;
 draw (0,mg*30) -- (0,mg*15) -- (mg*15,mg*15) -- (mg*15,0);
endchar;


cmchar "letter h sub u (coded as H)";
%%beginchar("1",width#,height#,0);
beginchar("I",width#,height#,0);
 getpen;
 draw (0,mg*30){down} .. (mg*7.5,mg*15){right} .. (mg*15,mg*30){up};
 draw (mg*7.5,mg*15) -- (mg*7.5,mg*7.5) -- (mg*15,mg*7.5) -- (mg*15,0);
endchar;


cmchar "letter s acute (coded as X)";
%%beginchar("2",width#,height#,0);
beginchar("X",width#,height#,0);
 getpen;
 draw (0,0) -- (mg*15,mg*7.5) -- (0,mg*15) --
(mg*15,mg*22.5) -- (0,mg*30);
endchar;


cmchar "letter f";
beginchar("f",width#,height#,0);
 getpen;
 draw (0,mg*15) -- (mg*7.5,0) -- (mg*15,mg*15) -- (mg*7.5,mg*30)
-- (0,mg*15);
endchar;


cmchar "letter `";
beginchar("`",width#,height#,0);
 getpen;
 draw (0,0) -- (0,mg*15) -- (mg*15,mg*15) -- (mg*15,0);
 draw (mg*7.5,mg*15) -- (mg*7.5,mg*22.5) -- (0,mg*22.5) -- (0,mg*30);
endchar;


cmchar "letter ` (coded as o)";
beginchar("o",width#,height#,0);
 getpen;
 draw (0,0) -- (0,mg*15) -- (mg*15,mg*15) -- (mg*15,0);
 draw (mg*7.5,mg*15) -- (mg*7.5,mg*22.5) -- (0,mg*22.5) -- (0,mg*30);
endchar;


cmchar "letter '";
beginchar("'",narrow#,height#,0);
 getpen;
 draw fullcircle scaled (10cscale) shifted(mg*5,mg*10);
endchar;


cmchar "letter ' (coded as a)";
beginchar("a",narrow#,height#,0);
 getpen;
 draw fullcircle scaled (10cscale) shifted(mg*5,mg*10);
endchar;


cmchar "letter d sub dot (coded as B)";
%%beginchar("D",width#,height#,0);
beginchar("B",width#,height#,0);
 getpen;
 draw (0,0) -- (mg*15,0) -- (mg*15,mg*30) -- (0,mg*30) -- (0,0);
 draw (0,mg*15) -- (mg*15,mg*15);
endchar;


cmchar "letter g";
beginchar("g",width#,height#,0);
 getpen;
 draw (0,mg*30) -- (mg*15,mg*30) -- (mg*15,0);
endchar;


cmchar "letter d";
beginchar("d",narrow#,height#,0);
 getpen;
 draw (0,mg*10) -- (0,mg*20) -- (mg*10,mg*15) -- (0,mg*10);
 draw (mg*10,0) -- (mg*10,mg*30);
endchar;


cmchar "letter g acute (coded as G)";
beginchar("G",width#,height#,0);
 getpen;
 draw (0,mg*25) -- (mg*5,mg*30) -- (mg*15,mg*30) -- (mg*15,0);
 draw (mg*5,0) -- (mg*5,mg*30);
endchar;


cmchar "letter t sub dot (coded as T)";
beginchar("T",width#,height#,0);
 getpen;
 draw (0,0) -- (mg*15,0) -- (mg*15,mg*30) -- (0,mg*30) -- (0,0);
 draw (mg*7.5,0) -- (mg*7.5,mg*30);
endchar;


cmchar "letter z";
beginchar("z",width#,height#,0);
 getpen;
 draw (0,0) -- (mg*15,mg*30) -- (0,mg*30) --
(mg*15,0) -- (0,0);
endchar;


cmchar "letter d sub bar (coded as D)";
%%beginchar("5",width#,height#,0);
beginchar("D",width#,height#,0);
 getpen;
 draw (0,0) -- (0,mg*30);
 draw (mg*15,0) -- (mg*15,mg*30);
 draw (0,mg*12.5) -- (mg*15,mg*12.5);
 draw (0,mg*17.5) -- (mg*15,mg*17.5);
endchar;


cmchar "letter y";
beginchar("y",narrow#,height#,0);
 getpen;
 draw fullcircle scaled (10cscale) shifted(mg*5,mg*25);
 draw (mg*5,mg*20) -- (mg*5,0);
endchar;


cmchar "letter t sub bar (coded as J)";
%%beginchar("4",narrow#,height#,0);
beginchar("J",narrow#,height#,0);
 getpen;
 draw fullcircle scaled (10cscale) shifted(mg*5,mg*25);
 draw (mg*5,mg*20) -- (mg*5,mg*10);
 draw fullcircle scaled (10cscale) shifted(mg*5,mg*5);
endchar;


cmchar "letter s sub dot (coded as x)";
%%beginchar("3",narrow#,height#,0);
beginchar("x",narrow#,height#,0);
 getpen;
 draw fullcircle scaled (10cscale) shifted(mg*5,mg*25);
 draw (mg*5,mg*20) -- (mg*5,mg*10);
 draw (0,0){up} .. (mg*5,mg*10){right} .. (mg*10,0){down};
endchar;


cmchar "letter z sub dot (coded as Z)";
beginchar("Z",narrow#,height#,0);
 getpen;
 draw fullcircle scaled (10cscale) shifted(mg*5,mg*25);
 draw (mg*5,mg*20) -- (mg*5,0);
 draw (mg*5,mg*10){right} .. (mg*10,0){down};
endchar;


end

