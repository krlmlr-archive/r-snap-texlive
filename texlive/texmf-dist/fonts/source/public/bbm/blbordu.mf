% Copyright 1993 Gilles F. Robert.
% You may freely use, modify and/or distribute this file.
% This statement added 2008/11/14 by Clea F. Rees on the basis of the email exchange archived at http://lists.debian.org/debian-tex-maint/2008/05/msg00040.html.
%
% Computer Modern Blackboard upper case:
% Was romanu.mf; modified for blackboard letter
% Gilles F. ROBERT (29-03-93) v1.0

% These letters were originally coded by D. E. Knuth in November, 1979,
% inspired by the Monotype faces used in {\sl The Art of Computer Programming}.
% Sans serif designs by Richard Southall were added in April, 1982.
% The programs were revised for the new \MF\ conventions in 1985.
% modified March 93 by Gilles F. ROBERT for blackboard letters

% Character codes \0101 through \0132 are generated.

cmchar "The letter A";
beginchar("A",13u#,cap_height#,0);
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric left_stem,right_stem,outer_jut,alpha;
right_stem=cap_stem-stem_corr;
left_stem=max(min(cap_hair if hefty: -3stem_corr fi,.5right_stem),tiny);
pen_duplicate(1,2);
outer_jut=.8cap_jut; x.G1l=w-x4r=l+letter_fit+outer_jut+.5u; y1=y4=0;
x2-x1=x4-x3; x3r=x.D2r+apex_corr; y2=y3=h+apex_o+apex_oo;
alpha=diag_ratio(2,left_stem+cap_interspace,y2-y1,x4r-x.G1l-apex_corr);
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
Delta_x = hround(alpha*(cap_interspace+left_stem));
z0=whatever[z.D1r,z.D2r]=whatever[z3l,z4l];
if y0<h-cap_notch_cut: y0:=h-cap_notch_cut;
 fill z0+.5right{down}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)
  --diag_end(4r,3r,1,1,D2l,D1l)--diag_end(D2l,D1l,1,1,D1r,D2r){z2-z1}
  ...{up}z0+.5left--cycle; % left and right diagonals
else: fill z0--diag_end(0,4l,1,1,4r,3r)--diag_end(4r,3r,1,1,D2l,D1l)
  --diag_end(D2l,D1l,1,1,D1r,0)--cycle; fi % left and right diagonals
fill diag_end(G2l,G1l,1,1,G1r,G2r)--diag_end(G1r,G2r,1,1,G2l,G1l)--cycle; % left outer diagonal
penpos5(whatever,angle(z2-z1)); z5=whatever[z.D1,z.D2];
penpos6(whatever,angle(z3-z4)); z6=whatever[z3,z4]; y6=y5;
if hefty: y5r else: y5 fi =5/12y0;
y5r-y5l=y6r-y6l=cap_band; penstroke z5e--z6e; % bar line
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3);
 prime_points_inside(2,1); pen_duplicate(1',2');
 if rt x.D1'r+cap_jut+.5u+1<=lft x4'l-cap_jut: inner_jut=cap_jut;
 else: rt x.D1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_biserif(1',2,a,1/2,outer_jut,ab,1/2,b,.6,inner_jut)(dark);  % left serif
 mid_biserif(2',1,ab',1/2);  % upper terminal
 dish_serif(4',3,c,1/2,inner_jut,d,1/3,outer_jut); % right serif
else: mid_biserif(1,2,ab,1/3)(dark); % lower terminal
 mid_biserif(2,1,ab',1/2); fi % upper terminal
penlabels(0,G1,D1,G2,D2,3,4,5,6); endchar;

cmchar "The letter B";
beginchar("B",12.5u#,cap_height#,0);
italcorr .75cap_height#*slant-.5u#;
adjust_fit(cap_serif_fit#,0);
numeric left_stem,right_curve,middle_weight;
left_stem=.5cap_stem-hround stem_corr; middle_weight=.6vair+.5;
pen_duplicate(1,2);
pickup tiny.nib; pos1(left_stem,0); pos2(left_stem,0);
Delta_x = hround(cap_interspace+left_stem);
lft x.G1l=lft x.G2l=hround max(2u,3u-left_stem); top y1=h; bot y2=0;
double filldraw stroke z1e--z2e; % double stem
penpos3(cap_band,90); penpos4(cap_band,90);
penpos6(middle_weight,-90); penpos7(middle_weight,-90);
penpos8(middle_weight,90); penpos9(middle_weight,90);
penpos5(right_curve-stem_corr,0); penpos10(right_curve,0);
penpos11(cap_band,-90); penpos12(cap_band,-90);
z3r=top z.G1; y4=y3; y5=.5[y4,y6]; y6=y7; y7l-y8l=vair;
z12r=bot z.G2; y11=y12; y10=.5[y11,y9]; y8=y9; .5[y7l,y8l]=.52h;
x4=x6; x9=x11=x4+.5u; x7=x8=x.D1; x9l:=x4+.25u;
x5r=hround(w-1.5u); x10r=hround(w-u);
if serifs: right_curve=cap_curve-stem_corr; x4=.5[x.G1,w-1.5u];
else: right_curve=cap_curve-3stem_corr; x4=.5[x.G1,w-2.5u];
 x4l:=x4l-.5u; x9l:=x9l-.5u; fi
x6l:=x6l-.5u; x11l:=x11l-.5u;
fill stroke z3e..super_arc.e(4,5) & super_arc.e(5,6)..z7e;  % upper lobe
fill stroke z8e..super_arc.e(9,10) & super_arc.e(10,11)..z12e;  % lower lobe
if serifs: nodish_biserif(1,2,a,1/3,cap_jut,ab,1/3,b,1/3,.5cap_jut);  % upper serif
 nodish_biserif(2,1,c,1/3,cap_jut,cd,1/3,d,1/3,.5cap_jut); fi  % lower serif
math_fit(0,.5ic#); penlabels(G1,D1,G2,D2,3,4,5,6,7,8,9,10,11,12); endchar;

cmchar "The letter C";
if serifs: beginchar("C",13u#,cap_height#,0);
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
 pickup fine.nib; pos1(cap_hair,0); pos2(cap_band,90);
 pos3(hround(.5cap_curve),180); pos4(cap_band,270); pos5(hair,360);
 pen_duplicate(3);
 rt x1r=rt x5r=hround(w-u); lft x.G3r=hround u; x2=x4=.55[x.G3,x1];
 top y2r=h+o; bot y4r=-o; y3=.5[y2,y4];
 bot y1=min(vround max(.6h,x_height-.5vair),bot y2l-eps);
 y5=max(good.y .95(h-y1),y4l+eps);
 (x2l',y2l)=whatever[z2r,z1l]; x2l:=min(x2l',x2l+.5u);
 (x4l',y4l)=whatever[z4r,z5l]; x4l:=min(x4l',x4l+.5u);
 filldraw stroke z1e{x2-x1,10(y2-y1)}
  ...pulled_arc.e(2,G3) & pulled_arc.e(G3,4)...{up}z5e; % exterior arc
 Delta_x = min(hround(cap_curve_interspace+.5cap_curve),x4r-x.G3l);
 define_upper_and_lower(D2,D4,D3,pulled_arc.r(2,G3) & pulled_arc.r(G3,4));
 pos6(.3[fine.breadth,cap_hair],0); x6r=x1r; top y6=h+o;
 x1r-x1'=2cap_curve-fine; y1'=y1;
 path upper_arc; upper_arc=z1{x2-x1,10(y2-y1)}..z2{left};
 numeric t; t=xpart(upper_arc intersectiontimes (z6l--z1'));
 filldraw z1r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % barb
else: beginchar("C",11.5u#,cap_height#,0);
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
 pickup fine.nib; pos1(1.2flare,80); pos2(slab,90);
 pos3(.5cap_curve,180); pos4(slab,270); pos5(flare,275);
 pen_duplicate(3);
 rt x1r=hround(w-1.1u); x2=x4=.5w+1.25u;
 lft x.G3r=hround max(u,2u-.5cap_curve); rt x5r=hround(w-.9u);
 top y1r=vround .95h+o; top y2r=h+o; y3=.5h;
 bot y4r=-o; bot y5r=vround .08h-o; y5l:=good.y y5l; x5l:=good.x x5l;
 filldraw stroke rterm.e(2,1,right,.9,4) & super_arc.e(2,G3)
  & super_arc.e(G3,4) & term.e(4,5,right,.8,4); % exterior arc and terminals
 Delta_x = min(hround(cap_curve_interspace+.5cap_curve),x4r-x.G3l);
 define_upper_and_lower(D2,D4,D3,super_arc.r(2,G3) & super_arc.r(G3,4)); fi
filldraw stroke z.D2e--z.D4e; % inner stroke
math_fit(-.3cap_height#*slant-.5u#,.5ic#);
penlabels(1,1',2,G3,4,D2,D3,D4,5,6); endchar;

cmchar "The letter D";
beginchar("D",13.5u#,cap_height#,0);
italcorr .7cap_height#*slant-.5u#;
adjust_fit(cap_serif_fit#,0);
pen_duplicate(1,2);
pickup tiny.nib; pos1(.5cap_stem',0); pos2(.5cap_stem',0);
lft x.G1l=lft x.G2l=hround max(2u,3u-.5cap_stem'); top y1=h; bot y2=0;
Delta_x = hround(cap_interspace+.5cap_stem');
double filldraw stroke z1e--z2e; % double stem
penpos3(cap_band,90); penpos4(cap_band,90);
penpos5(cap_curve-stem_corr,0);
penpos6(cap_band,-90); penpos7(cap_band,-90);
z3r=top z.G1; y4=y3; y5=.51[y4,y6]; y6=y7;
z7r=bot z.G2; x4=x6=.5w+.25u; x5r=hround(w-u);
x4l:=x6l:=x4-.25cap_curve;
fill stroke z3e..pulled_arc.e(4,5) & pulled_arc.e(5,6)..z7e;  % lobe
if serifs: nodish_biserif(1,2,a,1/3,cap_jut,ab,1/3,b,1/3,.5cap_jut);  % upper serif
 nodish_biserif(2,1,c,1/3,cap_jut,cd,1/3,d,1/3,.5cap_jut); fi  % lower serif
math_fit(0,ic#-.5u#); penlabels(G1,D1,G2,D2,3,4,5,6,7); endchar;

cmchar "The letter E";
beginchar("E",12u#-width_adj#,cap_height#,0);
italcorr cap_height#*slant-beak_jut#-.5u#;
adjust_fit(cap_serif_fit#,0);
h:=vround(h-stem_corr);
pen_duplicate(1,2);
pickup tiny.nib; pos1(.5cap_stem,0); pos2(.5cap_stem,0);
Delta_x = hround(cap_interspace+.5cap_stem);
lft x.G1l=lft x.G2l=hround max(2u,3u-.5cap_stem); top y1=h; bot y2=0;
double filldraw stroke z1e--z2e; % double stem
pickup crisp.nib; pos3(slab,90); pos4(hair,0);
top y3r=h; x3=x.G1; rt x4r=hround(w-u); y4=good.y(y3l-beak)-eps;
arm(3,4,e,beak_darkness,beak_jut);  % upper arm and beak
pos5(cap_bar,-90); pos6(hair,0);
top y5l=vround(.52[y2,y1]+.5cap_bar); x5=x.D1;
pos0(cap_bar,90); pos7(hair,0);
z0=z5; x6=x7; y6-y5l=y0l-y7;
if serifs: rt x6r=hround(w-4.4u+.5hair); y6=good.y(y5l+.6beak)+eps;
 rt x9r=hround(w-.5u);
else: rt x6r=hround(w-1.5u); y6=y5l+eps; rt x9r=hround(w-.75u); fi
arm(5,6,f,beak_darkness,0); arm(0,7,g,beak_darkness,0);  % middle arm and serif
pos8(slab if not serifs:+2stem_corr fi,-90); pos9(hair,0);
bot y8r=0; x8=x.G2; y9=good.y(y8l+7/6beak)+eps;
arm(8,9,h,beak_darkness,1.5beak_jut);  % lower arm and beak
if serifs: nodish_biserif(1,2,a,1/3,cap_jut,ab,1/3,b,1/3,.5cap_jut);  % upper serif
 nodish_biserif(2,1,c,1/3,cap_jut,cd,1/3,d,1/3,.5cap_jut); fi  % lower serif
math_fit(0,.5ic#); penlabels(0,G1,D1,G2,D2,3,4,5,6,7,8,9); endchar;

cmchar "The letter F";
beginchar("F",11.5u#-width_adj#,cap_height#,0);
italcorr cap_height#*slant-beak_jut#-.25u#;
adjust_fit(cap_serif_fit#,0);
h:=vround(h-stem_corr);
pen_duplicate(1,2);
pickup tiny.nib; pos1(.5cap_stem,0); pos2(.5cap_stem,0);
Delta_x = hround(cap_interspace+.5cap_stem);
lft x.G1l=lft x.G2l=hround max(2u,3u-.5cap_stem); top y1=h; bot y2=0;
double filldraw stroke z1e--z2e; % double stem
pickup crisp.nib; pos3(slab,90); pos4(hair,0);
top y3r=h; x3=x.G1; rt x4r=hround(w-.75u); y4=good.y(y3l-beak)-eps;
arm(3,4,e,beak_darkness,beak_jut);  % upper arm and beak
pos5(cap_bar,-90); pos6(hair,0);
top y5l=vround(.5[y2,y1]+.5cap_bar); x5=x.D1;
pos0(cap_bar,90); pos7(hair,0);
z0=z5; x6=x7; y6-y5l=y0l-y7;
if serifs: rt x6r=hround(w-4u+.5hair); y6=good.y(y5l+.6beak)+eps;
 rt x9r=hround(w-.5u);
else: rt x6r=hround(w-1.5u); y6=y5l+eps; rt x9r=hround(w-.75u); fi
arm(5,6,f,beak_darkness,0); arm(0,7,g,beak_darkness,0);  % middle arm and serif
if serifs: nodish_biserif(1,2,a,1/3,cap_jut,ab,1/3,b,1/3,.5cap_jut);  % upper serif
 dish_biserif(2,1,c,1/3,cap_jut,cd,1/3,d,1/3,1.25cap_jut); % lower serif
else: mid_biserif(2,1,cd,1/3); fi  % lower terminal
math_fit(0,ic#-2.5u#); penlabels(0,G1,D1,G2,D2,3,4,5,6,7,8,9); endchar;

cmchar "The letter G";
if serifs: beginchar("G",14u#,cap_height#,0);
 italcorr cap_height#*slant-1.5u#-.5cap_serif_fit#;
 adjust_fit(0,.5cap_serif_fit#);
 pickup tiny.nib; pos0(cap_stem,0); pos7(cap_stem,0);
 rt x0r=hround(w-2u); y0=good.y(.1[bar_height,x_height]+1); x7=x0;
 pickup fine.nib;
 if hefty: bot y7=0; pos5(cap_stem,0); x5=x0; top y5=tiny.top y0;
 else: pos5(cap_hair,0); pos8(cap_stem,0);
  y7=.5bar_height; z5l=z8l; z8=z7; x9=x8r; bot y9=0;
  filldraw z8l{down}..{4(x9-x8),y9-y8}z9--z8r--cycle; fi % spur
 pos1(cap_hair,0); pos2(cap_band,90);
 pos3(hround(.5cap_curve),180); pos4(cap_band,270);
 pen_duplicate(3);
 rt x1r=hround(w-2u); lft x.G3r=hround u; x2=x4=.55[x.G3,x1];
 top y2r=h+o; bot y4r=-o; y3=.5[y2,y4];
 bot y1=min(vround max(.6h,x_height-.5vair),bot y2l-eps);
 (x2l',y2l)=whatever[z2r,z1l]; x2l:=min(x2l',x2l+.5u);
 (x4l',y4l)=whatever[z4r,z5l]; x4l:=min(x4l',x4l+.5u);
 filldraw stroke z1e{x2-x1,10(y2-y1)}
  ...pulled_arc.e(2,G3) & pulled_arc.e(G3,4)...z5e{up}; % arc
 Delta_x = min(hround(cap_curve_interspace+.5cap_curve),x4r-x.G3l);
 define_upper_and_lower(D2,D4,D3,pulled_arc.r(2,G3) & pulled_arc.r(G3,4));
 filldraw stroke z.D2e--z.D4e; % inner stroke
 pos6(.3[fine.breadth,cap_hair],0); x6r=x1r; top y6=h+o;
 x1r-x1'=2cap_curve-fine; y1'=y1;
 path upper_arc; upper_arc=z1{x2-x1,10(y2-y1)}..z2{left};
 numeric t; t=xpart(upper_arc intersectiontimes (z6l--z1'));
 filldraw z1r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % barb
 pickup tiny.nib; filldraw stroke z0e--z7e;  % stem
 dish_serif(0,7,a,1/3,max(cap_jut,2.25u),b,1/3,1.25u);  % serif
 math_fit(-.3cap_height#*slant-.5u#,ic#);
else: beginchar("G",12u#,cap_height#,0);
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
 pen_duplicate(3);
 pickup fine.nib; pos1(1.2flare,80); pos2(slab,90);
 pos3(hround(.75cap_curve),180); pos4(slab,270); pos5(flare,270);
 rt x1r=hround(w-1.35u); x2=x4=.5w+u;
 lft x.G3r=hround max(u,2u-.5cap_curve); rt x5l=hround(w-1.2u);
 top y1r=vround .93h+o; top y2r=h+o; y3=.5h;
 bot y4r=-o; bot y5r=vround .07h-o;
 filldraw stroke rterm.e(2,1,right,.9,4)&super_arc.e(2,G3)
  & super_arc.e(G3,4) & term.e(4,5,right,1,4); % arc and terminals
 Delta_x = min(hround(cap_curve_interspace+.5cap_curve),x4r-x.G3l);
 define_upper_and_lower(D2,D4,D3,super_arc.r(2,G3)  & super_arc.r(G3,4));
 filldraw stroke z.D2e--z.D4e; % inner stroke
 pos0(stem,0); pos7(stem,0);
 z7r=z5r; x0=x7; top y0=1+vround .35[bar_height,x_height];
 filldraw stroke z0e--z7e;  % stem
 pos8(cap_bar,90); pos9(cap_bar,90);
 z0r=z9r; y8=y9; lft x8=hround x4;
 filldraw stroke z8e--z9e;  % bar
 math_fit(-.3cap_height#*slant-.5u#,.5ic#); fi
penlabels(0,1,1',2,G3,4,D2,D3,D4,5,6,7,8,9); endchar;

cmchar "The letter H";
beginchar("H",13u#+width_adj#,cap_height#,0);
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(cap_serif_fit#,cap_serif_fit#);
pen_duplicate(1,2);
pickup tiny.nib; pos1(.5cap_stem,0); pos2(.5cap_stem,0);
pos3(cap_stem,0); pos4(cap_stem,0);
Delta_x = hround(cap_interspace+.5cap_stem);
lft x.G1l=lft x.G2l=hround max(2u,3u-.5cap_stem); x3r=x4r=w-x.G1l;
top y1=top y3=h; bot y2=bot y4=0;
double filldraw stroke z1e--z2e; % left (double) stem
filldraw stroke z3e--z4e; % right stem
penpos5(cap_bar,90); penpos6(cap_bar,90);
x5=x.D1; x6=x3; y5=y6=.52h;
fill stroke z5e--z6e;  % bar
if serifs: numeric inner_jut;
 if rt x.D1r+cap_jut+.5u+1<=lft x3l-cap_jut: inner_jut=cap_jut;
 else: rt x.D1r+inner_jut+.5u+1=lft x3l-inner_jut; fi
 dish_biserif(1,2,a,1/3,cap_jut,ab,1/3,b,1/3,inner_jut);  % upper left serif
 dish_biserif(2,1,c,1/3,cap_jut,cd,1/3,d,1/3,inner_jut); % lower left serif
 dish_serif(3,4,e,1/3,inner_jut,f,1/3,cap_jut);  % upper left serif
 dish_serif(4,3,g,1/3,inner_jut,h,1/3,cap_jut); % lower left serif
else: mid_biserif(1,2,ab,1/3);  % upper left terminal
 mid_biserif(2,1,cd,1/3); fi % lower left terminal
math_fit(0,.5ic#); penlabels(G1,D1,G2,D2,3,4,5,6); endchar;

cmchar "The letter I";
beginchar("I",max(6u#,4u#+cap_stem#),cap_height#,0);
italcorr cap_height#*slant-.25u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
pen_duplicate(1,2);
pickup tiny.nib; pos1(.5cap_stem,0); pos2(.5cap_stem,0); 
Delta_x = hround(cap_interspace+.5cap_stem);
lft x.G1l=lft x.G2l=hround(.5w-.5cap_stem-.5cap_interspace); top y1=h; bot y2=0;
double filldraw stroke z1e--z2e; % double stem
if serifs: dish_biserif(1,2,a,1/3,1.05cap_jut,ab,1/3,b,1/3,1.05cap_jut);  % upper serif
 dish_biserif(2,1,c,1/3,1.05cap_jut,cd,1/3,d,1/3,1.05cap_jut); % lower serif
else: mid_biserif(1,2,ab,1/3);  % upper terminal
 mid_biserif(2,1,cd,1/3);  fi % lower terminal
math_fit(0,.5ic#); labels(G1,D1,G2,D2); endchar;

cmchar "The letter J";
beginchar("J",9u#,cap_height#,0);
italcorr cap_height#*slant-cap_serif_fit#
 +.75cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(0,cap_serif_fit#);
pen_duplicate(1,2);
pickup tiny.nib; pos1(.5cap_stem',0); pos2(.5cap_stem',0);
top y1=h; rt x.D1r=hround(w-2u); x2=x1; y2=.21h;
if serifs: pos3(vair,-90); pos4(cap_hair,-180);
 pos5(flare+(cap_stem-stem),-180);
 bot y3r=-o; x3=.5[x4,x.D2]; y5=1/6h; rt x5l=hround 2.75u; z5r=z4r;
 filldraw stroke z.D1e--z.D2e&super_arc.e(D2,3);  % stem and arc
 Delta_x = min(hround(cap_interspace+.5cap_stem'),x.D2l-x3r);
 define_lower(G3,G2,super_arc.r(D2,3));
 filldraw stroke z.G1e--z.G3e;  % left (inner) stroke
 dish_biserif(1,2,a,1/3,1.3cap_jut,ab,1/3,b,1/3,.75cap_jut);  % serif
 pickup tiny.nib; bulb(3,4,5);  % bulb
else: pen_duplicate(3);
 pickup fine.nib; pos3(.5cap_stem',0); z3=z2;
 pos4(flare,angle(6.5u,-h)); pos5(1.1flare,-100);
 bot y4r=-o; x4r=.5[x5r,x.D3r]; lft x5r=hround.75u; bot y5r=vround.06h-o;
 filldraw stroke z.D3e{down}....term.e(4,5,left,1,4); % arc and terminal
 Delta_x = min(hround(cap_interspace+.5cap_stem'),x.D2l-x5l);
 define_lower(G4,G3,z.D3r{down}....term.r(4,5,left,1,4));
 filldraw stroke z.G3e--z.G4e;  % left (inner) stroke
 pickup tiny.nib; double filldraw stroke z1e--z2e;  % double stem
 mid_biserif(1,2,ab,1/3); fi % terminal
math_fit(0,.5ic#-.5u#); penlabels(G1,D1,G2,D2,D3,G3,4,G4,5); endchar;

cmchar "The letter K";
beginchar("K",13.5u#,cap_height#,0);
italcorr cap_height#*slant-.5u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric right_jut,stem[],alpha[];
if serifs: right_jut=.6cap_jut;
else: right_jut=.4tiny; fi
pen_duplicate(1,2);
pickup tiny.nib; pos1(.5fudged.cap_stem,0); pos2(.5fudged.cap_stem,0);
Delta_x = hround(cap_interspace+.5fudged.cap_stem);
lft x.G1l=lft x.G2l=hround max(2u,3u-.5fudged.cap_stem); top y1=h; bot y2=0;
double filldraw stroke z1e--z2e; % double stem
stem2=max(tiny.breadth,fudged.cap_stem-3stem_corr);
stem1=max(tiny.breadth,fudged.hair if hefty:-3stem_corr fi);
top y3=h; rt x3r=hround(r-letter_fit-u-right_jut);
bot y6=0; rt x6r=hround(r-letter_fit-.75u-right_jut);
x4=x.D1; y4=1/3h;
alpha1=diag_ratio(1,.5(stem1-tiny),y3-y4,x3r-x4);
penpos3(alpha1*(stem1-tiny),0); penpos4(whatever,-90);
alpha2=diag_ratio(1,.5(stem2-tiny),y1-y6,x6r-x.D1);
penpos6(alpha2*(stem2-tiny),0);
forsuffixes $=l,r: y3'$=h; y6'$=0; z4$=z3'$+whatever*(z3-z4);
 z5$=z6'$+whatever*(z1-z6)=whatever[z3,z4]; endfor
z5=.5[z5l,z5r];
z3'r=z3r+penoffset z3-z4 of currentpen+whatever*(z3-z4);
% we have also |z3'l=z3l+penoffset z4-z3 of currentpen+whatever*(z3-z4)|;\]
z6'r=z6r+penoffset z.D1-z6 of currentpen+whatever*(z1-z6);
z6'l=z6l+penoffset z6-z.D1 of currentpen+whatever*(z1-z6);
fill z4r--diag_end(4r,3'r,1,.5,3'l,4l)--z4l--cycle;  % upper diagonal
fill z5l--diag_end(5l,6'l,.5,1,6'r,5r)--z5r--cycle;  % lower diagonal
if serifs: numeric inner_jut;
 if rt x.D2r+cap_jut+.5u+1<=lft x6l-cap_jut: inner_jut=cap_jut;
 else: rt x.D2r+cap_jut+.5u+1=lft x6l-inner_jut; fi
 dish_biserif(1,2,a,1/3,cap_jut,ab,1/3,b,1/3,cap_jut); % upper stem serif
 dish_biserif(2,1,c,1/3,cap_jut,cd,1/3,d,1/3,cap_jut);  % lower stem serif
 dish_serif(3,4,e,2/3,1.2cap_jut,f,1/2,right_jut)(dark); % upper diagonal serif
 dish_serif(6,5,g,1/2,inner_jut,h,1/3,right_jut)(dark); % lower diagonal serif
else: mid_biserif(1,2,ab,1/3); % upper stem terminal
 mid_biserif(2,1,cd,1/3); fi % lower stem terminal
math_fit(0,.5ic#); penlabels(G1,D1,G2,D2,3,4,5,6); endchar;

cmchar "The letter L";
beginchar("L",11u#-width_adj#,cap_height#,0);
adjust_fit(cap_serif_fit#,0);
pen_duplicate(1,2);
pickup tiny.nib; pos1(.5cap_stem,0); pos2(.5cap_stem,0);
Delta_x = hround(cap_interspace+.5cap_stem);
lft x.G1l=lft x.G2l=hround max(2u,3u-.5cap_stem); top y1=h; bot y2=0;
double filldraw stroke z1e--z2e; % double stem
pickup crisp.nib; pos3(slab,-90); pos4(hair,0);
bot y3r=0; x3=x.G2; y4=y3l+7/6beak+eps; rt x4r=hround(w-.75u);
arm(3,4,e,1.2beak_darkness,beak_jut);  % lower arm and beak
if serifs: dish_biserif(1,2,a,1/3,cap_jut,ab,1/3,b,1/3,1.25cap_jut);  % upper serif
 nodish_biserif(2,1,c,1/3,cap_jut,cd,1/3,d,1/3,.5cap_jut); % lower serif
else: mid_biserif(1,2,ab,1/3); fi % upper terminal
math_fit(0,u#); penlabels(G1,D1,G2,D2,3,4); endchar;

cmchar "The letter M";
beginchar("M",16u#+width_adj#,cap_height#,0);
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric stem[]; % thicknesses of the four strokes
stem1=hround(max(.5fudged.hair+stem_corr,tiny));
stem2=hround(fudged.cap_stem-4stem_corr);
stem3=hround(fudged.hair-stem_corr);
stem4=hround(fudged.cap_stem-3stem_corr);
if stem4<2stem1: stem4:=2stem1; fi
pen_duplicate(1,2);
pickup tiny.nib; pos1(stem1,0); pos2(stem1,0);
Delta_x = hround(cap_interspace+stem1);
pos3(stem4,0); pos4(stem4,0);
x1=x2; x3=x4; x.G1l=w-x3r; rt x3r=hround min(w-2u,w-3u+.5stem4);
top y1=top y3=h; bot y2=bot y4=0;
double filldraw stroke z1e--z2e; % left (double) stem
filldraw stroke z3e--z4e; % right stem
penpos5(stem2,0); penpos6(stem2,0); penpos7(stem3,0); penpos8(stem3,0);
x5l=x.D1; x6l=x7l; x8=lft x3l; x6-x5=x8-x7; y5=y8=h; y6=y7;
if hefty: y6=if monospace: vround 1/3h else: o fi;
 numeric upper_notch,lower_notch;
 upper_notch=h-cap_notch_cut; lower_notch=y6+cap_notch_cut;
 x1'=rt x.D1r; z1'=whatever[z5l,z6l]; x3'=lft x3l; z3'=whatever[z7r,z8r];
 z0=whatever[z5r,z6r]=whatever[z7l,z8l];
 fill z5l..
  if y1'<upper_notch: {right}(x1'+1,upper_notch){down}... fi
  {z6-z5}diag_in(5l,6l,1,6r)..diag_out(7l,1,7r,8r){z8-z7}
  if y3'<upper_notch: ...{up}(x3'-1,upper_notch){right} fi
  ..z8r--diag_out(8r,1,8l,7l){z7-z8}
  if y0<=lower_notch: ..{z7-z8}z0{z5-z6}..
  else: ...{down}(x0+.5,lower_notch)--(x0-.5,lower_notch){up}... fi
  {z5-z6}diag_in(6r,5r,1,5l)--cycle;  % diagonals
else: y6=0; z0=whatever[z5r,z6r]=whatever[z7l,z8l];
 fill z5l..{z6-z5}diag_in(5l,6l,1,6r)..diag_out(7l,1,7r,8r){z8-z7}
  ..z8r--diag_out(8r,1,8l,7l){z7-z8}..{z7-z8}z0{z5-z6}
  ..{z5-z6}diag_in(6r,5r,1,5l)--cycle; fi  % diagonals
if serifs: serif(G1,G2,a,1/3,-cap_jut); mid_biserif(1,2,a',1/3);  % upper left serif
 dish_biserif(2,1,b,1/2,cap_jut,bc,1/2,c,1/2,cap_jut)(dark); % lower left serif
 serif(3,4,d,1/3,cap_jut); %  upper right serif
 dish_serif(4,3,e,1/3,cap_jut,f,1/3,cap_jut); % lower right serif
else: mid_biserif(1,2,a',1/3);  % upper left terminal
 mid_biserif(2,1,bc,1/2)(dark); fi % lower left terminal
math_fit(0,max(.5ic#-.5u#,0));
penlabels(0,G1,D1,1',G2,D2,3,3',4,5,6,7,8); endchar;

cmchar "The letter N";
beginchar("N",13u#+width_adj#,cap_height#,0);
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric thin_stem,thin_stem'; thin_stem=hround(fudged.hair+stem_corr);
 thin_stem'=hround(max(.5(fudged.hair+stem_corr),tiny+eps)); pen_duplicate(1,2);
pickup tiny.nib; pos1(thin_stem',0); pos2(thin_stem',0);
Delta_x = hround(cap_interspace+thin_stem');
pos3(thin_stem,0); pos4(thin_stem,0);
pickup tiny.nib; top y1=top y3=h; bot y2=bot y4=0;
x1=x2; x3=x4; x.G1l=w-x3r;
rt x3r=hround min(w-2u,w-3u+.5fudged.cap_stem);
double filldraw stroke z1e--z2e; % left (double) stem
filldraw stroke z3e--z4e; % right stem
if hefty: penpos5(fudged.cap_stem,0); penpos6(fudged.cap_stem,0);
 if x.D1r-x.G1l<x5r-x5l: lft x5l=tiny.lft x.G1l; else: x5r=tiny.rt x.D1r; fi
  x6r=x4; y5=h; y6=0;
  numeric upper_notch,lower_notch;
  upper_notch=h-cap_notch_cut; lower_notch=cap_notch_cut;
  x1'=rt x.G1r; z1'=whatever[z5l,z6l]; x4'=lft x4l; z4'=whatever[z5r,z6r];
  fill z5l..
   if y1'<upper_notch: {right}(x1'+1,upper_notch){down}... fi
   {z6-z5}diag_in(5l,6l,1,6r)--z6r..
   if y4'>lower_notch: {left}(x4'-1,lower_notch){up}... fi
   {z5-z6}diag_in(6r,5r,1,5l)--cycle;  % diagonal
else: penpos6(whatever,90);
 z6l=z4l;
 if xpart((z.D1r-z.G1l) rotated (-angle(z.G1l-z6l)+90))<cap_stem-3stem_corr-tiny: z5l=z.G1l; 
  penpos5(whatever,0);
  z7=z6l+(max(eps,cap_stem-3stem_corr-tiny),0) rotated (angle(z5l-z6l)-90);
  z5r=z7+whatever*(z5l-z6l); z6r=z7+whatever*(z5l-z6l);
 else: z5r=z.D1r; numeric alpha;
  alpha=diag_ratio(1,cap_stem-3stem_corr-tiny,y5r-y6l,x6l-x5r);
  penpos5(alpha*max(eps,cap_stem-3stem_corr-tiny),0);
  z6r=z5r+whatever*(z5l-z6l); fi
  %z7=z6l+(max(eps,cap_stem-3stem_corr-tiny),0) rotated (angle(z5l-z6l)-90);
  %z5r=z7+whatever*(z5l-z6l); z6r=z7+whatever*(z5l-z6l); fi
 filldraw stroke z5e..z6e; fi  % diagonal
if serifs: if hefty: serif(G1,G2,a,1/3,-cap_jut);
 mid_biserif(1,2,a',1/3); % upper left serif
 else: serif(5,6,a,1/3,-cap_jut-x5l+x.G1l); fi  % upper left serif
 dish_biserif(2,1,b,1/2,cap_jut,bc,1/2,c,1/2,cap_jut)(dark); % lower left serif
 dish_serif(3,4,e,1/2,cap_jut,f,1/2,cap_jut)(dark); % upper right serif
else: mid_biserif(1,2,a',1/3); % upper left terminal
 mid_biserif(2,1,bc,1/2)(dark); fi % lower left terminal
math_fit(0,max(.5ic#-.5u#,0)); penlabels(G1,D1,1',G2,D2,3,4,4',5,6,7); endchar;

cmchar "The letter O";
beginchar("O",14u#-width_adj#,cap_height#,0);
italcorr .7cap_height#*slant-.5u#;
adjust_fit(0,0);
penpos1(vair',90); penpos3(vround(vair+1.5vair_corr),-90);
penpos2(hround(.5cap_curve),180); penpos4(cap_curve,0);
pen_duplicate(2);
if monospace: x.G2r=hround 1.5u;
 interim superness:=sqrt superness;  % make |"O"|, not |"0"|
else: x.G2r=hround u; fi
x4r=w-x.G2r; x1=x3=.5w; y1r=h+o; y3r=-o;
y2=y4=.5h-vair_corr; y2l:=y4l:=.52h;
penstroke pulled_super_arc.e(1,G2)(.5superpull)
 & pulled_super_arc.e(G2,3)(.5superpull)
 & pulled_super_arc.e(3,4)(.5superpull)
 & pulled_super_arc.e(4,1)(.5superpull) & cycle;  % bowl
Delta_x = min(hround(cap_curve_interspace+.5cap_curve),x1r-x.G2l) ;
define_upper_and_lower(D1,D3,D2,
 pulled_super_arc.r(1,G2)(.5superpull) & pulled_super_arc.r(G2,3)(.5superpull));
filldraw stroke z.D1e--z.D3e; % inner stroke
math_fit(-.3cap_height#*slant-.5u#,ic#-.5u#); penlabels(1,G2,3,D1,D2,D3,4); endchar;

cmchar "The letter P";
beginchar("P",12u#,cap_height#,0);
italcorr .75cap_height#*slant-.5u#;
adjust_fit(cap_serif_fit#,0);
pen_duplicate(0,0',1,2);
pickup tiny.nib; penpos1(.5cap_stem'-tiny,0); penpos2(.5cap_stem-tiny,0);
pos0(.5cap_stem',0); pos0'(.5cap_stem,0);
Delta_x = hround(cap_interspace+.5cap_stem);
lft x.G1l=hround max(2u,3u-.5cap_stem'); top y1=h; bot y2=0;
x1l=x2l=x0l=x0'l; y.D0=y.D0'=y7;
penpos3(cap_band,90); penpos4(cap_band,90);
penpos5(cap_curve if hefty:-3stem_corr fi,0);
penpos6(.5[vair,cap_band],-90); penpos7(.5[vair,cap_band],-90);
z3r=top z.G1; y4=y3; y5=.5[y4l,y6l]; y6=y7;
x7=x.D2; y7l=vround .5h; x4=x6=.5w+.75u; x5r=hround(w-u);
x4l:=x6l:=x4-.25cap_curve;
double filldraw stroke z1e--z0e--z0'e--z2e; % double stem
fill stroke z3e..pulled_arc.e(4,5) & pulled_arc.e(5,6)..z7e;  % lobe
if serifs: nodish_biserif(1,0,a,1/3,cap_jut,ab,1/3,b,1/3,.5cap_jut);  % upper serif
 dish_biserif(2,0',c,1/3,cap_jut,cd,1/3,d,1/3,cap_jut);  % lower serif
else: mid_biserif(2,0',cd,1/3); fi  % lower terminal
math_fit(0,ic#-2.5u#); penlabels(G0,D0,G1,D1,G2,D2,3,4,5,6,7); endchar;

cmchar "The letter Q";
beginchar("Q",14u#-width_adj#,cap_height#,comma_depth#);
italcorr .7cap_height#*slant-.5u#;
adjust_fit(0,0);
numeric light_curve; light_curve=cap_curve-hround stem_corr;
penpos1(vair',90); penpos3(vair',-90);
penpos2(hround(.5light_curve),180); penpos4(light_curve,0);
pen_duplicate(2);
if monospace: x.G2r=hround 1.5u;
 interim superness:=sqrt superness;  % make |"Q"| like |"O"|
else: x.G2r=hround u; fi
x4r=w-x.G2r; x1=x3=.5w; y1r=h+o; y2=y4=.5h-vair_corr; y3r=-o;
penstroke pulled_super_arc.e(1,G2)(.5superpull)
 & pulled_super_arc.e(G2,3)(.5superpull)
 & pulled_super_arc.e(3,4)(.5superpull)
 & pulled_super_arc.e(4,1)(.5superpull) & cycle;  % bowl
Delta_x = min(hround(cap_curve_interspace+.5cap_curve),x1r-x.G2l) ;
define_upper_and_lower(D1,D3,D2,
 pulled_super_arc.r(1,G2)(.5superpull) & pulled_super_arc.r(G2,3)(.5superpull));
filldraw stroke z.D1e--z.D3e; % inner stroke
pickup tiny.nib;
if hefty: penpos5(1.2(.5[cap_bar,light_curve]),0); penpos6(1.2light_curve,0);
 x5=.5w; x6r=hround(w-1.5u);
 y5=vround .28h; y6=-d;
 fill diag_end(6r,5r,.25,1,5l,6l)--diag_end(5l,6l,.5,1,6r,5r)--cycle;  % tail
else: pos3'(vair,270); pos5(vair,180); pos6(vair,90);
 pos7(cap_curve,85); penpos8(eps,180);
 z3'=z3; x6=x3; top y6r=vround(.2h+.5vair);
 lft x5r=hround(.5w-1.25u-.5vair); y5=.5[y3,y6];
 bot y7l=-d; x7l=2/3[x6,x8]; y8=0; rt x8=hround(x4r+.1u);
 filldraw stroke z3'e{left}...z5e{up}...z6e{right}
  ..z7e{right}...z8e{up}; fi % tail
math_fit(-.3cap_height#*slant-.5u#,ic#);
penlabels(1,G2,3,D1,D2,D3,4,5,6,7,8); endchar;

cmchar "The letter R";
beginchar("R",if serifs: 12u#+.5max(2u#,cap_curve#)
 else:12.5u#-.5width_adj# fi,cap_height#,0);
italcorr .75cap_height#*slant- if serifs: 1.75 else: .5 fi\\ u#;
adjust_fit(cap_serif_fit#,0);
pen_duplicate(1,2);
pickup tiny.nib; pos1(.5cap_stem',0); pos2(.5cap_stem',0);
Delta_x = hround(cap_interspace+.5cap_stem');
lft x.G1l=lft x.G2l=hround max(2u,3u-.5cap_stem'); top y1=h; bot y2=0;
double filldraw stroke z1e--z2e; % double stem
penpos3(cap_band,90); penpos4(cap_band,90);
penpos5(cap_curve if hefty:-3stem_corr fi,0);
penpos6(vair,-90); penpos7(vair,-90);
z3r=top z.G1; y4=y3; y5=.5[y4l,y6l]; y6=y7;
x7=x.D2; y7l=vround(.5h+.5vair); x4=x6;
if serifs: x4=.5w-.5u; x5r=hround(w-2.25u);
else: x4=.5w+.5u; x5r=hround(w-u); fi
x4l:=x6l:=x4-.125cap_curve;
fill stroke z3e..pulled_arc.e(4,5) & pulled_arc.e(5,6)..z7e;  % lobe
if serifs: pos6'(vair,-90); pos0(cap_stem,180);
 pos8(cap_curve,180); pos9(vair,-90); pos10(hair,0);
 z6'=z6; lft x0r=lft x8r=hround(x5-2/3u-.5cap_curve);
 y8=1/3[y2,y7]; y0=3/5[y2,y7]; x9=.5[x8l,x10r];
 bot y9r=-o; rt x10r=hround(w-.05u); y10=1/4[y2,y7];
 filldraw stroke z6'e{right}..z0e---z8e....z9e{right}..z10e{up};  % tail
 nodish_biserif(1,2,a,1/3,cap_jut,ab,1/3,b,1/3,.5cap_jut);  % upper serif
 dish_biserif(2,1,c,1/3,cap_jut,cd,1/3,d,1/3,cap_jut);  % lower serif
else: penpos8(cap_stem-2stem_corr,0); penpos9(cap_stem,0);
 x8=x6+.5u; y8=y6; x9r=hround(w-.5u); y9=0;
 fill z8l--diag_end(8l,9l,.5,1,9r,8r)--z8r--cycle;  % tail
 mid_biserif(2,1,cd,1/3); fi  % lower terminal
math_fit(0,.75ic#); penlabels(0,G1,D1,G2,D2,3,4,5,6,7,8,9,10); endchar;

cmchar "The letter S";
beginchar("S",10u#,cap_height#,0);
italcorr cap_height#*slant-u#;
adjust_fit(0,0);
numeric theta; theta=90-angle(50u,h); slope:=-h/50u;  % angle at middle
numeric s_slab; s_slab=if serifs:slab else:vround .2[slab,cap_stem] fi;
numeric ess'; ess'=max(fine.breadth,cap_ess);
pen_duplicate(3,6);
pickup fine.nib; pos2(max(fine.breadth,s_slab-vround vair_corr),-90);
pos0(ess',theta); pos7(s_slab,-90);
x2+x7=2x0=w; x7-x2=if serifs: u else: 0 fi; top y2l=h+o; bot y7r=-o;
y0=.52h; lft x.G3l=hround u; rt x.D6r=hround(w-u);
x3r-x3l=x6r-x6l=hround (.5[.75s_slab,.75ess']-fine);
Delta_x = min(hround(cap_curve_interspace+x3r-x3l+fine),x2l-x.G3r); % x2l-x.G3r=x.D6l-x7r
ellipse_set(2l,G3l,4l,0l); ellipse_set(2r,G3r,4r,0r); y3=y3r;
ellipse_set(7l,D6l,5l,0l); ellipse_set(7r,D6r,5r,0r); y6=y6r;
filldraw stroke super_arc.e(2,G3) & z.G3e{down}
 ..z4e---z5e..z.D6e{down} & super_arc.e(D6,7);  % main stroke
define_upper_and_lower(D2,D4,D3,
 super_arc.l(2,G3) & (z.G3l{down}..z4l---z5l));
define_upper_and_lower(G5,G7,G6,
 (z4r---z5r..z.D6r{down}) & super_arc.r(D6,7));
filldraw stroke z.D2e--z.D4e; filldraw stroke z.G5e--z.G7e; % inner strokes
if serifs: pos1(hair,180); pos8(hair,180);
 rt x1l=hround(w-1.5u); lft x8r=hround u;
 bot y1=vround 2/3h+1; top y8=vround 1/3h-1;
 filldraw stroke z1e{x2-x1,10(y2-y1)}....{left}z2e;  % upper arc
 filldraw stroke z7e{left}....{up}z8e;  % lower arc
 path upper_arc, lower_arc;
 upper_arc=z1{x2-x1,10(y2-y1)}....{left}z2;
 lower_arc=z7{left}....{up}z8;
 pos10(.3[fine.breadth,cap_hair],0); pos9(.3[fine.breadth,cap_hair],0);
 x10r=x1l; top y10=top y2l; x9l=x8r; bot y9=bot y7r;
 x1l-x1'=x8'-x8r=1.6cap_curve-fine; y1'=y1; y8'=y8;
 numeric t; t=xpart(upper_arc intersectiontimes(z10l--z1'));
 filldraw z1l--z10r--z10l--subpath(t,0) of upper_arc--cycle;  % upper barb
 t:=xpart(lower_arc intersectiontimes(z9r--z8'));
 filldraw z8r--z9l--z9r--subpath(t,1) of lower_arc--cycle;  % lower barb
else: pos1(1.2flare,-100); pos8(1.2flare,-100);
 x1l=good.x(x1l+w-1.75u-rt x1); lft x8r=hround .8u;
 top y1l=vround .93h+o; bot y8r=vround .1h-o;
 filldraw stroke term.e(2,1,right,.9,4);  % upper arc and terminal
 filldraw stroke term.e(7,8,left,1,4); fi  % lower arc and terminal
math_fit(0,.5ic#); penlabels(0,1,1',2,G3,4,D2,D3,D4,5,D6,7,G5,G6,G7,8,8',9,10); endchar;

cmchar "The letter T";
beginchar("T",13u#-width_adj#,cap_height#,0);
italcorr cap_height#*slant-beak_jut#-.25u#;
adjust_fit(0,0);
h:=vround(h-2stem_corr);
pen_duplicate(1,2);
pickup tiny.nib; pos1(.5cap_stem,0); pos2(.5cap_stem,0);
lft x.G1l=lft x.G2l=hround(.5w-.5cap_stem-.5cap_interspace); top y1=h; bot y2=0;
Delta_x = hround(cap_interspace+.5cap_stem);
double filldraw stroke z1e--z2e; % double stem
pickup crisp.nib; pos3(slab,90); pos4(hair,0);
top y3r=h; x3=x1; rt x4r=hround(w-.65u); y4=good.y(y3l-beak)-eps;
arm(3,4,e,beak_darkness,.7beak_jut);  % right arm and beak
pos5(hair,180); x5=w-x4; y5=y4;
arm(3,5,f,beak_darkness,-.7beak_jut);  % left arm and beak
if serifs: dish_biserif(2,1,c,1/3,1.414cap_jut,cd,1/3,d,1/3,1.414cap_jut);  % lower serif
 nodish_biserif(1,2,a,1/3,.5cap_jut,ab,1/3,b,1/3,.5cap_jut);  % upper bracketing
else: mid_biserif(2,1,cd,1/3); fi  % lower terminal
math_fit(-.75cap_height#*slant,ic#-2.5u#); penlabels(G1,D1,G2,D2,3,4,5,6); endchar;

cmchar "The letter U";
beginchar("U",13u#+.5width_adj#,cap_height#,0);
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
if monospace: adjust_fit(cap_serif_fit#-.5u#,cap_serif_fit#-.5u#)
else: adjust_fit(cap_serif_fit#,cap_serif_fit#) fi;
pen_duplicate(1,2,2');
pickup tiny.nib; pos1(.5cap_stem,0); pos2(.5cap_stem,0);
pos2'(.5cap_stem,180); z2'=z2;
pos3(cap_band,-90);
pos4(cap_hair,0); pos5(cap_hair,0);
x1=x2; x3=.5[x.G1,x5]; x4=x5; x.G1l=w-x5r;
top y1=top y5=h; y2=y4=1/3h; bot y3r=-o;
lft x.G1l=hround max(2u,3u-.5cap_stem-.5cap_interspace);
Delta_x = min(hround(cap_interspace+.5cap_stem),x3r-x.G2r);
double filldraw stroke z1e--z2e; % left (double) stem
filldraw stroke pulled_arc.e(G2',3)
 & pulled_arc.e(3,4)&z4e--z5e; % arc and right stem
define_lower(D3,D2',pulled_arc.r(G2',3));
filldraw stroke z.D2'e--z.D3e; % inner stroke
if serifs: dish_biserif(1,2,a,1/3,cap_jut,ab,1/3,b,1/3,cap_jut); % left serif
 dish_serif(5,4,c,1/2,cap_jut,d,1/2,cap_jut)(dark);  % right serif
else: mid_biserif(1,2,ab,1/3); fi % left terminal
math_fit(-cap_serif_fit#-.3cap_height#*slant-min(cap_height#*slant,u#),
 max(.5ic#-.5u#,0)); penlabels(G1,D1,G2,D2,3,D3,4,5); endchar;

cmchar "The letter V";
beginchar("V",13u#,cap_height#,0);
italcorr cap_height#*slant+.25u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric left_stem,right_stem,outer_jut,alpha;
left_stem=cap_stem-stem_corr;
right_stem=min(cap_hair if hefty:-2stem_corr fi,left_stem);
pen_duplicate(1,2);
outer_jut=.75cap_jut; x.G1l=w-x4r=l+letter_fit+outer_jut+.25u; y1=y4=h;
x2-x1=x4-x3; x.D2r=x3r; y2=y3=-apex_o;
alpha=diag_ratio(2,right_stem,y1-y2,x4r-x.G1l-apex_corr-cap_interspace);
penpos1(alpha*.5left_stem,0); penpos2(alpha*.5left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
Delta_x = hround(alpha*(cap_interspace+.5left_stem));
z0=whatever[z.D1r,z.D2r]=whatever[z3l,z4l];
if y0>cap_notch_cut: y0:=cap_notch_cut;
  fill z0+.5right{up}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)
    --diag_end(4r,3r,1,1,D2l,D1l)--diag_end(D2l,D1l,1,1,D1r,D2r){z2-z1}
    ...{down}z0+.5left--cycle; % left and right diagonals
else: fill z0--diag_end(0,4l,1,1,4r,3r)--diag_end(4r,3r,1,1,D2l,D1l)
    --diag_end(D2l,D1l,1,1,D1r,0)--cycle; fi % left and right diagonals
fill diag_end(G2l,G1l,1,1,G1r,G2r)--diag_end(G1r,G2r,1,1,G2l,G1l)--cycle; % lower left diagonal
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3);
 prime_points_inside(2,1); pen_duplicate(1',2');
 if rt x.D1'r+cap_jut+.5u+1<=lft x4'l-cap_jut: inner_jut=cap_jut;
 else: rt x.D1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_biserif(1',2,a,1/3,outer_jut,ab,1/3,b,1/2,inner_jut);  % left serif
 mid_biserif(2',1,ab',1/3);  % lower terminal
 dish_serif(4',3,c,.6,inner_jut,d,1/2,outer_jut)(dark);  % right serif
else: mid_biserif(1,2,ab,1/3);  % upper terminal
 mid_biserif(2,1,ab',1/3); fi  % lower terminal
math_fit(.75u#-cap_height#*slant,ic#-4u#); penlabels(0,G1,D1,G2,D2,3,4); endchar;

cmchar "The letter W";
beginchar("W",18u#,cap_height#,0);
italcorr cap_height#*slant+.25u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric stem[],outer_jut,upper_notch,alpha,mid_corr;
outer_jut=.7cap_jut; x.G1l=w-x8r=l+letter_fit+outer_jut+.25u;
stem1=fudged.cap_stem-stem_corr;
stem4=min(fudged.hair if hefty:-2stem_corr fi,stem1);
stem2=stem4 if hefty:-2stem_corr fi;
stem3=stem1 if hefty:-2stem_corr fi;
pen_duplicate(1,2);
x2-x1=x4-x3=x6-x5=x8-x7; x.D2r=x3r; x6l+apex_corr=x7l;
y1=y8=h; y2=y3=y6=y7=-apex_o; y4=y5=if monospace: vround .6 fi\\ h;
mid_corr=if monospace or hefty:-apex_corr else: 1/3jut fi;
alpha=diag_ratio(4,stem2-stem3+stem4,y1-y2,x8r-x.G1l+mid_corr-2apex_corr+cap_interspace);
penpos1(alpha*.5stem1,0); penpos2(alpha*.5stem1,0);
penpos3(alpha*stem2,0); penpos4(alpha*stem2,0);
penpos5(alpha*stem3,0); penpos6(alpha*stem3,0);
penpos7(alpha*stem4,0); penpos8(alpha*stem4,0);
Delta_x = hround(alpha*(cap_interspace+.5stem1));
x4l-x1l=floor(x4l-x1l+.5(x5r+mid_corr-x4r)); % |x5r+mid_corr| $\approx$ |x4r|
z23=whatever[z.D1r,z.D2r]=whatever[z3l,z4l];
z45=whatever[z3r,z4r]=whatever[z5l,z6l];
z67=whatever[z5r,z6r]=whatever[z7l,z8l]; upper_notch=y4-cap_notch_cut;
fill diag_end(D1l,D2l,1,1,3r,4r){z4-z3}
 ...if y45<upper_notch:(x45,upper_notch)+.5left{up}
   --(x45,upper_notch)+.5right{down} else: z45&z45 fi
 ...{z6-z5}diag_end(5l,6l,1,1,7r,8r)--diag_end(7r,8r,1,1,8l,7l){z7-z8}
 ...if y67>cap_notch_cut:(x67,cap_notch_cut)+.5right{down}
   --(x67,cap_notch_cut)+.5left{up} else: z67&z67 fi
 ...{z5-z6}special_diag_end(6,5,4,3){z3-z4}
 ...if y23>cap_notch_cut:(x23,cap_notch_cut)+.5right{down}
   --(x23,cap_notch_cut)+.5left{up} else: z23&z23 fi
 ...{z1-z2}diag_end(D2r,D1r,1,1,D1l,D2l)--cycle;  % diagonals
fill diag_end(G2r,G1r,1,1,G1l,G2l)--diag_end(G1l,G2l,1,1,G2r,G1r)--cycle; % lower left diagonal
if serifs: numeric inner_jut[]; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(5,6); prime_points_inside(8,7);
 prime_points_inside(2,1); pen_duplicate(1',2');
 if monospace: inner_jut1=inner_jut4=1.5cap_jut;
 elseif hefty: inner_jut1=inner_jut4=cap_jut;
 else: fill diag_end(6r,5r,1,1,5l,6l)--.9[z5l,z6l]
   ..{z5-z6}.1[z5r,z6r]--cycle;  % middle stem
  inner_jut2=.7inner_jut1; inner_jut4=1.1inner_jut3;
  if rt x.D1'r+cap_jut+.5u+1<=lft x5'l-.7cap_jut: inner_jut1=cap_jut;
  else: rt x.D1'r+inner_jut1+.5u+1=lft x5'l-inner_jut2; fi
  if rt x5'r+cap_jut+.5u+1<=lft x8'l-1.1cap_jut: inner_jut3=cap_jut;
  else: rt x5'r+inner_jut3+.5u+1=lft x8'l-inner_jut4; fi
  dish_serif(5',6,e,1/3,inner_jut2,f,1/2,inner_jut3); fi  % middle serif
 dish_biserif(1',2,a,1/3,outer_jut,ab,1/3,b,1/2,inner_jut1);  % left serif
 mid_biserif(2',1,ab',1/3);  % lower left terminal
 dish_serif(8',7,c,.6,inner_jut4,d,1/2,outer_jut)(dark);  % right serif
else: mid_biserif(1,2,ab,1/3);  % left serif
 mid_biserif(2,1,ab',1/3); fi  % lower left terminal
math_fit(.75u#-cap_height#*slant,ic#-2.5u#);
penlabels(0,G1,D1,G2,D2,3,4,5,6,7,8,23,45,67); endchar;

cmchar "The letter X";
beginchar("X",13u#,cap_height#,0);
italcorr cap_height#*slant-.25u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric stem[],outer_jut,xjut,alpha[];
stem1=cap_stem-2stem_corr; stem2=min(cap_hair,stem1);
outer_jut=.75cap_jut; xjut= if serifs: (stem1-stem2)/4 else: 0 fi;
pen_duplicate(1,4);
x.G1l=l+letter_fit+.5u+outer_jut; x2r=r-letter_fit-u-outer_jut-xjut;
x3l=l+letter_fit+.25u+outer_jut+xjut; x.D4r=r-letter_fit-.25u-outer_jut;
y1=y2=h; y3=y4=0;
alpha1=diag_ratio(1,stem1+cap_interspace,h,x.D4r-x.G1l);
alpha2=diag_ratio(1,stem2,h,x2r-x3l);
penpos1(alpha1*.5stem1,0); penpos2(alpha2*stem2,0);
penpos3(alpha2*stem2,0); penpos4(alpha1*.5stem1,0);
Delta_x = hround(alpha1*(cap_interspace+.5stem1));
if hefty:
 z12=whatever[z2l,z3l]=whatever[z.D1r,z.D4r];
 z13=whatever[z2l,z3l]=whatever[z.G1l,z.G4l];
 z24=whatever[z2r,z3r]=whatever[z.D1r,z.D4r];
 z34=whatever[z2r,z3r]=whatever[z.G1l,z.G4l];
 fill diag_end(12,D1r,.5,1,D1l,D4l)--diag_end(D1l,D4l,.5,1,D4r,24)--z24
  --diag_end(24,2r,1,.5,2l,12)--z12--cycle; % upper diagonals
 fill diag_end(34,G4l,.5,1,G4r,G1r)--diag_end(G4r,G1r,.5,1,G1l,13)--z13
  --diag_end(13,3l,1,.5,3r,34)--z34--cycle; % lower diagonals
else: forsuffixes e=l,,r: forsuffixes $=G,D:
  z$0e = whatever[z2e,z3e] = whatever[z$1,z$4]; endfor endfor
 fill diag_end(D4r,D1r,.5,1,D1l,D4l)
  --diag_end(D1l,D4l,.5,1,D4r,D1r)--cycle; % upper left diagonal
 fill diag_end(G4r,G1r,.5,1,G1l,G4l)
  --diag_end(G1l,G4l,.5,1,G4r,G1r)--cycle; % lower left diagonal
 fill z.D0l--z.D0r--diag_end(D0r,2r,.5,1,2l,D0l)--cycle;  % upper right diagonal
 fill diag_end(G0l,3l,.5,1,3r,G0r)--z.G0r--z.G0l--cycle; fi  % lower right diagonal
if serifs: numeric inner_jut[]; pickup tiny.nib;
 prime_points_inside(1,4); prime_points_inside(4,1); pen_duplicate(1',4');
 prime_points_inside(2,3); prime_points_inside(3,2);
 if rt x.D1'r+cap_jut+.5u+1<=lft x2'l-cap_jut-xjut: inner_jut1=cap_jut;
 else: rt x.D1'r+inner_jut1+.5u+1=lft x2'l-inner_jut1-xjut; fi
 if rt x3'r+cap_jut+.5u+1<=lft x.G4'l-cap_jut-xjut: inner_jut2=cap_jut;
 else: rt x3'r+inner_jut2+.5u+1=lft x.G4'l-inner_jut2-xjut; fi
 dish_biserif(1',4,a,1/3,outer_jut,ab,1/2,b,2/3,inner_jut1);  % upper left serif
 dish_biserif(4',1,c,2/3,inner_jut2,cd,1/2,d,1/3,outer_jut);  % lower right serif
 dish_serif(2',3,e,2/3,inner_jut1+xjut,
  f,1/2,outer_jut+xjut)(dark);  % upper right serif
 dish_serif(3',2,g,1/2,outer_jut+xjut,
  h,2/3,inner_jut2+xjut)(dark);  % lower left serif
else: mid_biserif(1,4,ab,1/2);  % upper left terminal
 mid_biserif(4,1,cd,1/2); fi  % lower right terminal
math_fit(0,.5ic#); penlabels(0,G0,D0,G1,D1,2,3,G4,D4,12,13,24,34); endchar;

cmchar "The letter Y";
beginchar("Y",13u#,cap_height#,0);
italcorr cap_height#*slant+.45u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric left_stem,right_stem,outer_jut,dy,alpha;
left_stem=cap_stem-stem_corr;
right_stem=min(cap_hair if hefty:-2stem_corr fi,left_stem);
pen_duplicate(1,2,5,6);numeric theta;
outer_jut=.75cap_jut; x.G1l=w-x4r=l+letter_fit+outer_jut+.05u;
x.G2l=x.G5l=hround(.5w-.5left_stem-.5cap_interspace); x3r=x.D5r; y1l=y4=h; y2l=y3=y5=.4h;
%dy=y1-y2; alpha=((x2l-x1l)++dy)/dy;
%pair dz; forsuffixes $=1,2,6: z$r-z$=z$-z$l=endfor .5dz;
penpos5(.5left_stem,0); penpos6(.5left_stem,0); x5=x6; y6=0;
theta = angle(z2l-z1l)+90; alpha = 1/cosd(.5theta);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
penpos1(alpha*.5left_stem,0);penpos2(.5left_stem,theta); 
%dz=(alpha*.5left_stem,0)rotated theta; x5=x6; y6=0;
Delta_x = hround(alpha*(cap_interspace+.5left_stem));
z0=whatever[z.D1r,z.D2r]=whatever[z3l,z4l];
if y0>y2+cap_notch_cut: y0:=y2+cap_notch_cut;
  fill z0+.5right{up}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)--z.D5r
    --diag_end(D5r,D6r,1,1,D6l,D5l)--z.D5l--diag_end(D2l,D1l,1,1,D1r,D2r){z2-z1}
    ...{down}z0+.5left--cycle; % diagonals and stem
else: fill z0--diag_end(0,4l,1,1,4r,3r)--z.D5r
    --diag_end(D5r,D6r,1,1,D6l,D5l)--z.D5l % z.D5l=z.D2l
    --diag_end(D2l,D1l,1,1,D1r,0)--cycle; fi  % diagonals and stem
fill z.G2r..z.G5r--diag_end(G5r,G6r,1,1,G6l,G5l)--z.G5l--diag_end(G2l,G1l,1,1,G1r,G2r)--cycle;
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3);
 prime_points_inside(6,5); pen_duplicate(1',6');
 if rt x.D1'r+cap_jut+.5u+1<=lft x4'l-cap_jut: inner_jut=cap_jut;
 else: rt x.D1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_biserif(1',2,a,1/3,outer_jut,ab,1/3,b,1/2,inner_jut);  % upper left serif
 dish_serif(4',3,c,.6,inner_jut,d,1/2,outer_jut)(dark);  % upper right serif
 dish_biserif(6',5,e,1/3,cap_jut,ef,1/3,f,1/3,cap_jut);  % lower serif
else: mid_biserif(1,2,ab,1/3);  % upper left terminal
 mid_biserif(6,5,ef,1/3); fi  % lower terminal
math_fit(.5u#-cap_height#*slant,ic#-4u#);
penlabels(0,G1,D1,G2,D2,3,4,G5,D5,G6,D6); endchar;

cmchar "The letter Z";
beginchar("Z",11u#,cap_height#,0);
italcorr cap_height#*slant-.5u#;
adjust_fit(0,0);
numeric arm_thickness[],z_stem;
if hefty: arm_thickness1=Vround(slab-vair_corr); arm_thickness2=slab;
 z_stem=max(.8[vair,.5cap_stem],tiny+eps);
else: arm_thickness1=slab; arm_thickness2=vround(slab+vair_corr);
 z_stem=max(.9[vair,.5cap_stem],tiny+eps); fi
pen_duplicate(1,2,3,4);
pickup tiny.nib;
x3l=x4l=w-x1r=w-x2r; lft x.G3l=hround u;
top y1=h; y2=min(y1,h-2/3arm_thickness1);
bot y4=0; y3=max(y4,2/3arm_thickness2);
numeric alpha; alpha=diag_ratio(1,z_stem+cap_interspace-tiny,y2-y3,x.D2r-x.G3l);
penpos1(alpha*(z_stem-tiny),0);penpos2(alpha*(z_stem-tiny),0);
penpos3(alpha*(z_stem-tiny),0);penpos4(alpha*(z_stem-tiny),0);
Delta_x = hround(alpha*(cap_interspace+z_stem));
pair delta; delta=penoffset z3-z2 of currentpen;
double
fill top lft z1l--z2l+delta---z3l+delta..lft z3l---lft z4l..bot z4l
 ---bot rt z4r--z3r-delta---z2r-delta..rt z2r---rt z1r..top z1r
 ---cycle;  % double diagonal
pickup crisp.nib; pos5(arm_thickness1,90); pos6(hair,180);
top y5r=h; x5=x.D1; lft x6r=hround 1.25u; y6=good.y(y5l-beak)-eps;
arm(5,6,a,beak_darkness**.8,-.4beak_jut);  % upper arm and beak
pos7(arm_thickness2,-90); pos8(hair,0);
bot y7r=0; x7=x.G4; rt x8r=hround(w-.9u); y8=good.y(y7l+1.2beak)+eps;
arm(7,8,b,beak_darkness**.9,.6beak_jut);  % lower arm and beak
math_fit(0,.5ic#); penlabels(G1,D1,G2,D2,G3,D3,G4,D4,5,6,7,8); endchar;


