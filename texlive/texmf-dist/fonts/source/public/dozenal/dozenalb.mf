% +AMDG  This program was begun on 14 November 2008, the
% feast of St. Josaphat, and it is humbly dedicated to him
% and to the Immaculate Heart of Mary for their prayers, and
% to the Sacred Heart of Jesus for His mercy.

define_pixels(u,fig_height,cap_stem,thin_join,o,stem,stem,bar,tiny);
define_blacker_pixels(thin_join, curve);

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Prepare the character for the numeral X                 %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

beginchar("X",9u#,fig_height#,0); "The Numeral X";

z1 = (w-u, h+o/2);
z2 = (w-u, (h+o/2)-1.5*cap_stem);
z3 = z2 - (2thin_join, 0);
x4 = (w-u-2thin_join);
y4 = ((h+o/2) - ((2cap_stem/3)));
z5 = (u+2thin_join, ((h+o/2) - ((2*cap_stem)/3)));
z6 = (w-u, fig_height/3);
z7 = (w/2, 0-o);
z8 = (u, fig_height/4);
z9 = z8 + (5tiny/2, 5tiny/2); 
z10 = z8 + (4tiny,0);
z11 = z10 - (3tiny, 0);
z12 = z7 + (0, 1.5thin_join);
z13 = z6 - (cap_stem, 0);
z14 = z5 - (2thin_join, 0);
z15 = (u, h+o/2);

path X;
X = z1--z2--z3--z4--z5{z6-z5}..z6..{left}z7{left}..{up}z8..z9..z10..{up}z11{down}..{right}z12{right}..{up}z13..{z14-z13}z14--z15--cycle;

fill X reflectedabout ((w/2,0),(w/2,h));

penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);

endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Prepare the character for the numeral E                 %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

beginchar("E",9u#,fig_height#,0); "The Numeral E";

z1 = (w/2, h+o);
z2 = (w/2, 0-o);
z3 = (w/2, h/2);
z4 = (w-u, h/4-5tiny/2);
z5 = (w-u, 3h/4+5tiny/2);
z6 = (u-o, (h/4));
z7 = (u-o, (3h/4));
z8 = z9 + (tiny,0);
z9 = z4 - (4tiny, 0);
z10 = z9 + (5tiny/2, 5tiny/2);
z11 = z12 + (tiny,0);
z12 = z5 - (4tiny, 0);
z13 = z12 + (5tiny/2, -5tiny/2);
z14 = z2 + (0,1.9thin_join);
z15 = z6 + (cap_stem, 0);
z16 = z3 + (cap_stem, -thin_join/2);
z17 = z16 + (0, thin_join);
z18 = z7 + (cap_stem, 0);
z19 = z1 - (0, 1.5thin_join);

fill z5..{left}z1{left}..{down}z7{down}..{right}z3{left}..{down}z6{down}..{right}z2{right}..z4..z10..z9..{up}z8{down}..{left}z14{left}..{up}z15{up}..{right}z16--z17{left}..{up}z18{up}..{right}z19{right}..{down}z11{up}..{down}z12..z13..cycle;

penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19);

endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Prepare the character for the tally mark 1              %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

beginchar("1",9u#,fig_height#,0); "The Tally Mark 1";

z0 = (w/2,h+o);
z1 = (w/2,0-o);

pickup pencircle xscaled 8 rotated 45;
draw z0--z1;

endchar;

beginchar("2",fig_height#+u#,fig_height#,0); "The Tally Mark 2";

z0 = (0+o,h+o);
z1 = (0+o,0-o);
z2 = (h-o,0-o);

pickup pencircle xscaled 8 rotated 45;
draw z0--z1--z2;

endchar;

beginchar("3",fig_height#+u#,fig_height#,0); "The Tally Mark 3";

z0 = (0+u,h+o);
z1 = (0+u,0-o);
z2 = (h-o,0-o);
z3 = (h-o,h+o);

pickup pencircle xscaled 8 rotated 45;
draw z0--z1--z2--z3;
endchar;

beginchar("4",fig_height#+u#,fig_height#,0); "The Tally Mark 4";

z0 = (0+u,h+o);
z1 = (0+u,0-o);
z2 = (h-o,0-o);
z3 = (h-o,h+o);

pickup pencircle xscaled 8 rotated 45;
draw z0--z1--z2--z3--z0;
endchar;

beginchar("5",fig_height#+u#,fig_height#,0); "The Tally Mark 5";

z0 = (0+u,h+o);
z1 = (0+u,0-o);
z2 = (h-o,0-o);
z3 = (h-o,h+o);

pickup pencircle xscaled 8 rotated 45;
draw z0--z1--z2--z3--z0;
pickup pencircle xscaled 6 rotated 45;
draw z0--z2;
endchar;

beginchar("6",fig_height#+u#,fig_height#,0); "The Tally Mark 6";

z0 = (0+u,h+o);
z1 = (0+u,0-o);
z2 = (h-o,0-o);
z3 = (h-o,h+o);

pickup pencircle xscaled 8 rotated 45;
draw z0--z1--z2--z3--z0;
pickup pencircle xscaled 6 rotated 45;
draw z0--z2;
pickup pencircle xscaled 6 rotated -45;
draw z3--z1;
endchar;


end;
