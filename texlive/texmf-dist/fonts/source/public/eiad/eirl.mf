%% This file is public domain.
%% Originally written 1998, Ivan A Derzhanski.
%% Notice added by Clea F. Rees 2009/01/06.
%% 
% eirl.mf: Irish lower case (minuscules)

def gen_letter =
adjust_fit(serif_fit#-2stem_shift# if monospace:-.5u# fi,
  serif_fit#-2stem_shift# if monospace:-.5u# fi);
numeric left_stem,right_stem,alpha,beta;
right_stem=curve-stem_corr;
left_stem=min(hair if hefty: -3stem_corr fi,right_stem);
x1l=w-x4r=l+letter_fit+.5u; y4=0;
y1=.1bar_height;
x2-x1=x4-x3+1.5u; x3r=x2r+apex_corr; y2=y3=x_height+apex_o+apex_oo;
alpha=diag_ratio(2,left_stem,y2-y1,x4r-x1l-apex_corr);
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*left_stem,0);
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
if y0<x_height-notch_cut: y0:=x_height-notch_cut;
 fill z0+.5right{z4l-z3l}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)
  --diag_end(4r,3r,1,1,2l,1l)--diag_end(2l,1l,1,1,1r,2r){z2-z1}
  ...{up}z0+.5left--cycle; % left and right diagonals
else: fill z0{z4r-z3r}..diag_end(0,4l,1,1,4r,3r)--diag_end(4r,3r,1,1,2l,1l)
  --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
beta=angle(z1-z2);
penpos5(stem,beta); z5=whatever[z1,z2];
penpos6(vair',angle(z4-z3)); z6=whatever[z3,z4];
y5=.4[y1,y0]; y6=.4[y4,y0];
penpos7(stem,beta); x7r=.5(w-u); bot y7r=-oo;
filldraw stroke z5e{z7r-z5l} ... z7e .. {z6l-z7r}z6e;
penlabels(0,1,2,3,4,5,6,7); enddef;

guta_beag ("ailm", 10u#, 2);

def gen_letter =
italcorr .7x_height#*slant+.5curve#-u#;
adjust_fit(serif_fit#,0);
pickup tiny.nib;
pos1(stem,0); pos2(stem,0); x1=x2;
lft x1l=hround(2.5u-.5stem); top y1=h; y2=.5x_height;
filldraw stroke z1e--z2e;  % stem
if serifs: trasnawn(1,a,jut); fi  % serif
pickup fine.nib; pos2'(stem,-180); pos3(vair,-90);
pos4(curve,0); pos5(vair,90); pos0(hair,180);
z0=z2=z2'; x3=x5=.5[x2,x4]; rt x4r=hround min(w-1.35u+.5curve,w-.6u);
bot y3r=-oo; top y5r=x_height+oo; y4=y2;
filldraw stroke pulled_arc.e(2',3) & pulled_arc.e(3,4)
 & pulled_arc.e(4,5) & super_arc.e(5,0);  % bowl
penlabels(0,1,2,3,4,5); enddef;

cmchar "beith"; beginchar("b",10u#+serif_fit#,asc_height#,0);
gen_letter; endchar;

cmchar "beith-h";
beginchar(beith_h,10u#+serif_fit#,min(old_asc_height#,10/7x_height#+.5dot_diam#),0);
gen_letter; aspirate(5); endchar;

def gen_letter =
italcorr x_height#*slant-.2u#;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
pickup fine.nib; pos2(vair',90); pos4(vair',270);
x2=x4=.5(w+u); top y2r=vround(x_height+1.5oo); bot y4r=-oo;
pos1(4/7[vair',flare],if serifs: 40 else: 80 fi);
rt x1r=hround(w-.6u); top y1r=vround .82[bar_height,top y2r];
filldraw stroke term.e(2,1,right,.8,4);  % upper terminal
pos3(curve,180); lft x3r=hround max(.6u,1.35u-.5curve); y3=.5x_height;
if serifs:
 pos5(hair,-40); rt x5r=hround(w-.5u);
 y5=max(good.y(.5bar_height-.9),y4l+vair');
 (x,y4l)=whatever[z4r,z5l];
 filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull)
  & term.e(4,5,right,.8,4);  % arc and lower terminal
else:
 pos5(.6[vair',flare],275); rt x5r=hround(w-.5u);
 y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
 forsuffixes e=l,r: path p.e; p.e=z4e{right}..tension .9 and 1..z5e;
  if angle direction 1 of p.e>75:
   p.e:=z4e{right}..tension atleast.9 and 1..{dir 75}z5e; fi endfor
 filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull) & p.e; fi  % arc and lower terminal
penlabels(1,2,3,4,5); enddef;

consan_beag ("coll", if serifs: 8.5u# else: 8u# fi, 0, 4);

def gen_letter =
italcorr x_height#*slant+.5stem#-u#;
adjust_fit(0,if monospace: .5u# else: 0 fi);
pickup fine.nib;
pos1(vair,90); pos2(curve,0); pos3(vair,-90); pos4(curve,-180);
x1=x3=.5[x2,x4]; w-rt x2r=hround max(.5u,1.25u-.5curve);
lft x4r=hround 2u;
y1r=x_height+vround 1.5oo; y2=y4=.5[y1,y3]; y3r=-oo;
filldraw circ_stroke pulled_arc.e(1,4) & pulled_arc.e(4,3)
 & pulled_arc.e(3,2) & pulled_arc.e(2,1);  % bowl
if serifs: pos8(hair,210); lft x8r=hround u; y8=x_height+apex_oo;
 path arc; arc=z1l{left}..{z8-z2}z8r;
 pos9(slab,-60); lft x9l=.5u; y9r=.8x_height;
 filldraw z8r{down}..z9l--z9r{z1-z9}..subpath(.4,0) of arc
   --z1r{left}..{z8-z2}z8l--cycle;  % lower barb
else: pos6(vstem,80); x6=.5u; top y6r=top y1r+oo;
 filldraw stroke z6e{z2l-z6r} .. {right}z1e; fi  % ear
penlabels(1,2,3,4,6,8,9); enddef;

consan_beag ("dair", 11u#, 0, 1);

def gen_letter =
italcorr .5[bar_height#,x_height#]*slant+.5min(curve#-1.5u#,0);
adjust_fit(if monospace: .25u#,.5u# else: 0,0 fi);
numeric left_curve,right_curve;
left_curve=right_curve+6stem_corr=curve if not serifs: -3stem_corr fi;
if right_curve<tiny.breadth: right_curve:=tiny.breadth; fi
if left_curve<tiny.breadth: left_curve:=tiny.breadth; fi
pickup tiny.nib; pos1(right_curve,0);
pos2(vair,90); pos3(left_curve,180);
y1=good.y bar_height; top y2r=x_height+vround 1.5oo; y0l=bot y1;
rt x1r=hround min(w-1.5u,w-2u+.5right_curve);
lft x3r=hround max(.5u,1.25u-.5left_curve); x2=.5[x3l,x1l];
{{interim superness:=more_super;
 filldraw stroke super_arc.e(1,2)}};  % right bowl
y3=.5[y2,y4]; bot y4r=-oo;
if serifs: pos4(vair',270); pos5(hair,360); x4=x2;
 y5=max(good.y(.5bar_height-.9),y4l+vair); x5r=x1r;
 (x,y4l)=whatever[z4r,z5]; x4l:=min(x,x4l+.5u);
 filldraw stroke pulled_arc.e(2,3) & pulled_arc.e(3,4)
  ...{x5-x4,5(y5-y4)}z5e; % left bowl, arc, and terminal
else: pos4(vair,270); x4=.5w+.5u;
 filldraw stroke super_arc.e(2,3) & super_arc.e(3,4);  % left bowl and arc
 pickup fine.nib; pos4'(vair,270); z4=z4';
 pos5(.5[vair,flare],275); rt x5r=hround(w-1.6u);
 y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
 filldraw stroke term.e(4',5,right,1,4); fi  % terminal
path testpath; testpath=super_arc.r(2,3) & super_arc.r(3,4);
y1'r=y0r=y0l+.6[thin_join,vair]; y1'l=y0l; x1'l=x1'r=w-.5u;
forsuffixes $=l,r:
 x0$=xpart(((0,y0$)--(x1,y0$)) intersectionpoint testpath); endfor
fill stroke z0e--z1'e;  % crossbar
penlabels(0,1,1',2,3,4,5); enddef;

guta_beag ("eabhadh", 9u#, 2);

def gen_sail =
italcorr x_height#*slant if not serifs:+.25u# fi;
adjust_fit(serif_fit#+stem_shift#,0);
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
numeric shaved_stem; shaved_stem=hround(stem-2stem_corr);
pos1'(shaved_stem,0); pos2'(shaved_stem,0); pos3(4/7[vair',flare],40);
lft x1l=hround(2.5u-.5stem); x1l=x1'l=x2l=x2'l; rt x3r=hround(w-.6u);
top y1=x_height+min(oo,serif_drop); bot y2=-d; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
 y3=1/2[bar_height,x_height];
 penpos2''(x2r-x2l,0); x2''=x2; y2''=1/8[bar_height,x_height];
 filldraw stroke z2''e--z2e;  % thicken the lower left stem
 penpos.a0(min(rt x2r-lft x2l,thin_join)-fine,180); pickup fine.nib;
 rt x.a0l=tiny.rt x2r; y.a0=y2'';
 pos.a1(vair,90); pos3'(4/7[vair',flare]+tiny,40); z3'=z3;
 x.a1=.5[rt x.a0l,rt x3'r]; top y.a1r=x_height+oo;
 (x.a,y.a1l)=whatever[z.a1r,z.a0l]; x.a1l:=x.a;
 filldraw stroke z.a0e{up}...{right}z.a1e
  &{{interim superness:=hein_super; super_arc.e(a1,3')}};  % arch
if serifs: trasnawn(1',b,jut); fi  % upper left serif
labels(a0); penlabels(a1,1,2,3,4); enddef;

def gen_letter =
numeric arm_thickness; arm_thickness=hround .9[vair,.85stem];
gen_sail;
pickup crisp.nib; bot y13l=bot y14l=.25bar_height;
pos13(arm_thickness,90); pos14(arm_thickness,90);
lft x13=x1; rt x14=x3l; filldraw stroke z13e--z14e;  % bar
penlabels(13,14); enddef;

consan_beag ("fearn", 10u#, desc_depth#, a1);

def gen_letter =
italcorr x_height#*slant+.25u#;
adjust_fit(0,0);
numeric arm_thickness; arm_thickness=hround .9[vair,.85stem];
numeric s_slab; s_slab=if serifs:vair else:Vround .1[vair,stem] fi;
numeric ess'; ess'=max(fine.breadth,ess);
pickup crisp.nib; pos17(arm_thickness,90); pos18(arm_thickness,90);
top y17r=top y18r=x_height; rt x17=hround(w-.8u); lft x18r=hround .9u;
filldraw stroke z17e--z18e;
numeric theta; theta=90-angle(40u,x_height);
slope:=-x_height/40u;  % angle at middle
pickup fine.nib; pos0(ess',theta); pos7(s_slab,-90); x0=x7=.5w;
bot y7r=-d-oo;
pos2(s_slab,-90); z2=(x17-u,y17);
lft x3l=hround 2u; rt x6r=hround(w-1.4u);
x3r-x3l=x6r-x6l=hround .5[s_slab,ess']-fine;
pos12(s_slab,-90); z12=.5[z17l,z18l];
y0=.5[y7l,y12r];
ellipse_set(12l,3l,4l,0l); ellipse_set(12r,3r,4r,0r); y3=y3r;
ellipse_set(7l,6l,5l,0l); ellipse_set(7r,6r,5r,0r); y6=y6r;
interim superness:=more_super;
filldraw stroke super_arc.e(2,3) & z3e{down}
 ..z4e---z5e..z6e{down} & super_arc.e(6,7);  % main stroke
if serifs:
 pos9(slab,-70); lft x9l=hround 1.25u; bot y9r=good.y-5/6d;
 pos8(hair,180); x8=x9+u; y8=apex_oo;
 path arc; arc=z7r{left}....{up}z8r;
 filldraw z8r{down}..z9l--z9r...subpath(.3,0) of arc
   --z7l{left}....{up}z8l--cycle;  % barb
else: pos8(.5[vair,flare],-100);
 lft x8r=hround 1.25u; bot y8r=good.y-5/6d;
 filldraw stroke term.e(7,8,left,1,4); fi  % arc and terminal
penlabels(0,2,3,4,5,6,7,8,9,10,12,17,18); enddef;

consan_beag ("gort", 11u#, desc_depth#, 12);

cmchar "uath";
beginchar("h",10.5u#,asc_height#,0);
italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,
  serif_fit#-2stem_shift# if monospace:-.5u# fi);
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
pos1'(stem',0); pos2'(stem',0); pos3(stem,0);
lft x1l=hround(2.5u-.5stem); x1l=x1'l=x2l=x2'l; x3=w-x1;
top y1=h; bot y2=0; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
h_stroke(2,a,3,4);  % arch and right stem
if serifs: trasnawn(1',b,jut); % upper left serif
 numeric inner_jut; pickup tiny.nib;
 if rt x2r+jut+.5u+1<=lft x4l-jut: inner_jut=jut;
 else: rt x2r+jut+.5u+1=lft x4l-inner_jut; fi fi
penlabels(1,2,3,4); endchar;

def gen_letter =
italcorr h#*slant-serif_fit#-2u#;
adjust_fit(serif_fit#+stem_shift# if monospace:+.25u#fi,
  serif_fit#-2stem_shift# if monospace:-.5u# fi);
pickup tiny.nib; pos1(stem',0); pos2(stem',0);
if odd(w-stem'): change_width; fi
lft x1l=hround(.5w-.5stem'); x1=x2;
top y1=x_height if serifs: +min(oo,serif_drop) fi; bot y2=0;
filldraw stroke z1e--z2e;  % stem
if serifs: trasnawn(1,a,jut); fi  % upper serif
penlabels(1,2,3,4); enddef;

guta_beag ("iodha", 5u#, 1);

cmchar "luis";
beginchar("l",7.5u#,asc_height#,0);
italcorr x_height#*slant if serifs: -.9u# else: -.4u# fi;
adjust_fit(serif_fit#+stem_shift#,if serifs: 0 else: -.5u# fi);
pickup tiny.nib; pos1(stem',180); pos2(stem',180);
lft x1r=lft x2r=hround(2.5u-.5stem);
top y1=h; y2=max(.5bar_height,2vair);
filldraw stroke z1e--z2e;
pickup fine.nib; interim superness:=more_super;
pos3(stem',180); z3=z2;
pos4(vair',-90); bot y4r=-oo; rt x5r=hround(w-u);
if serifs: pos5(hair,0); y5=y3; x4l=.5[x3l,x5l];
 (x,y4r)=whatever[z4l,z5l]; x4r:=max(x,.5[x3r,x4]);
 filldraw stroke super_arc.e(3,4)...{up}z5e;  % stem and hook
else: pos5(vair,-75); top y5l=vround .2[top y4l,bar_height];
 x5l:=good.x x5l; x4l=1/3[x3l,x5l]; x4r:=1/3[x3r,x5r]; y3l:=y3l+.2vair;
 filldraw stroke z3e..super_arc.e(3,4);  % stem and hook
 path p; p=stroke z4e{right}..tension .9 and atleast 1..z5e;  % terminal
 if (xpart(z5l-precontrol 1 of p)<0) or (xpart(z5r-postcontrol 2 of p)<0):
  filldraw stroke z4e{right}...{up}z5e;
 else: filldraw p; fi fi;
if serifs: z1l=z1'r; z1r=z1'l; trasnawn(1',a,jut); fi  % upper serif
penlabels(1,2,3,4,5); endchar;

def gen_letter =
italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,
  serif_fit#-2stem_shift# if monospace:-.5u# fi);
numeric shaved_stem; shaved_stem=hround(mfudged.stem-2stem_corr);
pickup tiny.nib; pos1(mfudged.stem,0); pos2(mfudged.stem,0);
pos1'(shaved_stem,0); pos2'(shaved_stem,0);
pos3(mfudged.stem,0); pos5(mfudged.stem,0);
lft x1l=hround(2.5u-.5stem); x1l=x1'l=x2l=x2'l;
lft x3l=hround(.5w-.5stem); x5-x3=x3-x1;
%if not monospace: r:=hround(x5+x1)+l; fi  % change width for better fit
top y1=x_height+min(oo,serif_drop); bot y2=0; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
h_stroke(2,a,3,4);  % left arch and middle stem
h_stroke(4,b,5,6);  % right arch and right stem
if serifs: trasnawn(1',c,jut); fi  % upper left serif
penlabels(1,2,3,4,5,6); enddef;

consan_beag ("muin", 15u#, 0, 3);

cmchar "nuin";
beginchar("n",10.5u#,x_height#,0);
italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,
  serif_fit#-2stem_shift# if monospace:-.5u# fi);
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
numeric shaved_stem; shaved_stem=hround(stem-2stem_corr);
pos1'(shaved_stem,0); pos2'(shaved_stem,0); pos3(stem,0);
lft x1l=hround(2.5u-.5stem); x1l=x1'l=x2l=x2'l; x3=w-x1;
top y1=h+min(oo,serif_drop); bot y2=0; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
h_stroke(2,a,3,4);  % arch and right stem
if serifs: trasnawn(1',b,jut); fi  % upper left serif
penlabels(1,2,3,4); endchar;

def gen_letter =
italcorr .7x_height#*slant;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
penpos1(vair,90); penpos3(vair',-90);
penpos2(curve,180); penpos4(curve,0);
x2r=hround max(.5u,1.25u-.5curve);
x4r=w-x2r; x1=x3=.5w; y1r=x_height+vround 1.5oo; y3r=-oo;
y2=y4=.5x_height-vair_corr; y2l:=y4l:=.52x_height;
penstroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1) & cycle;  % bowl
penlabels(1,2,3,4); enddef;

guta_beag ("oir", 9u#, 1);

def gen_letter =
italcorr .5x_height#*slant+min(.5curve#-.85u#,-.1u#);
adjust_fit(serif_fit#,0);
pickup tiny.nib; pos1(stem',0); pos2(stem,0);
pos0'(stem',0); pos0(stem,0); z0l=z0'l; x0'=x1; x0=x2;
lft x1l=hround(2.5u-.5stem');
top y1=x_height if serifs: +min(oo,serif_drop) fi;
numeric edge; edge=rt x2r;
pickup fine.nib; pos3(if hefty:thin_join else: hair fi,180);
pos4(vair',90); pos5(curve,0); pos6(vair,-90);
rt x3l=hround(2.5u-.5stem')+stem'; y0=y3; top y3=bar_height;
x4l=w-.5(w-serif_fit)+.5u; top y4r=x_height+oo;
rt x5r=hround min(w-1.35u+.5curve,w-.6u); y5=.5x_height;
x6l=x4l+u; bot y6r=-oo;
(x,y4r)=whatever[z3l,z4l]; x4r:=min(x,.5[x5r,x4]);
%(x',y6r)=whatever[z7l,z6l]; x6r:=min(x',.5[x5r,x6]);
%x6r:=x6r-x4r+x4l;
filldraw stroke z3e{up}...{right}z4e&super_arc.e(4,5)
 &super_arc.e(5,6)&z6e{left};  % bowl
if serifs:
 pos9(slab,-70); lft x9l=rt x3l+.5u; y9r=.05x_height;
 pos8(hair,180); x8=x9+u; top y8=vround .3x_height-1;
 path arc; arc=z6r{left}....{up}z8r;
 filldraw z8r{down}..z9l--z9r...subpath(.3,0) of arc
   --z6l{left}....{up}z8l--cycle;  % barb
else: pos8(1.2flare,-100);
 lft x8r=rt x3l+hround .5u; bot y8r=vround .1x_height-o;
 filldraw stroke term.e(6,8,left,1,4); fi  % arc and terminal
pickup tiny.nib; bot y2=-d;
filldraw stroke z1e--z0'e--z0e--z2e;  % stem
if serifs: trasnawn(1,a,jut); fi  % upper serif
penlabels(0,1,2,3,4,5,6,8,9); enddef;

consan_beag ("peith_bhog", 10.5u#+serif_fit#, desc_depth#, 4);

cmchar "ruis";
beginchar("r",9u#,x_height#,desc_depth#);
italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
numeric shaved_stem; shaved_stem=hround(stem-2stem_corr);
pos1'(shaved_stem,0); pos2'(shaved_stem,0); pos3(vair,0);
lft x1l=hround(2.5u-.5stem); x1l=x1'l=x2l=x2'l; rt x3r=w-u; y3=0;
top y1=h+min(oo,serif_drop); bot y2=-d; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
 penpos2''(x2r-x2l,0); x2''=x2; y2''=bar_height;
 filldraw stroke z2''e--z2e;  % thicken the lower left stem
 penpos5(min(rt x2r-lft x2l,thin_join)-fine,180); pickup fine.nib;
 rt x5l=tiny.rt x2r; y5=y2'';
 pos6(vair,90); x6=.8[rt x5l,rt x3r]; top y6r=x_height+oo;
 pos4(stem,angle(z6-z5)); z4r=z6r;
 filldraw stroke z5e--z6e--z4e..z4e-(0,x_height-bar_height)...z3e;
if serifs: trasnawn(1',b,jut); fi % upper left serif
penlabels(1,2,3,4,5,6); endchar;

let gen_letter = gen_sail;
consan_beag ("sail", 10u#, desc_depth#, a1);

def gen_letter =
italcorr .8x_height#*slant-.4u#;
adjust_fit(0,0);
numeric arm_thickness, hair_vair; arm_thickness=hround .9[vair,.85stem];
hair_vair=min(.25[vair,hair],arm_thickness);
pickup crisp.nib; pos7(arm_thickness,90); pos8(arm_thickness,90);
top y7r=top y8r=x_height; rt x7=hround(w-.8u); lft x8r=hround .9u;
filldraw stroke z7e--z8e;
pickup fine.nib; pos2(min(vair',arm_thickness),90); pos4(vair',270);
x2=x4=.5(w+u); bot y2l=crisp.bot y7l; bot y4r=-oo;
pos3(curve,180); lft x3r=w-rt x5r+.5u=hround 2.1u; y3=.5[y2l,y4l];
if serifs:
 pos5(hair,-40);
 y5=max(good.y(.5bar_height-.9),y4l+vair');
 (x,y4l)=whatever[z4r,z5l];
 filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull)
  & term.e(4,5,right,.8,4);  % arc and lower terminal
else:
 pos5(.6[vair',flare],275);
 y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
 forsuffixes e=l,r: path p.e; p.e=z4e{right}..tension .9 and 1..z5e;
  if angle direction 1 of p.e>75:
   p.e:=z4e{right}..tension atleast.9 and 1..{dir 75}z5e; fi endfor
 filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull) & p.e; fi  % arc and lower terminal
penlabels(2,3,4,5,7,8); enddef;

consan_beag ("teine", 10.5u#, 0, 4);

def gen_letter =
italcorr x_height#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,
  serif_fit#-2stem_shift# if monospace:-.5u# fi);
numeric light_vair; light_vair=vair if hefty: -vround 2vair_corr fi;
if light_vair<fine.breadth: light_vair:=fine.breadth; fi
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
pos3(stem,0); pos4(stem',0);
lft x1l=hround(2.5u-.5stem); x1=x2; x3=w-x1; x3r=x4r;
if serifs: top y1=x_height+min(oo,serif_drop); bot y4=-min(oo,serif_drop);
else: top y1=x_height; bot y4=0; fi
pos0(stem,0); pos0'(stem',0); x0=x3; x0'=x4; y0=y0';
penpos2'(stem-fine,-180); z2'=z2; y3=y1; y2=.5bar_height;
penpos5(max(eps,light_vair-fine),-90); penpos6(thin_join-fine,0);
y6=y0=bar_height; %2/3bar_height;
filldraw stroke z1e--z2e;  % left stem
filldraw stroke z3e..z0e--z0'e..z4e;  % right stem
pickup fine.nib; bot y5r=-oo; x5l=.5w-.25u; lft x6l=tiny.lft x0l;
(x,y5r)=whatever[z5l,z6l]; x5r:=max(x,.5[x5,x2'r]);
filldraw stroke {{interim superness:=hein_super;
  pulled_arc.e(2',5)}} & z5e{right}...{up}z6e;  % arc
if serifs: trasnawn(1,a,jut); % upper left serif
 pickup tiny.nib; trasnawn(3,b,jut); fi % upper right serif
penlabels(1,2,3,4,5); labels(6); enddef;

guta_beag ("uwr", 10.5u#, 5);
