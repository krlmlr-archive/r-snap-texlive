%% This file is public domain.
%% Originally written 1998, Ivan A Derzhanski.
%% Notice added by Clea F. Rees 2009/01/06.
%% 
% gaem7mb.mf
% Irish Caps and Small Caps: Driver File

spanish_shriek=oct"016"; spanish_query=oct"017";
cap_asc_height#:=1.15cap_height#;
cap_height#:=max(.85cap_height#,45/38x_height#);
old_asc_height#:=asc_height#;
asc_height#:=max(.85asc_height#,44/38x_height#);
lower.cap_asc_height#:=cap_asc_height#*lower.cap_height#/x_height#;
serif_drop#:=max(0,6serif_drop#);

mode_setup; font_setup;
define_whole_vertical_pixels(cap_asc_height,old_asc_height);

guta_offset := 0;
input eira;  % diacritics and macros
input eiru;  % upper case (majuscules)
input romand;  % numerals
input italp;  % ampersand, question marks, currency sign
input punct;  % punctuation symbols common to roman and italic text
{{interim ligs:=0; input accent;}}; % accents common to roman and italic text
if ligs>0:
  underbar = ASCII "_"; input comlig; fi  % ligatures common with italic text
input romsub;  % substitutes for ligatures

%%% fine lower
forsuffixes $= u, width_adj, cap_serif_fit, letter_fit,
 body_height, cap_asc_height, cap_height, x_height, bar_height,
 comma_depth, flare, cap_hair, stem, cap_stem, cap_curve, cap_ess,
 cap_jut, beak_jut, beak, slab, cap_bar, cap_band, cap_notch_cut,
 o, apex_o:  $.#:=lower.$.#; endfor
fudge:=lower.fudge;
font_setup; % now try again with |lower| settings
extra_endchar:=extra_endchar&"charcode:=charcode+code_offset";
code_offset:=ASCII"a" - ASCII"A"; guta_offset:=8;
input eiru;  % majuscules (in lowercase positions)

ligtable "!": "`" =: spanish_shriek;
ligtable "?": "`" =: spanish_query;
font_slant slant; font_x_height x_height#;
if monospace: font_normal_space 9u#; % no stretching or shrinking
 font_quad 18u#;
 font_extra_space 9u#;
else: font_normal_space 6u#+2letter_fit#;
 font_normal_stretch 3u#; font_normal_shrink 2u#;
 font_quad 18u#+4letter_fit#;
 font_extra_space 2u#;
% k#:=-.5u#; kk#:=-1.5u#; kkk#:=-2u#; % three degrees of kerning
fi

ligtable "7": "`" =: agus_lig;
ligtable "a": "/" =: ailm_fada;
ligtable "b": "h" =: beith_h;
ligtable "c": "h" =: coll_h;
ligtable "d": "h" =: dair_h;
ligtable "e": "/" =: eabhadh_fada;
ligtable "f": "h" =: fearn_h;
ligtable "g": "h" =: gort_h;
ligtable "i": "/" =: iodha_fada;
ligtable "m": "h" =: muin_h;
ligtable "o": "/" =: oir_fada;
ligtable "p": "h" =: peith_bhog_h;
ligtable "s": "h" =: sail_h;
ligtable "t": "h" =: teine_h;
ligtable "u": "/" =: uwr_fada;

ligtable "A": "/" =: Ailm_fada;
ligtable "B": "h" =: Beith_h, "H" =: Beith_h;
ligtable "C": "h" =: Coll_h, "H" =: Coll_h;
ligtable "D": "h" =: Dair_h, "H" =: Dair_h;
ligtable "E": "/" =: Eabhadh_fada;
ligtable "F": "h" =: Fearn_h, "H" =: Fearn_h;
ligtable "G": "h" =: Gort_h, "H" =: Gort_h;
ligtable "I": "/" =: Iodha_fada;
ligtable "M": "h" =: Muin_h, "H" =: Muin_h;
ligtable "O": "/" =: Oir_fada;
ligtable "S": "h" =: Sail_h, "H" =: Sail_h;
ligtable "T": "h" =: Teine_h, "H" =: Teine_h;
ligtable "U": "/" =: Uwr_fada;

 % there are ligature/kern programs
 % for |"-"|, |"`"|, and |"'"| in the {\tt comlig} file
bye.
