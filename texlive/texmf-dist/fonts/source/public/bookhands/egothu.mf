%%
%% This is file `egothu.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% egothic.dtx  (with options: `maj')
%% 
%%  Copyright 1999 Peter R. Wilson
%% 
%%  This program is provided under the terms of the
%%  LaTeX Project Public License distributed from CTAN
%%  archives in directory macros/latex/base/lppl.txt.
%% 
%% Author: Peter Wilson (CUA)
%%         now at: peter.r.wilson@boeing.com
%% 

cmchar "EGOTH letter A";
beginchar("A", 2cap_sb#+caprat*hstretch*(14/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pair pr[];
pickup capquill;
x100 = 0;
n1 := sqrcjut;
n2 := sqrcjutrat*sqrcjut;
%% legs
x1 = 6/16w; top y1=h;
rt x3 = w-pcshiftx - n1; bot y3= 0;
pickup capthinquill;
lft x2 = pcshiftx; bot y2 = 0;
x21 = x2 + n2; y21 = y2;
y22 = y21+n2; z22 = whatever[z2,z1];
pickup capquill;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z3;  draw (x1-n1,y1)--(x1,y1); draw (x3,y3)--(x3+n1,y3);  %% right leg
pickup capthinquill;
draw z1--z2;                        %% left leg
filldraw z2--z21{left}..{z1-z2}z22--cycle;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,11,12,13,14,15,100);
endchar; % end "A"

cmchar "EGOTH letter B";
beginchar("B", 2cap_sb#+caprat*hstretch*(14/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
path pth[];
pcshiftx := cap_sb;
pickup capquill;
x100 = 0;
n1 := sqrcjut;
%% stem
lft x1 = pcshiftx + n1; top y1 = h;
x2 = x1; bot y2 = 0;
z12 = 1/2[z1,z2];
%% bottom bowl
lft x21 = pcshiftx; bot y21 = 0;
z22=z2;
lft x27 = rt x12; y27 = y12;
rt x25 = w-pcshiftx; y25 = 1/2[y22,y27];
pth1 := z21--z22{right}..z25{up}..{left}z27;
%% top bowl
z32 = z27; z37 = z1;
x38 = x21; y38 = y37;
rt x35 = x25; y35 = 1/2[y32,y37];
pth2 := z38--z37{right}..z35{down}..{left}z27;
if ductus:
  pickup stylus;
fi
draw z1--z2;
draw pth1; draw pth2;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,100);
endchar; % end "B"

cmchar "EGOTH letter C";
beginchar("C", 2cap_sb#+caprat*hstretch*(14/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
pcshiftx := cap_sb;
pickup capquill;
n1:=w-2pcshiftx;     % actual letter width
n2:=6/5w-2pcshiftx;  % width of full bowl
x1=pcshiftx + 1/2n1; top y1 = h;
lft x2 = pcshiftx; y2 = 1/2h;
x3 = x1; bot y3 = 0;
rt x4 = w-pcshiftx; y4 = 1/4h;
x24 = x4; y24 = 3/4h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z24..z1{left}..z2{down}..z3{right}..z4{3pdir};
ductus:=false;
penlabels(1,2,3,4,5,6,14,16,17,18,100);
endchar; % end "C"

cmchar "EGOTH letter D";
beginchar("D", 2cap_sb#+caprat*hstretch*(14/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
path pth[];
pcshiftx := cap_sb;
pickup capquill;
x100 = 0; n1 := rt x100 - lft x100;
%% stem
lft x1 = pcshiftx+sqrcjut; top y1=h;
x2 = x1; bot y2 = 0;
%% bowl
lft x3 = pcshiftx; y3 = y1;
z7 = (x3,y2);
rt x5 = w-pcshiftx; y5 = 1/2[y3,y7];
pth1 := z3--z1{right}..{down}z5..{left}z2--z7;
if ductus:
  pickup stylus;
fi
draw z1--z2;    %% stem
draw pth1;
ductus:=false;
penlabels(0,1,2,3,4,5,6,7,8,100);
endchar; % end "D"

cmchar "EGOTH letter E";
beginchar("E", cap_sb#+caprat*hstretch*(9/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup capquill;
x100 = 0;
n1 = rt x100 - lft x100;
%% stem
lft x1 = pcshiftx+sqrcjut; top y1 = h;
x2 = x1; bot y2=0;
%% bars
lft x3 = pcshiftx; y3 = y1; rt x4 = w-pcshiftx; y4=y3;
x5=x1; y5 = 6/16[y1,y2]; x6 = x4; y6 = y5;
x7 = x3; y7 = y2; x8 = x4; y8=y7;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2;  % stem
draw z3--z4; draw z5--z6; draw z7--z8;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,17,70,100);
endchar; % end "E"

cmchar "EGOTH letter F";
beginchar("F", 2cap_sb#+caprat*hstretch*(9/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
pcshiftx := cap_sb;
pickup capquill;
x100 = 0;
n1 := rt x100 - lft x100;
%% stem
lft x1=lft x2=pcshiftx+sqrcjut; bot y1=h; bot y2=0;
%% bars
lft x3 = pcshiftx; y3 = y1; rt x4 = w-pcshiftx; y4 = y3;
x5=x1; y5 = 6/16[y1,y2]; x6 = x4; y6 = y5;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2;
draw z3--z4; draw z5--z6;
ductus:=false;
penlabels(1,2,3,4,5,6,7,14,15,16,100);
endchar; % end "F"

cmchar "EGOTH letter G";
beginchar("G", 2cap_sb#+caprat*hstretch*(14/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
path pth[];
pair vec[];
numeric n[];
pcshiftx := cap_sb;
pickup capquill;
x100 = 0;
n1 := sqrcjut;
%% bar
rt x52 = w - pcshiftx; y52 = 1/2h;
x51 = x52 - 2n1; y51 = y52;
z512 = 1/2[z51,z52];
x54=x512; bot y54 = 0;
%% bowl
lft x2 = pcshiftx; y2 = 1/2h;
x4 = x512; y4 = 1/4h;
x3 = x1 = 1/2[x2,x4]; bot y3 = 0; top y1 = h;
x24 = x512; y24 = 3/4h;
pth1 := z24..z1{left}..z2{down}..z3{right}..z4;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1;
%%draw z4--z512; draw z51--z52;
draw z512--z54; draw (x512-n1,y512)--(x512,y512);
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,11,12,13,24,31,32,33,34,35,36,51,52,512,100);
endchar; % end "G"

cmchar "EGOTH letter H";
beginchar("H", 2cap_sb#+caprat*hstretch*(14/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
pickup capquill;
numeric n[];
x100 = 0;
n1 := sqrcjut;
%% stems
lft x1=lft x2=pcshiftx+sqrcjut;
top y1=h; bot y2=0;
rt x3 = w - pcshiftx - sqrcjut; x4 = x3; y3 = y1; y4 = y2;
%% bar
z12 = 7/16[z1,z2]; z34 = 7/16[z3,z4];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw z3--z4; draw z12--z34;
%% serifs
draw (x1-n1,y1)--(x1,y1); draw (x2,y2)--(x2+n1,y2);
draw (x3-n1,y3)--(x3,y3); draw (x4,y4)--(x4+n1,y4);
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "H"

cmchar "EGOTH letter I";
beginchar("I", 2cap_sb#+caprat*hstretch*(thick#+2sqrcjut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
pickup capquill;
x100 = 0;
n1 := rt x100 - lft x100;
x1 = x2 = 1/2w;
top y1=h; bot y2=0;
%% serifs
lft x111 = pcshiftx; rt x112 = w-pcshiftx;
x211 = x111; x212 = x112;
draw z1--z2;
%% serifs
draw (x111,y1)--(x112,y1);
draw (x211,y2)--(x212,y2);
ductus:=false;
penlabels(1,2,100);
endchar; % end "I"

cmchar "EGOTH letter J";
beginchar("J", 2cap_sb#+caprat*hstretch*(2thick#+sqrcjut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
path pth[];
numeric n[];
pickup capquill;
x100 = 0;
n1 := sqrcjut;
%% stem
rt x1 = w-pcshiftx; top y1 = h;
x2 = x1; y2 = 1/4h;
%% hook
lft x4 = pcshiftx; bot y4 = 0;
pth1 := z4{pdir}..z2{up};
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2{down}..{-pdir}z4;               % stem & hook
draw (x1-n1,y1)--z1;
ductus:=false;
penlabels(1,2,3,100);
endchar; % end "J"

cmchar "EGOTH letter K";
beginchar("K", 2cap_sb#+caprat*hstretch*(16/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
path pth[];
numeric n[];
pickup capquill;
x100 = 0;
n1 := sqrcjut;
%% stem
lft x1 = pcshiftx+n1; top y1 = h;
x2 = x1; bot y2 = 0;
z12 = 1/2[z1,z2];
%% top arm
x3 = rt x12; y3 = y12;
x5 = 1/2[w - pcshiftx, x1]; y5 = y1;
%%pth1 := z3{pdir}..{up}z5;
pth1 := z3--z5;
%% lower arm
z7 = z3;
rt x9 = w-pcshiftx; bot y9 = 0;
pth2 := z7{-ppdir}..{right}z9;
pickup capquill;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw pth1; draw pth2;
draw (x1-n1,y1)--(x1,y1);
draw (x2-n1,y2)--(x2+n1,y2);
draw (x5,y5)--(x5+n1,y5);
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,13,14,100);
endchar; % end "K"

cmchar "EGOTH letter L";
beginchar("L", 2cap_sb#+caprat*hstretch*(9/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
pickup capquill;
x100 = 0;
n1 := sqrcjut;
lft x1=pcshiftx+n1; bot y1=h;
x2=x1; bot y2 =0;
x3 = x2; rt x4 = w-pcshiftx; y3 = y4 = y2;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw z3--z4;
draw (x1-n1,y1)--(x1,y1);
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "L"

cmchar "EGOTH letter M";
beginchar("M", 2cap_sb#+caprat*hstretch*(19/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup capquill;
x100 = 0;
n1 := sqrcjut;
n2 := sqrcjutrat*sqrcjut;
%% thick stem tops
lft x1=pcshiftx+n1; top y1=h;
rt x3 = w - pcshiftx -2n1; y3 = y1;
%% right stem bottom
rt x4 = w - pcshiftx - n1; bot y4 = 0;
%% the V
x24 = 1/2w; bot y24 = 0;
%% left stem top and bottom
x81 = lft x1; y81 = top y1;
pickup capthinquill;
lft x11 = x81; top y11 = y81;
lft x2 = pcshiftx; bot y2 = 0;
x2a = x2+n2; y2a = y2;
y2b = y2+n2; z2b = whatever[z2,z11];
pickup capquill;
%% thin middle top and bottom
x83 = lft x3; y83 = top y3;
x824 = rt x24; y824 = bot y24;
pickup capthinquill;
lft x13 = x83; top y13 = y83;
rt x124 = x824; bot y124 = y824;
pickup capquill;
pth124 := z1{-ppdir}..z24;
pth34 := z3{down}..z4;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
%% thick legs
draw pth124; draw pth34;
draw (x1-n1,y1)--(x1,y1); draw (x3-n1,y3)--(x3,y3); draw (x4,y4)--(x4+n1,y4);
%% thin legs
pickup capthinquill;
draw z11--z2;
filldraw z2--z2a{left}..{z11-z2}z2b--cycle;
%%%%pickup quarterquill;
 draw z13--z124;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,13,14,33,34,100);
endchar; % end "M"

cmchar "EGOTH letter N";
beginchar("N", 2cap_sb#+caprat*hstretch*(14/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
path pth[];
numeric n[];
pickup capquill;
x100 = 0;
n1 := sqrcjut;
n2 := sqrcjutrat*sqrcjut;
%% diagonal
lft x1=pcshiftx+n1; top y1=h;
rt x4 = w -pcshiftx-n1; bot y4 = 0;
x101 = lft x1;
x104 = rt x4;
pth1 := z1{-ppdir}..z4;
%% stems
pickup capthinquill;
lft x21 = x101; top y21 =h;
x22 = x21; bot y22 = 0;
x22a = x22+n2; y22a=y22;
x22b = x22; y22b=y22+n2;
rt x24 = x104; bot y24 = 0;
x23 = x24; top y23 = h;
x23a = x23-n2; x23b = x23+n1; y23a = y23b = y23;
x23c = x23; y23c = y23-n2;
pickup capquill;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
%% diagonal
draw pth1; draw (x1-n1,y1)--(x1,y1);
%% stems
pickup capthinquill;
draw z21--z22;
filldraw z22--z22a{left}..{up}z22b--cycle;
draw z23--z24;
filldraw z23--z23a{right}..{down}z23c{up}..{right}z23b--cycle;
ductus:=false;
penlabels(1,2,3,4,5,6,13,14,23,23a,23b,23c,100);
endchar; % end "N"

cmchar "EGOTH letter O";
beginchar("O", 2cap_sb#+caprat*hstretch*(17/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
pickup capquill;
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h+o; bot y4=-o;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2..z3..z4..cycle;
ductus:=false;
penlabels(1,2,3,4,5,100);
endchar; % end "O"

cmchar "EGOTH letter P";
beginchar("P", 2cap_sb#+caprat*hstretch*(13/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
path pth[];
numeric n[];
pickup capquill;
x100 = 0;
n1:= sqrcjut;
%% stem
lft x1=lft x2=pcshiftx+n1;
top y1=h; bot y2=0;
z12 = 1/2[z1,z2];
%% bowl
lft x3 = pcshiftx; y3 = y1;
x7 = x12; top y7 = y12;
rt x5 = w-pcshiftx; y5 = 1/2[y3,y7];
pth1 := z3--z1{right}..z5{down}..{left}z7;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw pth1;
%%draw (x2-n1,y2)--(x2+n1,y2);
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,100);
endchar; % end "P"

cmchar "EGOTH letter Q";
beginchar("Q", 2cap_sb#+caprat*hstretch*(17/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup capquill;
%% bowl
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h+o; bot y4=-o;
pth1 := z1..z4..z3..z2..cycle;
%% tail
n1 := rt x1 - lft x1;
pth2 := z1..z2..z3..z4;
z5 = directionpoint pdir of pth1;
x7 = x3 + 2n1; top y7 = -n1;
pth3 := z5{-ppdir}..{pdir}z7;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1;
draw pth3;
ductus:=false;
penlabels(0,1,2,3,4,5,6,7,8,100);
endchar; % end "Q"

cmchar "EGOTH letter R";
beginchar("R", 2cap_sb#+caprat*hstretch*(15/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup capquill;
x100 = 0;
n1:= sqrcjut;
n3 := sqrcjutrat*sqrcjut;
n2 := 11/15;    %% P/R width ratio
%% stem
pickup capthinquill;
lft x1=lft x2=pcshiftx+n1;
top y1=h; bot y2=0;
x2a = x2 + n3; y2a = y2;
x2b = x2; y2b = y2+n3;
z12 = 1/2[z1,z2];
pickup capquill;
%% bowl
lft x3 = pcshiftx; top y3 = h; x13 = x1; y13 = y3;
lft x7 = x12; top y7 = y12;
rt x5 = w*n2 - pcshiftx; y5 = 1/2[y3,y7];
pth1 := z3--z13{right}..z5{down}..{left}z7;
%% leg
z21 = z7;
rt x23 = w-pcshiftx; bot y23 = 0;
pth2 := z21{-ppdir}..{right}z23;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1; draw pth2;
pickup capthinquill;
draw z1--z2;
filldraw z2--z2a{left}..{up}z2b--cycle;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,100);
endchar; % end "R"

cmchar "EGOTH letter S";
beginchar("S", 2cap_sb#+caprat*hstretch*(15/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
path pth[];
pcshiftx := cap_sb;
pickup capquill;
x100 = 0;
n1 := rt x100 - lft x100;
x1 = x3 = x5 = 1/2w;
top y1 = h; bot y5 = 0; y3 = 1/2[y1,y5];
lft x2 = pcshiftx; rt x4 = w-pcshiftx;
y2 = 1/2[y1,y3]; y4 = 1/2[y3,y5];
rt x101 = w-pcshiftx; top y101 = y1;
lft x102 = pcshiftx; bot y102 = y5;
pth1 := z101..z1..z2..z3..z4..z5..z102;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1;
ductus:=false;
penlabels(1,2,3,4,5,6,7,11,17,20,26,27,28,100);
endchar; % end "S"

cmchar "EGOTH letter T";
beginchar("T", 2cap_sb#+caprat*hstretch*(10/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup capquill;
%% bar
lft x1 = pcshiftx; rt x2=w-pcshiftx;
top y1 = top y2 = h;
pth1 := z1{pdir}..{pdir}z2;
%% stem
z3 = 1/2[z1,z2];
x4 = x3; bot y4 = 0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1;             % bar
draw z3--z4;           % stem
ductus:=false;
penlabels(1,2,3,4,5,6,11,12,100);
endchar; % end "T"

cmchar "EGOTH letter U";
beginchar("U", 2cap_sb#+caprat*hstretch*(15/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup capquill;
x100 = 0;
n1 := sqrcjut;
%% left stem
lft x1 = pcshiftx + n1; top y1 = h;
lft x13=pcshiftx; y13=1/3h;
rt x3=w-pcshiftx-n1; y3=y1;
x4=x3; bot y4=0;
z33=1/4[z4,z3];  % start of bowl
x14=1/3[x1,x3]; bot y14=0;
pth1 := z1..z13{down}..z14{right}..{pdir}z33;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1;
draw (x1-n1,y1)--(x1,y1);
draw z3--z4;
draw (x3-n1,y3)--(x3,y3);
draw (x4,y4)--(x4+n1,y4);
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,11,13,14,16,33,100);
endchar; % end "U"

cmchar "EGOTH letter V";
beginchar("V", 2cap_sb#+caprat*hstretch*(13/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup capquill;
x100 = 0;
n1 := sqrcjut;
n2 := sqrcjutrat*sqrcjut;
%% stem tops
lft x1 = pcshiftx + n1; top y1 = h;
pickup capthinquill;
rt x3 = w - pcshiftx; top y3 = h;
x3a = x3-n2; y3a=y3;
x3b = x3; y3b = y3-n2;
pickup capquill;
%% bottom
x13 = 10/16[x1,x3]; bot y13 = 0;
x83 = rt x13; y83 = top y13;
pth1 := z1{down}..z13;
pth2 := z3{down}..z83;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1;
draw (x1-n1,y1)--(x1,y1);

pickup capthinquill;
draw pth2;
filldraw z3--z3a{right}..{down}z3b--cycle;
ductus:=false;
penlabels(1,2,3,4,5,11,13,14,23,26,100);
endchar; % end "V"

cmchar "EGOTH letter W";
beginchar("W", 2cap_sb#+caprat*hstretch*(22/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup capquill;
x100 = 0;
n1 := sqrcjut;
n2 := sqrcjutrat*sqrcjut;
%% thick leg tops
lft x1 = pcshiftx + n1; top y1 = h;
x3 = 1/2w; y3 = y1;
%% thin leg top
pickup capthinquill;
rt x5 = w-pcshiftx; top y5 = h;
x5a = x5-n2; y5a = y5;
x5b = x5; y5b = y5-n2;
pickup capquill;
%% V bottoms
x13 = 1/2[x1,x3]; bot y13 = 0;
x35 = 1/2[x3,x5]; bot y35 = 0;
pth113 := z1{down}..z13;
pth335 := z3{down}..z35;
x83 = x3; y83 = top y3;
x813 = rt x13; y813 = bot y13;
x835 = rt x35; y835 = bot y35;
%% thin legs
pickup capthinquill;
rt x113 = x813; bot y113 = y813;
rt x135 = x835; bot y135 = y835;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
pickup capquill;
draw pth113; draw pth335; draw (x1-n1,y1)--(x1,y1);
pickup capthinquill;
draw z113..{up}z3; draw z135..{up}z5;
filldraw z5--z5a{right}..{down}z5b--cycle;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,11,13,14,33,34,53,16,100);
endchar; % end "W"

cmchar "EGOTH letter X";
beginchar("X", 2cap_sb#+caprat*hstretch*(17/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
path pth[];
numeric n[];
pcshiftx := cap_sb;
pickup capquill;
x100 = 0;
n1 := sqrcjut;
n2 := sqrcjutrat*sqrcjut;
%% diag top left to bottom right
lft x1=pcshiftx+n1; top y1 = h;
rt x2 = w - pcshiftx - n1; bot y2 = 0;
%% diag top right to bottom left
pickup capthinquill;
rt x3 = w - pcshiftx - n1; top y3 = h;
lft x4 = pcshiftx; bot y4 = 0;
x3a = x3-n2; x3b = x3+n1; y3a = y3b = y3;
y3c = y3-n2; z3c = whatever[z3,z4];
x4a = x4+n2; y4a = y4;
y4b = y4+n2; z4b = whatever[z4,z3];
pickup capquill;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2;
draw (x1-n1,y1)--(x1,y1);
draw (x2,y2)--(x2+n1,y2);
pickup capthinquill;
draw z3--z4;
filldraw z3--z3a{right}..{z4-z3}z3c{z3-z4}..{right}z3b--cycle;
filldraw z4--z4a{left}..{z3-z4}z4b--cycle;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,10,25,251,252,40,50,150,60,160,100);
endchar; % end "X"

cmchar "EGOTH letter Y";
beginchar("Y", 2cap_sb#+caprat*hstretch*(13/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
path pth[];
numeric n[];
pickup capquill;
x100 = 0;
n1 := sqrcjut;
n2 := sqrcjutrat*sqrcjut;
%% upright
x5 = 1/2w; y5 = 1/2h;
x6 = x5; bot y6 = 0;
%% left stem top
lft x1 = pcshiftx + n1; top y1 = h;
%% right stem
x85 = rt x5; y85 = top y5;
pickup capthinquill;
rt x3=w-pcshiftx; top y3 = h;
x4=x85; y4 = y85;
x3a=x3-n2; y3a = y3;
y3b = y3-n2; z3b = whatever[z3,z4];
pickup capquill;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z5--z6; draw (x6-n1,y6)--(x6+n1,y6);  % stem
draw z1--z5; draw (x1-n1,y1)--(x1,y1);     % left arm
pickup capthinquill;
draw z3--z4;
filldraw z3--z3a{right}..{z4-z3}z3b--cycle;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,10,100);
endchar; % end "Y"

cmchar "EGOTH letter Z";
beginchar("Z", 2cap_sb#+caprat*hstretch*(13/13x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
n1 := sqrcjutrat*sqrcjut;
path pth[];
pcshiftx := cap_sb;
pickup capquill;
%% top bar ends
lft x1 = pcshiftx; rt x2 = w -pcshiftx; top y1 = top y2 = h;
x101 = lft x1; y101 = top y1;
%% bottom bar ends
lft x3 = pcshiftx; rt x4 = w -pcshiftx; bot y3 = bot y4 = 0;
x104 = rt x4; y104 = bot y4;
pickup stylus;
lft x11 = x101; top y11 = y101;
x12 = x11; y12 = y11-n1;
x13 = x11+n1; y13 = y11;
rt x14 = x104; bot y14 = y104;
x15 = x14; y15 = y14 + n1;
x16 = x14 - n1; y16 = y14;
pickup capquill;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw z3--z4;
draw z2--z3;
pickup stylus;
filldraw z11--z12{up}..{right}z13--cycle;
filldraw z14--z15{down}..{left}z16--cycle;
ductus:=false;
penlabels(1,2,3,4,5,6,10o,10i,14,14,16,20o,20i,100);
endchar; % end "Z"

