%%
%% This is file `auncll.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% auncial.dtx  (with options: `min')
%% 
%%  Copyright 1999 Peter R. Wilson
%% 
%%  This program is provided under the terms of the
%%  LaTeX Project Public License distributed from CTAN
%%  archives in directory macros/latex/base/lppl.txt.
%% 
%% Author: Peter Wilson (CUA)
%%         now at: peter.r.wilson@boeing.com
%% 

cmchar "AUncial letter a";
beginchar("a", 2u#+hstretch*(x_height#+jut#), x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx+2jut; rt x2=w-u-jut;
top y1=h; bot y2=0;
z3=3/4[z2,z1];
x5=lft x3; y5=bot y3;
z13=1/2[z2,z1];
x15=lft x13; y15=bot y13;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_istem(1,2,srad);  % the stem
pickup thickstylus;         % for the bowl
lft x4=pcshiftx; bot y4=0;
z14=0.2[z4,z5];
draw z14{pdir}...{z5-z4}z15;   % bowl bottom
draw z5--z4;         % bowl top
ductus:=false;
penlabels(1,2,3,4,5,6,14,15,100);
endchar; % end "a"

cmchar "AUncial letter b";
beginchar("b", 2u#+hstretch*(x_height#+jut#), x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=lft x2=pcshiftx+jut;
top y1=h; bot y2=0;
rt x4=w-pcshiftx; y4=1/3h;
lft x5=rt x1+2thin; y5=2/3h;
z6=z1;
rt x7=0.67[x1,x4]; y7=y6;
x9=rt x7-thin; y9=h-thin;
x8=lft x5+thin; y8=bot y5+thin;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tlserif(1,2,srad);                             % the stem
draw z2{right}..z4{up}..{left}z5;                   % bottom bowl
draw z6--z7;                                        % top bowl
pickup stylus;
draw z9--z8;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,100);
endchar; % end "b"

cmchar "AUncial letter c";
beginchar("c", 2u#+hstretch*(x_height#), x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h; bot y4=0;
x5=x6=x3;
y5=0.1h;
top y6=0.95h; y6'=top y6; y6''=bot y6; x6''=lft x6;
pickup stylus;
rt x16=w-pcshiftx; top y16=y6';
x17=x6; y17=y16-jut;
lft x18=x6''; bot y18=y6'';
pickup quill;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z5{-pdir}..{left}z4..z1..z2..z6;
pickup stylus;
filldraw z17..z18--z16--cycle;
ductus:=false;
penlabels(1,2,3,4,5,6,16,17,18,100);
endchar; % end "c"

cmchar "AUncial letter d";
beginchar("d", 2u#+hstretch*(4/3x_height#), 4/3x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; rt x3=w-pcshiftx;
x2=x4=w/2; top y2=3/4h; bot y4=0;
y1=y3=0.5[y4,y2];
lft x5=pcshiftx; top y5=h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z2{-pdir}..z1{down}..z4{right}..z3{up}..z5;
ductus:=false;
penlabels(0,1,2,3,4,5,6,7,8,100);
endchar; % end "d"

cmchar "AUncial letter e";
beginchar("e", 2u#+hstretch*(x_height#), x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h; bot y4=0;
x5=x6=x3;
y5=0.1h;
top y6=0.95h; y6'=top y6; y6''=bot y6; x6''=lft x6;
pickup stylus;
rt x16=w-pcshiftx; top y16=y6';
x17=x6; y17=y16-jut;
lft x18=x6''; bot y18=y6'';
pickup quill;
x7=x1; y7=y1;
x8=w-pcshiftx; y8=y7;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z5{-pdir}..{left}z4..z1..z2..z6; % the bowl
pickup stylus;
filldraw z17..z18--z16--cycle;        % the serif
draw_fork(7,8,jut);                   % the bar
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,10,16,17,18,100);
endchar; % end "e"

cmchar "AUncial letter f";
beginchar("f", 2u#+hstretch*(x_height#), x_height#, desc_depth#);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=lft x2=pcshiftx+jut;
top y1=h; bot y2=-d/2;
x3=x4=x1; y3=y1; bot y4=0;
rt x5= rt x6=w-pcshiftx; y5=y3; y6=y4;
x15=x16=rt x5-1/2thin;
y15=y5; y16=y6;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tlserif(1,2,srad);          % stem
draw_tail(2,jut);
draw z3--z5;                     % top bar
draw_rtriangle(15,jut);
draw z4--z6;                     % bottom bar
draw_rtriangle(16,jut);
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "f"

cmchar "AUncial letter g";
beginchar("g", 2u#+hstretch*(x_height#), x_height#, desc_depth#);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h; bot y4=0;
x5=x6=x3;
y5=0.1h;
top y6=0.95h; y6'=top y6; y6''=bot y6; x6''=lft x6;
pickup stylus;
rt x16=w-pcshiftx; top y16=y6';
x17=x6; y17=y16-jut;
lft x18=x6''; bot y18=y6'';
pickup quill;
x7=rt x5-1/2thin; y7=top y5-1/2thin;
pickup stylus;
lft x8=w/2; bot y8=-d;
pickup quill;
pair vec[]; vec1:=z7-z8; vec2:=z8-z7;
y21=y8+3/2thick; z21=whatever[z8,z7];
y22=y8+   thick; z22=whatever[z8,z7];
x23=x8-thick; y23=y8;
x24=x23+1/2thick; y24=y23+1/2thick;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z5{-pdir}..{left}z4..z1..z2..z6;  % the bowl
pickup stylus;
filldraw z17..z18--z16--cycle;         % the serif
draw z7--z22;                          % the stem
filldraw z22{vec2}..{left}z23--z24{right}..{vec1}z21--cycle;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,16,17,18,21,22,23,24,100);
endchar; % end "g"

cmchar "AUncial letter h";
beginchar("h", 2u#+hstretch*(x_height#+jut#), asc_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=lft x2=pcshiftx+jut;
top y1=h; bot y2=0;
rt x5=w-pcshiftx; y5=3/8h;
lft x6=2/3w; bot y6=0;
x4=0.5[x1,x5]; top y4=3/4h;
x4'=0.5[x4,x1]; y4'=y4;
z4''=z4' shifted (-w*pdir);
z3'=whatever[z4',z4'']=whatever[z1,z2];
x3=rt x3'; y3=y3';
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_istem(1,2,srad);                    % stem
draw z3{pdir}..z4{right}..z5{down}..z6;  % bowl
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "h"

cmchar "AUncial letter i";
beginchar("i", 2u#+hstretch*(thick#+jut#), x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=lft x2=pcshiftx+jut;
top y1=h; bot y2=0;
%%ductus:=true;
draw_tlserif(1,2,srad);
draw_tail(2,jut);
ductus:=false;
penlabels(1,2,100);
endchar; % end "i"

cmchar "AUncial letter j";
beginchar("j", 2u#+hstretch*(thick#+jut#), x_height#, desc_depth#);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=lft x2=pcshiftx+jut;
top y1=h; bot y2=-1/2d;
%%ductus:=true;
draw_tlserif(1,2,srad);
draw_tail(2,jut);
ductus:=false;
penlabels(1,2,100);
endchar; % end "j"

cmchar "AUncial letter k";
beginchar("k", 2u#+hstretch*(4/3x_height#), asc_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=lft x2=pcshiftx+jut;
top y1=h; bot y2=0;
numeric rise,fall,fjut;
x3=rt x1; y3=5/8h;
rise:=1/6h; fall:=-rise/2; fjut:=1/2w;
%%x4=x1; y4=y3;
z4=z3;
x5=w-pcshiftx-jut; y5=0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tlserif(1,2,srad);          % stem
draw_tail(2,jut);
draw_flick(3,rise,fall,fjut);    % upper diag
draw_tstem(4,5,srad);            % lower diag
ductus:=false;
penlabels(1,2,3,4,5,100);
endchar; % end "k"

cmchar "AUncial letter l";
beginchar("l", 2u#+hstretch*(x_height#), asc_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=lft x2=pcshiftx+jut;
top y1=h; bot y2=0;
z3=z2;
x4=w-u; y4=0;
%%ductus:=true;
draw_tlserif(1,2,srad);                    % the stem
draw_fork(3,4,jut);                        % the foot
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "l"

cmchar "AUncial letter m";
beginchar("m", 2u#+hstretch*(2x_height#), x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
rt x1=1/4w; bot y1=0;
lft x2= pcshiftx; y2=h/2;
x4=w/2; y4=2/3h;
x3=0.5[x2,x4]; top y3=h;
x5=x4; y5=y1;
z6=z4;
x7=3/4w; top y7=h;
rt x8=w-pcshiftx; y8=y2;
lft x9=3/4w; y9=y1;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2{up}..z3{right}..{down}z4;      % left half
draw_tstem(4,5,srad);                      % middle stem
draw z6..z7{right}..z8{down}..{-pdir}z9;          % right half
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,100);
endchar; % end "m"

cmchar "AUncial letter n";
beginchar("n", 2u#+hstretch*(2jut#+x_height#), x_height#, 0.5desc_depth#);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx+jut; top y1=h;
x2=x1; bot y2=0;
rt x4=w-pcshiftx-jut; y4=y1;
x3=x4; bot y3=0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tlserif(1,2,srad);              % left stem
draw_tail(2,jut);
draw_istem(4,3,srad);                % right stem
draw z1--z3;                         % diagonal
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "n"

cmchar "AUncial letter o";
beginchar("o", 2u#+hstretch*(7/6x_height#), x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h+o; bot y4=-o;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2..z3..z4..cycle;
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "o"

cmchar "AUncial letter p";
beginchar("p", 2u#+hstretch*(7/6x_height#), x_height#, desc_depth#);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=lft x2=pcshiftx+jut; top y1=h; bot y2=-1/2d;
z3=z1;
rt x5=w-pcshiftx; y5=1/2h;
lft x7=rt x1; bot y7=0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tlserif(1,2,srad);               % stem
draw_tail(2,jut);
draw z3{pdir}..z5{down}..{-pdir}z7;   % bowl
ductus:=false;
penlabels(1,2,3,4,5,6,7,100);
endchar; % end "p"

cmchar "AUncial letter q";
beginchar("q", 2u#+hstretch*(7/6x_height#), x_height#, desc_depth#);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
rt x1=rt x2=w-pcshiftx; top y1=h; bot y2=-1/2d;
z3=z1;
lft x5=pcshiftx; y5=1/2h;
rt x7=lft x1; y7=0.1h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2;                          % right stem
draw_tail(2,jut);
draw z3{left}..z5{down}..{pdir}z7;    % bowl
ductus:=false;
penlabels(1,2,3,4,5,6,7,100);
endchar; % end "q"

cmchar "AUncial letter r";
beginchar("r", 2u#+hstretch*(4/3x_height#), x_height#, 1/2desc_depth#);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=lft x2=pcshiftx+jut; top y1=h; bot y2=0;
lft x3=rt x1-thin; y3=y1;
rt x5=3/4w; y5=3/4h;
lft x7=rt x1; bot y7=1/3h;
z8=z7;
rt x9=w-pcshiftx; bot y9=0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tlserif(1,2,srad);                   % stem
draw_tail(2,jut);
draw z3{pdir}..z5{down}..{-pdir}z7;       % bowl
draw z8{-ppdir}..{right}z9;               % leg
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,100);
endchar; % end "r"

cmchar "AUncial letter s";
beginchar("s", 2u#+hstretch*(x_height#), x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
rt x1=w-pcshiftx; top y1=h-thin;
x2=w/2; top y2=h;
lft x3=pcshiftx; y3=0.75h;
x4=w/2; y4=h/2;
rt x5=w-pcshiftx; y5=0.25h;
x6=w/2; bot y6=0;
lft x7= pcshiftx; bot y7 = thin;
path p[];
p1=z1..z2{left}..z3{down}..z4..z5{down}..z6{left}..z7;
pair vec[];
vec1= direction 0 of p1;
vec2= direction (length p1) of p1;
z11=z1 shifted vec1;
z17=z7 shifted -vec2;
y6'=top y1; y6''=bot y1; x6''=lft x1;
pickup stylus;
rt x26=w-pcshiftx; top y26=y6';
x27=x1; y27=y26-0.75jut;
lft x28=x6''; bot y28=y6'';
pickup quill;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
%%draw_serif(1,11,srad);
%%draw_serif(7,17,srad);
%%draw z1a..z2{left}..z3{down}..z4..z5{down}..z6{left}..z7a;
draw p1;                          % the curve
pickup stylus;
filldraw z27..z28--z26--cycle;    % the serif
ductus:=false;
penlabels(1,2,3,4,5,6,7,11,17,26,27,28,100);
endchar; % end "s"

cmchar "AUncial letter t";
beginchar("t", 2u#+hstretch*(4/3x_height#), x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
numeric alpha; alpha:=0.5jut;
pickup quill;
x1=x2=w/2;
top y1=h; bot y2=0;
lft x3=pcshiftx; rt x4=w-pcshiftx; y3=y4=y1;
x5=pcshiftx+alpha; y5=h-1/2thin;
x6=w-pcshiftx-1/2thin; y6=y5;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tstem(1,2,srad);   % stem
draw z3--z4;   % top bar
draw_triangle(5,2alpha); draw_rtriangle(6,2alpha);
ductus:=false;
penlabels(1,2,3,4,5,100);
endchar; % end "t"

cmchar "AUncial letter u";
beginchar("u", 2u#+hstretch*(4/3x_height#+jut#), x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x6=pcshiftx+jut;
top y6=h-2thin;
lft x7=pcshiftx; y7=h/2;
x8=0.5[x6,x1]; bot y8=0;
rt x1=w-pcshiftx-jut; top y1=h;
x2=x1; bot y2=0;
x5=0.5[x8,x1]; y5=y8;
z5'=z5 shifted (w*pdir);
path p[];
p1=z5--z5';
p2=z1--z2;
z9= p2 intersectionpoint p1;
pair vec[];
p3=z6..z7{down}..{right}z8;
vec3 = direction 0 of p3;
z16=z6 shifted vec3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_serif(6,16,srad);                      % left half
draw z6a..z7{down}..z8{right}..{pdir}z9;
draw_istem(1,2,srad);                       % right stem
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,11,16,100);
endchar; % end "u"

cmchar "AUncial letter v";
beginchar("v", 2u#+hstretch*(7/6x_height#+jut#), x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
numeric midx; midx:= pcshiftx;
lft x1=midx+jut; top y1=h;
x2=x1; y2=h/2;
rt x4=w-pcshiftx; y4=y2;
x3=0.5[x2,x4]; bot y3=0;
x5=x4-thick; y5=y1;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tlserif(1,2,srad);
draw z2{down}..z3{right}..z4{up}..z5;
ductus:=false;
penlabels(1,2,3,4,5,11,100);
endchar; % end "v"

cmchar "AUncial letter w";
beginchar("w", 2u#+hstretch*(2x_height#), x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
numeric midx; midx:= w/2-jut;
lft x1=midx+jut; top y1=h;
x2=x1; y2=h/2;
rt x4=w-pcshiftx; y4=y2;
x3=0.5[x2,x4]; bot y3=0;
x5=x4-thick; y5=y1;
lft x6=pcshiftx+jut;
top y6=h-2thin;
lft x7=pcshiftx; y7=y2;
x8=0.5[x7,x1]; y8=y3;
x15=0.5[x8,x1]; y15=y8;
z15'=z15 shifted (w*pdir);
x2'=x2; y2'=y15;
path p[];
p1=z1--z2';
p2=z15--z15';
z9= p2 intersectionpoint p1;
pair vec[];
p3=z6..z7{down}..{right}z8;
vec3 = direction 0 of p3;
z16=z6 shifted vec3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tlserif(1,2,srad);                      % right half
draw z2{down}..z3{right}..z4{up}..z5;
draw_serif(6,16,srad);                      % left half
draw z6a..z7{down}..z8{right}..z9;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,11,16,100);
endchar; % end "w"

cmchar "AUncial letter x";
beginchar("x", 2u#+hstretch*(4/3x_height#+jut#), x_height#, 1/2desc_depth#);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx+jut; rt x2=w-pcshiftx-jut;
y1=h; y2=0;
lft x3=pcshiftx; bot y3=-d;
rt x5=w-pcshiftx; top y5=h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_istem(1,2,srad);            % thick diagonal
draw z3..{pdir}z5;               % thin diagonal
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,100);
endchar; % end "x"

cmchar "AUncial letter y";
beginchar("y", 2u#+hstretch*(4/3x_height#), x_height#, desc_depth#);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx+jut; top y1=h;
rt x2=3/4w; top y2=0;
pickup stylus;
rt x3=w-pcshiftx; top y3=h;
lft x4=pcshiftx; bot y4=-d;
x5=x3-3thin; y5=y3;
x7=x2-3/2thin; y7=y2;
pickup quill;
x8=w/2; y8=h+jut;
if ductus:
  pickup stylus;
fi
draw_tlserif(1,2,srad);       % left stroke
draw_pdot(8,dot_size);        % the dot
pickup stylus;
filldraw z3..z2..z4 & z4..z7..z5--cycle;  % right stroke
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,10,100);
endchar; % end "y"

cmchar "AUncial letter z";
beginchar("z", 2u#+hstretch*(7/6x_height#), x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x4=pcshiftx; bot y4=0;
x6=w-pcshiftx; y6=0;
x1=x4; top y1=h;
rt x3=6/7w; y3=y1;
x11=pcshiftx+0.5jut; y11=h-thin/2; % y11=h-thin;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z3;    % top stroke
draw_triangle(11,jut);
draw_fork(4,6,jut);
draw z4--z3;    % diagonal
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "z"

