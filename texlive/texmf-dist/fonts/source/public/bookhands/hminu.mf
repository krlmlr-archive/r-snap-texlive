%%
%% This is file `hminu.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% humanist.dtx  (with options: `maj')
%% 
%%  Author: Peter Wilson (CUA) now at peter.r.wilson@boeing.com
%%                             (or pandgwilson@earthlink.net)
%%  Copyright 2002, 2003 Peter R. Wilson
%% 
%%  This work may be distributed and/or modified under the
%%  conditions of the LaTeX Project Public License, either
%%  version 1.3 of this license or (at your option) any
%%  later version.
%%  The latest version of the license is in
%%     http://www.latex-project.org/lppl.txt
%%  and version 1.3 or later is part of all distributions of
%%  LaTeX version 2003/06/01 or later.
%% 
%%  This work has the LPPL maintenance status "author-maintained".
%% 
%%  This work consists of the files listed in the README file.
%% 

cmchar "HMIN letter A";
beginchar("A", 2sb#+caprat*hstretch*(20/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
numeric n[];
path pth[];
pair pr[];
pickup quill;
x100 = 0;
n2 := rt x100 - lft x100;
n1 := w - 2pcshiftx - n2; % effective letter width
%% legs
x1 = pcshiftx + n1/2; top y1=h;
lft x2 = pcshiftx; bot y2 = 0;
rt x3 = pcshiftx + n1; y3=y2;
rt x4 = w-pcshiftx; y4=y3;
%% bar
z12=10/16[z1,z2]; z83=10/16[z1,z3];
z13 = (x83 - n2,y83);
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw z1--z3; draw z12--z13;  %% legs and bar
draw z3--z4; draw z2--(x2+n2,y2); draw (x1-n2,y1)--z1;  %% serifs
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,11,12,13,14,15,100);
endchar; % end "A"

cmchar "HMIN letter B";
beginchar("B", 2sb#+caprat*hstretch*(14/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
path pth[];
pcshiftx := sb;
pickup quill;
x100 = 0;
n1 := rt x100 - lft x100;
%% stem
lft x1 = pcshiftx + n1; top y1 = h;
x2 = x1; bot y2 = 0;
z12 = 1/2[z1,z2];
%% bottom bowl
lft x21 = pcshiftx; bot y21 = 0;
z22=z2;
lft x27 = rt x12; y27 = y12;
rt x25 = w-pcshiftx; y25 = 1/2[y22,y27];
pth1 := z21--z22{right}..z25{up}..{left}z27;
%% top bowl
z32 = z27; z37 = z1;
x38 = x21; y38 = y37;
rt x35 = x25; y35 = 1/2[y32,y37];
pth2 := z38--z37{right}..z35{down}..{left}z27;
if ductus:
  pickup stylus;
fi
draw z1--z2;
draw pth1; draw pth2;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,100);
endchar; % end "B"

cmchar "HMIN letter C";
beginchar("C", 2sb#+caprat*hstretch*(16/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
pcshiftx := sb;
pickup quill;
n1:=w-2pcshiftx;     % actual letter width
n2:=6/5w-2pcshiftx;  % width of full bowl
x1=pcshiftx + 1/2n1; top y1 = h;
lft x2 = pcshiftx; y2 = 1/2h;
x3 = x1; bot y3 = 0;
rt x4 = w-pcshiftx; y4 = 1/4h;
x24 = x4; y24 = 3/4h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z24..z1{left}..z2{down}..z3{right}..z4{pdir};
ductus:=false;
penlabels(1,2,3,4,5,6,14,16,17,18,100);
endchar; % end "C"

cmchar "HMIN letter D";
beginchar("D", 2sb#+caprat*hstretch*(18/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
path pth[];
pcshiftx := sb;
pickup quill;
x100 = 0; n1 := rt x100 - lft x100;
%% stem
lft x1 = pcshiftx+n1; top y1=h;
x2 = x1; bot y2 = 0;
%% bowl
lft x3 = pcshiftx; y3=y1;
x7 = x3; y7=y2;
rt x5 = w-pcshiftx; y5 = 1/2[y3,y7];
pth1 := z3--z1{right}..{down}z5..{left}z2--z7;
if ductus:
  pickup stylus;
fi
draw z1--z2;    %% stem
draw pth1;
ductus:=false;
penlabels(0,1,2,3,4,5,6,7,8,100);
endchar; % end "D"

cmchar "HMIN letter E";
beginchar("E", sb#+caprat*hstretch*(14/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
numeric n[];
path pth[];
pickup quill;
x100 = 0;
n1 = rt x100 - lft x100;
%% stem
lft x1 = pcshiftx+n1; top y1 = h;
x2 = x1; bot y2=0;
%% bars
lft x3 = pcshiftx; y3 = y1; rt x4 = w-pcshiftx; y4=y3;
x5=x1; y5 = 1/2[y1,y2]; x6 = x4; y6 = y5;
x7 = x3; y7 = y2; x8 = x4; y8=y7;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2;  % stem
draw z3--z4; draw z5--z6; draw z7--z8;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,17,70,100);
endchar; % end "E"

cmchar "HMIN letter F";
beginchar("F", 2sb#+caprat*hstretch*(14/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
pcshiftx := sb;
pickup quill;
x100 = 0;
n1 := rt x100 - lft x100;
%% stem
lft x1=lft x2=pcshiftx+n1; top y1=h; bot y2=0;
%% bars
lft x3 = pcshiftx; y3 = y1; rt x4 = w-pcshiftx; y4 = y3;
x5=x1; y5 = 1/2[y1,y2]; x6 = x4; y6 = y5;
%% serif
x211 = x2 - n1; y211 = y2;
x212 = x2 + n1; y212 = y2;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2;
draw z3--z4; draw z5--z6;
draw z211--z212;
ductus:=false;
penlabels(1,2,3,4,5,6,7,14,15,16,100);
endchar; % end "F"

cmchar "HMIN letter G";
beginchar("G", 2sb#+caprat*hstretch*(19/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
path pth[];
pair vec[];
numeric n[];
pcshiftx := sb;
pickup quill;
x100 = 0;
n1 := rt x100 - lft x100;
%% bar
rt x52 = w - pcshiftx; y52 = 1/2h;
x51 = x52 - 2n1; y51 = y52;
z512 = 1/2[z51,z52];
%% bowl
lft x2 = pcshiftx; y2 = 1/2h;
x4 = x512; y4 = 1/4h;
x3 = x1 = 1/2[x2,x4]; bot y3 = 0; top y1 = h;
x24 = x512; y24 = 3/4h;
pth1 := z24..z1{left}..z2{down}..z3{right}..z4;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1;
draw z4--z512; draw z51--z52;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,11,12,13,31,32,33,34,35,36,100);
endchar; % end "G"

cmchar "HMIN letter H";
beginchar("H", 2sb#+caprat*hstretch*(18/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
pickup quill;
numeric n[];
x100 = 0;
n1 := rt x100 - lft x100;
%% stems
lft x1=lft x2=pcshiftx+n1;
top y1=h; bot y2=0;
rt x3 = w - pcshiftx - n1; x4 = x3; y3 = y1; y4 = y2;
%% bar
z12 = 1/2[z1,z2]; z34 = 1/2[z3,z4];
%% serifs
rt x111 = lft x1; y111 = y1; lft x112 = rt x1; y112 = y111;  % TL
x211 = x111; x212 = x112; y211 = y212 = y2;                  % BL
rt x311 = lft x3; y311 = y3; lft x312 = rt x3; y312 = y311;  % TR
x411 = x311; x412 = x4+n1; y411 = y412 = y4;                 % BR
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw z3--z4; draw z12--z34;
draw (x1-n1,y1)--(x1+n1,y1); draw (x2-n1,y2)--(x2+n1,y2);
draw (x3-n1,y3)--(x3+n1,y3); draw (x4-n1,y4)--(x4+n1,y4);
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "H"

cmchar "HMIN letter I";
beginchar("I", 2sb#+caprat*hstretch*(thick#+2jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
numeric n[];
pickup quill;
x100 = 0;
n1 := rt x100 - lft x100;
x1 = x2 = 1/2w;
top y1=h; bot y2=0;
%% serifs
rt x111 = lft x1; lft x112 = rt x1; y111 = y112 = y1;
x211 = x111; x212 = x112; y211 = y212 = y2;
%%ductus:=true;
draw z1--z2;
draw z111--z112; draw z211--z212;
ductus:=false;
penlabels(1,2,100);
endchar; % end "I"

cmchar "HMIN letter J";
beginchar("J", 2sb#+caprat*hstretch*(10/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
path pth[];
numeric n[];
pickup quill;
x100 = 0;
n1 := rt x100 - lft x100;
%% stem serif
rt x112 = w - pcshiftx; top y112 = h;
x111 = x112 - n1; y111 = y112;
%% stem
z1 = 1/2[z111,z112];
x2 = x1; y2 = 1/4h;
%% hook
lft x4 = pcshiftx; y4 = 1/2y2;
x3 = 1/2[x2,x4]; bot y3 = 0;
pth1 := z2{down}..z3{left}..z4;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2;               % stem
draw z111--z112;
draw pth1;
ductus:=false;
penlabels(1,2,3,100);
endchar; % end "J"

cmchar "HMIN letter K";
beginchar("K", 2sb#+caprat*hstretch*(17/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
path pth[];
numeric n[];
pickup quill;
x100 = 0;
n1 := rt x100 - lft x100;
%% stem
lft x1 = pcshiftx+n1; top y1 = h;
x2 = x1; bot y2 = 0;
z12 = 1/2[z1,z2];
%% top arm
x3 = rt x12; y3 = y12;
rt x5 = w - pcshiftx - n1; y5 = y1;
%%pth1 := z3{pdir}..{up}z5;
pth1 := z3--z5;
%% lower arm
z7 = z3;
rt x9 = w-pcshiftx; bot y9 = 0;
pth2 := z7{-ppdir}..{right}z9;
pickup quill;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw pth1; draw pth2;
draw (x1-n1,y1)--(x1+n1,y1);
draw (x2-n1,y2)--(x2+n1,y2);
draw (x5,y5)--(x5+n1,y5);
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,13,14,100);
endchar; % end "K"

cmchar "HMIN letter L";
beginchar("L", 2sb#+caprat*hstretch*(12/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
numeric n[];
pickup quill;
x100 = 0; n1 := rt x100 - lft x100;
lft x1=pcshiftx+n1; top y1=h;
x2=x1; bot y2 =0;
lft x3 = pcshiftx; rt x4 = w-pcshiftx; y3 = y4 = y2;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw z3--z4;
draw (x1-n1,y1)--(x1+n1,y1);
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "L"

cmchar "HMIN letter M";
beginchar("M", 2sb#+caprat*hstretch*(25/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
numeric n[];
path pth[];
pickup quill;
x100 = 0;
n1 := rt x100 - lft x100;
%% left stem
lft x1=pcshiftx+n1; top y1=h;
lft x2=pcshiftx; bot y2=0;
%% right stem
rt x4 = w - pcshiftx - n1; y4 = y2;
x3 = x4 - n1; y3 = y1;
%% the V
z24 = 1/2[z2,z4];
pth1 := z1{-ppdir}..{down}z24;
pth2 := z3{down}..{down}z24;
%% serifs
x111 = x1 - n1; y111 = y1;                        % TL
x212 = x2+n1; y212 = y2;                          % BL
x311 = x3-n1; y311 = y3;                          % TR
x412 = x4+n1; y412 = y4;                          % BR
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw z3--z4; draw pth1; draw pth2;
draw z111--z1; draw z2--z212;
draw z311--z3; draw z4--z412;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,13,14,33,34,100);
endchar; % end "M"

cmchar "HMIN letter N";
beginchar("N", 2sb#+caprat*hstretch*(22/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
path pth[];
numeric n[];
pickup quill;
x100 = 0;
n1 := rt x100 - lft x100;
%% stems
lft x1=pcshiftx+n1; top y1=h; x2=x1; bot y2=0;
rt x3 = w - pcshiftx - n1; x4 = x3; y3 = y1; y4 = y2;
pth1 := z1{-ppdir}..{-ppdir}z4;
%% serifs
x111 = x1 - n1; x112 = x1 + n1; y111 = y112 = y1;  % TL
x211 = x111; x212 = x112; y211 = y212 = y2;        % BL
x311 = x3 - n1; x312 = x3 + n1; y311 = y312 = y3;  % TR
x411 = x311; x412 = x312; y411 = y412 = y4;        % BR
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw z3--z4; draw pth1;
draw z111--z112; draw z211--z212;
draw z311--z312; draw z411--z412;
ductus:=false;
penlabels(1,2,3,4,5,6,13,14,100);
endchar; % end "N"

cmchar "HMIN letter O";
beginchar("O", 2sb#+caprat*hstretch*(18/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
pickup quill;
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h+o; bot y4=-o;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2..z3..z4..cycle;
ductus:=false;
penlabels(1,2,3,4,5,100);
endchar; % end "O"

cmchar "HMIN letter P";
beginchar("P", 2sb#+caprat*hstretch*(14/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
path pth[];
numeric n[];
pickup quill;
x100 = 0;
n1:= rt x100 - lft x100;
%% stem
lft x1=lft x2=pcshiftx+n1;
top y1=h; bot y2=0;
z12 = 1/2[z1,z2];
%% bowl
z3 = (pcshiftx,y1);
x7 = x12; top y7 = y12;
rt x5 = w-pcshiftx; y5 = 1/2[y3,y7];
pth1 := z3--z1{right}..z5{down}..{left}z7;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw pth1;
draw (x2-n1,y2)--(x2+n1,y2);
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,100);
endchar; % end "P"

cmchar "HMIN letter Q";
beginchar("Q", 2sb#+caprat*hstretch*(18/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
numeric n[];
path pth[];
pickup quill;
%% bowl
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h+o; bot y4=-o;
pth1 := z1..z4..z3..z2..cycle;
%% tail
n1 := rt x1 - lft x1;
pth2 := z1..z2..z3..z4;
z5 = directionpoint pdir of pth1;
x7 = x3 + 2n1; top y7 = -n1;
pth3 := z5{-ppdir}..{pdir}z7;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1;
draw pth3;
ductus:=false;
penlabels(0,1,2,3,4,5,6,7,8,100);
endchar; % end "Q"

cmchar "HMIN letter R";
beginchar("R", 2sb#+caprat*hstretch*(18/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
numeric n[];
path pth[];
pickup quill;
x100 = 0;
n1:= rt x100 - lft x100;
n2 := 14/18;    %% P/R width ratio
%% stem
lft x1=lft x2=pcshiftx+n1;
top y1=h; bot y2=0;
z12 = 1/2[z1,z2];
%% bowl
z3 = (pcshiftx,y1);
x7 = x12; top y7 = y12;
rt x5 = w*n2 - pcshiftx; y5 = 1/2[y3,y7];
pth1 := z3--z1{right}..z5{down}..{left}z7;
%% leg
z21 = z7;
x23 = w-pcshiftx; bot y23 = 0;
pth2 := z21{-ppdir}..{right}z23;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw pth1; draw pth2;
draw (x2-n1,y2)--(x2+n1,y2);
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,100);
endchar; % end "R"

cmchar "HMIN letter S";
beginchar("S", 2sb#+caprat*hstretch*(15/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
path pth[];
pcshiftx := sb;
pickup quill;
x100 = 0;
n1 := rt x100 - lft x100;
x1 = x3 = x5 = 1/2w;
top y1 = h; bot y5 = 0; y3 = 1/2[y1,y5];
lft x2 = pcshiftx; rt x4 = w-pcshiftx;
y2 = 1/2[y1,y3]; y4 = 1/2[y3,y5];
rt x101 = w-pcshiftx; top y101 = y1;
lft x102 = pcshiftx; bot y102 = y5;
pth1 := z101..z1..z2..z3..z4..z5..z102;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1;
ductus:=false;
penlabels(1,2,3,4,5,6,7,11,17,20,26,27,28,100);
endchar; % end "S"

cmchar "HMIN letter T";
beginchar("T", 2sb#+caprat*hstretch*(16/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
numeric n[];
pickup quill;
%% bar
lft x1 = pcshiftx; rt x2=w-pcshiftx;
top y1 = top y2 = h;
%% stem
z3 = 1/2[z1,z2];
x4 = x3; bot y4 = 0;
%% foot
n1 := rt x4 - lft x4;
rt x5 = lft x4; x6 = x4 + n1; y5 = y6 = y4;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2;           % bar
draw z3--z4;           % stem
draw z5--z6;           % foot
ductus:=false;
penlabels(1,2,3,4,5,6,11,12,100);
endchar; % end "T"

cmchar "HMIN letter U";
beginchar("U", 2sb#+caprat*hstretch*(20/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
numeric n[];
path pth[];
pickup quill;
x100 = 0;
n1 := rt x100 - lft x100;
%% left stem
lft x1 = pcshiftx + n1; top y1 = h;
x13=x1; y13=1/3h;
rt x3=w-pcshiftx-n1; y3=y1;
x4=x3; bot y4=0;
z33=1/4[z4,z3];  % start of bowl
x14=1/3[x1,x3]; bot y14=0;
pth1 := z1--z13{down}..z14{right}..{pdir}z33;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z3--z4; draw pth1;
draw (x1-n1,y1)--(x1+n1,y1);
draw (x3-n1,y3)--(x3+n1,y3);
draw (x4,y4)--(x4+n1,y4);
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,11,13,14,16,33,100);
endchar; % end "U"

cmchar "HMIN letter V";
beginchar("V", 2sb#+caprat*hstretch*(20/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
numeric n[];
path pth[];
pickup quill;
x100 = 0;
n1 := rt x100 - lft x100;
%% stem tops
lft x1 = pcshiftx + 2n1; top y1 = h;
rt x3 = w - pcshiftx - 2n1; top y3 = h;
%% bottom
x13 = 1/2[x1,x3]; bot y13 = 0;
pth1 := z1{-pdir}..{-ppdir}z13;
pth2 := z13{pdir}..z3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1; draw pth2;
draw (x1-2n1,y1)--(x1,y1);
draw (x3,y3)--(x3+2n1,y3);
ductus:=false;
penlabels(1,2,3,4,5,11,13,14,23,26,100);
endchar; % end "V"

cmchar "HMIN letter W";
beginchar("W", 2sb#+caprat*hstretch*(26/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
numeric n[];
path pth[];
pickup quill;
x100 = 0;
n1 := rt x100 - lft x100;
%% stem tops
lft x1 = pcshiftx + 2n1; top y1 = h;
rt x5 = w - pcshiftx - 2n1; top y5 = h;
z93 = 1/2[z1,z5];
x3 = rt x93; y3 = y93;
%% V bottoms
x13 = 1/2[x1,x3]; bot y13 = 0;
x35 = 1/2[x3,x5]; bot y35 = 0;
pth1 := z1{-pdir}..{-ppdir}z13;
%%pth2 := z13{pdir}..z3;
pth3 := z3{-pdir}..{-ppdir}z35;
z335 = directionpoint down of pth3;
%%pth2 := z13..{up}z3;
%%pth3 := z3{down}..z35;
pth2 := z13..{up}z335;
pth4 := z35{pdir}..z5;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1; draw (x1-2n1,y1)--(x1,y1);
draw pth2;
draw (x3-n1,y3)--(x3,y3);
draw pth3;
draw pth4; draw (x5,y5)--(x5+2n1,y5);
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,11,13,14,33,34,53,16,100);
endchar; % end "W"

cmchar "HMIN letter X";
beginchar("X", 2sb#+caprat*hstretch*(18/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
path pth[];
numeric n[];
pcshiftx := sb;
pickup quill;
x100 = 0;
n1 := rt x100 - lft x100;
%% diag top left to bottom right
lft x1=pcshiftx+n1; top y1 = h;
rt x2 = w - pcshiftx - n1; bot y2 = 0;
%% diag top right to bottom left
rt x3 = w - pcshiftx - n1; top y3 = h;
lft x4 = pcshiftx; bot y4 = 0;
pth1 := z4{pdir}..z3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw pth1;
draw (x1-n1,y1)--(x1+n1,y1);
draw (x2-n1,y2)--(x2+n1,y2);
draw (x3-n1,y3)--(x3+n1,y3);
draw (x4,y4)--(x4+n1,y4);
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,10,25,251,252,40,50,150,60,160,100);
endchar; % end "X"

cmchar "HMIN letter Y";
beginchar("Y", 2sb#+caprat*hstretch*(15/18x_height#),
               caprat*x_height#, desc_depth#);
adjust_fit(0,0);
pcshiftx := sb;
path pth[];
numeric n[];
pickup quill;
x100 = 0;
n1 := rt x100 - lft x100;
%% left stem top
lft x1 = pcshiftx + n1; top y1 = h;
%% upright
x5 = 1/2w; y5 = 1/2y1;
x6 = x5; bot y6 = 0;
%% right stem
rt x3=w-pcshiftx-n1; top y3 = h;
pth1 := z5{ppdir}..{pdir}z1;   % left
pth2 := z5{pdir}..{left}z3;    % right
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z5--z6; draw (x6-n1,y6)--(x6+n1,y6);  % stem
draw pth1; draw (x1-n1,y1)--(x1,y1);
draw pth2;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,10,100);
endchar; % end "Y"

cmchar "HMIN letter Z";
beginchar("Z", 2sb#+caprat*hstretch*(14/18x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
path pth[];
pcshiftx := sb;
pickup quill;
%% top bar ends
lft x1 = pcshiftx; rt x2 = w -pcshiftx; top y1 = top y2 = h;
%% bottom bar ends
lft x3 = pcshiftx; rt x4 = w -pcshiftx; bot y3 = bot y4 = 0;
%% middle bar
lft x5 = x1; rt x6 = x2; y5 = y6 = 1/2h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw z3--z4; draw z5--z6;
draw z2--z3;
ductus:=false;
penlabels(1,2,3,4,5,6,10o,10i,20o,20i,100);
endchar; % end "Z"

