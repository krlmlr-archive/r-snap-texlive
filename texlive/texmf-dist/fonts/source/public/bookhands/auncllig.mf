%%
%% This is file `auncllig.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% auncial.dtx  (with options: `lig')
%% 
%%  Copyright 1999 Peter R. Wilson
%% 
%%  This program is provided under the terms of the
%%  LaTeX Project Public License distributed from CTAN
%%  archives in directory macros/latex/base/lppl.txt.
%% 
%% Author: Peter Wilson (CUA)
%%         now at: peter.r.wilson@boeing.com
%% 


ligtable "`": "`"=:oct"134";
ligtable "'": "'"=:oct"042"; %% , "?" kern 2u#, "!" kern 2u#;

ligtable "-": "-"=:oct"173";
ligtable oct"173": "-"=:oct"174";


cmchar "Uncial closing quotes";
beginchar(oct"042",2u#+hstretch*(comma_width#+3dot_size#),4/3x_height#,0);
adjust_fit(0,0);
pcshiftx:=u;
x1=w-pcshiftx-(0.5hstretch*dot_size); y1=y2=h;
x2=x1-(2hstretch*dot_size);
draw_pcomma(1,dot_size,comma_width);  % right
draw_pcomma(2,dot_size,comma_width);  % left
penlabels(1,2);
endchar; % end of "''"


cmchar "Uncial opening quotes";
beginchar(oct"134",2u#+hstretch*(comma_width#+3dot_size#),4/3x_height#,0);
adjust_fit(0,0);
pcshiftx:=u;
x1=pcshiftx+(0.5hstretch*dot_size); y1=y2=h;
x2=x1+(2hstretch*dot_size);
draw_plq(1,dot_size,comma_width); % left
draw_plq(2,dot_size,comma_width); % right
penlabels(1,2);
endchar; % end of "``"


cmchar "Uncial hyphen";
beginchar("-",u#+hstretch*(0.8x_height#),x_height#,0);
adjust_fit(0,0);
pcshiftx:=u/2;
numeric alpha; alpha:=w/8;
pickup quill;
lft x1 = pcshiftx; rt x2=w-pcshiftx; y1=y2=h/2;
x11=lft x1+alpha/2; y11=top y1-0.5thin;
x12=rt x2; y12=bot y1+0.5thin;
draw z1--z2;  % bar
draw_triangle(11,alpha);
draw_rutriangle(12,alpha);
penlabels(1,2);
endchar; % end of "-"


cmchar "Uncial en dash";
beginchar(oct"173",u#+hstretch*(x_height#),x_height#,0);
adjust_fit(0,0);
pcshiftx:=u/2;
numeric alpha; alpha:=w/8;
pickup quill;
lft x1 = pcshiftx; rt x2=w-pcshiftx; y1=y2=h/2;
x11=lft x1+alpha/2; y11=top y1-0.5thin;
x12=rt x2; y12=bot y1+0.5thin;
draw z1--z2;  % bar
draw_triangle(11,alpha);
draw_rutriangle(12,alpha);
penlabels(1,2);
endchar; % end of "--"


cmchar "Uncial em dash";
beginchar(oct"174",u#+hstretch*(2x_height#),x_height#,0);
adjust_fit(0,0);
pcshiftx:=u/2;
numeric alpha; alpha:=w/8;
pickup quill;
lft x1 = pcshiftx; rt x2=w-pcshiftx; y1=y2=h/2;
x11=lft x1+alpha/2; y11=top y1-0.5thin;
x12=rt x2; y12=bot y1+0.5thin;
draw z1--z2;  % bar
draw_triangle(11,alpha);
draw_rutriangle(12,alpha);
penlabels(1,2);
endchar; % end of "---"


cmchar "Uncial ampersand";
beginchar("&", 2u#+hstretch*(7/4x_height#), x_height#, 0.5desc_depth#);
adjust_fit(0,0);
pcshiftx:=u;
numeric alpha; alpha:=4/7w-u;
pickup quill;
lft x1=pcshiftx; rt x3= alpha; y1=y3=h/2;
x2=x4=alpha/2; top y2=h; bot y4=0;
x5=x6=x3;
y5=0.1h;
top y6=0.95h; y6'=top y6; y6''=bot y6; x6''=lft x6;
pickup stylus;
rt x16=alpha; top y16=y6';
x17=x6; y17=y16-jut;
lft x18=x6''; bot y18=y6'';
pickup quill;
x7=x1; y7=y1;
x8=w-pcshiftx; y8=y7;
lft x31=alpha+2thin; y31=y8;
x32=x31; bot y32=-d;
x33=rt x8-0.5thin; y33=top y31-0.5thin;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z5{-pdir}..{left}z4..z1..z2..z6;     % the bowl
draw_tstem(31,32,srad);                   % t stem
draw z7--z8;                              % the (top) bar
draw_rtriangle(33,jut);                   % t bar serif
pickup stylus;
filldraw z17..z18--z16--cycle;            % e serif
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,10,16,17,18,31,32,33);
endchar; % end of "&"

