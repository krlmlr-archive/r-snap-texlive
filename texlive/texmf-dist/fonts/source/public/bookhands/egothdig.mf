%%
%% This is file `egothdig.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% egothic.dtx  (with options: `dig')
%% 
%%  Copyright 1999 Peter R. Wilson
%% 
%%  This program is provided under the terms of the
%%  LaTeX Project Public License distributed from CTAN
%%  archives in directory macros/latex/base/lppl.txt.
%% 
%% Author: Peter Wilson (CUA)
%%         now at: peter.r.wilson@boeing.com
%% 

cmchar "EGOTH digit 1";
beginchar("1", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
y100 = h/2; x100 = pcshiftx+y100;  % square center
pickup quill;
x1=x2=w/2;
y1=h; bot y2=0;
%%ductus:=true;
draw_tstem(1,2,srad);
draw_flatserif(1,2,srad);
draw_flatserif(2,1,srad);
ductus:=false;
proofpcbb(100, y100, y100);
penlabels(1,2,100);
endchar; % end "1"

cmchar "EGOTH digit 2";
beginchar("2", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx+o; top y1=h-2thin;
x2=1/2w; top y2= h+o;
rt x3=w-pcshiftx-o; y3=3/4h;
lft x5=pcshiftx; bot y5=0;
rt x6=w-pcshiftx; y6=y5;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_wave(5,6,thin);                          % foot
draw z1..z2{right}.. tension 0.75 ..z3{down}..{-pdir}z5wl;    % curve
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "2"

cmchar "EGOTH digit 3";
beginchar("3", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x3=pcshiftx; x10=x3;
rt x5=w-pcshiftx; x8=x5-3thin;
x4=0.5[x3,x5];
x9=0.5[x10,x8];
%%lft x6=pcshiftx+3thin; x7=x6;
x6=1/2[x3,x9]; x7=x6;
bot y3=thin; top y10=h-2thin;
y6=0.5[y3,y10]; y7=y6;
bot y4=-thin; top y9=h+thin;
y5=0.5[y3,y6];
y8=11/12[y7,y10];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
%%draw z3..z4{right}..z5{up}...{-pdir}z6;                 % bottom bowl
%%draw z7{pdir}..z8{up}..z9{left}..z10;                   % top bowl
draw z3..z4{right}..z5{up}...{left}z6;                 % bottom bowl
draw z7{right}..z8{up}..z9{left}..z10;                   % top bowl
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,10,100);
endchar; % end "3"

cmchar "EGOTH digit 4";
beginchar("4", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
x1=x2=w-pcshiftx-jut;
y1=h; bot y2=0;
lft x3=pcshiftx; y3=0.33h;
rt x4=w-pcshiftx; y4=y3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tstem(1,2,srad);              % main stem
draw_wave(3,4,thin);               % crossbar
draw z3wl--z1;                     % diagonal
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "4"

cmchar "EGOTH digit 5";
beginchar("5", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
rt x1=w-pcshiftx; top y1=h;
lft x2=pcshiftx; y2=y1;
x3=x2; y3=0.5h+thin;
x4=0.5[x1,x3]; y4=0.5[y3,y5];
rt x5=w-pcshiftx; y5=0.3h;
x6=w/2; bot y6=0;
lft x7=pcshiftx; bot y7=2thin;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_wave(2,1,thin);                      % top squiggle
draw z3{pdir}..z5{down}..z6{left}..z7;    % curve
draw z2wl--z3;                            % vertical
ductus:=false;
penlabels(1,2,3,4,5,6,7,100);
endchar; % end "5"

cmchar "EGOTH digit 6";
beginchar("6", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
rt x1=w-pcshiftx; top y1=h-2thin;
x2=1/2w; top y2= h;
lft x3=pcshiftx; y3=1/2h;
x4=x2; bot y4=0;
rt x5=w-pcshiftx; y5=1/4h;
lft x6=rt x3; bot y6=y3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2{left}.. tension 0.8 ..z3{down}.. tension 0.8 ..z4{right}..z5{up}.. tension 0.8 ..{-pdir}z6;
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "6"

cmchar "EGOTH digit 7";
beginchar("7", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx;
rt x2=w-pcshiftx;
top y1=top y2=h;
x4=w/2; bot y4=0;
x3=0.5[x2,x4]-3thin; y3=0.5[y2,y4];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_wave(1,2,thin);         % top bar
draw z1wr..z3..z4;
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "7"

cmchar "EGOTH digit 8";
beginchar("8", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
numeric alpha; alpha:=1;
pickup quill;
lft x1=alpha*pcshiftx+o; rt x3=w-alpha*pcshiftx-o; y1=y3=3/4h;
x2=x4=w/2; top y2=h+o; bot y4=-o;
x5=x2; y5=h/2+2o;
lft x11=pcshiftx; rt x13=w-pcshiftx; y11=y13=1/4h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..  tension 0.8 ..z2.. tension 0.8 ..z3..z5..
     z11.. tension 0.8 ..z4.. tension 0.8 ..z13..z5..cycle;
ductus:=false;
penlabels(1,2,3,4,5,11,13,100);
endchar; % end "8"

cmchar "EGOTH digit 9";
beginchar("9", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; bot y1=2thin;
x2=1/2w; bot y2= 0;
rt x3=w-pcshiftx; y3=1/2h;
x4=x2; top y4=h;
lft x5=pcshiftx; y5=3/4h;
rt x6=lft x3; top y6=y3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2{right}.. tension 0.8 ..z3{up}.. tension 0.8 ..z4{left}..z5{down}..{pdir}z6;
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "9"

cmchar "EGOTHIC digit 0";
beginchar("0", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
y100 = h/2; x100 = pcshiftx+y100;  % square center
pickup quill;
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h+o; bot y4=-o;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1.. tension 0.8 ..z2.. tension 0.8 ..z3.. tension 0.8 ..z4.. tension 0.8 ..cycle;
ductus:=false;
proofpcbb(100, y100, y100);
penlabels(1,2,3,4,100);
endchar; % end "0"

