%%
%% This is file `rustu.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% rustic.dtx  (with options: `maj')
%% 
%%  Copyright 1999 Peter R. Wilson
%% 
%%  This program is provided under the terms of the
%%  LaTeX Project Public License distributed from CTAN
%%  archives in directory macros/latex/base/lppl.txt.
%% 
%% Author: Peter Wilson (CUA)
%%         now at: peter.r.wilson@boeing.com
%% 

cmchar "Rustic letter A";
beginchar("A", 2u#+caprat*hstretch*(4/5x_height#+jut#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
z1-z2=whatever*thick_tltobr;
top y1=h;
rt x2=w-pcshiftx-jut; bot y2=0;
z3=9/10[z2,z1];
x4=pcshiftx+footjut+extra_fhjut; bot y4=0;
x7=x4; y7=0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_istem(1,2,srad);           % right leg
draw_bow(3,4,bowoff);           % left leg
draw_foot(7);                   % left leg foot
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "A"

cmchar "Rustic letter B";
beginchar("B", 2u#+caprat*hstretch*(7/10x_height#), caprat*(11/10x_height#), 0);
adjust_fit(0,0);
pcshiftx := u+hookjut+thin;
pickup quill;
x1=x2=pcshiftx;
y1=h; y2=0;
rt x3=x1; bot y3=thin;
rt x5=w-u; y5 = 1/6h;
x4=0.4[x2,x5]; y4=0;
lft x6=x2; y6=2/3h;
z7=z6;
x8=0.25[x4,x5]; top y8=y1;
x9=0.5[x1,x8]; bot y9=h;
z10=z1;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_rusticstem(1,2);                                 % the stem
draw z3..z4{right}..z5..{dir(pangle+90)}z6;         % bottom bowl
draw z7{pdir}..z8{up}..z9{left}..z10;                 % top bowl
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,10,100);
endchar; % end "B"

cmchar "Rustic letter C";
beginchar("C", 2u#+caprat*hstretch*(3/5x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; rt x3=pcshiftx+sqxside; y1=y3=h/2;
x2=x4=w/2; top y2=h; bot y4=0;
rt x5= rt x6=w-pcshiftx;
top y5=1/5h;
top y6=4/5h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z5{-pdir}..{left}z4..z1..z2..z6;
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "C"

cmchar "Rustic letter D";
beginchar("D", 2u#+caprat*hstretch*(3/5x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
x1=x2=pcshiftx+hookjut+thin;
top y1=h; y2=0;
rt x3=x1; bot y3=thin;
rt x5=w-u; y5 = 2/5h;
x4=0.4[x2,x5]; y4=0;
x6=x1; top y6=h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_rusticstem(1,2);            % stem
draw z3..z4{right}..z5{up}..z6;  % bowl
ductus:=false;
penlabels(0,1,2,3,4,5,6,100);
endchar; % end "D"

cmchar "Rustic letter E";
beginchar("E", 2u#+caprat*hstretch*(4/10x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
x1=x2=pcshiftx+stem_offset;
top y1=h; y2=0;
rt x3=x1; x4=w-u;
top y3=top y4=h;
x5=x3; x6=x4;
y5=y6=3/5h;
x7=x3; x8=x4;
bot y7= bot y8=0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_rusticstem(1,2);            % stem
draw_squiggle(3,4,wiggle,srad);  % top bar
draw_squiggle(5,6,wiggle,srad);  % middle bar
draw_squiggle(7,8,wiggle,srad);  % bottom bar
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,100);
endchar; % end "E"

cmchar "Rustic letter F";
beginchar("F", 2u#+caprat*hstretch*(4/10x_height#), caprat*(6/5x_height#), 0);
adjust_fit(0,0);
pcshiftx := u +stem_offset;
pickup quill;
x1=x2=pcshiftx;
top y1=h; y2=0;
x3=x1; x4=w-u;
top y3=top y4=h;
x5=x3; x6=x4;
y5=y6=2/3h;
x7=x3; x8=x4;
bot y7= bot y8=0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_footstem(1,2);            % stem
draw_squiggle(3,4,wiggle,srad);  % top bar
draw_squiggle(5,6,wiggle,srad);  % middle bar
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,100);
endchar; % end "F"

cmchar "Rustic letter G";
beginchar("G", 2u#+caprat*hstretch*(3/5x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; rt x3=pcshiftx+sqxside; y1=y3=h/2;
x2=x4=w/2; top y2=h; bot y4=0;
rt x5= rt x6=w-pcshiftx;
top y5=1/5h;
top y6=4/5h;
lft x7=w/2; y7=h/2;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z7..z5{down}..{left}z4..z1..z2..z6;  % the bowl
ductus:=false;
penlabels(1,2,3,4,5,6,7,100);
endchar; % end "G"

cmchar "Rustic letter H";
beginchar("H", 2u#+caprat*hstretch*(4/5x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
y100 = sqyside/2; x100 = pcshiftx+y100;  % square center
pickup quill;
x1=x2=pcshiftx+stem_offset;
y1=h; y2=0;
y5=y6=7/10h;
lft x5=pcshiftx; rt x6=w-pcshiftx;
rt x7=w-pcshiftx; top y7=h;
x8=x7; bot y8=0;
x9=x8-2thin; y9=0.5[y8,y7];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_footstem(1,2);              % left stem
draw_hserif(1,2,srad);           % its serif
draw_squiggle(5,6,wiggle,srad);  % the bar
draw z7..z9..z8;                 % right stem
ductus:=false;
penlabels(1,1',2,2',3,4,5,6,7,7',8,8',99,99',100);
endchar; % end "H"

cmchar "Rustic letter I";
beginchar("I", 2u#+caprat*hstretch*(2footjut#+hookjut#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
x1=x2=pcshiftx+stem_offset;
y1=h; y2=0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_footstem(1,2);               % stem and foot
draw_hserif(1,2,srad);            % serif
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "I"

cmchar "Rustic letter J";
beginchar("J", 4u#+caprat*hstretch*(3/5x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := 3u;
pickup quill;
x1=x2=pcshiftx+2/3(w-4u);
y1=h; y2=0;
rt x4=w-u; top y4=h;
x1-x3 = x4-x1; y3=y4;
rt x5 = lft x3;
top y5=2/5h;
x7=x2; bot y7=0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_rusticstem(1,2);              % stem
draw_squiggle(3,4,wiggle,srad);    % top bar
draw z7{left}..{pdir}z5;           % bowl
ductus:=false;
penlabels(1,2,3,4,5,6,7,100);
endchar; % end "J"

cmchar "Rustic letter K";
beginchar("K", 2u#+caprat*hstretch*(x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
x1=x2=pcshiftx+stem_offset;
y1=h; y2=0;
lft x13=pcshiftx; bot y13=0;
x2-x13=x14-x2; y14=y13;
x3=x1; y3=h/2;
x4=x1+y3; top y4=h;
x5=x4-jut; x6-x4=x4-x5;
top y5=top y6=h;
x24=x4; y24=h;
lft x7=x3; bot y7=y3;
rt x9=w-pcshiftx; bot y9=0;
x8=0.5[x7,x9]; y8=0.7[y7,y9];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_footstem(1,2);             % the stem
draw_hserif(1,2,srad);            % its serif
draw z3--z4;                      % upper diagonal
draw_head(24);
draw z7..z8..z9;                  % bottom leg
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,100);
endchar; % end "K"

cmchar "Rustic letter L";
beginchar("L", 2u#+caprat*hstretch*(1/2x_height#), caprat*(6/5x_height#), 0);
adjust_fit(0,0);
pcshiftx := u+hookjut;
pickup quill;
x1=x2=pcshiftx+thin+hookjut;
y1=h; y2=0;
rt x3=x1; rt x4=w-u;
bot y3= bot y4=0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_rusticstem(1,2);            % stem
draw_hserif(1,2,srad);           % serif
draw_squiggle(3,4,wiggle,srad);  % foot
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "L"

cmchar "Rustic letter M";
beginchar("M", 2u#+caprat*hstretch*(6/5x_height#+jut#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
z11-z12=whatever*thick_tltobr;
rt x12=w-pcshiftx-jut; bot y12=0;
top y11=h;
pickup bowpen;
x4=pcshiftx+footjut+extra_fhjut; bot y4=thin;
z3=9/10[z2,z1];
pickup quill;
x7=x4; y7=0;   % for the foot
z13=9/10[z12,z11];
z3-z4=z13-z14; y4=y14;
z1-z2=z11-z12; % y2=y12;
x12-x2=1/3w;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_istem(1,2,srad);           % right leg
draw_bow(3,4,bowoff);           % left leg
draw_foot(7);                   % left leg foot
draw_istem(11,12,srad);
draw_bow(13,14,bowoff);
ductus:=false;
penlabels(1,2,3,4,5,6,11,12,13,14,100);
endchar; % end "M"

cmchar "Rustic letter N";
beginchar("N", 2u#+caprat*hstretch*(9/10x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
x1=x2=pcshiftx+stem_offset;
y1=h; y2=0;
x5=x6=w-u-hookjut-thin;
y5=y1; y6=y2;
x7=x1; y7=h;
x9=x6; bot y9=0;
x8=0.5[x7,x9]; y8=0.6[y7,y9];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_footstem(1,2);            % left stem
draw_rusticstem(5,6);            % right stem
draw_hserif(5,6,srad);           % its serif
draw z7..z8..z9;                 % diagonal
draw_serif(9,8,srad);            % its bottom serif
ductus:=false;
penlabels(1,2,5,6,7,8,9,100);
endchar; % end "N"

cmchar "Rustic letter O";
beginchar("O", 2u#+caprat*hstretch*(3/5x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h+o; bot y4=-o;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2..z3..z4..cycle;
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "O"

cmchar "Rustic letter P";
beginchar("P", 2u#+caprat*hstretch*(5/10x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
x1=x2=pcshiftx+stem_offset;
top y1=h; y2=0;
lft x7=x2; y7=1/2h;  % y7=2/3h;
rt x8=w-pcshiftx; top y8=y1;
z10=z1;
x9=0.5[x1,x8]; y9=h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_footstem(1,2);                    % stem
draw z7{pdir}..z8{up}..z9{left}..z10;    % top bowl
ductus:=false;
penlabels(1,2,7,8,9,10,100);
endchar; % end "P"

cmchar "Rustic letter Q";
beginchar("Q", 2u#+caprat*hstretch*(3/5x_height#), caprat*x_height#,
                                                   caprat*(2/3thick#));
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h+o; bot y4=-o;
path p;
p = z4{right}..{up}z3;
z5 = directionpoint pdir of p;
x7= x4+w/2; top y7=0;
x6=0.5[x5,x7]; y6=y7;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2..z3..z4..cycle;  % O shape
draw z5..z6..z7;             % the tail
ductus:=false;
penlabels(0,1,2,3,4,5,6,6',7,100);
endchar; % end "Q"

cmchar "Rustic letter R";
beginchar("R", 2u#+caprat*hstretch*(4/5x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
numeric alpha, beta;
alpha:=thin*(xpart pdir); beta:=thin*(ypart pdir);
pickup quill;
x1=x2=pcshiftx+stem_offset;
top y1=h; y2=0;
lft x7=x2; bot y7=1/2h;
rt x8=5/8w; top y8=y1;
z10=z1;
x9=0.5[x1,x8]; y9=h;
z11=z7 shifted (thin*pdir);
rt x13=w-pcshiftx; bot y13=0;
x12=0.5[x11,x13]; y12=0.7[y11,y13];
x11l=lft x11 + alpha; y11l=bot y11 + beta;
x11r= rt x11 - alpha; y11r=top y11 - beta;
x12l=0.6[lft x12, x12]; y12l=0.6[bot y12, y12];
x12r=0.6[rt x12, x12];  y12r=0.6[top y12, y12];
x14=w-pcshiftx-thin; y14=thin;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_footstem(1,2);                       % stem and foot
draw z7{pdir}..z8{up}..z9{left}..z10;     % bowl
pickup stylus;                            % drawing the leg
filldraw z11l..z12l..{right}z14 & z14{left}..z12r..z11r--cycle;
ductus:=false;
penlabels(1,2,7,8,9,10,11,12,14,100);
endchar; % end "R"

cmchar "Rustic letter S";
beginchar("S", 2u#+caprat*hstretch*(3/5x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x7=pcshiftx+extra_fhjut; bot y7=0;
rt x1=w-pcshiftx; top y1=h-thin;
lft x3=pcshiftx+extra_fhjut; y3=0.75h;
x4=0.5[x1,x3]; y4=0.5[y3,y5];
x2=x4; top y2=h;
x5=x1; y5=0.3h;
x6=0.75w; bot y6=0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2{left}..z3{down}..z4..z5{down}..{-pdir}z6;  % S curve
draw_squiggle(7,6,wiggle,srad);                       % base
ductus:=false;
penlabels(1,2,3,4,5,6,7,100);
endchar; % end "S"

cmchar "Rustic letter T";
beginchar("T", 2u#+caprat*hstretch*(2/5x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
x1=x2=w/2;
y1=h; y2=0;
lft x3=pcshiftx; top y3=h;
rt x4=w-pcshiftx; y4=y3;
rt x5=x2; bot y5= bot y6=0;
rt x6=w-pcshiftx;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_footstem(1,2);            % the stem
draw_squiggle(3,4,wiggle,srad);  % the top crossbar
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "T"

cmchar "Rustic letter U";
beginchar("U", 2u#+caprat*hstretch*(4/5x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
x5=x6=w-pcshiftx-hookjut-thin; y5=h; y6=-thin;
lft x1= lft x2=pcshiftx+jut;
top y1=h; y2=2thick;
x3=0.5[x1,x5]; bot y3=0;
rt x4=x5; bot y4=0.25thick;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_rusticstem(5,6);                       % right stem
draw_hserif(5,6,srad);                      % its serif
draw_tlserif(1,2,srad);                     % left stem and serif
draw z2{down}..z3{right}..{pdir}z4;         % the bowl
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,11,16,100);
endchar; % end "U"

cmchar "Rustic letter V";
beginchar("V", 2u#+caprat*hstretch*(x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
z1-z2=whatever*thick_tltobr;
lft x1=pcshiftx+jut; top y1=h;
bot y2=0;
pickup bowpen;
x3=w-pcshiftx-footjut-extra_fhjut; top y3=h;
pickup quill;
x4=lft x2; y4=0;
x5=x3; y5=h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_istem(1,2,srad);      % left part
draw_bow(3,4,bowoff); % right part
draw_head(5);         % and its head
ductus:=false;
penlabels(1,2,3,4,5,100);
endchar; % end "V"

cmchar "Rustic letter W";
beginchar("W", 2u#+caprat*hstretch*(8/5x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
z1-z2=whatever*thick_tltobr;
lft x1=pcshiftx+jut; top y1=h; bot y2=0;
pickup bowpen;
x13=w-pcshiftx-footjut-extra_fhjut; top y13=h-thin;
x23=x13; y23=h;  % for the head
bot y14=thin;
pickup quill;
z11-z12=z1-z2;
x12-x2=1/3w; y12=y2;
z3-z4=z13-z14;
x4=x2;
x14=x12;
y3=y13;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_istem(1,2,srad);       % left and right thick diags
draw_istem(11,12,srad);
draw_bow(3,4,bowoff);       % left thin diag
draw_bow(13,14,bowoff);     % right thin diag
draw_head(23);              % its head
ductus:=false;
penlabels(1,2,3,4,11,12,13,14,23,100);
endchar; % end "W"

cmchar "Rustic letter X";
beginchar("X", 2u#+caprat*hstretch*(9/10x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx+jut; top y1=h;
rt x2= w-pcshiftx-jut; bot y2=0;
pickup bowpen;
x3=w-pcshiftx-footjut-extra_fhjut-thin; top y3=h;
x4=pcshiftx+footjut+extra_fhjut+thin; bot y4=0;
x7=x3; y7=h;
x8=x4; y8=0;
pickup quill;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_istem(1,2,srad);            % thick diagonal
draw_bow(3,4,bowoff);            % thin diagonal
draw_head(7);                    % its head
draw_foot(8);                    % its foot
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,100);
endchar; % end "X"

cmchar "Rustic letter Y";
beginchar("Y", 2u#+caprat*hstretch*(4/5x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx+jut; top y1=h;
x2=1/2w; bot y2=1/3h;
pickup bowpen;
x3=w-pcshiftx-footjut-extra_fhjut; top y3=h;
lft x4=x2; bot y4=y2;
pickup quill;
x5=x3; y5=h;
x7=x2; y7=y2;
x8=x7; y8=0;
pickup bowpen;
x10=lft x8; x11=rt x8;
pickup stylus;
bot y10= bot y11=thin;
pickup quill;
%% ductus:=true;
if ductus:
  pickup stylus;
fi
draw_istem(1,2,srad);     % left stroke
draw_bow(3,4,bowoff);     % right stroke
draw_head(5);             % its head
draw_foot(8);             % the foot
pickup stylus;            % the leg
filldraw z7--z10--z11--cycle;
ductus:=false;
penlabels(1,2,3,4,5,7,8,9,100);
endchar; % end "Y"

cmchar "Rustic letter Z";
beginchar("Z", 2u#+caprat*hstretch*(4/5x_height#), caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x4=pcshiftx; bot y4=0;
rt x6=w-pcshiftx; y6=y4;
x1=x4; top y1=h;
rt x3 =7/8w-pcshiftx; y3=y1;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_squiggle(1,3,1/2wiggle,srad);    % top stroke
draw_squiggle(4,6,1/2wiggle,srad);    % bottom stroke
pickup stylus;
draw z4--z3;
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "Z"

