%%
%% This is file `rustdig.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% rustic.dtx  (with options: `dig')
%% 
%%  Copyright 1999 Peter R. Wilson
%% 
%%  This program is provided under the terms of the
%%  LaTeX Project Public License distributed from CTAN
%%  archives in directory macros/latex/base/lppl.txt.
%% 
%% Author: Peter Wilson (CUA)
%%         now at: peter.r.wilson@boeing.com
%% 

cmchar "Rustic digit 1";
beginchar("1", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
x1=x2=w/2;
y1=h; y2=0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_footstem(1,2);               % stem and foot
draw_hserif(1,2,srad);            % serif
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "1"

cmchar "Rustic digit 2";
beginchar("2", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; top y1=h-2thin;
x2=1/2w; top y2= h;
rt x3=w-pcshiftx; y3=3/4h;
x5=pcshiftx+extra_fhjut; bot y5=0;
x6=w-pcshiftx-extra_fhjut; y6=y5;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2{right}..z3{down}..{-pdir}z5;    % curve
draw_squiggle(5,6,wiggle,srad);             % foot
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "2"

cmchar "Rustic digit 3";
beginchar("3", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x3=pcshiftx; x10=x3;
rt x5=w-pcshiftx; x8=x5-3thin;
x4=0.5[x3,x5];
x9=0.5[x10,x8];
lft x6=pcshiftx+extra_fhjut; x7=x6;
bot y3=thin; top y10=h-2thin;
y6=0.5[y3,y10]; y7=y6;
bot y4=-thin; top y9=h+thin;
y5=0.5[y3,y6];
y8=11/12[y7,y10];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
%%draw_rusticstem(1,2);                                 % the stem
draw z3..z4{right}..z5{up}...{-pdir}z6;                 % bottom bowl
draw z7{pdir}..z8{up}..z9{left}..z10;                   % top bowl
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,10,100);
endchar; % end "3"

cmchar "Rustic digit 4";
beginchar("4", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
x1=x2=w-pcshiftx-footjut-extra_fhjut;
y1=h; y2=0;
x3=pcshiftx+extra_fhjut; y3=0.33h;
x4=w-pcshiftx-extra_fhjut; y4=y3;
x5=lft x3; y5=bot y3+thin;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_footstem(1,2);               % stem and foot
%%draw_hserif(1,2,srad);            % serif
draw_squiggle(3,4,wiggle,srad);   % squiggle
pickup stylus;
draw z5{pdir}..z1;                % thin diagonal
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "4"

cmchar "Rustic digit 5";
beginchar("5", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x7=pcshiftx+extra_fhjut; bot y7=0;
rt x1=w-pcshiftx-extra_fhjut; top y1=h;
lft x2=pcshiftx+extra_fhjut; y2=y1;

x3=x2; y3=0.5h;
x4=0.5[x1,x3]; y4=0.5[y3,y5];
rt x5=w-pcshiftx; y5=0.3h;
x6=0.75w; bot y6=0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_squiggle(2,1,wiggle,srad);            % top squiggle
draw z3{pdir}..z5{down}..{-pdir}z6;        % curve
draw_squiggle(7,6,wiggle,srad);            % base
pickup stylus;
draw z2--z3;                               % vertical
ductus:=false;
penlabels(1,2,3,4,5,6,7,100);
endchar; % end "5"

cmchar "Rustic digit 6";
beginchar("6", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
rt x1=w-pcshiftx; top y1=h-2thin;
x2=1/2w; top y2= h;
lft x3=pcshiftx; y3=1/2h;
x4=x2; bot y4=0;
rt x5=w-pcshiftx; y5=1/4h;
lft x6=rt x3; bot y6=y3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2{left}..z3{down}..z4{right}..z5{up}..{-pdir}z6;
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "6"

cmchar "Rustic digit 7";
beginchar("7", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
x1=pcshiftx+extra_fhjut;
x2=w-pcshiftx-extra_fhjut;
top y1=top y2=h;
rt x4=w-pcshiftx-3thin; bot y4=0;
x3=0.5[x2,x4]-2thin; y3=0.5[y2,y4];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_squiggle(1,2,wiggle,srad);
draw z2..z3..z4;
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "7"

cmchar "Rustic digit 8";
beginchar("8", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx-o; rt x3=w-pcshiftx; y1=y3=3/4h;
x2=x4=w/2; top y2=h+o; bot y4=-o;
x5=x2; y5=h/2;
x11=x1+o; x13=x3+o; y11=y13=1/4h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2..z3..z5..z11..z4..z13..z5..cycle;
ductus:=false;
penlabels(1,2,3,4,5,11,13,100);
endchar; % end "8"

cmchar "Rustic digit 9";
beginchar("9", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; bot y1=2thin;
x2=1/2w; bot y2= 0;
rt x3=w-pcshiftx; y3=1/2h;
x4=x2; top y4=h;
lft x5=pcshiftx; y5=3/4h;
rt x6=lft x3; top y6=y3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2{right}..z3{up}..z4{left}..z5{down}..{pdir}z6;
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "9"

cmchar "Rustic digit 0";
beginchar("0", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h+o; bot y4=-o;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2..z3..z4..cycle;
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "0"

