%%
%% This is file `pgothdig.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% pgothic.dtx  (with options: `dig')
%% 
%%  Copyright 1999 Peter R. Wilson
%% 
%%  This program is provided under the terms of the
%%  LaTeX Project Public License distributed from CTAN
%%  archives in directory macros/latex/base/lppl.txt.
%% 
%% Author: Peter Wilson (CUA)
%%         now at: peter.r.wilson@boeing.com
%% 

cmchar "PGOTH digit 1";
beginchar("1", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
pickup quill;
x1 = x2 = 1/2w; y1 = h; y2 = 0;
z12 = 1/2[z1,z2];
draw_updiamondstem(1,12,1/2,1/2,diawd,diang);
draw z12--z2; draw_flat_foot(2);
penlabels(1,2,100);
endchar; % end "1"

cmchar "PGOTH digit 2";
beginchar("2", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
numeric st; st := stem_width;
numeric n[];
n1 := 1/2st;
pickup quill;
lft x1=pcshiftx+n1; top y1=h-2n1;
rt x3=w-pcshiftx-n1; y3=3/4h;
x2=1/2[x1,x3]; top y2= h;
x5= lft x1; bot y5=0;
x6= rt x3; y6=y5;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_wave(5,6,thin);                          % foot
draw z1--z2--z3--z5wl;                          % curve
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "2"

cmchar "PGOTH digit 3";
beginchar("3", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
numeric n[];
numeric st; st := stem_width;
n1 := st;
pcshiftx := u;
pickup quill;
%% top
lft x1 = pcshiftx + n1; rt x3 = w - pcshiftx - n1;
x2 = 1/2[x1,x3]; x4 = 1/2[x1,x2];
top y2 = h; y3 = 12/16h;
(z2-z1) = whatever*pdir;
(z3-z4) = whatever*pdir;
%% bottom
x5 = 12/16[x4, rt x4]; x6 = x3; x7 = x2;
x18 = x1-n1; x17 = x7;
bot y7 = thin;
y5 = 12/16[y4, top y4];
(z5-z6) = whatever*(z2-z3);
bot y18 = 0; bot y17 = 0;

draw z1--z2--z3--z4;   % top
draw_wave(18,17,thin);
draw z5--z6--z18wr;       % bottom
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,10,17,18,100);
endchar; % end "3"

cmchar "PGOTH digit 4";
beginchar("4", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
numeric st; st := stem_width;
numeric n[];
pcshiftx := u;
pickup quill;
x1=x2=w-pcshiftx-(wided+widedmin)*st;
y1=h;  y2=0;
lft x3=pcshiftx; y3=7/16h;
rt x4=w-pcshiftx; y4=y3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2; draw_flat_foot(2);
draw z3--z4;                       % horizontal
draw z1--z3;                       % diagonal
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "4"

cmchar "PGOTH digit 5";
beginchar("5", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
numeric n[];
numeric st; st := stem_width;
n1 := st;
pickup quill;
rt x1=w-pcshiftx; top y1=h;
lft x2=pcshiftx+n1; y2=y1;
x3=x2; y3=0.5h;
x4=0.5[x1,x3]; y4=0.5[y3,y5];
rt x5=w-pcshiftx; y5=1/2[thick,y3];
x6=w/2; bot y6=0;
%%x16 = 12/16[x3,x5];
x16 = lft x5;
bot y16 = 0;
lft x17=pcshiftx; bot y17=0;
z167 = 1/2[z16,z17];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_wave(2,1,thin);                      % top squiggle
draw z2wl--z3;                            % vertical
%%draw_wave(17,16,thin);                    % foot
draw z17{pdir}..{pdir}z16;              % foot
draw z3{pdir}..z5{down}..z16;    % curve
ductus:=false;
penlabels(1,2,3,4,5,6,7,100);
endchar; % end "5"

cmchar "PGOTH digit 6";
beginchar("6", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
numeric n[];
numeric st; st := stem_width;
n1 := 24/16st;
pickup quill;
%% bottom left
x1 = x4 = w/2; top y1 = h - thick; bot y4 = 0;
x2 = x3 = x1 + n1; x5 = x6 = x1 - n1;
n2 = top y1;
y2 = y6 = 3/4[y4,y1];
y3 = y5 = 1/4[y4,y1];
%% bowl
draw z1--z2--z3--z4--z5--z6--cycle;
%% head
x11 = x5; top y11 = h;
x12 = x2; y12 = y11;
draw_wave(11,12,thin);
x16 = x11; y16 = y11-thin;
draw z6--z16;
penlabels(1,2,3,4,5,6,12,100);
endchar; % end "6"

cmchar "PGOTH digit 7";
beginchar("7", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx;
rt x2=w-pcshiftx;
top y1=top y2=h;
x4=w/2; bot y4=0;
x3=0.5[x2,x4]-3thin; y3=0.5[y2,y4];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_wave(1,2,thin);         % top bar
draw z1wr..z3..z4;
draw_flat_foot(4);
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "7"

cmchar "PGOTH digit 8";
beginchar("8", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
numeric alpha; alpha:=1;
pickup quill;
lft x1= pcshiftx; rt x3=w-pcshiftx; top y1 = y3 = 3/4h;
rt x2 = lft x4 = w/2; top y2=h; bot y4=0;
x5=x2; y5=h/2+2o;
lft x11=pcshiftx; rt x13=w-pcshiftx; y11 = bot y13 = 1/4h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
%%draw z1..z2..z3..z5..z11..z4..z13..z5..cycle;
draw z1--z2--z3--z11--z4--z13--cycle;
ductus:=false;
penlabels(1,2,3,4,5,11,13,100);
endchar; % end "8"

cmchar "PGOTH digit 9";
beginchar("9", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
numeric n[];
numeric st; st := stem_width;
n1 := 24/16st;
pickup quill;
%% bottom left
x1 = x4 = w/2; top y1 = h; bot y4 = thick;
x2 = x3 = x1 + n1; x5 = x6 = x1 - n1;
y2 = y6 = 3/4[y4,y1];
y3 = y5 = 1/4[y4,y1];
%% bowl
draw z1--z2--z3--z4--z5--z6--cycle;
%% foot
x11 = x5; bot y11 = 0;
x12 = x2; y12 = y11;
draw_wave(11,12,thin);
x13 = x12; y13 = y12+thin;
draw z3--z13;
penlabels(1,2,3,4,5,6,12,100);
endchar; % end "9"

cmchar "PGOTH digit 0";
beginchar("0", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := sb;
numeric n[];
numeric st; st := stem_width;
n1 := 24/16st;
pickup quill;
%% bottom left
x1 = x4 = w/2; top y1 = h; bot y4 = 0;
x2 = x3 = x1 + n1; x5 = x6 = x1 - n1;
y2 = y6 = 3/4h;
y3 = y5 = 1/4h;

draw z1--z2--z3--z4--z5--z6--cycle;

penlabels(1,2,3,4,5,6,12,100);
endchar; % end "0"

