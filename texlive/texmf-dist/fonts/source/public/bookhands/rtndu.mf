%%
%% This is file `rtndu.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% rotunda.dtx  (with options: `maj')
%% 
%%  Copyright 1999 Peter R. Wilson
%% 
%%  This program is provided under the terms of the
%%  LaTeX Project Public License distributed from CTAN
%%  archives in directory macros/latex/base/lppl.txt.
%% 
%% Author: Peter Wilson (CUA)
%%         now at: peter.r.wilson@boeing.com
%% 

cmchar "RTND letter A";
beginchar("A", 2cap_sb#+caprat*hstretch*(3/4x_height#+jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pair pr[];
pickup quill;
n1 := w - 2pcshiftx -jut; % effective letter width
x1 = pcshiftx + 6/8n1; top y1=h;
rt x2 = pcshiftx + n1; bot y2=0;
pth1 := z1{down}..z2;
z20 = 7/8[z1,z2];

rt x4 = lft x1; top y4 = y1;

lft x8 = pcshiftx; top y8 = -thin;

x6 = 3/8w; y6 = 1/2h;
pth2 := z4..{down}z6..{-pdir}z8;

lft x11 = pcshiftx; top y11 = h - thick;

x14 = x1; y14 = y1;

x13 = 1/2[x11,x14]; y13 = y14;
pth3 := z11{pdir}.. z13..z14;

%%ductus:=true;
if ductus:
  pickup stylus;
else:
  % draw stem
  draw pth1;
  draw_roundserif(2,20,srad);
  % draw bowl
  % left leg
  draw pth2;
  % top
  draw pth3;
fi
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,11,12,13,14,15,20,100);
endchar; % end "A"

cmchar "RTND letter B";
beginchar("B", 2cap_sb#+caprat*hstretch*(3/4x_height#+jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
path pth[];
pcshiftx := cap_sb;
pickup quill;
n1:=w-2pcshiftx;    % actual letter width
n2:=h/ascratio;    % top of bowl
%%ductus:=true;
lft x1 = pcshiftx+2jut; top y1=7/8h;
lft x13 = pcshiftx; top y13 = y1;
x2 = x1; bot y2 = 0;
z3 = 9/16[z2,z1];
pth1 := z1--z2;  % stem

rt x6 = w-pcshiftx; y6 = 2/3[y2,y3];

pth2 := z2{right}..z6...{-pdir}z3; % bottom bowl

rt x8 = lft x6; y8 = 13/16[y3,y1];

pth3 := z3{pdir}...z8..{-pdir}z13;  % top bowl
if ductus:
  pickup stylus;
fi
draw pth1;
draw pth2;
draw pth3;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,100);
endchar; % end "B"

cmchar "RTND letter C";
beginchar("C", 2cap_sb#+caprat*hstretch*(5/8x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
pcshiftx := cap_sb;
pickup quill;
n1:=w-2pcshiftx;     % actual letter width
n2:=6/5w-2pcshiftx;  % width of full bowl
x1=pcshiftx + 1/2n1; top y1 = h;
lft x2 = pcshiftx; y2 = 1/2h;
x3 = x1; bot y3 = 0;
rt x4 = w-pcshiftx; y4 = 1/4h;
x24 = x4; y24 = 3/4h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z24..z1{left}..z2{down}..z3{right}..z4{pdir};
ductus:=false;
penlabels(1,2,3,4,5,6,14,16,17,18,100);
endchar; % end "C"

cmchar "RTND letter D";
beginchar("D", 2cap_sb#+caprat*hstretch*(3/4x_height#+jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup quill;
%% stem
lft x1=lft x2=pcshiftx+2jut;
top y1=h; bot y2=0;
%% bowl
rt x5 = w-pcshiftx;
x4=1/2[x1,x5]; top y4 = h;
x6'=x4; bot y6' = 5/16h;
y5=1/2[y4,y6'];
x7 = x2; y7 = y2;
x6 = 1/2[x6',x5]; y6 = 1/2[y6',y7];
lft x3 = pcshiftx; top y3 = y1;
pth1 := z3{pdir}..z4..{down}z5..z6..{-pdir}z7; % bowl
z14 = point 0.75 of pth1;
pth2 := z2{pdir}..{up}z14;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth2;
draw pth1;
ductus:=false;
penlabels(1,2,3,4,5,6,6',7,8,100);
endchar; % end "D"

cmchar "RTND letter E";
beginchar("E", 2cap_sb#+caprat*hstretch*(5/8x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup quill;
%% bowl
n1 := w-2pcshiftx;    % actual letter width
n2 := 6/5w-2pcshiftx;   % width of full bowl
x1=pcshiftx + 1/2n1; top y1 = h;
lft x2 = pcshiftx; y2 = 1/2h;
x3 = x1; bot y3 = 0;
rt x4 = w-pcshiftx; y4 = 1/4h;
x24 = x4; y24 = 3/4h;
pth1 := z24..z1{left}..z2{down}..z3{right}..z4{pdir};
x31 = x2; y31 = y2;
x32 = x4; y32 = y31;
pth2 = z31--z32;

%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1;
draw pth2;
ductus:=false;
penlabels(1,2,3,4,5,6,14,16,17,18,24,31,32,100);
endchar; % end "E"

cmchar "RTND letter F";
beginchar("F", 2cap_sb#+caprat*hstretch*(10/16x_height#+jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
pcshiftx := cap_sb;
pickup quill;
n1 := h/ascratio;      % x-height
%% stem
lft x1=lft x2=pcshiftx+jut; top y1=n1; bot y2=0;
%% flick
x4=x2; y4 = 1/2n1;
rt x6=w-pcshiftx; y6 = 2/3h;
x5 = 2/3[x4,x6]; top y5=h;
%% bar
z14=1/2[z4,z1];
rt x16 = w -pcshiftx; top y16 = 1/2[y14,y6];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tstem(1,2,srad);            % stem
draw_flatserif(1,2,srad);
draw_roundserif(2,1,srad);
draw z4{up}...z5{right}...z6;    % top
draw z14{right}..z16{pdir};      % bar
ductus:=false;
ductus:=false;
penlabels(1,2,3,4,5,6,7,14,15,16,100);
endchar; % end "F"

cmchar "RTND letter G";
beginchar("G", 2cap_sb#+caprat*hstretch*(5/8x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup quill;
%% bowl
n1 := w-2pcshiftx;    % actual letter width
n2 := 6/5w-2pcshiftx;   % width of full bowl
x1=pcshiftx + 1/2n1; top y1 = h;
lft x2 = pcshiftx; y2 = 1/2h;
x3 = x1; bot y3 = 0;
rt x4 = w-pcshiftx; y4 = 1/4h;
x24 = x4; y24 = 3/4h;
pth1 := z24..z1{left}..z2{down}..z3{right}..z4{pdir};
x31 = x2; y31 = y2;
x32 = x4; y32 = y31;
pth2 = z31--z32;
z41 = 5/8[z31,z32];
z42 = z32;
z43 = z4;
lft x45 = x3; top y45 = - thick;
pth3 := z41--z42--z43..{-pdir}z45;

%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1;
draw pth3;
ductus:=false;
penlabels(1,2,3,4,5,6,14,16,17,18,24,31,32,100);
endchar; % end "G"

cmchar "RTND letter H";
beginchar("H", 2cap_sb#+caprat*hstretch*(5/8x_height#+2jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
path pth[];
pickup quill;
numeric n[];
%% stem
lft x1=lft x2=pcshiftx+3jut;
top y1=h; bot y2=0;
lft x16 = pcshiftx; top y16 = h;
x18 = x1; y18 = y1;
pth1 := z16{pdir}..{pdir}z18;

%% bowl
z3=1/2[z2,z1];
rt x5 = w-pcshiftx; y5=y3;
x4=1/2[x3,x5]; top y4 = h/ascratio;
%%lft x6=x4; bot y6=0;
x6=1/2[x4,x5];
top y6 = 0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tstem(1,2,srad);                       % stem
%%draw_flatserif(1,2,srad);
draw_roundserif(2,1,srad);
draw pth1;
draw z3{pdir}..z4..z5..z6{-pdir};

ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "H"

cmchar "RTND letter I";
beginchar("I", 2cap_sb#+caprat*hstretch*(thick#+3jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
path pth[];
pickup quill;
rt x1 = w - pcshiftx - jut;
x2 = x1;
top y1=h; bot y2=0;
lft x6 = pcshiftx; top y6 = h;
x8 = x1; y8 = y1;
pth1 := z6{pdir}..{pdir}z8;
draw_tstem(1,2,srad);
%%draw_flatserif(1,2,srad);
draw_roundserif(2,1,srad);
draw pth1;
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "I"

cmchar "RTND letter J";
beginchar("J", cap_sb#+caprat*hstretch*(thick#+2jut#),
               caprat*x_height#, desc_depth#);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup quill;
%% stem
rt x1 = w-pcshiftx; top y1 = h;
x2=x1; y2=-1/2d;
%% bottom hook
lft x3 = 0; bot y3 = -d;
pth1 := z1--z2..z3{-pdir};
lft x6 = pcshiftx; top y6 = h;
x8 = x1; y8 = y1;
pth2 := z6{pdir}..{pdir}z8;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1;               % stem
draw pth2;               % bar
ductus:=false;
penlabels(1,2,3,6,7,8,100);
endchar; % end "J"

cmchar "RTND letter K";
beginchar("K", 2cap_sb#+caprat*hstretch*(7/8x_height#+jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
path pth[];
numeric n[];
pickup quill;
%% stem
n1 = h;                 % x-height
lft x1=lft x2=pcshiftx+2jut;
top y1=h; bot y2=0;
lft x26 = pcshiftx; top y26 = h;
x28 = x1; y28 = y1;
pth22 := z26{pdir}..{pdir}z28;
%% top arm
%%z3=(x1,1/2n1);
x3 = x1; top y3 = 1/2n1;
rt x5=5/8[x1,w-pcshiftx]; top y5=n1;
x4=1/2[x3,x5]; top y4=n1;
pth1 := z3{pdir}..z5{up};
%% lower arm
pth2 := (rt x1,0)--(rt x1,h);
z13 = pth1 intersectionpoint pth2;
pickup threequarterquill;
lft x14 = x13; bot y14 = y13;
rt x7=w-pcshiftx; bot y7=0;
pickup quill;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tstem(1,2,srad);            % stem
draw_roundserif(2,1,srad);
draw pth22;
draw pth1;                        % upper arm
pickup threequarterquill;
draw z14{-ppdir}..z7{right};              % lower arm
pickup quill;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,13,14,100);
endchar; % end "K"

cmchar "RTND letter L";
beginchar("L", 2cap_sb#+caprat*hstretch*(thick#+5jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
path pth[];
pickup quill;
lft x1= pcshiftx + 3jut; top y1=h;
lft x2= pcshiftx; bot y2 =0;
pth1 := z1{down}..{-pdir}z2;
lft x6 = pcshiftx; top y6 = h;
x8 = x1; y8 = y1;
pth2 := z6{pdir}..{pdir}z8;
x3 = x2; y3 = y2;
rt x5 = w - pcshiftx; bot y5 = 0;
pth3 := z3{pdir}..{pdir}z5;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1;  % stem
draw pth2;  % bar
draw pth3;  % foot
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,100);
endchar; % end "L"

cmchar "RTND letter M";
beginchar("M", 2cap_sb#+caprat*hstretch*(x_height#+2jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup quill;
%% left stem
lft x1=pcshiftx+jut; top y1=h;
x2'=x1; bot y2'=0;
%% start of bowl
x13=x1; y13=3/4h;
%% bottom of left, right and middle stems
rt x6' = w -pcshiftx - jut; y6'=y2';
z4 = 1/2[z2',z6'];
x6 = 1/2[x4,x6']; bot y6 = -thick;
rt x2 = lft x4; bot y2 = 0;
%% top of middle and right stems
x3=x4; y3=y1;
x5=x6'; y5=y13;
%% top of bowls
x14=2/3[x13,x3]; top y14 = h;
x33=x4; y33=y13;
x34=2/3[x33,x5]; y34 = y14;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z2..z13..{right}z14..z33{down}--z4;
draw_roundserif(4,3,srad);
%%draw z33{pdir}..z34{right}..z5{down}--z6;
%%draw_roundserif(6,5,srad);
draw z33{pdir}..z34{right}..z5{down}..{-pdir}z6;  % rightmost
ductus:=false;
penlabels(1,2,2',3,4,5,6,6',7,8,9,13,14,33,34,100);
endchar; % end "M"

cmchar "RTND letter N";
beginchar("N", 2cap_sb#+caprat*hstretch*(5/8x_height#+2jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
pickup quill;
%% left stem
lft x1=pcshiftx+jut; top y1=h;
x2=x1; bot y2=0;
x13=x1; y13=3/4h;
%% right stem
rt x4 = w-pcshiftx-jut; y4=y2;
x3 = x4; y3 = y13;
x14 = 2/3[x13,x3]; top y14 = h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tstem(1,2,srad);                % left stem
draw_flatserif(1,2,srad);
draw_roundserif(2,1,srad);
draw z13{pdir}..z14{right}..z3{down}--z4;
draw_roundserif(4,3,srad);
ductus:=false;
penlabels(1,2,3,4,5,6,13,14,100);
endchar; % end "N"

cmchar "RTND letter O";
beginchar("O", 2cap_sb#+caprat*hstretch*(7/8x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
path pth[];
pickup quill;
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h+o; bot y4=-o;
pth1 := z2..z3..z4{left}.. tension 0.8 ..{up}z1..cycle;
if ductus:
  pickup stylus;
fi
draw pth1;
ductus:=false;
penlabels(1,2,3,4,5,100);
endchar; % end "O"

cmchar "RTND letter P";
beginchar("P", 2cap_sb#+caprat*hstretch*(3/4x_height#+jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup quill;
%% stem
lft x1=lft x2=pcshiftx+2jut;
top y1=h; bot y2=0;
%% bowl
rt x5 = w-pcshiftx;
x4=1/2[x1,x5]; top y4 = h;
x6=x4; bot y6 = 5/16h;
y5=1/2[y4,y6];
x7 = x1; y7 = 1/2[y6,y5];
lft x3 = pcshiftx; top y3 = y1;
pth1 := z3{pdir}..z4..{down}z5..{left}z6..z7;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2;
draw_roundserif(2,1,srad)
draw pth1;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,100);
endchar; % end "P"

cmchar "RTND letter Q";
beginchar("Q", 2cap_sb#+caprat*hstretch*(7/8x_height#+jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup quill;
lft x1=pcshiftx; rt x3=w-pcshiftx-jut; y1=y3=h/2;
x2=x4=w/2; top y2=h+o; bot y4=-o;
pth1 := z2..z3..z4{left}.. tension 0.8 ..{up}z1..cycle;
z5 = point 1.5 of pth1;
rt x7 = w - pcshiftx; bot y7 = 0;
pth2 := z5..{right}z7;
if ductus:
  pickup stylus;
fi
draw pth1;
draw pth2;
ductus:=false;
penlabels(1,2,3,4,5,6,7,100);
endchar; % end "Q"

cmchar "RTND letter R";
beginchar("R", 2cap_sb#+caprat*hstretch*(3/4x_height#+jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
path pth[];
pickup quill;
%% stem
lft x1=lft x2=pcshiftx+2jut;
top y1=h; bot y2=0;
%% bowl
rt x5 = w-pcshiftx;
x4=1/2[x1,x5]; top y4 = h;
x6=x4; bot y6 = 6/16h;
y5=1/2[y4,y6];
x7 = x1;
y7 = y6;
lft x3 = pcshiftx; top y3 = y1;
pth1 := z3{pdir}..z4..{down}z5..{left}z6..z7;
%% leg
x16 = x6; y16 = y6;
rt x18 = w - pcshiftx; bot y18 = 0;
pth2 := z16..{right}z18;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2;
draw_roundserif(2,1,srad)
draw pth1;
draw pth2;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,100);
endchar; % end "R"


cmchar "RTND letter S";
beginchar("S", 2cap_sb#+caprat*hstretch*(7/8x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
pcshiftx := cap_sb;
pickup quill;

z50=(1/2w, 1/2h);
x60 = w-pcshiftx; z60 = z50 shifted (whatever*pdir);
x40 = pcshiftx;   z40 = z50 shifted (whatever*pdir);
rt x5 = x50; top y5 = y50;
z54 = (lft x5, bot y5);
lft x6 = x50; bot y6 = y50;
z56 = (rt x6, top y6);

z1 = 1/2[z56,z60];
x3 = 1/2[x5,x1]; top y3 = h;

z10 = 1/2[z54,z40];
x8 = x56;
bot y8 = 0;

%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z5{ppdir}..z3..z1{-ppdir};

draw z6..z8{left}..z10{ppdir};
pickup stylus;
draw z60--z10;
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,10,11,17,20,26,27,28,40,50,54,56,60,100);
endchar; % end "S"

cmchar "RTND letter T";
beginchar("T", 2cap_sb#+caprat*hstretch*(x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
pickup quill;
%% bar
top y3=h; n3 := h - top y3;
lft x1 = pcshiftx; rt x2=w-pcshiftx;
%%top y1 = y3-n3; bot y2=y3+n3;
y1 = y3-n3; y2=y3;
n1 := w;     % width of bowl
n2 := h;     % height of bowl
x3=1/2[x1,x2];
lft x4 = pcshiftx + 1/8n1; y4 = 1/2h;
x5=x3; bot y5 = 0;
rt x6 = pcshiftx + 3/4n1; top y6 = 1/4h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1{pdir}..z3..z2{pdir};           % bar
draw z3--z5;
draw_roundserif(5,3,srad);
ductus:=false;
penlabels(1,2,3,4,5,6,11,12,100);
endchar; % end "T"

cmchar "RTND letter U";
beginchar("U", 2cap_sb#+caprat*hstretch*(5/8x_height#+2jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
numeric n[];
pickup quill;
%% left stem
lft x1 = pcshiftx + jut; top y1 = h;
x13=x1; y13=1/3h;
rt x3=w-pcshiftx-jut; y3=y1;
x4=x3; bot y4=0;
z33=1/4[z4,z3];  % start of bowl
x14=1/3[x1,x3]; bot y14=0;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z13{down}..z14{right}..z33{pdir};
draw_flatserif(1,13,srad);
draw_tstem(3,4,srad);           % right stem
draw_flatserif(3,4,srad);
draw_roundserif(4,3,srad);
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,11,13,14,16,33,100);
endchar; % end "U"

cmchar "RTND letter V";
beginchar("V", 2cap_sb#+caprat*hstretch*(3/4x_height#+jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
pickup quill;
%% left stem
lft x1 = pcshiftx + jut; top y1 = h;
x13=x1; y13=1/3h;
rt x3=w-pcshiftx-jut; y3=y1;
x4=x3; bot y4=0;
z33=1/4[z4,z3];  % start of bowl
x14=1/3[x1,x3]; bot y14=0;
rt x23=w-pcshiftx; y23=3/4h;
lft x26=x14; top y26=h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z13{down}..z14{right}; % left stem
draw_flatserif(1,13,srad);
draw z14{pdir}..z23{up}..z26;

ductus:=false;
penlabels(1,2,3,4,5,11,13,14,23,26,100);
endchar; % end "V"

cmchar "RTND letter W";
beginchar("W", 2cap_sb#+caprat*hstretch*(x_height#+2jut#),
               caprat*x_height#, 0);
adjust_fit(0,0);
pcshiftx := cap_sb;
pickup quill;
%% left stem
lft x1 = pcshiftx + jut; top y1 = h;
x13=x1; y13=1/3h;
rt x5=w-pcshiftx-jut; y5=y1;
x6=x5; bot y6=0;
z53=1/4[z6,z5];  % start of bowl
z3 = 1/2[z1,z5];
z33=(x3,y13);
x14=1/3[x1,x3]; bot y14=0;
x34=1/3[x3,x5]; y34=y14;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z13{down}..z14{right}..z33{pdir};  % left
draw_flatserif(1,13,srad);
draw z3--z33{down}..z34{right}..z53{pdir};  % middle
draw_flatserif(3,33,srad);
draw_tstem(5,6,srad);           % right stem
draw_flatserif(5,6,srad);
draw_roundserif(6,5,srad);
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,11,13,14,33,34,53,16,100);
endchar; % end "W"

cmchar "RTND letter X";
beginchar("X", 2cap_sb#+caprat*hstretch*(x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
path pth[];
numeric n[];
pcshiftx := cap_sb;
pickup quill;
n1 := w-2pcshiftx;    % total width
n2 := thick;          % triangle side
%% diag top left to bottom right
lft x1=pcshiftx; bot y1=2/3h;
x2=pcshiftx+1/7n1; top y2=h;
x5=pcshiftx+11/14n1; bot y5=-1/4d;
rt x6=pcshiftx+n1; top y6=1/6h;
z25 = 1/3[z2,z5];
pth1 := z2{right}..z5{right};
z251 = point 1/3 of pth1;
z252 = point 2/3 of pth1;
%% diag bottom left to top right
lft x10 = -w + 2pcshiftx; bot y10=-d;
x50=x5; top y50=h;
rt x60 = w -pcshiftx; bot y60=2/3h;
x40=pcshiftx+8/14n1; y40=h;

x150=x2; bot y150 = 0;
lft x160 = pcshiftx; top y160=1/3h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2{right}..z5{right}..z6;    % top left to bottom right
%%draw z10{1/2pdir}..z25..z50{right}..z60;   % bottom left to top right
draw z251..z50{right}..z60;   % middle to top right
draw z252..z150{left}..z160;  % middle to bottom left
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,10,25,251,252,40,50,150,60,160,100);
endchar; % end "X"

cmchar "RTND letter Y";
beginchar("Y", 2cap_sb#+caprat*hstretch*(7/8x_height#),
               caprat*x_height#, desc_depth#);
adjust_fit(0,0);
pcshiftx := cap_sb;
path pth[];
numeric n[];
pickup quill;
%% left stem top
lft x1 = pcshiftx + jut; top y1 = h;
bot y3 = 0;
%% right stem
rt x5=w-pcshiftx; top y5 = h;
lft x9=-1/2w; bot y9 = -d;
pth1 := z9{right}..z5{up};
pth2 := (0,y3)--(w,y3);
z99 = pth1 intersectionpoint pth2;
x3 = x99;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1{down}..z3;        % left stem
draw_flatserif(1,3,srad);
draw pth1;                % right stem
draw_flatserif(5,3,srad);
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,10,100);
endchar; % end "Y"

cmchar "RTND letter Z";
beginchar("Z", 2cap_sb#+caprat*hstretch*(7/8x_height#),
               caprat*x_height#, 0);
adjust_fit(0,0);
numeric n[];
path pth[];
pcshiftx := cap_sb;
pickup quill;
%% top bar ends
top y12 = h;
n1 := h - y12;
lft x1 = pcshiftx; rt x2 = w-pcshiftx;
%%y1 = y12-n1; y2=y12+n1;
top y1 = y12-n1; y2=y12;
x12 = 1/2[x1,x2];
pth1 := z1{pdir}..z12{right}..z2{pdir};
%% bottom bar ends
bot y34 = 0;
x3=x1; x4=x2;
%%y3=y34-n1; y4=y34+n1;
y3=y34-n1; bot y4=y34;
x34 = 1/2[x3,x4];
pth2 := z3{pdir}..z34{right}..z4{pdir};
%% diagonal
z5=z3;
z6 = z2;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw pth1; draw pth2;              %  bars
draw z5--z6;
ductus:=false;
penlabels(1,2,3,4,5,6,12,34,10o,10i,20o,20i,100);
endchar; % end "Z"

