%%
%% This is file `auncldig.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% auncial.dtx  (with options: `dig')
%% 
%%  Copyright 1999 Peter R. Wilson
%% 
%%  This program is provided under the terms of the
%%  LaTeX Project Public License distributed from CTAN
%%  archives in directory macros/latex/base/lppl.txt.
%% 
%% Author: Peter Wilson (CUA)
%%         now at: peter.r.wilson@boeing.com
%% 

cmchar "AUncial digit 1";
beginchar("1", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
x1=x2=w/2;
y1=h; bot y2=0;
%%ductus:=true;
draw_tlserif(1,2,srad);
draw_tail(2,jut);
ductus:=false;
penlabels(1,2,100);
endchar; % end "1"

cmchar "AUncial digit 2";
beginchar("2", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; y1=3/4h;
x2=1/2w; top y2= h+o;
rt x3=w-pcshiftx; y3=3/4h;
lft x5=pcshiftx; bot y5=0;
rt x6=w-pcshiftx; y6=y5;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_fork(5,6,jut);                          % foot
draw z1{up}..z2{right}..z3{down}..{down}z5;    % curve
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "2"

cmchar "AUncial digit 3";
beginchar("3", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x3=pcshiftx; x10=x3;
rt x5=w-pcshiftx; x8=x5-3thin;
x4=0.5[x3,x5];
x9=0.5[x10,x8];
lft x6=pcshiftx; x7=x6;
y3=1/4h; y10=3/4h;
y6=0.5[y3,y10]; y7=y6;
bot y4=-thin; top y9=h+thin;
y5=0.5[y4,y6];
y8=0.5[y7,y9];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z3{down}..z4{right}..z5{up}...{left}z6;                 % bottom bowl
draw z7{right}...z8{up}..z9{left}..{down}z10;                   % top bowl
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,10,100);
endchar; % end "3"

cmchar "AUncial digit 4";
beginchar("4", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
x1=x2=w-pcshiftx-jut;
y1=h; bot y2=0;
lft x3=pcshiftx; y3=0.33h;
rt x4=w-pcshiftx; y4=y3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw_tstem(1,2,srad);              % main stem
draw_wave(3,4,thin);               % crossbar
draw z3wl--z1;                     % diagonal
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "4"

cmchar "AUncial digit 5";
beginchar("5", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
rt x1=w-pcshiftx; top y1=h;
lft x2=pcshiftx; y2=y1;
x3=x2; y3=0.5h+thin;
x4=0.5[x1,x3]; y4=0.5[y3,y5];
rt x5=w-pcshiftx; y5=0.3h;
x6=w/2; bot y6=0;
lft x7=pcshiftx; bot y7=2thin;
x8=w-pcshiftx; y8=h-0.5thin;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
%%draw_wave(2,1,thin);                      % top squiggle
draw z2--z1;                              % top bar
draw_rtriangle(8,3/4jut);
draw z3{pdir}..z5{down}..z6{left}..z7;    % curve
draw z2--z3;                            % vertical
ductus:=false;
penlabels(1,2,3,4,5,6,7,100);
endchar; % end "5"

cmchar "AUncial digit 6";
beginchar("6", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
rt x1=w-pcshiftx; top y1=h;
x2=1/2w; top y2= h;
lft x3=pcshiftx; y3=1/2h;
x4=x2; bot y4=0;
rt x5=w-pcshiftx; y5=1/4h;
lft x6=rt x3; bot y6=y3;
x8=w-pcshiftx; y8=h-0.5thin;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1{left}..z3{down}..z4{right}..z5{up}..{-pdir}z6;
draw_rtriangle(8,3/4jut);
ductus:=false;
penlabels(1,2,3,4,5,6,8,100);
endchar; % end "6"

cmchar "AUncial digit 7";
beginchar("7", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx;
rt x2=w-pcshiftx;
top y1=top y2=h;
x8=pcshiftx+3/8jut; y8=h-0.5thin;
x4=w/2; bot y4=0;
x3=0.5[x2,x4]-3thin; y3=0.5[y2,y4];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2;                 % top bar
draw_triangle(8,3/4jut);
draw z2..z3..z4;             % leg
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,100);
endchar; % end "7"

cmchar "AUncial digit 8";
beginchar("8", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
numeric alpha; alpha:=1;
pickup quill;
lft x1=alpha*pcshiftx+o; rt x3=w-alpha*pcshiftx-o; y1=y3=3/4h;
x2=x4=w/2; top y2=h+o; bot y4=-o;
x5=x2; y5=h/2+2o;
lft x11=pcshiftx; rt x13=w-pcshiftx; y11=y13=1/4h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2..z3..z5..z11..z4..z13..z5..cycle;
ductus:=false;
penlabels(1,2,3,4,5,11,13,100);
endchar; % end "8"

cmchar "AUncial digit 9";
beginchar("9", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; bot y1=2thin;
x2=1/2w; bot y2= 0;
rt x3=w-pcshiftx; y3=1/2h;
x4=x2; top y4=h;
lft x5=pcshiftx; y5=3/4h;
rt x6=lft x3; top y6=y3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2{right}..z3{up}..z4{left}..z5{down}..{pdir}z6;
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "9"

cmchar "Uncial digit 0";
beginchar("0", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
y100 = h/2; x100 = pcshiftx+y100;  % square center
pickup quill;
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h+o; bot y4=-o;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2..z3..z4..cycle;
ductus:=false;
proofpcbb(100, y100, y100);
penlabels(1,2,3,4,100);
endchar; % end "0"

