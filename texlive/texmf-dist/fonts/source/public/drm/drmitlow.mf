% +AMDG  This document was begun on 26 May 11EX, the
% commemoration of St. Felix I, PM, and it is humbly
% dedicated to him and to the Immaculate Heart of Mary for
% their prayers, and to the Sacred Heart of Jesus for His
% mercy.

def lftupbulb(suffix i,j,k,m) =
	z.j.l = z.i.l;
	z.k.l = z.i.l;
	z.m.l = z.i.l;
	penpos.j(bulbsh,90);
	penpos.k(bulbsh,0);
	penpos.m(bulbsh,-90);
enddef;

def topcurl(suffix i,j,k) =
	top z.j = z.i shifted (-curlw,curlh);
	z.k = z.j shifted (-curlw/2,-curlh);
	penpos.j(thickl/2,90);
	penpos.k(thinl,180);
enddef;

def botcurl(suffix i,j,k) =
	bot z.j = z.i shifted (curlw,-curlh);
	z.k = z.j shifted (curlw/2,curlh);
	penpos.j(thickl/2,90);
	penpos.k(thinl,180);
enddef;

def backbotcurl(suffix i,j,k) =
	bot z.j = z.i shifted (-curlw/2,1.5curlh);
	z.k = z.j shifted (curlw/2,curlh/2);
	penpos.j(thickl/2,180);
	penpos.k(thinl,90);
enddef;

def frontbotcurl(suffix i,j,k) =
	bot z.j = z.i shifted (curlw/2,curlh/2);
	z.k = z.j shifted (-curlw/2,curlh/2);
	penpos.j(thickl/2,0);
	penpos.k(thinl,90);
enddef;

beginchar("i",0.6exw#+2ss#,ex#,0); "The Letter i";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
z0 = (w/2,curlh);
z1 = (w/2,h-curlh);
penpos0(thickl,0);
penpos1(thickl,0);
topcurl(1,2,3);
botcurl(0,4,5);
penstroke z5e..z4e..z0e--z1e..z2e..z3e;
dotlessi := currentpicture;
% dot
z17 = (w/2,h+2userh+dotwid);
z18 = z17 shifted (0,dotwid);
z19 = z17 shifted (0,-dotwid);
z20 = z17 shifted (dotwid,0);
z21 = z17 shifted (-dotwid,0);
fill z18..z20..z19..z21..cycle;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("l",0.6exw#+2ss#,asch#,0); "The Letter l";
italcorr(asch#*slant);

pickup pencircle yscaled thinl;
z0 = (w/2,curlh);
z1 = (w/2,h-curlh);
penpos0(thickl,0);
penpos1(thickl,0);
topcurl(1,2,3);
botcurl(0,4,5);
penstroke z5e..z4e..z0e--z1e..z2e..z3e;

lowl := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("a",1.3exw#+2ss#,ex#,0); "The Letter a";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thinl,90);
penpos1(thinl,-90);
penpos2(curvl,180);
penpos3(thickl,0);
penpos4(thickl,0);
penpos7(thickl,0);
penpos8(thickl,0);
top z0 = (w/2-curlw+thickl/2,h+o);
bot z1 = (w/2-curlw+thickl/2,0-o);
lft z2r = (ss,h/2);
rt z3r = (w-ss-hair-curlw,h/2);
rt z4r = (w-ss-hair-curlw,curlh);
rt z8r = (w-ss-hair-curlw,h);
botcurl(4,5,6);
penstroke z1e..z2e..z0e..z3e..cycle;
penstroke z8e--z3e{down}..z4e..z5e..z6e;

lowa := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("q",1.3exw#+2ss#,ex#,dep#); "The Letter q";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thinl,90);
penpos1(thinl,-90);
penpos2(thickl,180);
penpos3(thickl,0);
penpos4(thickl,0);
penpos7(thickl,0);
penpos8(thickl,0);
penpos9(thickl,0);
top z0 = (w/2-curlw+2ss,h+o);
bot z1 = (w/2-curlw+2ss,0-o);
lft z2r = (ss,h/2);
rt z3r = (w-2ss-curlw,h/2);
rt z4r = (w-2ss-curlw,curlh);
rt z8r = (w-2ss-curlw,h);
z9 = (x4,-d+curlh);
botcurl(9,5,6);
penstroke z1e..z2e..z0e..z3e..cycle;
penstroke z8e--z3e{down}..z4e..z9e..z5e..z6e;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("p",1.3exw#+2ss#,ex#,dep#); "The Letter p";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thinl,90);
penpos1(thinl,-90);
penpos2(thickl,180);
penpos3(thickl,0);
penpos4(thickl,0);
penpos7(thickl,0);
penpos8(thickl,0);
penpos9(thickl,0);
top z0 = (w/2+curlw-2ss,h+o);
bot z1 = (w/2+curlw-2ss,0-o);
lft z2r = (2ss+curlw,h/2);
rt z3r = (w-ss,h/2);
lft z4r = (x2l,curlh);
lft z8r = (x2l,h-curlh);
z9 = (x4,-d);
topcurl(8,5,6);
penstroke z1e..z2e..z0e..z3e..cycle;
penstroke z9e--z4e---z8e..z5e..z6e;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("u",1.4exw#+2ss#,ex#,0); "The Letter u";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thickl,0);
penpos1(thickl,0);
penpos2(thinl,90);
penpos3(thickl,180);
penpos4(thickl,180);
penpos5(thickl,0);
penpos6(thickl,0);
penpos9(thickl,0);
lft z0l = (ss+hair+curlw,h-curlh);
lft z1l = (ss+hair+curlw,2curlh);
z2l = (w/2,0-o);
rt z3l = (w-curlw-2ss,curlh);
rt z4l = (w-curlw-2ss,h);
z5 = z4; z6 = z3;
botcurl(3,7,8);
topcurl(0,10,11);
penstroke z11e..z10e..z0e{down}..{down}z1e..{right}z2e{right}..z3e{up}..{up}z4e;
penstroke z6e..z7e..z8e;

lowu := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("d",1.3exw#+2ss#,asch#,0); "The Letter d";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thinl,90);
penpos1(thinl,-90);
penpos2(thickl,180);
penpos3(thickl,0);
penpos4(thickl,0);
penpos7(thickl,0);
penpos8(thickl,0);
penpos9(thickl,0);
top z0 = (w/2-curlw+2ss,ex+o);
bot z1 = (w/2-curlw+2ss,0-o);
lft z2r = (ss,ex/2);
rt z3r = (w-2ss-curlw,ex/2);
rt z4r = (w-2ss-curlw,curlh);
rt z8r = (w-2ss-curlw,h);
z9 = (x4,curlh);
botcurl(9,5,6);
penstroke z1e..z2e..z0e..z3e..cycle;
penstroke z8e--z3e{down}..z4e..z9e..z5e..z6e;

lowd := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("b",1.3exw#+2ss#,asch#,0); "The Letter b";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thinl,90);
penpos1(thinl,-90);
penpos2(thickl,180);
penpos3(thickl,0);
penpos4(thickl,0);
penpos7(thickl,0);
penpos8(thickl,0);
penpos9(thickl,0);
top z0 = (w/2+curlw-2ss,ex+o);
bot z1 = (w/2+curlw-2ss,0-o);
lft z2r = (2ss+curlw,ex/2);
rt z3r = (w-ss,ex/2);
lft z4r = (x2l,curlh);
lft z8r = (x2l,h-curlh);
z9 = (x4,0);
topcurl(8,5,6);
penstroke z1e..z2e..z0e..z3e..cycle;
penstroke z9e--z4e---z8e..z5e..z6e;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("h",1.2exw#+2ss#,asch#,0); "The Letter h";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thinl,90);
penpos1(thinl,-90);
penpos2(thickl,180);
penpos3(thickl,0);
penpos4(thickl,0);
penpos7(thickl,0);
penpos8(thickl,0);
penpos9(thickl,0);
penpos10(thickl,180);
penpos11(thickl,180);
top z0 = (w/2+curlw-2ss,ex+o);
bot z1 = (w/2+curlw+hair,0-o);
lft z2r = (2ss+curlw,ex/2);
rt z3r = (w-ss,ex/2);
lft z4r = (x2l,curlh);
lft z8r = (x2l,h-curlh);
z9 = (x4,0);
z10 = z9;
z11 = z4;
topcurl(8,5,6);
backbotcurl(1,12,13);
penstroke z10e..z11e..z2e..z0e..z3e..z1e..z12e..z13e;
penstroke z9e--z4e---z8e..z5e..z6e;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("o",1exw#+2ss#,ex#,0); "The Letter o";
italcorr(ex#*slant);

penpos0(thinl,90);
penpos1(thinl,-90);
penpos2(curvl,180);
penpos3(curvl,0);
top z0r = (w/2,h+o);
bot z1r = (w/2,0-o);
lft z2r = (ss,h/2);
rt z3r = (w-ss,h/2);

penstroke z0e..z3e..z1e..z2e..cycle;

lowo := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("j",0.6exw#+2ss#,ex#,dep#); "The Letter j";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
z0 = (w/2,-d+curlh);
z1 = (w/2,h-curlh);
bot z4 = z0 shifted (-curlw,-curlh);
z5 = z4 shifted (-curlw,curlh);
penpos0(thickl,0);
penpos1(thickl,0);
penpos4(thinl,-90);
penpos5(bulbsh,-180);
topcurl(1,2,3);
lftupbulb(5,6,7,8);
penstroke z8e..z7e..z6e..z5e..z4e..z0e---z1e..z2e..z3e;
dotlessj := currentpicture;
% dot
z17 = (w/2,h+2userh+dotwid);
z18 = z17 shifted (0,dotwid);
z19 = z17 shifted (0,-dotwid);
z20 = z17 shifted (dotwid,0);
z21 = z17 shifted (-dotwid,0);
fill z18..z20..z19..z21..cycle;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("k",1.3exw#+2ss#,asch#,0); "The Letter k";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(1.5thinl,20);
penpos1(thinl,-90);
penpos2(thickl,180);
penpos3(thinl,-90);
penpos4(thickl,0);
penpos7(thickl,0);
penpos8(thickl,0);
penpos9(thickl,0);
penpos11(thinl,225);
rt z0r = (w-ss-thinl,ex+o-curlh);
bot z1 = (w-ss-curlw,0-o);
lft z2r = (2ss+curlw,ex/2);
rt z3r = (2w/3,0.4ex);
lft z4r = (x2l,curlh);
lft z8r = (x2l,h-curlh);
z9 = (x4,0);
z10 = z2;
z12 = (w,y2) rotatedaround (z2,45);
z11 = (z3--z10) intersectionpoint (z2--z12);
topcurl(8,5,6);
frontbotcurl(1,13,14);
penstroke z9e--z4e---z8e..z5e..z6e;
penstroke z2e{dir 45}..z0e..z3e..z11e;
penstroke z11e{dir -0}..z1e{right}..z1e..z13e..z14e;

penlabels(0,1,2,3,4,5,6,7,8,9,10,12);
endchar;

beginchar("g",exw#+2ss#,ex#,dep#); "The Letter g";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thinl,90);
penpos1(thinl,-90);
penpos2(thickl,180);
penpos3(thickl,0);
penpos4(thickl,0);
penpos5(thinl,-90);
penpos6(1thinl,180);
penpos7(thickl,0);
penpos8(thickl,0);
penpos9(thickl,0);
top z0 = (w/2,h+o);
bot z1 = (w/2,0-o);
lft z2r = (ss,h/2);
rt z3r = (w-ss,h/2);
rt z4r = (w-ss,curlh);
rt z8r = (w-ss,h);
z9 = (x4,-d/2);
bot z5 = (w/2,-d);
lft z6r = (ss,-d/2);
lftupbulb(6,10,11,12);
penstroke z1e..z2e..z0e..z3e..cycle;
penstroke
z8e--z3e{down}--z4e--z9e..{left}z5e{left}..z6e..z10e..z11e..z12e;

lowg := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("c",exw#+2ss#,ex#,0); "The Letter c";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
rt z0r = (w-ss,3h/4);
z1r = (w/2,h+o);
lft z2r = (ss,h/2);
z3r = (w/2,0-o);
rt z4r = (w-ss,h/4);
penpos0(thinl,0);
penpos1(thinl,90);
penpos2(curvl,180);
penpos3(thinl,270);
penpos4(thinl,0);

penstroke z0e..z1e..z2e..z3e..z4e;

lowc := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("e",exw#+2ss#,ex#,0); "The Letter e";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(curvl,0);
penpos1(thinl,90);
penpos2(curvl,180);
penpos3(thinl,270);
penpos4(thickl,0);
penpos5(thinl,-90);
penpos6(thinl,-90);
rt z0r = (w-ss,3h/4);
z1r = (w/2,h+o);
lft z2r = (ss,h/2);
z3r = (w/2,0-o);
rt z4r = (w-ss,h/4);
z5 = (x1,h/2);
z6 = z2;

penstroke z6e{right}..z5e{right}..z0e..z1e{left}..z2e..z3e..z4e;

lowe := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("f",exw#+2ss#,asch#,dep#); "The Letter f";
italcorr(ex#*slant);

penpos0(thickl,0);
z0 = (w/2,h-curlh);
penpos1(thickl,0);
z1 = (w/2,-d+curlh);
penpos2(thinl,-90);
z2 = (3w/4,h+o);
penpos3(thinl,-90);
z3 = (x1-curlw,-d-o);
penpos4(thickl,180);
z4 = (x2+curlw,y0);
penpos5(thickl,-180);
z5 = (x3-curlw,y1);
penpos6(thinl,70);
z6r = (w/2,ex);
penpos7(thinl,70);
z7 = (ss,y6);
penpos8(thinl,70);
z8 = (w-ss,y6);
penpos9(thinl,0);
z9r = z4r;
penpos10(thinl,225);
z10r = z9r;
penpos11(thinl,0);
z11l = z5l;
penpos12(thinl,225);
z12l = z11l;

penstroke
z12e..z11e..z5e..{right}z3e{right}..z1e---z0e..{right}z2e{right}..z4e..z9e..z10e;
penstroke z7e--z8e;

picture lowf;
lowf := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("m",1.3em#+2ss#,ex#,0); "The Letter m";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thickl,0);
z0 = (ss+2curlw,h-curlh);
penpos1(thickl,0);
z1 = (ss+2curlw,0);
penpos2(thinl,-90);
z2l = (x0 + 0.2em,h+o);
penpos3(thickl,-180);
z3 = (x2 + 0.2em,y0);
penpos4(thickl,-180);
z4 = (x3,0);
penpos5(thinl,90);
z5r = (x3 + 0.2em,h+o);
penpos6(thickl,0);
z6 = (x5 + 0.2em,y3);
penpos7(thickl,0);
z7 = (x6,curlh);

topcurl(0,11,12);
botcurl(7,8,9);
penstroke z12e..z11e..z0e---z1e;
penstroke z1e---z0e..z2e..z3e---z4e;
penstroke z4e---z3e..z5e..z6e---z7e..z8e..z9e;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("n",0.7em#+2ss#,ex#,0); "The Letter n";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thickl,0);
z0 = (ss+hair+curlw,h-curlh);
penpos1(thickl,0);
z1 = (ss+hair+curlw,0);
penpos3(thickl,0);
z3 = (w-ss-hair-curlw,y0);
penpos4(thickl,0);
z4 = (w-ss-hair-curlw,curlh);
penpos2(thinl,90);
z2r = ((x0+x3)/2,h+o);
penpos5(thickl,180);
z5 = z1;
penpos6(thickl,180);
z6 = z0;

topcurl(0,11,12);
botcurl(4,8,9);
penstroke z12e..z11e..z0e---z1e;
penstroke z5e---z6e..z2e..z3e---z4e..z8e..z9e;

lown := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("r",1.3exw#+2ss#,ex#,0); "The Letter r";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thickl,0);
z0 = (ss+2curlw,0);
penpos1(thickl,0);
z1 = (ss+2curlw,h-curlh);
topcurl(1,2,3);
penpos4(1.5thinl,180);
z4l = (w-ss,y1-hair);
penpos5(thinl,-90);
z5l = (((x1+x4)/2),h+o);
penpos6(1.5thinl,90);
z6r = z4r;
penpos7(1.5thinl,0);
z7r = z6r;
penpos8(1.5thinl,-60);
z8r = z7r;

penstroke z3e..z2e..z1e---z0e;
penstroke z1e..{right}z5e{right}..z4e..z6e..z7e..z8e;

lowr := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("s",exw#+2ss#,ex#,0); "The Letter s";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(1.5thinl,0);
z0r = (w-ss,3h/4);
penpos1(thinl,90);
z1r = (w/2,h+o);
penpos2(curvl,180);
z2r = (ss,3h/4);
penpos3(curvl,-180);
z3l = (w-ss,h/4);
penpos4(thinl,90);
z4l = (w/2,0-o);
penpos5(1.5thinl,0);
z5l = (ss,h/4);
penpos6(thinl,-90);
z6 = (w/2,h/2);
penpos7(thinl,-90);
z7l = z0l;
penpos8(thinl,-180);
z8l = z7l;
penpos9(thinl,90);
z9l = z8l;
penpos10(thinl,90);
z10r = z5r;
penpos11(thinl,180);
z11r = z10r;
penpos12(thinl,-90);
z12r = z11r;

penstroke z9e..z8e..z7e..
z0e{up}..{left}z1e{left}..{down}z2e{down}..z6e..{down}z3e{down}..{left}z4e{left}..{up}z5e..z12e..z11e..z10e;

lows := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("t",exw#/phi+2ss#,theight#,0); "The Letter t";
italcorr(ex#*slant);

z0 = (ss+hair,ex);
z1 = z0 shifted (0,-thinl);
z2 = (w-ss/2-hair,ex);
z3 = z2 shifted (0,-thinl);
z4 = (w/2+thickl/2,h+o);
z5 = (w/2,0-o);
z6 = z5 shifted (-thickl/2,2lserh);
z7 = z6 shifted (thickl,0);
z8 = (x7,y2);
z9 = (x7,y3);
z10 = (x6,y1);
z11 = (w-ss,y7);
z12 = z11 shifted (-thinl/2,0);
z13 = 0.5[z7,z12] shifted (0,-y7/2);
z14 = (x7,y5);
z15 = 0.5[z2,z3] shifted (hair,0);
z16 = 0.5[z0,z1] shifted (-hair,0);
z17 = (x10,y0);
z18 = 0.5[z0,z17];
z19 = z4 shifted (-hair/4,0);

pickup pencircle scaled 2;
fill
z4--z8--z2..z15..z3--z9--z7{down}..{right}z13{right}..{up}z12..z11{down}..{left}z14{left}..{up}z6--z10--z1..z16..z0--z18{right}..{dir
80}z19--z4--cycle;

lowt := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51);
endchar;

beginchar("v",exw#+2ss#,ex#,0); "The Letter v";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thinl,180);
z0r = (ss,h-curlh/2);
penpos1(1.5thinl,90);
z1r = (x0+curlw/3,h+o);
penpos2(thinl,45);
z2 = (x1+curlw/3,h-curlh/2);
penpos3(thinl,180);
z3r = (x2+curlw/2,h+o);
penpos4(thickl,0);
z4 = (x2,h/3);
penpos5(thinl,90);
z5l = (w/2,0-o);
penpos6(thickl,180);
z6l = (w-ss,h/3);
penpos7(thinl,-90);
z7l = (w-x2,h+o);
penpos8(thinl,0);
z8 = z3;
penpos9(thinl,0);
z9r = z7r;
penpos10(thinl,120);
z10r = z9r;
penpos11(thinl,180);
z11r = z10r;

penstroke z0e..z1e..z2e..{up}z3e;
penstroke z8e{down}..z4e..{right}z5e{right}..z6e..z7e;
penstroke z7e..z9e..z10e..z11e..z7e;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("w",1em#+2ss#,ex#,0); "The Letter w";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thinl,180);
z0r = (ss,h-curlh/2);
penpos1(1.5thinl,90);
z1r = (x0+curlw/3,h+o);
penpos2(thinl,45);
z2 = (x1+curlw/3,h-curlh/2);
penpos3(thinl,180);
z3r = (x2+curlw/2,h+o);
penpos4(thickl,0);
z4 = (x2,h/3);
penpos5(thinl,90);
z5l = (w/4,0-o);
penpos6(thickl,180);
z6 = (w/2,h/3);
penpos7(thickl,180);
z7r = (w/2,h+o);
penpos8(thinl,0);
z8 = z3;
penpos9(thinl,90);
z9l = (x6 + ((x5-x4)),0-o);
penpos10(thinl,180);
z10l = (w-x8,h+o);
penpos11(thickl,0);
z11 = z6;
penpos12(thickl,180);
z12l = (w-ss,h/3);
penpos13(thinl,0);
z13 = z7;

penstroke z0e..z1e..z2e..{up}z3e;
penstroke z8e{down}..z4e..{right}z5e{right}..z6e..{up}z7e;
penstroke z11e{down}..{right}z9e{right}..z12e..tension 1.2..z10e;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("x",1.2exw#+2ss#,ex#,0); "The Letter x";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(slantl,0);
z0 = (ss+2curlw,h-curlh);
penpos1(slantl,0);
z1 = (w-ss-2curlw,curlh);
topcurl(0,2,3);
botcurl(1,4,5);
penpos6(1.5thinl,0);
z6r = (w-ss-curlw/2,h);
penpos7(1.5thinl,0);
z7l = (ss+curlw/2,0);

penstroke z3e..z2e..z0e---z1e..z4e..z5e;
penstroke z6e--z7e;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("y",1.2exw#+2ss#,ex#,dep#); "The Letter y";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thickl,0);
z0r = (w-ss,h);
penpos1(thickl,-30);
z1 = (3w/4,curlh/2);
penpos2(thinl,-90);
z2r = (2curlw,-d);
penpos3(1.5thinl,-180);
z3r = (ss,-d+curlh);
penpos4(1.5thinl,90);
z4l = z3l;
penpos5(1.5thinl,0);
z5l = z4l;
penpos6(1.5thinl,-90);
z6l = z5l;
penpos7(1.5thinl,0);
z7 = (ss+2curlw,h-curlh);
penpos8(1.5thinl,45);
z8 = (x1,0);
topcurl(7,9,10);

penstroke z0e..z1e..{left}z2e..z3e..z4e..z5e..z6e;
penstroke z10e..z9e..z7e..z8e;

lowy := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar("z",1exw#+2ss#,ex#,dep#); "The Letter z";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thickl,90);
z0r = (ss,h-o);
penpos1(2thinl,180);
z1l = (w-ss,h);
penpos2(thickl/2,90);
z2 = 0.666[z0,z1] shifted (-thinl,-thinl);
penpos3(2thinl,180);
z3r = (ss,0-o);
penpos4(thinl,70);
z4r = (x0l+thinl/2,y0l);
penpos5(3thickl/4,90);
z5r = ((x0+x2)/2,h+o);
penpos6(thickl,-90);
z6r = (w-ss,-2d/3);
penpos7(thickl/2,-90);
z7 = (w/2,0);
penpos8(3thickl/4,-90);
z8 = 0.5[(x7,0),(x6,0)] shifted (0,-d/3);
penpos9(thinl,230);
z9l = z3l;

penstroke z4e--z0e;
penstroke z0e..z5e..z2e..z1e;
penstroke z1e--z3e;
penstroke z3e..z9e{right}..z7e..z8e..z6e;

lowz := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar(oct"031",0.6exw#+2ss#,ex#,0); "dotless i";
italcorr(ex#*slant);

currentpicture := dotlessi;

endchar;

beginchar(oct"032",0.6exw#+2ss#,ex#,dep#); "dotless j";
italcorr(ex#*slant);

currentpicture := dotlessj;

endchar;

beginchar(oct"360",exw#+2ss#,asch#,0); "The Letter eth";
italcorr(ex#*slant);

z0 = (ss,h);
z1 = z0 shifted (0,-thinl);
z2 = 0.5[z0,z1] shifted (-o,0);
z3 = (w/2,ex+o/2);
z4 = z3 shifted (0,-thinl);
z5 = (w/2,-o/2);
z6 = z5 shifted (0,thinl);
z7 = (ss,ex/2);
z8 = z7 shifted (thickl,0);
z9 = (w-ss,ex/2);
z10 = z9 shifted (-thickl,0);

fill
z7{up}..{right}z3{right}..{down}z9{down}..{left}z5{left}..{up}cycle;
unfill
z8{up}..{right}z4{right}..{down}z10{down}..{left}z6{left}..{up}cycle;
fill z9{up}..{left}z0..z2..z1{right}..{down}z10--cycle;

pickup pencircle scaled thinl;
top rt z11 = (3w/4,h);
z13 = (0,h/2);
bot lft z14 = (z11--z13) intersectionpoint
	(z10{up}..{left}z1);
z12 = 2.5[z11,z14];
draw z11--z12;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51);
endchar;

beginchar(oct"370",1exw#+2ss#,ex#,0); "The slashed o";
italcorr(ex#*slant);

currentpicture := lowo;

pickup pencircle scaled thinl;
bot lft z0 = (ss,0-o);
top rt z1 = (w-ss,h+o);
draw z0--z1;

penlabels(0,1,2,3,4);
endchar;

beginchar(oct"033",1.6exw#+2ss#,asch#,0); "The ligature ff";
italcorr(ex#*slant);

currentpicture := lowf + lowf shifted (0.5ex,0);

endchar;

beginchar(oct"034",1.1exw#+2ss#,asch#,0); "The ligature fi";
italcorr(ex#*slant);

currentpicture := lowf;
pickup pencircle yscaled thinl;
z0 = (w/2,curlh) shifted (0.45ex,0);
z1 = (w/2,ex) shifted (0.45ex,0);
penpos0(thickl,0);
penpos1(thickl,0);
botcurl(0,4,5);
penstroke z5e..z4e..z0e--z1e;

picture filig; filig := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51);
endchar;

beginchar(oct"036",1.85exw#+2ss#,asch#,0); "The ligature ffi";
italcorr(ex#*slant);

currentpicture := lowf + filig shifted (0.5ex,0);

endchar;

beginchar(oct"035",1.2exw#+2ss#,asch#,0); "The ligature fl";
italcorr(ex#*slant);

currentpicture := lowf;
pickup pencircle yscaled thinl;
z0 = (w/2,curlh) shifted (0.39ex,0);
z1 = (w/2,h-curlh) shifted (0.39ex,0);
penpos0(thickl,0);
penpos1(thickl,0);
botcurl(0,4,5);
penstroke z5e..z4e..z0e--z1e;

picture fllig; fllig := currentpicture;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51);
endchar;

beginchar(oct"037",1.85exw#+2ss#,asch#,0); "The ligature ffl";
italcorr(ex#*slant);

currentpicture := lowf + fllig shifted (0.5ex,0);

endchar;

beginchar(oct"040",((exw#/phi)*2)+ss#+2o#,asch#,0); "The ligature ft";
italcorr(ex#*slant);

currentpicture := lowf + lowt shifted (0.68ex,0);
pickup pencircle scaled thinl;
penpos0(1.6thinl,45);
lft z0r = (ss,ex);
penpos1(1.6thinl,45);
z1r = (w-thickl,ex);
penstroke z0e--z1e;

penlabels(0,1);
endchar;

beginchar(oct"377",1exw#+2ss#,asch#,dep#); "The Letter Eszett";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thickl,0);
z0r = (w-ss,3h/4);
penpos1(thinl,90);
z1r = (w/2,h+o);
penpos2(thickl,180);
z2r = (ss,3h/4);
penpos3(thickl,180);
z3r = (ss,-d+curlh);
penpos4(1.5thinl,90);
z4l = (x3r-curlw,-d-o);
penpos5(1.5thinl,0);
z5 = (x4-curlw,y4+curlh);
penpos6(thinl,-90);
z6l = ((x2l+x1)/2,3h/5);
penpos7(thickl,180);
z7l = (w-ss,h/4);
penpos8(thinl,90);
z8l = (x6+thinl,0-o);
penpos9(thinl,0);
z9r = z8r shifted (-thinl,thinl);
penpos10(thinl,-90);
z10r = z9r shifted (thinl,thinl);

penstroke z5e..z4e..z3e---z2e..z1e..z0e..{left}z6e;
penstroke z6e{right}..z7e..{left}z8e..z9e..z10e;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51);
endchar;

beginchar(oct"367",1.4exw#+2ss#,ex#,0); "the ligature oe";
italcorr(ex#*slant);

hw = w/2;
z30 = (hw+hw/2-ss,h+o);
z31 = (w-ss,h/2);
z33 = z30 shifted (0,-thinl);
z34 = (w/2-thickl/2,h/2);
z35 = z34 shifted (thickl,0);
z36 = z30 shifted (0,-h-o);
z37 = z36 shifted (0,thinl);
z38 = z31 shifted (0,-h/4);
z39 = z38 shifted (0,hair/2);
z40 = z39 shifted (-hair/2,0);
z41 = z31 shifted (-thickl,thinl);
z42 = z35 shifted (0,thinl);
% o part
z0 = (ss,h/2);
z1 = (hw/2+ss,h+o);
z2 = z0 shifted (thickl,0);
z3 = z1 shifted (0,-thinl);
z4 = (hw/2+ss,-o/2);
z5 = z4 shifted (0,thinl);

fill z1..z35..z4..z0..cycle;
unfill z3..z34..z5..z2..cycle;

fill
z31{up}..{left}z30{left}..{down}z34{down}..{right}z36{right}..z38..z39..z40..{left}z37{left}..{up}z35{up}..{right}z33{right}..{down}z41--z42--z35--z31--cycle;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51);
endchar;

beginchar(oct"346",1.4exw#+2ss#,ex#,0); "the ligature ae (aesc)";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thinl,90);
penpos1(thinl,-90);
penpos2(curvl,180);
penpos3(thickl,0);
penpos4(thickl,0);
penpos7(thickl,0);
penpos8(thickl,0);
top z0 = (hw/2+thickl/2,h+o);
bot z1 = (hw/2+thickl/2,0-o);
lft z2r = (ss,h/2);
rt z3r = (hw+thickl/2,h/2);
rt z4r = (hw+thickl/2,curlh);
rt z8r = (hw+thickl/2,h);
penpos9(thickl,0);
z9 = (x8,0);
penstroke z1e..z2e..z0e..z3e..cycle;
penstroke z8e--z9e;
% e part
pickup pencircle yscaled thinl;
penpos10(thickl,0);
penpos11(thinl,90);
penpos12(thickl,180);
penpos13(thinl,270);
penpos14(thickl,0);
penpos15(thinl,-90);
penpos16(thinl,-90);
rt z10r = (w-ss,3h/4);
z11r = (hw+hw/2,h+o);
z12l = z3r;
z13r = (hw+hw/2,0-o);
rt z14r = (w-ss,h/4);
z15 = (x11,h/2);
z16 = z12;

penstroke z16e{right}..z15e{right}..z10e..z11e{left}..
	z12e..z13e..z14e;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51);
endchar;

beginchar(oct"255",exw#+2ss#,ex#,dep#); "The Letter ng";
italcorr(ex#*slant);

z0 = (ss+lserwl,lserhl);
z1 = z0 shifted (thickl,0);
z2 = z1 shifted (o+2lserwl,0);
z3 = z2 shifted (thickl,0);
z20 = (ss+userwl,h-userhl);
z21 = z20 shifted (thickl,0);
z22 = z21 shifted (0,userhl);
z23 = z21 shifted (o+2userwl,0);
z24 = z23 shifted (thickl,-thinl);
z29 = z20 shifted (0,userhl);
z30 = z24 shifted (0,userhl+thinl);
z31 = 0.5[z30,z29] shifted (0,o/2);
z32 = z31 shifted (0,-0.8thinl);
z33 = z28 shifted (0,-userhl);
z40 = z2;
z41 = z3;
z49 = (x40-6o,-dep);
z50 = z49 shifted (0,thinl);
z51 = (x1,-dep/2);
z56 = 0.5[z2,(x2,-dep)];

llserif(0,4,5,6,7)(1);
lrserif(1,8,9,10,11)(1);
ulserif(20,25,26,27,28)(1);
ulftbulb(51,52,53,54,55);

fill
z6--z5..z4..z0--z20..z25..z26--z27--z28--z33--z20..{right}z31{right}..{down}z24--z3{down}..z49..{up}z51..z52..z53..{left}z54{down}..z50..z56..{up}z2--z23..z32..z21--z1..z8..z9--z10--z6--cycle;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56);
endchar;

beginchar(oct"376",1.3exw#+2ss#,asch#,dep#); "The Letter thorn";
italcorr(ex#*slant);

pickup pencircle yscaled thinl;
penpos0(thinl,90);
penpos1(thinl,-90);
penpos2(thickl,180);
penpos3(thickl,0);
penpos4(thickl,0);
penpos7(thickl,0);
penpos8(thickl,0);
penpos9(thickl,0);
top z0 = (w/2+curlw-2ss,ex+o);
bot z1 = (w/2+curlw-2ss,0-o);
lft z2r = (2ss+curlw,ex/2);
rt z3r = (w-ss,ex/2);
lft z4r = (x2l,curlh);
lft z8r = (x2l,h-curlh);
z9 = (x4,-d);
topcurl(8,5,6);
penstroke z1e..z2e..z0e..z3e..cycle;
penstroke z9e--z4e---z8e..z5e..z6e;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;
