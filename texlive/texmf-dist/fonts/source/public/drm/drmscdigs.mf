% +AMDG  This document was begun on 6 Aug 11EX, the Feast of
% the Transfiguration of Our Lord Jesus Christ, and the
% commemorations of St Xystus, PM, and Felicissimus and
% Agapitus, MM, and it is humbly dedicated to them and to
% the Immaculate Heart of Mary for their prayers, and to the
% Sacred Heart of Jesus for His mercy.

picture six;

beginchar("1",0.65digwid#+2ss#,ex#,0); "The digit 1";
italcorr(ex#*slant);

z0 = (w/2-thickstroke/2,lserhl);
z1 = z0 shifted (thickstroke,0);
z10 = (w/2-thickstroke/2,h-2userh);
z11 = z10 shifted (thickstroke,0);
z12 = z11 shifted (0,2userh+o);
z13 = z12 shifted (-thickstroke,0);
z14 = z10 shifted (-lserwl-o,0);
z15 = z14 shifted (0,thinl);

llserif(0,2,3,4,5)(1);
lrserif(1,6,7,8,9)(1);

fill
z0..z2..z3--z4--z8--z7..z6..z1--z12--z13{down}..{left}z15--z14--z10--z0--cycle;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64,65,66,67,68,69,70,71,72,73,74,75,76,77,78,79,80);
endchar;

beginchar("2",0.75digwid#+2ss#,ex#,0); "The digit 2";
italcorr(ex#*slant);

z0 = (w/2,h+o);
z1 = z0 shifted (0,-thinl);
z2 = (ss,3h/4);
z3 = z2 shifted (bulbsh,-bulbsh);
z4 = z3 shifted (bulbsh,bulbsh);
z5 = z4 shifted (-bulbsh,bulbsh);
z6 = (w-ss,3h/4);
z7 = z6 shifted (-curvl,0);
z8 = (ss,thickl);
z9 = z8 shifted (1.5thinl,0);
z10 = (ss,0);
z11 = (w-ss,0);
z12 = z11 shifted (0,thickl);
z13 = z12 shifted (0,thinl);
z14 = z13 shifted (-thinl/2,0);
z15 = z12 shifted (-thinl/2,0);

fill
z5{right}..{down}z4{down}..{left}z3{left}..{up}z2{up}..{right}z0{right}..{down}z6{down}..{down}z9--z15--z14--z13--z11--z10--z8{up}..{up}z7{up}..{left}z1{left}..{dir
-120}cycle;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64,65,66,67,68,69,70,71,72,73,74,75,76,77,78,79,80);
endchar;

beginchar("3",0.75digwid#+2ss#,ex#,0); "The digit 3";
italcorr(ex#*slant);

z0 = (w/2,h+o);
z1 = (w/2,0-o);
z2 = (ss,.75h);
z3 = (ss,.25h);
z4 = z0 shifted (0,-thinstroke);
z5 = z1 shifted (0,thinstroke);
z6 = z8 shifted (-curvwid,0);
z7 = z9 shifted (-curvwid,0);
z8 = (w-ss,3h/4);
z9 = (w-ss,h/4);
z10 = (w/2-2thinstroke,h/2+thinstroke/2);
z11 = z10 shifted (0,-thinstroke);
z20 = z10 shifted (1.5thickstroke,-thinstroke/2);
z21 = z9 shifted (-o/4,0);
z22 = z10 shifted (1.5thickl,0);
z23 = z11 shifted (1.5thickl,0);

ulftbulb(2,12,13,14,15);
ulftbulb(3,16,17,18,19);

fill
z2{up}..{right}z0{right}..{down}z8{down}..z20..{left}z11--z10{right}..z20..{down}z9{down}..{left}z1{left}..{up}z3{up}..{right}z16{right}..{down}z17{down}..{left}z18{dir
-60}..{right}z5{right}..{up}z7{up}..{left}z11--z10{right}..{up}z6{up}..{left}z4{left}..{dir
-120}z12{right}..{down}z13{down}..{left}z14{left}..{up}cycle;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64,65,66,67,68,69,70,71,72,73,74,75,76,77,78,79,80);
endchar;

beginchar("4",0.75digwid#+2ss#,ex#,0); "The digit 4";
italcorr(ex#*slant);

z0 = (w-ss-lserwl,h+2o);
z1 = (x0,2h/5);
y2 = y1; x2 = ss;
z3 = z0 shifted (0,-thinstroke);
z4 = z2 shifted (thinstroke,0);
z6 = z1 shifted (-thickstroke,0);
z7 = z0 shifted (-thickstroke,0);
z5 = zca[z3,z4];
z5 = zcb[z6,z7];
z8 = z2 shifted (0,-thinstroke);
z9 = z4 shifted (0,-thinstroke);
z10 = z6 shifted (0,-thinstroke);
z11 = z1 shifted (0,-thinstroke);
z12 = z1 shifted (lserwl,0);
z13 = z11 shifted (lserwl,0);
x14 = x10; y14 = lserhl;
z15 = z14 shifted (thickstroke,0);

llserif(14,16,17,18,19)(1);
lrserif(15,20,21,22,23)(1);

fill
z14..z16..z17--z18--z22--z21..z20..z15--z11--z13--z12--z1--z0--z2--z8--z10--cycle;
unfill z4--z6--z5--z4--cycle;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64,65,66,67,68,69,70,71,72,73,74,75,76,77,78,79,80);
endchar;

beginchar("5",0.75digwid#+2ss#,ex#,0); "The digit 5";
italcorr(ex#*slant);

z0 = (w-ss-bulbsh,h+o);
z1 = (ss+bulbsh,h+o);
z2 = z0 shifted (0,-thickstroke);
z3 = z1 shifted (0,-thickstroke);
z4 = (w/2,0);
z5 = z4 shifted (0,thinstroke);
z6 = z3 shifted (thinstroke,0);
z7 = (ss+0.6bulbsh,0.55h);
z19 = z7 shifted (thinstroke,0);
z8 = (w-ss,2h/3);
z9 = z7 shifted (thinstroke,thinstroke);
z10 = (w-ss-thickstroke,h/3);
z11 = (ss,h/3);
z16 = z10 shifted (thickstroke,0);
z17 = (w/2,2h/3);
z18 = z17 shifted (0,-thinstroke);
z29 = z2 shifted (0,-thinl);
z20 = z29 shifted (-thinl/2,0);
z21 = z20 shifted (0,thinl);

ulftbulb(11,12,13,14,15);

fill
z6--z21--z20--z29--z2--z0--z1--z7--z19..{right}z18{right}..{down}z10{down}..{left}z5{left}..{up}z14{right}..{up}z13..z12..z11{down}..{right}z4{right}..z16{up}..{left}z17{left}..z9--z6--cycle;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64,65,66,67,68,69,70,71,72,73,74,75,76,77,78,79,80);
endchar;

beginchar("7",0.65digwid#+2ss#,ex#,0); "The digit 7";
italcorr(ex#*slant);

z0 = (ss,h+o);
z1 = z0 shifted (0,-thickstroke);
z2 = (w-ss,h+o);
z3 = z2 shifted (0,-thickstroke);
z4 = (w/4,0);
z5 = z4 shifted (thickstroke,0);
z6 = z3 shifted (-1.8thinstroke,0);
z7 = (w/3,0.6h);
z8 = z7 shifted (1.2thinstroke,0);
z9 = z1 shifted (0,-0.7userh);
z10 = z9 shifted (0.5thinstroke,0);
z11 = z10 shifted (0,0.7userh);

fill z11--z10--z9--z0--z2--z3..z8..z5--z4..z7..z6--cycle;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64,65,66,67,68,69,70,71,72,73,74,75,76,77,78,79,80);
endchar;

beginchar("8",0.75digwid#+2ss#,ex#,0); "The digit 8";
italcorr(ex#*slant);

z0 = (w/2,h+o);
z1 = (w/2,0-o);
z2 = (ss+bulbsh/2,3h/4);
z3 = (ss,h/4);
z4 = (w-ss-bulbsh/2,3h/4);
z5 = (w-ss,h/4);
z6 = z0 shifted (0,-thinstroke);
z7 = z1 shifted (0,thinstroke);
z8 = z2 shifted (0.8curvwid,0);
z9 = z3 shifted (curvwid,0);
z10 = z5 shifted (-curvwid,0);
z11 = z4 shifted (-0.8curvwid,0);
z12 = (w/2,h/2+thinstroke);
z13 = z12 shifted (0,-thinstroke);

pickup pencircle scaled 1;

fill z0..z4..z13--z12..z5..z1..z3..z12--z13..z2..cycle;
unfill z8..z6..z11..z12..cycle;
unfill z7..z10..z13..z9..cycle;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64,65,66,67,68,69,70,71,72,73,74,75,76,77,78,79,80);
endchar;

beginchar("0",0.75digwid#+2ss#,ex#,0); "The digit 0";
italcorr(ex#*slant);

z0 = (w/2,h+o);
z1 = (w/2,-o);
z2 = (w-ss,h/2);
z3 = (ss,h/2);
z4 = z0 shifted (0,-thinstroke);
z5 = z1 shifted (0,thinstroke);
z6 = z2 shifted (-curvwid,0);
z7 = z3 shifted (curvwid,0);

fill z0{right}..{down}z2{down}..{left}z1{left}..{up}z3{up}..{right}cycle;
unfill z4..z6..z5..z7..cycle;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64,65,66,67,68,69,70,71,72,73,74,75,76,77,78,79,80);
endchar;

beginchar("6",0.75digwid#+2ss#,ex#,0); "The digit 6";
italcorr(ex#*slant);

penpos0(thinl,90);
z0r = (2w/3,h+o);
penpos1(2thinl,180);
z1r = (ss+thinl,2h/3);
penpos2(thickl,180);
z2r = (ss,ex/4);
penpos3(thinl,-90);
z3r = (w/2,0-o);
penpos4(thickl,0);
z4r = (w-ss,ex/4);
penpos5(thinl,90);
z5l = (w/2,ex/2+o);
penpos6(thinl,0);
z6l = z0l;
penpos7(thinl,-90);
z7l = z6l;
penpos8(thinl,180);
z8l = z7l;

penstroke z5e..z2e..z3e..z4e..cycle;
penstroke z0e..z1e..z2e;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51);
endchar;

beginchar("9",0.75digwid#+2ss#,ex#,0); "The digit 9";
italcorr(ex#*slant);

penpos0(thinl,90);
z0l = (w/3,0-o);
penpos1(2thinl,180);
z1l = (w-ss-thinl,h/3);
penpos2(thickl,180);
z2l = (w-ss,h-ex/4);
penpos3(thinl,90);
z3r = (w/2,h-ex/2-o);
penpos4(thickl,0);
z4l = (ss,h-ex/4);
penpos5(thinl,-90);
z5l = (w/2,h+o);

penstroke z5e..z2e..z3e..z4e..cycle;
penstroke z0e..z1e..z2e;

penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51);
endchar;
