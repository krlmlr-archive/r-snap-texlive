% Schulausgangsschrift (SAS) wesa
% Walter Entenmann
% 16.10.2011
%

% Aufstrich am Wortanfang 
beginchar(oct"200",11.556ut#,ht#,0);
  pickup wesa_pen;
  draw ((0,0)--(((ht)-Delta)/m,ht-Delta));
  xpos:=((ht)-Delta)/m;
  zeigen;
endchar;

% Verbindungsstrich normal
beginchar(oct"005",13.556ut#,ht#,0);
  pickup wesa_pen;
  draw ((0,0){right}..tension2 and 3.5..{1,m}(rho+((ht)-Delta)/m,ht-Delta));
  xpos:=rho+((ht)-Delta)/m;
  zeigen;
endchar;

 % "e" mit Aufstrich am Wortanfang 
beginchar(oct"030",11.733ut#,ht#,0);
  pickup wesa_pen;
     xpos:=0;
     draw ((0,0)--((he)/m,he)) shifted (xpos,0);
     xpos:=xpos+(he)/m;
     latin_e_form(xpos);
     zeigen;
endchar;

% Endstueck gerade
beginchar(oct"007",3.5ut#,ht#,0);
    pickup wesa_pen;
    draw ((0,0)--(3.5ut,m'*3.5ut));
  endchar;

% Endstueck  rund
  beginchar(oct"006",5ut#,ht#,0);
      pickup wesa_pen;
draw ((0,0){right}..(5ut,2ut));
  endchar;

% Verlaengerungsstriche mit e,
% 10ut
  beginchar(oct"000",23.733ut#,ht#,0);
     pickup wesa_pen;
     xpos:=0;
     draw ((0,0){right}..(10ut+rho+(he)/m,he){1,m}) shifted (xpos,0);
     xpos:=xpos+10ut+rho+(he)/m;
     latin_e_form(xpos);
     zeigen;
endchar;

% 12.5ut
  beginchar(oct"001",26.233ut#,ht#,0);
     pickup wesa_pen;
     xpos:=0;
     draw ((0,0){right}..(12.5ut+rho+(he)/m,he){1,m}) shifted (xpos,0);
     xpos:=xpos+12.5ut+rho+(he)/m;
     latin_e_form(xpos);
      zeigen;
endchar;

% 15ut
  beginchar(oct"002",28.733ut#,ht#,0);
     pickup wesa_pen;
     xpos:=0;
     draw ((0,0){right}..(15ut+rho+(he)/m,he){1,m}) shifted (xpos,0);
     xpos:=xpos+15ut+rho+(he)/m;
     latin_e_form(xpos);
      zeigen;
endchar;

% 17.5ut
  beginchar(oct"003",31.233ut#,ht#,0);
     pickup wesa_pen;
     xpos:=0;
     draw ((0,0){right}..(17.5ut+rho+(he)/m,he){1,m}) shifted (xpos,0);
      xpos:=xpos+17.5ut+rho+(he)/m;
      latin_e_form(xpos);
       zeigen;
endchar;

% 20ut
  beginchar(oct"004",33.733ut#,ht#,0);
     pickup wesa_pen;
     xpos:=0;
     draw ((0,0){right}..(20ut+rho+(he)/m,he){1,m}) shifted (xpos,0);
      xpos:=xpos+20ut+rho+(he)/m;
      latin_e_form(xpos);
       zeigen;
     endchar;

     % Aufstriche zu spitzen Buchstaben:
% normal
   beginchar(oct"035",12.5ut#,ht#,0);
  pickup wesa_pen;
  draw ((0,0){right}..tension 1.2..{up}(12.5ut,ht));
endchar;

% nach Unterlaengen
   beginchar(oct"036",12.5ut#,ht#,0);
  pickup wesa_pen;
draw ((0,0){1,m'}..tension 1..{up}(12.5ut,ht));
endchar;

% Lange Aufstriche vor spitzen BSt.
% 18.5ut
  beginchar(oct"021",18.5ut#,ht#,0);
     pickup wesa_pen;
axe:=18.5ut; sigm:=0.71;
draw ((0,0){right}..{axe,ht}(sigm*axe,(1-sigm)*ht)..{up}(axe,ht));
endchar;

% 21ut
  beginchar(oct"031",21ut#,ht#,0);
     pickup wesa_pen;
 axe:=21ut; sigm:=0.72;
      draw ((0,0){right}..{axe,ht}(sigm*axe,(1-sigm)*ht)..{up}(axe,ht));
endchar;

% 23.5ut
  beginchar(oct"032",23.5ut#,ht#,0);
     pickup wesa_pen;
 axe:=23.5ut; sigm:=0.73;
       draw ((0,0){right}..{axe,ht}(sigm*axe,(1-sigm)*ht)..{up}(axe,ht));
endchar;

% 26ut
  beginchar(oct"033",26ut#,ht#,0);
     pickup wesa_pen;
 axe:=26ut; sigm:=0.74;
	draw ((0,0){right}..{axe,ht}(sigm*axe,(1-sigm)*ht)..{up}(axe,ht));
endchar;

% 28.5ut
  beginchar(oct"034",28.5ut#,ht#,0);
     pickup wesa_pen;
 axe:=28.5ut; sigm:=0.75;
     draw ((0,0){right}..{axe,ht}(sigm*axe,(1-sigm)*ht)..{up}(axe,ht));
      endchar;
      
% fuer oben verbundene Buchstaben
% von b,o,v,w
      beginchar(oct"027",14.222ut#,ht#,0);
	     pickup wesa_pen;
	     draw ((0,ht){1,-mfahne}..tension 1.2..{up}(bfahne,ht));
	     xpos:=bfahne;
	     zeigen;
  endchar;
  
% von r
    beginchar(oct"037",12ut#,ht#,0);
           pickup wesa_pen;
	   draw ((0,ht){down}..tension 1.2..{up}(rfahne,ht));
	   xpos:=rfahne;
	   zeigen;
	 endchar;

% Verbindungsstrich von A ร F H zu spitzen BSt.
	 beginchar(oct"236",15ut#,ht#,0);
	   pickup wesa_pen;
	   draw ((0,ht-Delta-eta)--(8ut,ht-Delta-eta){right}...{up}(15ut,ht));
	   xpos:=10ut;
	   	   zeigen;
	 endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% Ziffern und Satzzeichen
%
% Ziffern
beginchar("0",Wz#,Hz#,0);
  pickup wesa_pen;
draw ((0,h){left}..(-w'/2,h/2){down}..(0,0){right}..(w'/2,h/2){up}..{left}(0,h)) shifted (w/2,0);
endchar;

beginchar("1",Wz#,Hz#,0);
  pickup wesa_pen;
draw ((-w'/2,0.6*h)--(0,h)--(0,0))  shifted (w/2+s'/2,0);
endchar;

beginchar("2",Wz#,Hz#,0);
  pickup wesa_pen;
draw ((0,h-r)..(r,h){right}..(2r,h-r){down}..tension2 and 3..{-1,-1}(0,0))  shifted (s',0);
draw ((0,0)--(w',0)) shifted (s',0);
endchar;

beginchar("3",Wz#,Hz#,0);
    pickup wesa_pen;
    draw ((w'/8,7h/8)..(w'/2,h){right}..(7w'/8,0.75h){down}..(w'/2,0.5h){left})
 shifted (s',0);
    draw ((w'/2,0.5h){right}..tension1..(w',w'/2){down}..(w'/2,0){left}..(0,w'/4)) shifted (s',0);
endchar;

beginchar("4",Wz#,Hz#,0);
  pickup wesa_pen;
draw ((w'/4,h)--(0,h/4)--(w',h/4)) shifted (s',0);
draw ((0.675w',3h/4)--(0.675w',0)) shifted (s',0);
endchar;


beginchar("5",Wz#,Hz#,0);
  pickup wesa_pen;
draw ((w',h)--(0,h)--(0,5h/8-dd){1,1}..(w'/2,5h/8){right}..(w',2.5h/8){down}..(w'/2,0){left}..(0,h/8)) shifted (s',0);
endchar;


beginchar("6",Wz#,Hz#,0);
    pickup wesa_pen;
draw ((7w'/8,15h/16)..(w'/2,h){left}..(0,3h/8){down}..(w'/2,0){right}..(w',3h/8){up}..(w'/2,5h/8){left}..(0,3h/8){down}) shifted (s',0);
endchar;


beginchar("7",Wz#,Hz#,0);
  pickup wesa_pen;
  xpos:=0;
draw ((0,h)--(w',h)--(w'/4,0)) shifted (s',0);
quer((h/2),xpos+12.5ut+s')
endchar;

beginchar("8",Wz#,Hz#,0);
  pickup wesa_pen;
draw ((0,0.5625h){1,0.15}..(3w'/8,h-3w'/8){up}..(0,h){left}..(-3w'/8,h-3w'/8){down}..(0,0.5625h){1,-0.15}..
  (w'/2,h/4){down}..(0,0){left}..(-w'/2,h/4){up}..{1,0.15}(0,0.5625h)) shifted (s'+w'/2,0);
endchar;



beginchar("9",Wz#,Hz#,0);
  pickup wesa_pen;
  hoch:=5h/16; breit:=w'/2;
draw ((0,hoch){left}..(-breit,0){down}..(0,-hoch){right}..(breit,0){up}..{left}(0,hoch)) shifted (w/2,0.6875h);
draw ((w',0.6875h){down}..(w'/2,0){left}..(w'/8,h/16)) shifted (s',0);
endchar;


% Satzzeichen
% ASCII-Zeichen
% !
beginchar("!",20ut#,Hz#,0);
  pickup wesa_pen;
  stamm((Hz),10ut,10ut);
  pickup wesa_pen_thick;
  drawdot (0,0) shifted (10ut,0);
endchar;

% ", oct"042", ASCII-Doppelapostroph, hier identisch mit oct"020", dt. Anfz. o.
beginchar(oct"042",25ut#,Ht#,0);
  pickup wesa_pen;
  komma(Ht,10ut);
  komma(Ht,15ut);
endchar;

% #
beginchar("#",60ut#,Ht#,dt#);
  pickup wesa_pen;
 gap:=10ut;
p:= ((0,-(Ht+dt)/2)--(0,(Ht+dt)/2));
draw (p shifted (-gap/2,0) slanted 1/4) shifted (w/2,ht/2);
draw (p shifted (gap/2,0) slanted 1/4) shifted (w/2,ht/2);
q:= ((-20ut,0)--(20ut,0));
  draw (q shifted (0,-gap/2) slanted 1/4) shifted (w/2,ht/2);
  draw (q shifted (0,gap/2) slanted 1/4) shifted (w/2,ht/2);
endchar;
  
% $
beginchar("$",35ut#,Ht#,0);
  pickup wesa_pen;
  radi:=10ut;
draw ((radi,0){up}..(0,radi){left}..{down}(-radi,0)) shifted (w/2,Ht-radi);
  draw ((-radi,0){down}..(0,-radi){right}..{up}(radi,0))  shifted (w/2,radi);
   draw ((-radi,Ht-radi){down}..{down}(radi,radi)) shifted (w/2,0);
   draw ((0,Ht+dd)--(0,-dd)) shifted (w/2,0);
endchar;

% %
beginchar("%",40ut#,Ht#,0);
  pickup wesa_pen;
  sep:=7.5ut;
  q := ((0,0)--(25ut,Ht));
  breit:=10ut; hoch:=15ut;
  p := (breit/2,0){up}..(0,hoch/2){left}..(-breit/2,0){down}..(0,-hoch/2){right}..{up}(breit/2,0);
  x0:=whatever;y0:=whatever;
  z0=(directionpoint (-1,2) of (subpath (0,1) of p)) shifted (sep+breit/2,Ht-hoch/2);
  draw (p shifted (sep+breit/2,Ht-hoch/2));
  draw (p shifted (sep+25ut-breit/2,hoch/2));
  draw (q  shifted (sep,0));
  draw ( z0{1,-2}..{dir 58}(sep+25ut,Ht));
  endchar;

% &
  beginchar("&",40ut#,Ht#,0);
  pickup wesa_pen;
  p := (25ut,20ut){down}..tension 1.2..(10ut,0){left}..(0,10ut){up}..
(15ut,Ht-5ut){up}..(10ut,Ht){left}..(2.5ut,Ht-7.5ut){down}..tension1.5 and 5..{right}(22.5ut,0)..{1,2}(27.5ut,2.5ut);
  draw p shifted (7.5ut,0);
  endchar;

% ยง
  beginchar(oct"237",43.333ut#,Ht#,dt#);
  pickup wesa_pen;
sep:=10ut; breit:=20ut;
p := ((8ut,20ut){curl 10}..(0ut,30ut){left}..(-breit/2,22ut){down}..
  (breit/2,-2.5ut){down}..{-1,2/3}(2ut,-11ut)) scaled ((Ht+dt)/60ut);
  draw p shifted (sep+breit*0.583,ht/2);
  draw p rotated 180 shifted (sep+breit*0.583,ht/2);
  endchar;

% ' (Apostroph)
beginchar("'",15ut#,Ht#,0);
  pickup wesa_pen;
komma((Ht),7.5ut);
endchar;


% "("
beginchar("(",20ut#,Ht#,0);
  pickup wesa_pen;
draw ((5ut,Ht)..(0,ht){down}..(5ut,0)) shifted (10ut,0);
endchar;

% ")"
beginchar(")",20ut#,Ht#,0);
  pickup wesa_pen;
draw ((5ut,Ht)..(10ut,ht){down}..(5ut,0));
endchar;

% "*"
beginchar("*",30ut#,Ht#,0);
  path p;
  pickup wesa_pen;
  z0=(15ut,ht+dt/4);
  z1=(0ut,7.5ut);
  z2=(0ut,-7.5ut);
  p := z1--z2;
  draw p   shifted z0 ;
  draw p   rotated 60 shifted z0 ;
  draw p   rotated -60 shifted z0 ;
  labels(range 0 thru 2); 
endchar;

% "+"
beginchar("+",30ut#,22.5ut#,0);
  path p;
  pickup wesa_pen;
  z0=(15ut,hmath);
  z1=(0ut,10ut);
  z2=(0ut,-10ut);
  p := z1--z2;
  draw p   shifted z0 ;
  draw p   rotated 90 shifted z0 ;
  labels(range 0 thru 2); 
endchar;

% "," Komma
beginchar(",",20ut#,ht#,dt#);
  pickup wesa_pen;
komma(2.5ut,10ut);
endchar;

% ASCII Bindestrich oct"055" (OT1: auch Trennstrich)
beginchar("-",27.5ut#,ht#,0);
  pickup wesa_pen;
  draw ((0,0)--(12.5ut,0)) shifted (7.5ut,hdash);
endchar;

% T1: Trennstrich
beginchar(oct"177",27.5ut#,ht#,0);
  pickup wesa_pen;
  draw ((0,0)--(12.5ut,0)) shifted (7.5ut,hdash);
endchar;

% "." Punkt
beginchar(".",20ut#,ht#,0);
  pickup wesa_pen_thick;
  drawdot (0,0) shifted (10ut,0);
endchar;

% "/" 
beginchar("/",25ut#,Ht#,0);
  pickup wesa_pen;
draw ((5ut,0)--(20ut,Ht));
endchar;

% ":" Doppelpunkt
beginchar(":",20ut#,ht#,0);
  pickup wesa_pen_thick;
  drawdot (0,0) shifted (10ut,0);
  drawdot (0,0) shifted (10ut,15ut);
endchar;

% ";" Strichpunkt
beginchar(";",20ut#,ht#,dt#);
  pickup wesa_pen;
komma(2.5ut,10ut);
  pickup wesa_pen_thick;
  drawdot (0,0) shifted (10ut,15ut);
endchar;

% ">" 
beginchar(">",30ut#,Ht#,0);
  path p;
  pickup wesa_pen;
p:=(0,0)--(20ut,0);
draw p rotated 30 shifted (6.33ut,hmath);
  draw p rotated -30 shifted (6.33ut,hmath);
endchar;


% "=" 
beginchar("=",30ut#,ht#,0);
    pickup wesa_pen;
  draw((0,0)--(20ut,0)) shifted (5ut,hmath-2.5ut);
  draw((0,0)--(20ut,0)) shifted (5ut,hmath+2.5ut);
endchar;


% "<" 
beginchar("<",30ut#,Ht#,0);
  path p;
  pickup wesa_pen;
p:=(0,0)--(-20ut,0);
draw p rotated 30 shifted (23.66ut,hmath);
  draw p rotated -30 shifted (23.66ut,hmath);
endchar;


% ?
beginchar("?",35ut#,Hz#,0);
  radi:=7.5ut;
  pickup wesa_pen;
  z1=(-radi,Hz-radi);
  z2=(0,Hz);
  z3=(radi,Hz-radi);
  z5=(-radi,10ut+radi);
  z6=(0,10ut);
  z7=(radi,10ut+radi);
  p := z1.. z2{right}..z3{down}..{down}z5..z6{right}..z7;
  draw p shifted (17.5ut,0);
  pickup wesa_pen_thick;
  drawdot (0,0) shifted (17.5ut,0);
endchar;

% spezielle Sonderzeichen (ec-Kodierung(
% oct"020", wie ASCII " (oct"042"), dt. Anfuehrungszeichen oben
beginchar(oct"020",15ut#,Ht#,0);
  pickup wesa_pen;
  komma((Ht),5ut);
  komma((Ht),10ut);
endchar;

% oct"022", dt. Anfuehrungszeichen unten
beginchar(oct"022",25ut#,ht#,dt#);
  pickup wesa_pen;
  komma(2.5ut,10ut);
  komma(2.5ut,15ut);
endchar;

% oct"025" Bis-Strich --, Gedankenstrich 
beginchar(oct"025",30ut#,ht#,0);
  pickup wesa_pen;
draw ((0,0)--(20ut,0)) shifted (5ut,hdash);
endchar;

% engl. Gedankenstrich ---
beginchar(oct"026",45ut#,ht#,0);
  pickup wesa_pen;
draw ((0,0)--(30ut,0)) shifted (7.5ut,hdash);
endchar;


% oct"023" franz. Anfz. <<
beginchar(oct"023",32.5ut#,ht#,0);
  path p, q;
  pickup wesa_pen;
  z1=(0ut,0ut);
  z2=(1.1*ht/2,0);
  z3=(6,-1);
  z4=(6,1);
  z5=(7.5ut,ht/2);
z6=(15ut,ht/2);
  p := (z1{z3}..z2) rotated 45;
    q := (z1{z4}..z2) rotated -45;
  draw p shifted z5  ;
  draw p shifted z6 ;
    draw q shifted z5 ;
  draw q shifted z6 ;
  labels(range 1 thru 6);
endchar;  


% oct"024" franz. Anfz. >>
beginchar(oct"024",32.5ut#,ht#,0);
  path p, q;
  pickup wesa_pen;
  z1=(0ut,0ut);
  z2=(1.1*ht/2,0);
  z3=(6,-1);
    z4=(6,1);
  z5=(17.5ut,ht/2);
  z6=(25ut,ht/2);

  p := (z1{z4}..z2) rotated 135;
    q := (z1{z3}..z2) rotated -135;
  draw p shifted z5 ;
  draw p shifted z6 ;
    draw q shifted z5 ;
  draw q shifted z6 ;
  labels(range 1 thru 6);
endchar;  

% oct"016" franz. Anfz. <
beginchar(oct"016",25ut#,ht#,0);
  path p, q;
  pickup wesa_pen;
  z1=(0ut,0ut);
  z2=(1.1*ht/2,0);
  z3=(6,-1);
  z4=(6,1);
  z5=(7.5ut,ht/2);
  p := (z1{z3}..z2) rotated 45;
    q := (z1{z4}..z2) rotated -45;
  draw p shifted z5  ;
    draw q shifted z5 ;
  labels(range 1 thru 5);
endchar;  


% oct"017" franz. Anfz. >
beginchar(oct"017",25ut#,ht#,0);
  path p, q;
  pickup wesa_pen;
  z1=(0ut,0ut);
  z2=(1.1*ht/2,0);
  z3=(6,-1);
    z4=(6,1);
  z5=(17.5ut,ht/2);
  p := (z1{z4}..z2) rotated 135;
    q := (z1{z3}..z2) rotated -135;
  draw p shifted z5 ;
    draw q shifted z5 ;
  labels(range 1 thru 5);
endchar;  

% oct"133" [
beginchar("[",25ut#,Ht#,0);
  pickup wesa_pen;
draw ((5ut,Ht)--(0,Ht)--(0,0)--(5ut,0)) shifted (10ut,0);
endchar;

% oct"135" ]
beginchar("]",25ut#,Ht#,0);
  pickup wesa_pen;
draw ((5ut,Ht)--(10ut,Ht)--(10ut,0)--(5ut,0)) shifted (5ut,0);
endchar;

% "@":
beginchar("@",50ut#,Ht#,0);
  pickup wesa_pen;
sep:=10ut;gap:=8ut;
draw (superellipse((a,0),(0,b),(-a,0),(0,-b),sigma)) shifted (gap+a+sep,ht);
draw ((0,ht)--(0,5ut){down}..(2ut,0){right}..tension 1.5..(gap,ht/2){up}..
  (-a,3ht/2){left}..(-2*a-gap,ht/2){down}..
  (-a,-ht/2){right}..(7.5ut,-5ut)) shifted (2*a+gap+sep,ht/2);
endchar;

% Euro
beginchar(oct"240",47.5ut#,Ht#,0);
  pickup wesa_pen;
  sep:=7.5ut; gap:=7.5ut;
draw ((7.5ut,Ht/2-2.5ut)..(0,Ht/2){left}..(-15ut,0){down}..{right}(0,-Ht/2)..(7.5ut,-Ht/2+2.5ut)) shifted (15ut+gap+sep,Ht/2);
  draw (((-25ut,2.5ut)--(0,2.5ut))  shifted (15ut+gap+sep,ht)) slanted 0.25;
  draw (((-25ut,-2.5ut)--(0,-2.5ut)) shifted (15ut+gap+sep,ht)) slanted 0.25;
endchar;

%%%%%%%%%%%%%%%%%%%% End of File %%%%%%%%%%%%%%%%%
